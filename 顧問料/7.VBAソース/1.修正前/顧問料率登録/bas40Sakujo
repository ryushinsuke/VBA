Attribute VB_Name = "bas40Sakujo"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料・顧問料率登録
'
'オブジェクト名：bas40Sakujo
'
'機能概要　：削除処理
'
'更新履歴　：2007/11/16 SRA 東       新規作成
'
'*************************************************************************

Const MODULE_NAME = "bas40Sakujo"


'*************************************************************************
'関数名　　：削除ボタン押下処理関数
'
'引　　数　：
'           objTarget        I           対象のシート
'
'戻り値　　：なし
'
'機能説明　：削除ボタン押下時の処理
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub gsub_SakujoMain(ByRef objTarget As Object)

On Error GoTo ErrorSection

    Dim blnRet          As Boolean
    Dim strRateCd     As String       '料率コード
    Dim strKakemeName   As String       '掛目名称
    Dim strMsg          As String       'メッセージ
    Dim lngRet          As Long
    
    blnRet = False
    strMsg = ""
    
    With objTarget
    
        
        '入力チェック処理(料率コード)
        blnRet = gfuncRateCodeCodeInputCheck(objTarget)
        
        If blnRet = False Then
            Exit Sub
        End If
        
        blnRet = False
        
        strRateCd = Trim(objTarget.Range("料率コード"))

        
        '確認メッセージ
        strMsg = gfunc_ErrorMsg(2, 14027, "", "", False)
        lngRet = MsgBox(strMsg, vbQuestion + vbOKCancel, Application.ActiveWorkbook.Name)
        If lngRet = vbCancel Then
            Exit Sub
        End If
        
        
        '削除処理
        blnRet = gfunc_DoDelete(strRateCd)
        
        If blnRet = False Then
            
            '削除失敗
            Call gfunc_ErrorMsg(1, 14028, "", "")
            
            Exit Sub
        End If
        
        '画面の初期化
        Call gCom01_SheetProtect(objTarget, False)
        Call g_subRateCodeIniScreenH(objTarget)   'ヘッダー部
        objTarget.Range("料率コード") = strRateCd
        Call g_subRateCodeIniScreenD(objTarget)   '明細部
        'コンボの再設定
        Call subIniCombo
        Call gCom01_SheetProtect(objTarget, True)

        'メッセージ
        Call gfunc_ErrorMsg(1, 14029, "", "", True)
        
    End With
    
    Exit Sub
    
ErrorSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gsub_KensakuMain", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub


'*************************************************************************
'関数名　　：料率削除処理
'
'引　　数　：
'           strCode        I           料率コード
'
'戻り値　　：
'
'機能説明　：引数の料率コードのデータを削除する。
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Private Function gfunc_DoDelete(ByVal strCode As String) As Boolean

On Error GoTo ErrorSection
    
    Dim blnRet              As Boolean
    Dim vData()             As Variant
    Dim lngDataCnt          As Long
    Dim dtKoushinYmdFirst   As Date     '検索データの更新日
    Dim lngStatus           As Long     '更新済みチェックのステータス
    Dim lngRow              As Long
    Dim lngMsgId            As Long
    
    gfunc_DoDelete = False
    
    'トランザクション開始
    cnAdo.BeginTrans
    
    
    '削除処理
    blnRet = func_DeleteRate(strCode)
    If blnRet = False Then
        '削除失敗
        Call gfunc_ErrorMsg(4, 11408, "", "")
        GoTo RollbackSection
    End If
    
    
    'コミット
    cnAdo.CommitTrans


    gfunc_DoDelete = True


    Exit Function
    
RollbackSection:

    'ロールバック
    cnAdo.RollbackTrans
    
    gfunc_DoDelete = False
    Exit Function
    
ErrorSection:

    gfunc_DoDelete = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfunc_DoDelete", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
On Error Resume Next
    'ロールバック
    cnAdo.RollbackTrans

End Function


'*************************************************************************
'関数名　　：料率コード削除処理
'
'引　　数　：
'           strCode        I           料率コード
'
'戻り値　　：
'
'機能説明　：引数の料率コードのデータを削除する。
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'更新履歴　：2008/01/17 SRA Y.Azuma    変更     (PH4)特殊文字バグ対応
'
'*************************************************************************
Private Function func_DeleteRate(ByVal strCode As String) As Boolean
On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean

    func_DeleteRate = False
    
    '料率の削除
    strSql = ""
    strSql = strSql & " UPDATE " & TBL_RATE
    strSql = strSql & " SET "
    strSql = strSql & " KOHSHIN_YMD       = SYSDATE "
    strSql = strSql & ",KOHSHIN_PGM_ID    = '" & UPDATE_PGM_ID & "'"
    strSql = strSql & ",KOHSHIN_TANTOHSHA = '" & UPDATE_USER & "'"
    strSql = strSql & ",SAKUJO_FLG        = '1'"
    strSql = strSql & " WHERE "
'------------<Modify Start azuma 2008/01/17 (PH4)> ★★★☆-----------------------
    strSql = strSql & " RATE_CODE   = '" & func_ChkQuoteshon(strCode, "'") & "'"
'    strSql = strSql & " RATE_CODE   = '" & strCode & "'"
'------------<Modify End   azuma 2008/01/17 (PH4)> ★★★☆-----------------------
    
    '実行
    blnRet = gclsdb.DoExequteSql(gcUPDATE, strSql, vData, lngCol, lngRow, strErrCode)
    
    
    If blnRet = False Then
        Exit Function
    End If
    
    Erase vData
    func_DeleteRate = True

    Exit Function

ErrorSection:

    func_DeleteRate = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_DeleteRate", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Erase vData

End Function
