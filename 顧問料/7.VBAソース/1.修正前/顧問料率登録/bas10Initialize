Attribute VB_Name = "bas10Initialize"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料・顧問料率登録
'
'オブジェクト名：bas10Initialize
'
'機能概要　：初期処理
'
'更新履歴　：2007/11/16 SRA 東       新規作成
'
'*************************************************************************
Const MODULE_NAME = "bas10Initialize"

'*************************************************************************
'関数名　　：初期化メイン
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：初期起動時に行われる初期処理のメイン
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub g_subInitialize()

    '画面の初期化
    Call g_subRateCodeIniScreenH(Worksheets(MAIN_SHEETNAME))   'ヘッダー部
    Call g_subRateCodeIniScreenD(Worksheets(MAIN_SHEETNAME))   '明細部
    
    'コンボ項目の初期化
    Call subIniCombo
    
End Sub


'*************************************************************************
'関数名　　：顧問料率登録画面のクリア処理(ヘッダー部)
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：ヘッダー部のセルをクリアする
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub g_subRateCodeIniScreenH(ByRef TargetSheets As Object)
On Error GoTo ErrHandler

    ' ヘッダー部の初期化
    With TargetSheets

        .Range("料率コード").Value = ""
        .cmdSearch.Enabled = True
        .cmdUp.Enabled = False
        .cmdDelete.Enabled = False
    End With
    
Exit Sub

ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "g_subRateCodeIniScreenH", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub

'*************************************************************************
'関数名　　：顧問料率登録画面のクリア処理(明細部)
'
'引　　数　：TargetSheets       I       処理対象シート
'
'戻り値　　：なし
'
'機能説明　：明細部行を全て削除する
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub g_subRateCodeIniScreenD(ByRef TargetSheets As Object)
On Error GoTo ErrHandler

    Dim lngCnt      As Long
    Dim lngDankai   As Long
    
    
    ' 明細部の初期化
    With TargetSheets
        .Range("料率名").Value = ""
        Call sub_Clear(TargetSheets, START_ROW, ROWS, .Range("金額From").Column)
        Call sub_Clear(TargetSheets, START_ROW, ROWS, .Range("金額To").Column)
        Call sub_Clear(TargetSheets, START_ROW, ROWS, .Range("料率額").Column)
        Call sub_Clear(TargetSheets, START_ROW, ROWS, .Range("料率P").Column)
        .Range("金額From") = "0"
    End With
    
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("検索時料率コード") = ""
    
Exit Sub
ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "g_subRateCodeIniScreenD", Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------

End Sub


'*************************************************************************
'関数名　　：顧問料率登録画面のクリア処理(明細部)
'
'引　　数　：TargetSheets       I       処理対象シート
'引　　数　：lngStartRow        I       クリア開始行
'            lngRows            I       クリア行数
'            lngCol             I       クリア列
'
'戻り値　　：なし
'
'機能説明　：明細部行を全て削除する
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub sub_Clear(ByRef TargetSheets As Object, _
                     ByVal lngStartRow As Long, _
                     ByVal lngRows As Long, _
                     ByVal lngCol As Long)
On Error GoTo ErrHandler
    Dim lngCnt As Long

    For lngCnt = lngStartRow To lngStartRow + lngRows - 1
    
        With TargetSheets
            .Cells(lngCnt, lngCol).Value = ""
            
            '金額Toをクリアする場合は、金額Fromもクリアする。
            If lngCol = .Range("金額To").Column Then
                .Cells(lngCnt + 1, .Range("金額From").Column).Value = ""
            End If
            
        End With
        
    Next lngCnt

Exit Sub
ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "g_subRateCodeIniScreenD", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub

'コンボ項目の初期化
Public Sub subIniCombo()

    Dim strErrMsg As String
    
    strErrMsg = ""

    '料率コードのデータ取得
    If fucGetRateCodeList(strErrMsg) = False Then
        Call gfunc_ErrorMsg(3, 14001, "", "(" & strErrMsg & ")")
        Exit Sub
    End If
    
    
    '資産と料率の名前定義
    Call subNameDefine
    
    

End Sub

'*************************************************************************
'関数名　　：料率テーブル抽出処理
'
'引　　数　：strErrMsg      O       エラーメッセージ
'
'戻り値　　：Boolean
'
'機能説明　：料率テーブルからコンボ項目を取得し「data」シートに記入
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規
'
'*************************************************************************
Private Function fucGetRateCodeList(ByRef strErrMsg As String) As Boolean
       
On Error GoTo ErrHandler
    
    Dim strSql          As String
    Dim vData()         As Variant
    Dim lngCol          As Long
    Dim lngRow          As Long
    Dim lngCnt          As Long
    Dim lngListCol      As Long
    
    fucGetRateCodeList = False
    
    strSql = ""
    strSql = strSql & "   SELECT RATE_CODE"
    strSql = strSql & "     FROM " & TBL_RATE
    strSql = strSql & "    WHERE SAKUJO_FLG = '0'"
    strSql = strSql & " ORDER BY RATE_CODE"
        
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Activate
    
    
    '「料率コードリスト」のデータセル範囲をクリア
    With ThisWorkbook.Worksheets(DATA_SHEETNAME)
        lngListCol = .Range("料率コードリスト").Column
        .Range(.Cells(2, lngListCol), .Cells(65536, lngListCol)).ClearContents
    End With
    
    '取得データをセルに設定
    For lngCnt = 0 To lngRow - 1
         ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(2 + lngCnt + 0, lngListCol).Value = vData(0, lngCnt)
    Next lngCnt
    
    Erase vData
    
    
    fucGetRateCodeList = True
    
    Exit Function
ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "fucGetRateCodeList", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Erase vData
    strErrMsg = "fucGetRateCodeList" & vbCrLf & Err.Description

End Function

'*************************************************************************
'関数名　　：dataシートのセル名再定義
'
'引　　数　：無し
'
'戻り値　　：無し
'
'機能説明　：料率コードリストの名前定義を再設定する。
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規
'
'*************************************************************************
Private Sub subNameDefine()

    Dim RowCnt          As Long
    Dim lngListCol      As Long
    
    lngListCol = ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("料率コードリスト").Column
    
    '*** 資産の名前定義
        
    For RowCnt = 2 + 0 To 65536
        If Trim(ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(RowCnt, lngListCol).Value) = "" Then
            RowCnt = RowCnt - 1
            Exit For
        End If
    Next RowCnt
    
    '再定義
    ThisWorkbook.Names.Add Name:="料率コードリスト", _
     RefersToR1C1:="=data!R2C" & lngListCol & ":R" & RowCnt & "C" & lngListCol
    
End Sub

