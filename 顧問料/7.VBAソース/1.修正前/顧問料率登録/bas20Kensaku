Attribute VB_Name = "bas20Kensaku"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料・顧問料率登録
'
'オブジェクト名：bas20Kensaku
'
'機能概要　：検索ボタン処理
'
'更新履歴　：2007/11/16 SRA 東       新規作成
'
'*************************************************************************

Const MODULE_NAME = "bas20Kensaku"



'*************************************************************************
'関数名　　：検索処理メイン処理
'
'引　　数　：
'           objTarget        I           対象のシート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub gsub_KensakuMain(ByRef objTarget As Object)

On Error GoTo ErrorSection

    Dim blnRet As Boolean
    
    blnRet = False
    
    
    With objTarget
    
        '明細部の初期化
        Call g_subRateCodeIniScreenD(objTarget)   '明細部
        
        '入力チェック処理(検索時)
        blnRet = gfuncRateCodeCodeInputCheck(objTarget)
        
        If blnRet = False Then
            Call g_subRateCodeIniScreenD(objTarget)   '明細部
            Exit Sub
        End If
        
        blnRet = False
        
        '検索処理とデータ設定
        blnRet = gfuncRate_DoSearch(objTarget)
        
        If blnRet = False Then
            
            'システムエラー
            Call gfunc_ErrorMsg(4, 14005, "", "")
            
            .cmdUp.Enabled = False
            .cmdDelete.Enabled = False

            Call g_subRateCodeIniScreenD(objTarget)   '明細部
            Exit Sub
        End If
        
        .Range("料率名").Select
    End With
    
    Exit Sub
    
ErrorSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gsub_KensakuMain", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Call g_subRateCodeIniScreenH(objTarget)   'ヘッダー部
    Call g_subRateCodeIniScreenD(objTarget)   '明細部

End Sub


'*************************************************************************
'関数名　　：入力チェック処理
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Function gfuncRateCodeCodeInputCheck(ByRef TargetSheets As Object) As Boolean

On Error GoTo ErrorSection
    Dim strRateCode     As String
    
    gfuncRateCodeCodeInputCheck = False
    strRateCode = ""
    
'１．【料率コードコード】
    strRateCode = Trim(TargetSheets.Range("料率コード"))

    '①入力ﾁｪｯｸ
      If Trim(strRateCode) = "" Then
        '"料率コード：料率コードが未入力です。"
            Call gfunc_ErrorMsg(3, 14002, "料率コード", "3桁で入力して下さい。")
            TargetSheets.Range("料率コード").Select
        Exit Function
      End If
    
    '②桁数ﾁｪｯｸ
    If Len(strRateCode) <> 3 Then
        '"料率コード：入力に誤りがあります。"
            Call gfunc_ErrorMsg(3, 14003, "料率コード", "3桁で入力して下さい。")
            TargetSheets.Range("料率コード").Select
        Exit Function
    End If
    
    '③バイト数チェック
    If LenB(StrConv(strRateCode, vbFromUnicode)) > 3 Then
        '"料率コード：入力に誤りがあります。"
            Call gfunc_ErrorMsg(3, 14004, "料率コード", "半角3桁で入力して下さい。")
            TargetSheets.Range("料率コード").Select
        Exit Function
    End If
    
    gfuncRateCodeCodeInputCheck = True


    Exit Function
    
ErrorSection:

    gfuncRateCodeCodeInputCheck = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfuncRateCodeCodeInputCheck", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------


End Function

    

    
    

'*************************************************************************
'関数名　　：料率検索処理
'
'引　　数　：
'           TargetSheets       I       シート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Function gfuncRate_DoSearch(ByRef TargetSheets As Object) As Boolean
On Error GoTo Error_Section
    
    Dim blnRet          As Boolean
    Dim strRateCode     As String
    Dim vDatRate()      As Variant      '取得データ(料率)
    Dim lngDataCnt      As Long         '取得データ数
    
    Erase vDatRate
    
    gfuncRate_DoSearch = False

'*******************************************************
'* 関数開始　                                        　*
'*******************************************************

    '検索キー情報取得
    strRateCode = Trim(TargetSheets.Range("料率コード"))

    'V料率の検索
    lngDataCnt = 0
    blnRet = func_GetRateInfo(strRateCode, vDatRate, lngDataCnt)
                                   
    If blnRet = False Then
        Erase vDatRate
        Exit Function
    End If
        
    
    '画面明細の初期化
    Call g_subRateCodeIniScreenD(TargetSheets)   '明細部
    
    '取得データの表示
    '情報の画面設定
    blnRet = func_SetRateInfo(TargetSheets, vDatRate, lngDataCnt)
    
    If blnRet = False Then
        With TargetSheets
            .cmdUp.Enabled = False       '更新ボタン
            .cmdDelete.Enabled = False   '削除ボタン
        End With
        Erase vDatRate
        Exit Function
    End If
    
    If lngDataCnt > 0 Then
        With TargetSheets
            .cmdUp.Enabled = True       '更新ボタン
            .cmdDelete.Enabled = True   '削除ボタン
        End With
    Else
        With TargetSheets
            .cmdUp.Enabled = True       '更新ボタン
            .cmdDelete.Enabled = False   '削除ボタン
        End With
    End If
    
    '取得データをメモリーから破棄
    Erase vDatRate
    
    gfuncRate_DoSearch = True

    Exit Function

Error_Section:

    gfuncRate_DoSearch = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfuncRate_DoSearch", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
    TargetSheets.cmdUp.Enabled = False       '更新ボタン
    TargetSheets.cmdDelete.Enabled = False   '削除ボタン
    Erase vDatRate

End Function



'*************************************************************************
'関数名　　：料率情報取得処理
'
'引　　数　：
'           strRateCode     I       料率コード
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：料率情報を取得する
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'更新履歴　：2008/01/17 SRA Y.Azuma    変更     (PH4)特殊文字バグ対応
'
'*************************************************************************
Public Function func_GetRateInfo(ByVal strRateCode As String _
                               , ByRef vData() As Variant _
                               , ByRef lngRow As Long) As Boolean
On Error GoTo ErrHandler
    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String
    
    func_GetRateInfo = False
    
    strSql = ""
    strSql = strSql & "   SELECT RATE_NAME"
    strSql = strSql & "         ,DANKAI"
    strSql = strSql & "         ,KINGAKU_FROM"
    strSql = strSql & "         ,KINGAKU_TO"
    strSql = strSql & "         ,RITSU_GAKU"
    strSql = strSql & "         ,RITSU"
    strSql = strSql & "     FROM " & TBL_VW_RATE
'------------<Modify Start azuma 2008/01/17 (PH4)> ★★★☆-----------------------
    strSql = strSql & "    WHERE RATE_CODE = '" & func_ChkQuoteshon(strRateCode, "'") & "'"
'    strSql = strSql & "    WHERE RATE_CODE = '" & strRateCode & "'"
'------------<Modify End   azuma 2008/01/17 (PH4)> ★★★☆-----------------------
    strSql = strSql & " ORDER BY DANKAI"

    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        lngRow = 0
        Exit Function
    End If
    
    func_GetRateInfo = True
    
    Exit Function

ErrHandler:
    func_GetRateInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetRateInfo", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
'    Erase vData
    lngRow = 0


End Function




'*************************************************************************
'関数名　　：ワークシートに取得情報を設定する
'
'引　　数　：
'           TargetSheets    O       処理対象ワークシート
'           vdata()         I       設定データ
'           lngRow          I       設定データ件数
'
'戻り値　　：Boolean
'
'機能説明　：処理対象ワークシートにデータを設定する。
'
'更新履歴　：2007/11/16 SRA Y.Azuma    新規作成
'
'*************************************************************************
Private Function func_SetRateInfo(ByRef TargetSheets As Object _
                                   , ByVal vData As Variant _
                                   , ByVal lngRow As Long) As Boolean
On Error GoTo ErrHandler

    Dim lngCnt   As Long
    Dim lngDankai   As Long
    Dim vInitKingakuFrom As Variant
    Dim vInitKingakuTo As Variant
    
    vInitKingakuFrom = Array("0", "100", "200", "300", "500", "1000", "2000", "3000", "5000")
'------------<Modify 1兆円超対応20080218 2008/02/29 SRA市川 Start > ----------------------------------------
'    vInitKingakuTo = Array("100", "200", "300", "500", "1000", "2000", "3000", "5000", "99999")
    vInitKingakuTo = Array("100", "200", "300", "500", "1000", "2000", "3000", "5000", "9999999")
'------------<Modify 1兆円超対応20080218 2008/02/29 SRA市川 Start > ----------------------------------------

    func_SetRateInfo = False
    
    If lngRow > 0 Then
        ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("検索時料率コード") = TargetSheets.Range("料率コード")
        TargetSheets.Range("料率名") = CStr(Trim(vData(EmGetVRateInfo.RATE_NAME, lngCnt - 0)))
        
        For lngCnt = 0 To lngRow - 1
            lngDankai = lngCnt
            With TargetSheets
                .Range("金額From").Offset(lngDankai, 0) = func_Format(CStr(vData(EmGetVRateInfo.KINGAKU_FROM, lngCnt - 0)), .Range("金額From").Column)
                .Range("金額To").Offset(lngDankai, 0) = func_Format(CStr(vData(EmGetVRateInfo.KINGAKU_TO, lngCnt - 0)), .Range("金額To").Column)
                .Range("料率額").Offset(lngDankai, 0) = func_Format(CStr(vData(EmGetVRateInfo.RITSU_GAKU, lngCnt - 0)), .Range("料率額").Column)
                .Range("料率P").Offset(lngDankai, 0) = func_Format(CStr(vData(EmGetVRateInfo.RITSU, lngCnt - 0)), .Range("料率P").Column)
            
                '料率(金額)、料率(％)が共に"0"以外の"0"は、NULLに設定する。
                If .Range("料率額").Offset(lngDankai, 0) <> "0" Or .Range("料率P").Offset(lngDankai, 0) <> "0" Then
                    If .Range("料率額").Offset(lngDankai, 0) = "0" Then
                        .Range("料率額").Offset(lngDankai, 0) = ""
                    End If
                    
                    If .Range("料率P").Offset(lngDankai, 0) = "0" Then
                        .Range("料率P").Offset(lngDankai, 0) = ""
                    End If
                End If
            End With
            
        Next lngCnt
    Else
        With TargetSheets
            ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("検索時料率コード") = ""
            For lngCnt = 0 To 8
                .Range("金額From").Offset(lngCnt, 0) = func_Format(CStr(vInitKingakuFrom(lngCnt)), .Range("金額From").Column)
                .Range("金額To").Offset(lngCnt, 0) = func_Format(CStr(vInitKingakuTo(lngCnt)), .Range("金額To").Column)
            Next lngCnt
        End With
    End If

    func_SetRateInfo = True

    Exit Function

ErrHandler:
    func_SetRateInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_SetRateInfo", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
'    MsgBox "Error:func_SetRateInfo" & vbCrLf & Err.Description

End Function





