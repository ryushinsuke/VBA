Attribute VB_Name = "basSearch"
Option Explicit

'*************************************************************************
'
'プロジェクト名：契約管理システム・基礎数値修正
'
'オブジェクト名：basSearch
'
'機能概要　：組入資産情報検索
'
'更新履歴　：2007/03/19 SRA T.Ichikawa  新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "basSearch"

'組入資産情報取得レコード
Private Enum eKumRec
    eSHISAN_BUNRUI_NM = 0        '資産分類名
    eSHISAN_CODE                 '資産コード
    eSHISAN_NM                   '資産名
    eSHISAN_BUNRUI_CODE          '資産分類コード
    eSEIKYU_KIKAN_FROM           '請求期間FROM
    eSEIKYU_KIKAN_TO             '請求期間TO
    eRATE_TEKIYOH_KIKAN_FROM     '料率適用期間FROM
    eRATE_TEKIYOH_KIKAN_TO       '料率適用期間TO
    eKISO_SUCHI_KEISAN_HOHHOH    '基礎数値計算方法
    eKISO_SUCHI_KEISAN_HOHHOH_NM '基礎数値計算方法名
    ePX_SHINTAKU_KBN             '基礎数値種類(PX信託区分)
    ePX_SHINTAKU_KBN_NM          '基礎数値種類名(PX信託区分)
    eKEISAN_HOHHOH               '顧問料計算方法
End Enum

'*************************************************************************
'関数名　　：検索ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：入力フィールドの初期化
'
'更新履歴　：2007/03/19 SRA T.Ichikawa  新規作成
'
'*************************************************************************
Function fncSearch() As Long
    
    Const PROCEDURE_NAME As String = "fncSearch"

On Error GoTo ErrorHandler

    Dim lngRet          As Long
    Dim strKouza_no     As String
    Dim strKouza_name   As String
    Dim strKijyunbi     As String
    Dim lngRowCnt       As Long
    Dim lngRow          As Long
    Dim vntRec()        As Variant
    Dim lngCnt          As Long

    Dim strSettei_date  As String
    Dim strCalloff_date As String

    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncSearch = 0
    
    '入力チェック
    With Sheets(SHEET_MAIN)
        Application.ScreenUpdating = False
        '組入資産情報 削除
        fncKsCmnClearField 1
        '基礎数値情報 削除
        fncKsCmnClearField 2
        
        '口座番号
        lngRet = fncChkKouzaNoFormat(.Range(RNG_KOUZA_NO).Value)
        If lngRet <> 0 Then
            .Range(RNG_KOUZA_NM).Value = vbNullString
            .Range(RNG_KOUZA_NO).Select
            fncSearch = lngRet
            Exit Function
        Else
            strKouza_no = Format(.Range(RNG_KOUZA_NO).Value, "0000000")
        End If
    
        '基準日
        If .Range(RNG_KIJYUNBI).Value = vbNullString Then
            '基準日が未入力の場合、DBのSYSDATEをセット
            lngRet = fncGetDbSysdate(strKijyunbi)
            If lngRet <> 0 Then
                fncSearch = lngRet
                Exit Function
            End If
            .Range(RNG_KIJYUNBI).Value = strKijyunbi
        Else
            '基準日の形式をチェック
            lngRet = fncChkDateFormat(.Range(RNG_KIJYUNBI).Value, MSG1_KIJYUNBI)
            If lngRet <> 0 Then
                .Range(RNG_KIJYUNBI).Select
                fncSearch = lngRet
                Exit Function
            End If
        End If

        '口座番号情報取得
'        lngRet = fncChkKouzaNoInfo(strKouza_no, .Range(RNG_KIJYUNBI).Value, strKouza_name)
        lngRet = fncChkKouzaNoInfo( _
                    strKouza_no, _
                    .Range(RNG_KIJYUNBI).Value, _
                    strKouza_name, _
                    strSettei_date, _
                    strCalloff_date)
        
        If lngRet = 0 Then
            '口座名称表示
            .Range(RNG_KOUZA_NM).Value = strKouza_name
        
            '口座開設日退避
            .Range(RNG_SV_SETTEI_DATE).Value = strSettei_date
            '口座閉鎖日退避
            .Range(RNG_SV_CALLOFF_DATE).Value = strCalloff_date
            
        Else
            .Range(RNG_KOUZA_NM).Value = vbNullString
            .Range(RNG_KOUZA_NO).Select
            fncSearch = lngRet
            Exit Function
        End If

        '組入資産情報取得
        lngRet = fncKumiireRead(strKouza_no, .Range(RNG_KIJYUNBI).Value, vntRec, lngRowCnt)
        If lngRet <> 0 Then
            fncSearch = lngRet
            Exit Function
        End If
    
        If lngRowCnt > 0 Then
            
            '組入資産情報 行追加
            fncKsCmnAddNrow 1, lngRowCnt
            
            'データセット
            lngRow = LIST_DATA_TOP_ROW
            For lngCnt = 0 To lngRowCnt - 1
            
                .Cells(lngRow, COLN_SHISAN_BUNRUI_NM) = vntRec(eKumRec.eSHISAN_BUNRUI_NM, lngCnt)
                .Cells(lngRow, COLN_SHISAN_CODE) = vntRec(eKumRec.eSHISAN_CODE, lngCnt)
                .Cells(lngRow, COLN_SHISAN_NM) = vntRec(eKumRec.eSHISAN_NM, lngCnt)
                .Cells(lngRow, COLN_SHISAN_BUNRUI_CODE) = vntRec(eKumRec.eSHISAN_BUNRUI_CODE, lngCnt)
                .Cells(lngRow, COLN_SEIKYU_KIKAN_FROM) = vntRec(eKumRec.eSEIKYU_KIKAN_FROM, lngCnt)
                .Cells(lngRow, COLN_SEIKYU_KIKAN_TO) = vntRec(eKumRec.eSEIKYU_KIKAN_TO, lngCnt)
                .Cells(lngRow, COLN_RATE_TEKIYOH_KIKAN_FROM) = vntRec(eKumRec.eRATE_TEKIYOH_KIKAN_FROM, lngCnt)
                .Cells(lngRow, COLN_RATE_TEKIYOH_KIKAN_TO) = vntRec(eKumRec.eRATE_TEKIYOH_KIKAN_TO, lngCnt)
                .Cells(lngRow, COLN_KISO_SUCHI_KEISAN_HOHHOH) = vntRec(eKumRec.eKISO_SUCHI_KEISAN_HOHHOH, lngCnt)
                .Cells(lngRow, COLN_PX_SHINTAKU_KBN) = vntRec(eKumRec.ePX_SHINTAKU_KBN, lngCnt)
                .Cells(lngRow, COLN_KISO_SUCHI_KEISAN_HOHHOH_NM) = vntRec(eKumRec.eKISO_SUCHI_KEISAN_HOHHOH_NM, lngCnt)
                .Cells(lngRow, COLN_PX_SHINTAKU_KBN_NM) = vntRec(eKumRec.ePX_SHINTAKU_KBN_NM, lngCnt)
                .Cells(lngRow, COLN_KEISAN_HOHHOH) = vntRec(eKumRec.eKEISAN_HOHHOH, lngCnt)
            
                lngRow = lngRow + 1
            Next
            
            '表示ボタンを押せるようにする
            .cmdDisp.Enabled = True
            
            Application.ScreenUpdating = True
        
        Else
            '3000 該当データが存在しません。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 3000, MSG1_KINF, , True
            fncSearch = 3000
            Exit Function

        End If
    
        '入力された口座番号・基準日を保存
        .Range(RNG_SV_KOUZA_NO).Value = strKouza_no
        .Range(RNG_SV_KIJYUNBI).Value = .Range(RNG_KIJYUNBI).Value
    
    End With
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrorHandler:
    Application.ScreenUpdating = True
    fncSearch = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    
End Function

