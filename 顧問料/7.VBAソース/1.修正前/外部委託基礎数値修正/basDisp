Attribute VB_Name = "basDisp"
Option Explicit
'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー計算システム・外部委託基礎数値修正
'
'オブジェクト名：basDisp
'
'機能概要　    ：基礎数値情報表示
'
'更新履歴　    ：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "basDisp"


'*************************************************************************
'関数名　　：基礎数値情報取得
'
'引　　数　：ARG1   I   Gi_no                   String  外部委託先番号(1桁)
'　　　　　：ARG2   I   Gi_shiharai_kikan_id    long    支払期間ID
'　　　　　：ARG3   I   Kouza_no                Strinng 口座番号("0000000")
'　　　　　：ARG4   I   Gi_shiharai_kikan_to    long    支払期間TO(YYYYMMDD)
'　　　　　：ARG5   I   Isscd                   Strinng 銘柄コード
'　　　　　：ARG6   I   Gi_keisan_kikan_id      long    計算期間ID
'　　　　　：ARG7   I   Kiso_suchi_renban       Strinng 基礎数値連番
'　　　　　：ARG8   O   vntRec                  Variant データ配列
'　　　　　：ARG9   O   lngRow                  long    取得レコード数
'
'戻り値　　：エラー番号
'
'機能説明　：基礎数値情報を取得する
'
'更新履歴　：2008/06/20 SRA H.Anpo  新規作成
'更新履歴　：2008/08/29 SRA Y.Azuma    変更   連結障害対応(No.21)
'
'*************************************************************************
Public Function fncGetKisoSuchiInfo( _
            Gi_no As String, _
            Gi_shiharai_kikan_id As Long, _
            Kohza_no As String, _
            Gi_shiharai_kikan_to As Long, _
            Isscd As String, _
            Gi_keisan_kikan_id As Long, _
            Kiso_suchi_renban As String, _
            vntRec() As Variant, _
            lngRow As Long) As Long

    Const PROCEDURE_NAME As String = "fncGetKisoSuchiInfo"

    On Error GoTo ErrHandler
    
    Dim strSql      As String
    Dim lngCol      As Long
    Dim strErrCd    As String
    Dim blnRet      As Boolean
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncGetKisoSuchiInfo = 0
    
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     B.SHUUSEIGAKU_SAIYOU_FLG,"
    strSql = strSql & "     B.KIJUN_YMD,"
    strSql = strSql & "     B.JIKA,"
    strSql = strSql & "     B.KISO_SUCHI_MOD,"
    strSql = strSql & "     B.SHISAN_JIKA_ISSCD"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_GI_KEISANKISO_SHISANJIKA A,"
    strSql = strSql & "     KYK_GI_SHISAN_JIKA B"
    strSql = strSql & " WHERE"
    strSql = strSql & "     A.GI_NO                   =   '" & Gi_no & "'"
    strSql = strSql & " AND A.GI_SHIHARAI_KIKAN_ID    =    " & Gi_shiharai_kikan_id & ""
    strSql = strSql & " AND A.KOHZA_NO                =   '" & Kohza_no & "'"
    strSql = strSql & " AND A.SHIHARAI_FLG            =   '1'" '支払フラグが支払"
    strSql = strSql & " AND A.KEISAN_TO_YMD           =    " & Gi_shiharai_kikan_to & ""
'------------<Modify Start azuma 2008/08/29 > 連結障害対応(No.21)-----------------------
    strSql = strSql & " AND A.ISSCD                   =   '" & func_ChkQuoteshon(Isscd, "'") & "'"
'''    strSql = strSql & " AND A.ISSCD                   =   '" & Isscd & "'"
'------------<Modify End   azuma 2008/08/29 > 連結障害対応(No.21)-----------------------
    strSql = strSql & " AND A.GI_KEISAN_KIKAN_ID      =    " & Gi_keisan_kikan_id & ""
    strSql = strSql & " AND A.KISO_SUCHI_RENBAN       =   '" & Kiso_suchi_renban & "'"
    strSql = strSql & " AND A.KOHZA_NO                =    B.KOHZA_NO"
    strSql = strSql & " AND A.SHISAN_JIKA_ISSCD       =    B.SHISAN_JIKA_ISSCD"
    strSql = strSql & " AND A.SHISAN_JIKA_KIJUN_YMD   =    B.KIJUN_YMD"
    strSql = strSql & " AND A.SAKUJO_FLG              =   '0'"
    strSql = strSql & " AND B.SAKUJO_FLG              =   '0'"
    strSql = strSql & " ORDER BY"
    strSql = strSql & "     B.KIJUN_YMD"
    
    blnRet = fncKsCmnDoExequteSql(mainmenu.gcSELECT, strSql, vntRec, lngCol, lngRow, strErrCd)
    If blnRet = False Then
        fncGetKisoSuchiInfo = -1
        Exit Function
    End If
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
ErrHandler:
    fncGetKisoSuchiInfo = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function


'*************************************************************************
'関数名　　：表示ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：表示実行時のメイン処理
'
'更新履歴　：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Function fncDisp() As Long
    
    Const PROCEDURE_NAME As String = "fncDisp"
    
On Error GoTo ErrorHandler

    Dim lngRet          As Long
    Dim lngSelRow       As Long
    Dim lngRowCnt       As Long
    Dim lngCnt          As Long
    
    Dim vntRecAry()     As Variant
    Dim lngRow          As Long
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)

    fncDisp = 0
    
    Application.ScreenUpdating = False
    
    '************************************
    '* 基礎数値情報の初期化
    '************************************
    fncKsCmnClearField 2
    
    With ThisWorkbook.Sheets(SHEET_MAIN)
    
        '************************************
        '* 入力チェック
        '************************************
        '更新ボタンを使用不可にする
        .cmdUpdate.Enabled = False
    
        '外部委託先
        If Trim(.Range("外部委託先").Value) <> .Range(RNG_SV_GI_NO).Value Then
            '21016 検索時の情報と異なります。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 21016, MSG1_GINO, MSG2_CNG_GINO, True
            fncDisp = lngRet
            Exit Function
        End If
        
        '口座番号
        If Format(.Range("口座番号").Value, "0000000") <> .Range(RNG_SV_KOUZA_NO).Value Then
            '21016 検索時の情報と異なります。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 21016, MSG1_NUMCD, MSG2_CNG_KOUZANO, True
            fncDisp = lngRet
            Exit Function
        End If
    
        '組入資産情報のオプションボタンが押されているかチェックする
        lngRet = fncChkOptionButton(lngSelRow)
        If lngRet <> 0 Then
            fncDisp = lngRet
            Exit Function
        End If
        
        '************************************
        '* 基礎数値情報取得
        '************************************
        '基礎数値取得をCALLし、修正額採用フラグ、基準日、基礎数値、基礎数値修正額を取得する
        lngRet = fncGetKisoSuchiInfo( _
                    Mid(Trim(.Range("外部委託先").Value), 1, 1), _
                    .Cells(lngSelRow, .Range("外部委託支払期間ID").Column), _
                    Format(.Range("口座番号").Value, "0000000"), _
                    .Cells(lngSelRow, .Range("外部委託支払期間TO").Column), _
                    .Cells(lngSelRow, .Range("PX銘柄コード").Column), _
                    .Cells(lngSelRow, .Range("外部委託計算期間ID").Column), _
                    Mid(Trim(.Cells(lngSelRow, .Range("計算基礎数値種類").Column)), 1, 1), _
                    vntRecAry(), _
                    lngRowCnt _
                    )
        If lngRet <> 0 Then
            fncDisp = lngRet
            Exit Function
        End If
        
        '************************************
        '* 基礎数値情報作成
        '************************************
        If lngRowCnt > 0 Then
    
            '基礎数値情報 行追加
            fncKsCmnAddNrow 2, lngRowCnt
            'データセット
            lngRow = fncKsCmnSearchDataTopRow(2)

            For lngCnt = LBound(vntRecAry) To UBound(vntRecAry, 2)

                '修正額採用
                If vntRecAry(0, lngCnt) = "1" Then
                    .Cells(lngRow, COLN_J_CHK_DEFAULT).Value = "TRUE"
                    .Cells(lngRow, COLN_J_CHK_FLG).Value = "TRUE"
                Else
                    .Cells(lngRow, COLN_J_CHK_DEFAULT).Value = "FALSE"
                    .Cells(lngRow, COLN_J_CHK_FLG).Value = "FALSE"
                End If

                '基準日
                .Cells(lngRow, .Range("基礎数値基準日").Column).Value = vntRecAry(1, lngCnt)

                '基礎数値
                .Cells(lngRow, .Range("計算基礎数値").Column).Value = vntRecAry(2, lngCnt)

                '基礎数値修正額
                .Cells(lngRow, .Range("計算基礎数値修正額").Column).Value = vntRecAry(3, lngCnt)

                '資産時価銘柄コード
                .Cells(lngRow, .Range("資産時価銘柄コード").Column).Value = vntRecAry(4, lngCnt)

                lngRow = lngRow + 1
            Next

            '************************************
            '* 表示時情報の退避
            '************************************
            '選択された行番号を保存
            .Range(RNG_SV_SELED_ROW).Value = lngSelRow
    
            '更新ボタンを使用可能にする
            .cmdUpdate.Enabled = True
            
            Application.ScreenUpdating = True
    
        End If
    
    End With
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrorHandler:
    Application.ScreenUpdating = True
    fncDisp = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    
End Function

