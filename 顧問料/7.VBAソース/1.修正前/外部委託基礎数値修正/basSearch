Attribute VB_Name = "basSearch"
Option Explicit
'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー計算システム・外部委託基礎数値修正
'
'オブジェクト名：basSearch
'
'機能概要　    ：組入PX銘柄情報検索
'
'更新履歴　    ：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "basSearch"

'=======================================
'組入PX銘柄情報取得レコード
'=======================================
Private Enum eKumRec
    eISSCD = 0                  '銘柄コード
    eGI_KEISAN_KIKAN_FROM       '計算期間(From)
    eGI_KEISAN_KIKAN_TO         '計算期間(To)
    eGI_KISO_SHURUI             '計算基礎数値種類
    eGI_KEISAN_HOHHOH           '外部委託計算方法
    eRATE_CODE                  '料率コード
    eUNYOH_HOHSHU_RATE_CODE     '運用報酬料率コード
    eGI_SHIHARAI_KIKAN_ID       '外部委託支払期間ID
    eGI_SHIHARAI_KIKAN_TO       '外部委託支払期間TO
    eGI_KEISAN_KIKAN_ID         '外部委託計算期間ID
End Enum

'*************************************************************************
'関数名　　：検索ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：検索実行時のメイン処理
'
'更新履歴　：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Function fncSearch() As Long
    
    Const PROCEDURE_NAME As String = "fncSearch"

On Error GoTo ErrorHandler

    Dim lngRet          As Long
    Dim strKouza_no     As String
    Dim strKouza_name   As String
    Dim strKijyunbi     As String
    Dim lngRowCnt       As Long
    Dim lngRow          As Long
    Dim vntRec()        As Variant
    Dim lngCnt          As Long

    Dim strSettei_date  As String
    Dim strCalloff_date As String

    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncSearch = 0
    
    '入力チェック
    With ThisWorkbook.Sheets(SHEET_MAIN)
        
        Application.ScreenUpdating = False
        
        '************************************
        '* 画面の初期化
        '************************************
        '組入資産情報 削除
        fncKsCmnClearField 1
        '基礎数値情報 削除
        fncKsCmnClearField 2
        '外部委託先・口座番号・基準日の退避エリアをクリア
        fncKsCmnClearSaveField
        
        .Range("口座名称").Value = vbNullString     '口座名称
        
        '************************************
        '* 入力チェック
        '************************************
        '外部委託先
        '必須チェック
        If Trim(.Range("外部委託先").Value) = vbNullString Then
            fncKsCmnMessageDisp MSGLVL_WARNNING, 21005, MSG1_GINO, , True
            fncSearch = 21005
            Exit Function
        End If

        '口座番号
        lngRet = fncChkKouzaNoFormat(.Range("口座番号").Value)
        If lngRet <> 0 Then
            .Range("口座名称").Value = vbNullString
            .Range("口座番号").Select
            fncSearch = lngRet
            Exit Function
        Else
            strKouza_no = Format(.Range("口座番号").Value, "0000000")
        End If
    
        '基準日
        If .Range("基準日").Value = vbNullString Then
            '基準日が未入力の場合、DBのSYSDATEをセット
            lngRet = fncGetDbSysdate(strKijyunbi)
            If lngRet <> 0 Then
                fncSearch = lngRet
                Exit Function
            End If
            .Range("基準日").Value = strKijyunbi
        Else
            '基準日の形式をチェック
            lngRet = fncChkDateFormat(.Range("基準日").Value, MSG1_KIJYUNBI)
            If lngRet <> 0 Then
                .Range("基準日").Select
                fncSearch = lngRet
                Exit Function
            End If
        End If

        '口座番号情報取得
        lngRet = fncChkKouzaNoInfo( _
                    strKouza_no, _
                    .Range("基準日").Value, _
                    strKouza_name, _
                    strSettei_date, _
                    strCalloff_date)

        If lngRet = 0 Then
            '口座名称表示
            .Range("口座名称").Value = strKouza_name
'            '口座開設日退避
'            .Range(RNG_SV_SETTEI_DATE).Value = strSettei_date
'            '口座閉鎖日退避
'            .Range(RNG_SV_CALLOFF_DATE).Value = strCalloff_date
        Else
            .Range("口座名称").Value = vbNullString
            .Range("口座番号").Select
            fncSearch = lngRet
            Exit Function
        End If

        '************************************
        '* 組入PX銘柄情報取得
        '************************************
        lngRet = fncKumiireRead( _
                    Mid(Trim(.Range("外部委託先").Value), 1, 1), _
                    strKouza_no, _
                    .Range("基準日").Value, _
                    vntRec, _
                    lngRowCnt)
        If lngRet <> 0 Then
            fncSearch = lngRet
            Exit Function
        End If
    
        '************************************
        '* 組入PX銘柄情報作成
        '************************************
        If lngRowCnt > 0 Then
            
            '組入PX銘柄情報 行追加
            '取得したレコード数の行を追加する
            fncKsCmnAddNrow 1, lngRowCnt
            
            'データセット
            lngRow = fncKsCmnSearchDataTopRow(1)
            For lngCnt = 0 To lngRowCnt - 1
            
                .Cells(lngRow, .Range("PX銘柄コード").Column) = vntRec(eKumRec.eISSCD, lngCnt)
                .Cells(lngRow, .Range("外部委託計算期間FROM").Column) = vntRec(eKumRec.eGI_KEISAN_KIKAN_FROM, lngCnt)
                .Cells(lngRow, .Range("外部委託計算期間TO").Column) = vntRec(eKumRec.eGI_KEISAN_KIKAN_TO, lngCnt)
                .Cells(lngRow, .Range("計算基礎数値種類").Column) = vntRec(eKumRec.eGI_KISO_SHURUI, lngCnt)
                .Cells(lngRow, .Range("外部委託計算方法").Column) = vntRec(eKumRec.eGI_KEISAN_HOHHOH, lngCnt)
                .Cells(lngRow, .Range("料率コード").Column) = vntRec(eKumRec.eRATE_CODE, lngCnt)
                .Cells(lngRow, .Range("運用報酬料率コード").Column) = vntRec(eKumRec.eUNYOH_HOHSHU_RATE_CODE, lngCnt)
                .Cells(lngRow, .Range("外部委託支払期間ID").Column) = vntRec(eKumRec.eGI_SHIHARAI_KIKAN_ID, lngCnt)
                .Cells(lngRow, .Range("外部委託支払期間TO").Column) = vntRec(eKumRec.eGI_SHIHARAI_KIKAN_TO, lngCnt)
                .Cells(lngRow, .Range("外部委託計算期間ID").Column) = vntRec(eKumRec.eGI_KEISAN_KIKAN_ID, lngCnt)
            
                lngRow = lngRow + 1
            Next
            
            '表示ボタンを押せるようにする
            .cmdDisp.Enabled = True
            
        Else
            '21000 該当データが存在しません。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 21000, MSG1_KINF, , True
            fncSearch = 21000
            Exit Function

        End If
    
        '************************************
        '* 検索時情報の退避
        '************************************
        '入力された外部委託先・口座番号・基準日を保存
        .Range(RNG_SV_GI_NO).Value = Trim(.Range("外部委託先").Value)
        .Range(RNG_SV_KOUZA_NO).Value = strKouza_no
        .Range(RNG_SV_KIJYUNBI).Value = .Range("基準日").Value
    
    End With
    
    Application.ScreenUpdating = True
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrorHandler:
    Application.ScreenUpdating = True
    fncSearch = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    
End Function


'*************************************************************************
'関数名　　：組入PX銘柄情報取得
'
'引　　数　：ARG1   I   GI_No       Strinng 外部委託先
'        　：ARG2   I   Kouza_no    Strinng 口座番号("0000000")
'　　　　　：ARG3   I   Kijyunbi    Strinng 基準日("YYYYMMDD")
'　　　　　：ARG4   O   vntRec      Variant データ配列
'　　　　　：ARG5   O   lngRow      取得行数
'
'戻り値　　：エラー番号
'
'機能説明　：組入資産情報を取得する
'
'更新履歴　：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Function fncKumiireRead( _
            Gi_no As String, _
            Kouza_no As String, _
            Kijyunbi As String, _
            vntRec() As Variant, _
            lngRow As Long) As Long

    Const PROCEDURE_NAME As String = "fncKumiireRead"

    On Error GoTo ErrHandler
    
    Dim strSql      As String
    Dim lngCol      As Long
    Dim strErrCd    As String
    Dim blnRet      As Boolean
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncKumiireRead = 0
    
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     ISSCD ,"                    'PX銘柄コード
    strSql = strSql & "     GI_KEISAN_KIKAN_FROM ,"     '計算期間FROM
    strSql = strSql & "     GI_KEISAN_KIKAN_TO ,"       '計算期間TO
    strSql = strSql & "     KISO_SUCHI_SHURUI ,"        '計算基礎数値種類
    strSql = strSql & "     GI_KEISAN_HOHHOH ,"         '外部委託計算方法
    strSql = strSql & "     RATE_NAME ,"                '料率コード
    strSql = strSql & "     UNYOH_HOHSHU_RATE_NAME ,"   '運用報酬料率コード
    strSql = strSql & "     GI_SHIHARAI_KIKAN_ID ,"     '外部委託支払期間ID
    strSql = strSql & "     GI_SHIHARAI_KIKAN_TO ,"     '外部委託支払期間TO
    strSql = strSql & "     GI_KEISAN_KIKAN_ID"         '外部委託計算期間ID
    strSql = strSql & " FROM ("
    strSql = strSql & "     SELECT"
    strSql = strSql & "         KGKK.ISSCD                                             ISSCD,"
    strSql = strSql & "         KGKK.GI_KEISAN_KIKAN_FROM                              GI_KEISAN_KIKAN_FROM,"
    strSql = strSql & "         KGKK.GI_KEISAN_KIKAN_TO                                GI_KEISAN_KIKAN_TO,"
    strSql = strSql & "         '1:' || KCV1.VALUE_NAME                                KISO_SUCHI_SHURUI,"
    strSql = strSql & "         DECODE(TRIM(KGKK.GI_KEISAN_HOHHOH), NULL, NULL, KGKK.GI_KEISAN_HOHHOH || ':' || KCV2.VALUE_NAME)        GI_KEISAN_HOHHOH,"
    strSql = strSql & "         DECODE(TRIM(KGKK.RATE_CODE), NULL, NULL, KGKK.RATE_CODE || ':' || KR1.RATE_NAME)                 RATE_NAME,"
    strSql = strSql & "         DECODE(TRIM(KGKK.UNYOH_HOHSHU_RATE_CODE), NULL, NULL, KGKK.UNYOH_HOHSHU_RATE_CODE || ':' || KR2.RATE_NAME)    UNYOH_HOHSHU_RATE_NAME,"
    strSql = strSql & "         KGSK.GI_SHIHARAI_KIKAN_ID                              GI_SHIHARAI_KIKAN_ID,"
    strSql = strSql & "         KGSK.GI_SHIHARAI_KIKAN_TO                              GI_SHIHARAI_KIKAN_TO,"
    strSql = strSql & "         KGKK.GI_KEISAN_KIKAN_ID                                GI_KEISAN_KIKAN_ID"
    strSql = strSql & "     FROM"
    strSql = strSql & "         KYK_GI_SHIHARAI_KIKAN   KGSK,"    '外部委託支払期間
    strSql = strSql & "         KYK_GI_KEISAN_KIKAN     KGKK,"    '外部委託計算期間
    strSql = strSql & "         KYK_CODE_VALUE          KCV1,"    'コード値(RAFI基礎数値種類)
    strSql = strSql & "         KYK_CODE_VALUE          KCV2,"    'コード値(外部委託計算方法)
    strSql = strSql & "         KYK_RATE                KR1,"     'レート(料率コード)
    strSql = strSql & "         KYK_RATE                KR2"      'レート(運用報酬料率コード)
    strSql = strSql & "     WHERE"
    strSql = strSql & "         KGSK.GI_NO = KGKK.GI_NO"
    strSql = strSql & "     AND KGSK.GI_SHIHARAI_KIKAN_ID     = KGKK.GI_SHIHARAI_KIKAN_ID"
    strSql = strSql & "     AND KGSK.SAKUJO_FLG               = '0'"
    strSql = strSql & "     AND KGKK.SAKUJO_FLG               = '0'"
    strSql = strSql & "     AND KGSK.GI_NO                    = '" & Gi_no & "'"
    strSql = strSql & "     AND KGKK.KOHZA_NO                 = '" & Format(Kouza_no, "0000000") & "'"
    strSql = strSql & "     AND KGSK.GI_SHIHARAI_KIKAN_FROM   <= " & Kijyunbi
    strSql = strSql & "     AND KGSK.GI_SHIHARAI_KIKAN_TO     >= " & Kijyunbi
    strSql = strSql & "     AND KCV1.CODE_NO               (+)= '" & CODENO_RAFI_KISO_SUCHI_SHURUI & "'"    'RAFI基礎数値種類名
    strSql = strSql & "     AND '1'                           = KCV1.CODE_VALUE(+)"
    strSql = strSql & "     AND KCV1.SAKUJO_FLG            (+)= '0'"
    strSql = strSql & "     AND KCV2.CODE_NO               (+)= '" & CODENO_KEISAN_HOHHOH & "'"             '外部委託計算方法名
    strSql = strSql & "     AND KGKK.GI_KEISAN_HOHHOH         = KCV2.CODE_VALUE(+)"
    strSql = strSql & "     AND KCV2.SAKUJO_FLG            (+)= '0'"
    strSql = strSql & "     AND KGKK.RATE_CODE                = KR1.RATE_CODE(+)"                           '料率コード名
    strSql = strSql & "     AND KR1.SAKUJO_FLG             (+)= '0'"
    strSql = strSql & "     AND KGKK.UNYOH_HOHSHU_RATE_CODE   = KR2.RATE_CODE(+)"                           '運用報酬料率コード名
    strSql = strSql & "     AND KR2.SAKUJO_FLG             (+)= '0'"
    strSql = strSql & "     UNION"
    strSql = strSql & "     SELECT"
    strSql = strSql & "         KGKK.ISSCD                                             ISSCD,"
    strSql = strSql & "         KGKK.GI_KEISAN_KIKAN_FROM                              GI_KEISAN_KIKAN_FROM,"
    strSql = strSql & "         KGKK.GI_KEISAN_KIKAN_TO                                GI_KEISAN_KIKAN_TO,"
    strSql = strSql & "         '2:' || KCV1.VALUE_NAME                                KISO_SUCHI_SHURUI,"
    strSql = strSql & "         DECODE(TRIM(KGKK.GI_KEISAN_HOHHOH), NULL, NULL, KGKK.GI_KEISAN_HOHHOH || ':' || KCV2.VALUE_NAME)        GI_KEISAN_HOHHOH,"
    strSql = strSql & "         DECODE(TRIM(KGKK.RATE_CODE), NULL, NULL, KGKK.RATE_CODE || ':' || KR1.RATE_NAME)                 RATE_NAME,"
    strSql = strSql & "         DECODE(TRIM(KGKK.UNYOH_HOHSHU_RATE_CODE), NULL, NULL, KGKK.UNYOH_HOHSHU_RATE_CODE || ':' || KR2.RATE_NAME)    UNYOH_HOHSHU_RATE_NAME,"
    strSql = strSql & "         KGSK.GI_SHIHARAI_KIKAN_ID                              GI_SHIHARAI_KIKAN_ID,"
    strSql = strSql & "         KGSK.GI_SHIHARAI_KIKAN_TO                              GI_SHIHARAI_KIKAN_TO,"
    strSql = strSql & "         KGKK.GI_KEISAN_KIKAN_ID                                GI_KEISAN_KIKAN_ID"
    strSql = strSql & "     FROM"
    strSql = strSql & "         KYK_GI_SHIHARAI_KIKAN   KGSK,"    '外部委託支払期間
    strSql = strSql & "         KYK_GI_KEISAN_KIKAN     KGKK,"    '外部委託計算期間
    strSql = strSql & "         KYK_CODE_VALUE          KCV1,"    'コード値(RAFI基礎数値種類)
    strSql = strSql & "         KYK_CODE_VALUE          KCV2,"    'コード値(外部委託計算方法)
    strSql = strSql & "         KYK_RATE                KR1,"     'レート(料率コード)
    strSql = strSql & "         KYK_RATE                KR2"      'レート(運用報酬料率コード)
    strSql = strSql & "     WHERE"
    strSql = strSql & "         KGSK.GI_NO = KGKK.GI_NO"
    strSql = strSql & "     AND KGSK.GI_SHIHARAI_KIKAN_ID     = KGKK.GI_SHIHARAI_KIKAN_ID"
    strSql = strSql & "     AND KGSK.SAKUJO_FLG               = '0'"
    strSql = strSql & "     AND KGKK.SAKUJO_FLG               = '0'"
    strSql = strSql & "     AND KGSK.GI_NO                    = '" & Gi_no & "'"
    strSql = strSql & "     AND KGKK.KOHZA_NO                 = '" & Format(Kouza_no, "0000000") & "'"
    strSql = strSql & "     AND KGSK.GI_SHIHARAI_KIKAN_FROM   <= " & Kijyunbi
    strSql = strSql & "     AND KGSK.GI_SHIHARAI_KIKAN_TO     >= " & Kijyunbi
    strSql = strSql & "     AND KGKK.GI_KEISAN_HOHHOH         IN ('1','3')"                                 '外部委託計算方法 = バランス型
    strSql = strSql & "     AND KCV1.CODE_NO               (+)= '" & CODENO_RAFI_KISO_SUCHI_SHURUI & "'"    'RAFI基礎数値種類名
    strSql = strSql & "     AND '2'                           = KCV1.CODE_VALUE(+)"
    strSql = strSql & "     AND KCV1.SAKUJO_FLG            (+)= '0'"
    strSql = strSql & "     AND KCV2.CODE_NO               (+)= '" & CODENO_KEISAN_HOHHOH & "'"             '外部委託計算方法名
    strSql = strSql & "     AND KGKK.GI_KEISAN_HOHHOH         = KCV2.CODE_VALUE(+)"
    strSql = strSql & "     AND KCV2.SAKUJO_FLG            (+)= '0'"
    strSql = strSql & "     AND KGKK.RATE_CODE                = KR1.RATE_CODE(+)"                           '料率コード名
    strSql = strSql & "     AND KR1.SAKUJO_FLG             (+)= '0'"
    strSql = strSql & "     AND KGKK.UNYOH_HOHSHU_RATE_CODE   = KR2.RATE_CODE(+)"                           '運用報酬料率コード名
    strSql = strSql & "     AND KR2.SAKUJO_FLG             (+)= '0'"
    strSql = strSql & " )"
    strSql = strSql & " ORDER BY"
    strSql = strSql & "     ISSCD,"
    strSql = strSql & "     GI_KEISAN_KIKAN_FROM,"
    strSql = strSql & "     KISO_SUCHI_SHURUI"
    
    blnRet = fncKsCmnDoExequteSql(mainmenu.gcSELECT, strSql, vntRec, lngCol, lngRow, strErrCd)
    If blnRet = False Then
        fncKumiireRead = -1
        Exit Function
    End If
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
ErrHandler:
    fncKumiireRead = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function


