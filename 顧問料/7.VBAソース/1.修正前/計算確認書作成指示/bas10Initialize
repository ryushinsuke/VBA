Attribute VB_Name = "bas10Initialize"
Option Explicit
'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー算出システム・計算確認書作成指示
'
'オブジェクト名：bas10Initialize
'
'機能概要　：初期処理
'
'更新履歴　：2008/06/30 SRA 東       新規作成
'
'*************************************************************************
Const MODULE_NAME As String = "bas10Initialize"


'*************************************************************************
'関数名　　：画面の初期化メイン
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：初期起動時に行われる初期処理のメイン
'
'更新履歴　：2008/06/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub g_subKakuninsyoInitialize()
On Error GoTo ErrHandler

    Const PROCEDURE_NAME As String = "g_subKakuninsyoInitialize"


    '画面の保護解除
    Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), False)
    
    With ThisWorkbook.Worksheets(MAIN_SHEETNAME)
        .chkCalc.Value = False
        .chkPrint.Value = True
        .Range("外部委託先").Value = ""
        .Range("送付日").Value = ""
        .Range("保存フォルダ").Value = "C:\EXT\KEIYAKU2\FILE_NEW"
        .cmdExecute.Enabled = True
        .cmdRef.Enabled = True
    End With

    'エラーシートの非表示
    Call gCom01_BookProtect(ThisWorkbook, False)
    ThisWorkbook.Worksheets(ERROR_SHEETNAME).Visible = False
    Call gCom01_BookProtect(ThisWorkbook, True)
    
    'コンボ項目の初期化
    Call sub_SakuseiCombo
    
    'シートの保護
    Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), True)
    
Exit Sub
ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
    
    'シートの保護
    Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), True)
    
'@    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Sub


'*************************************************************************
'関数名　　：コンボ項目の初期化
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：初期起動時に行われる初期処理のメイン
'
'更新履歴　：2008/06/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Private Sub sub_SakuseiCombo()
On Error GoTo ErrHandler

    Const PROCEDURE_NAME    As String = "sub_SakuseiCombo"
    Dim strErrMsg           As String
    
    strErrMsg = ""

    '外部委託先の一覧データ取得
    If fucGetGaibuitakusaki(strErrMsg) = False Then
        Call gfunc_ErrorMsg(3, 20010, "外部委託先リスト", "(" & strErrMsg & ")")
        Exit Sub
    Else
        '外部委託先リストの名前定義
        Call subNameDefine
    End If
    
Exit Sub

ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
    
'@    Err.Raise lng
'---- Error Log End   ----------------------------------------------------


End Sub


'*************************************************************************
'関数名　　：外部委託先の一覧取得
'
'引　　数　：無し
'
'戻り値　　：Boolean
'
'機能説明　：からコンボ項目を取得し「data」シートに記入
'
'更新履歴　：2008/06/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Private Function fucGetGaibuitakusaki(ByRef strErrMsg As String) As Boolean
On Error GoTo ErrHandler

    Const PROCEDURE_NAME    As String = "fucGetGaibuitakusaki"
    Dim strSql              As String
    Dim vdata()             As Variant
    Dim lngCol              As Long
    Dim lngRow              As Long
    Dim lngCnt              As Long
    
    fucGetGaibuitakusaki = False
    
    'コード値テーブルの抽出
    strSql = ""
    strSql = strSql & " SELECT B.CODE_VALUE||'：'||B.VALUE_NAME GAIBU_ITAKUSAKI"
    strSql = strSql & "   FROM KYK_CODE A LEFT JOIN KYK_CODE_VALUE B ON A.CODE_NO = B.CODE_NO"
    strSql = strSql & "  WHERE A.CODE_NO    = '56'"
    strSql = strSql & "    AND A.SAKUJO_FLG = '0'"
    strSql = strSql & "    AND B.SAKUJO_FLG = '0'"
    strSql = strSql & "  ORDER BY B.CODE_VALUE"
    
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vdata, lngCol, lngRow)
    
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Activate
    
    '「外部委託先リスト」のデータセル範囲をクリア
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("A2:A65536").ClearContents
    
    '取得データをセルに設定
    For lngCnt = 0 To lngRow - 1
         ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(2 + lngCnt, 1).Value = vdata(0, lngCnt)
    Next lngCnt
    
    Erase vdata

    fucGetGaibuitakusaki = True
    
    Exit Function
ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
    Erase vdata
    
'@    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：外部委託先リストの名前定義設定
'
'引　　数　：無し
'
'戻り値　　：無し
'
'機能説明　：外部委託先リストの名前定義を再設定する。
'
'更新履歴　：2008/06/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Private Sub subNameDefine()
On Error GoTo ErrHandler

    Const PROCEDURE_NAME    As String = "subNameDefine"
    Dim RowCnt              As Long

    '*** 信託銀行リストの名前定義
    For RowCnt = 2 To 65536
        If ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(RowCnt, 1).Value = "" Then
            RowCnt = RowCnt - 1
            Exit For
        End If
    Next RowCnt
    
    If RowCnt < 2 Then
        RowCnt = 2
    End If
    
    '再定義
    ThisWorkbook.Names.Add Name:=DATA_GI_SAKI_LIST, _
     RefersToR1C1:="=" & DATA_SHEETNAME & "!R2C1:R" & RowCnt & "C1"

Exit Sub
ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
    
'@    Err.Raise lng
'---- Error Log End   ----------------------------------------------------


End Sub


