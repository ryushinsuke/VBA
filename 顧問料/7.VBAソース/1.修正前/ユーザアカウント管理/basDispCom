Attribute VB_Name = "basDispCom"
'=========================================================================
'プロジェクト　　　：国内顧問料ユーザアカウント管理
'プログラム名　　　：画面編集用の共通関数モジュール
'プログラムＩＤ　　：basDispCom
'機能概要　　　　　：画面項目設定及び入力項目のチェック、編集機能
'
'日付　　　　　　　作成者　　　　　　　　　　修正内容
'-------------------------------------------------------------------------
'2008/05/14       K.Furuya<SRA>　　　　　　新規作成
'
'=========================================================================
Option Explicit

'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gstrDateFormat
'機能            :   日付を変換　(iFmtType = 1 ; YYYY/MM/DDに変換
'                                iFmtType = 2 ; YYYY/MMに変換
'                                iFmtType = 3 ; MM/DDに変換
'　　　　　　　　　　　　　　　　　）
'　　　　　　　　　　IsNumeric(sDate) = True の場合、"/"を編集
'                   変換できない場合はそのまま返す
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/23         K.Furuya<SRA>       新規作成
'===============================================================================
Public Function gstrDateFormat( _
    sDate As String, _
    iFmtType As Integer _
)
    
On Error Resume Next
    
    Dim sDateW As String

    gstrDateFormat = sDate
    
    '初期設定
    If (Len(sDate) = 0) Or (InStr(sDate, "/") <> 0) Then
        Exit Function
    End If
    
    '編集
    Select Case iFmtType%
        Case 1                                     'YYYY/MM/DDに変換
            If Len(sDate) = 8 Then
                gstrDateFormat = Mid(sDate, 1, 4) & "/" & Mid(sDate, 5, 2) & "/" & Mid(sDate, 7, 2)
            End If
        Case 2                                     'YYYY/MMに変換
            If Len(sDate) = 6 Then
                gstrDateFormat = Mid(sDate, 1, 4) & "/" & Mid(sDate, 5, 2)
            End If
        Case 3                                     'MM/DDに変換
            If Len(sDate) = 4 Then
                gstrDateFormat = Mid(sDate, 1, 2) & "/" & Mid(sDate, 3, 2)
            End If
    End Select

    
On Error GoTo 0

End Function



'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gfncGetMsgText
'機能            :   メッセージ画面用メッセージ取得関数
'パラメータ      ：   1．メッセージ番号
'
'戻り値　　      ：   取得メッセージ
'
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/26         K.Furuya<SRA>       新規作成
'===============================================================================
Public Function gfncGetMsgText(ByVal intMsgNo As Integer) As String
   Dim strMsgGet        As String
   Dim strMsgSet        As String
   
    'セルの関数を設定する
    strMsgSet = "=Lookup(" & intMsgNo & ",RC[-25]:R[65000]C[-25],RC[-24]:R[65000]C[-24])"
    
    '非表示の"MSG"シートに書かれている情報を読み取り、メッセージを取得する
    With ThisWorkbook.Sheets("MSG")
        .Cells(1, 26).FormulaR1C1 = strMsgSet
        strMsgGet = .Range("Z1").Value
    End With

    gfncGetMsgText = strMsgGet

End Function
'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gfncGetMsgParam
'機能            :   メッセージ画面用ボタン、アイコン、デフォルトボタン取得関数
'パラメータ      ：   1．ボタン番号、2．アイコン番号、デフォルトボタン番号(オプション)
'
'戻り値　　      ：   各定数の合計値
'
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/26         K.Furuya<SRA>       新規作成
'===============================================================================
Public Function gfncGetMsgParam( _
                                ByVal intParam1 As Integer, _
                                ByVal intParam2 As Integer, _
                                Optional intParam3 As Integer = 999 _
                                ) As Long
    Dim RetParam            As Long
    
    'ボタン定数を取得する
    Select Case intParam1
        Case 1   '[OK]ボタンのみを表示
            RetParam = vbOKOnly
        Case 2   '[OK]ボタンと[キャンセル]ボタンを表示
            RetParam = vbOKCancel
        Case 3   '[中止]、[再試行]、および[無視]の3つのボタンを表示
            RetParam = vbAbortRetryIgnore
        Case 4   '[はい]、[いいえ]、および[キャンセル]の3つのボタンを表示
            RetParam = vbYesNoCancel
        Case 5   '[はい]ボタンと[いいえ]ボタンを表示
            RetParam = vbYesNo
        Case 6   '[再試行]ボタンと[キャンセル]ボタンを表示
            RetParam = vbRetryCancel
    End Select
    'アイコン定数を取得する
    Select Case intParam2
        Case 1   '警告メッセージアイコンを表示
            RetParam = RetParam + vbCritical
        Case 2   '問い合わせメッセージアイコンを表示
            RetParam = RetParam + vbQuestion
        Case 3   '注意メッセージアイコンを表示
            RetParam = RetParam + vbExclamation
        Case 4   '情報メッセージアイコンを表示
            RetParam = RetParam + vbInformation
    End Select
    'デフォルトボタン定数を取得する
    Select Case intParam3
        Case 1   '第1ボタンを標準ボタンにする
            RetParam = RetParam + vbDefaultButton1
        Case 2   '第2ボタンを標準ボタンにする
            RetParam = RetParam + vbDefaultButton2
        Case 3   '第3ボタンを標準ボタンにする
            RetParam = RetParam + vbDefaultButton3
        Case 4   '第4ボタンを標準ボタンにする
            RetParam = RetParam + vbDefaultButton4
        Case Else
            'デフォルトボタンを省略
    End Select
    
    gfncGetMsgParam = RetParam
    
End Function


'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gfncGetMsgTitle
'機能            :   メッセージ画面用タイトル取得関数
'パラメータ      ：   1．タイトル番号
'
'戻り値　　      ：   取得タイトル
'
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/26         K.Furuya<SRA>       新規作成
'===============================================================================
Public Function gfncGetMsgTitle(ByVal intTitleNo As Integer) As String
   Dim strTitleGet        As String
   Dim strTitleSet        As String
   
    'セルの関数を設定する
    strTitleSet = "=Lookup(" & intTitleNo & ",RC[-25]:R[65000]C[-25],RC[-24]:R[65000]C[-24])"
    
    '非表示の"MSG"シートに書かれている情報を読み取り、メッセージを取得する
    With ThisWorkbook.Sheets("MSG")
        .Cells(1, 26).FormulaR1C1 = strTitleSet
        strTitleGet = .Range("Z1").Value
    End With

    gfncGetMsgTitle = strTitleGet

End Function
