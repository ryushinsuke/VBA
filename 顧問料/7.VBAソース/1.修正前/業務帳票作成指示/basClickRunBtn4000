Attribute VB_Name = "basClickRunBtn4000"
Option Explicit

'*************************************************************************
'プロジェクト名：新契約管理システム・業務帳票作成指示
'オブジェクト名：basClickRunBtn4000
'機能概要　：統計情報 顧問料算定基準一覧
'更新履歴　：2007/11/16 SRA 安保     新規作成
'        　：2008/02/06 SRA 安保     PH4総合テスト障害一覧 項番24対応
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn4000"

Private Const cBaseFile = gc帳票Base40
Private Const cPreFName = gc帳票名40

'*************************************************************************
'関数名　　：統計情報 顧問料算定基準一覧(帳票作成)
'引　　数　：
'           objTarget        I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2007/11/16 SRA             新規作成
'更新履歴　：2007/12/04 SRA Y.Azuma    変更     (PH4)印刷エラー対策
'
'*************************************************************************
Public Function fncClickRunBtn4000(ByRef objTarget As Object) As Boolean
    
    Const PROCEDURE_NAME As String = "fncClickRunBtn4000"

On Error GoTo ErrSection

    Dim strDay          As String
    Dim strOutFName     As String
    Dim strOutFNameOnly As String
    
    Dim inData()        As Variant              '顧問料算定基準一覧 データ取得用
    
    Dim strKjnLastYmd   As String               '基準月末日
    Dim wDate           As Date                 '日付編集用WORK
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    fncClickRunBtn4000 = False
    With objTarget

        'マウスポインターをWAITにする
        ThisWorkbook.Application.Cursor = xlWait

        ' 作成選択の場合
        If .chkMake.Value = True Then
        
            strDay = Trim(.Range("基準年月4000").Value)

            '基準年月の月末日取得
            wDate = Mid(strDay, 1, 4) & "/" & Mid(strDay, 5, 2) & "/01"
            wDate = DateAdd("m", 1, wDate)
            wDate = DateAdd("d", -1, wDate)
            strKjnLastYmd = Format(wDate, "YYYYMMDD")
            
            ' データ取得
            If fncGetDBData4000(strKjnLastYmd, inData) = False Then
                GoTo RtnErrSection
            End If
        
            ' 帳票ファイル取得
            If fncGetFile4000(strDay, strOutFName) = False Then
                GoTo RtnErrSection
            End If
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            strOutFNameOnly = strOutFName
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        
            ' フォルダをつける。
            strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName
            
            ' ファイルコピー
            If fncCopyBook(cBaseFile, strOutFName) = False Then
                GoTo RtnErrSection
            End If
            
            ' ファイル作成
            If fncMakeBook4000(strOutFName, strKjnLastYmd, inData()) = False Then
                ' ファイル削除
                Call fncDeleteFile(strOutFName)

                GoTo RtnErrSection
            End If

'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            If .chkPrint.Value = True Then
                Call ToukeiChouhyouPrint(strOutFNameOnly)
            End If
            
            Workbooks(strOutFNameOnly).Close SaveChanges:=True
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        
        End If
    
        ' 印刷選択の場合
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        If .chkMake.Value = False And .chkPrint.Value = True Then
'        If .chkPrint.Value = True Then
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
    'マウスポインターを戻す
    ThisWorkbook.Application.Cursor = xlDefault
    
    End With
   
    fncClickRunBtn4000 = True
    Erase inData
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                     *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション                              *
'*******************************************************
RtnErrSection:
                
    'マウスポインターを戻す
    ThisWorkbook.Application.Cursor = xlDefault
                
    fncClickRunBtn4000 = False
    Erase inData
    
End Function

'*************************************************************************
'関数名　　：統計情報 顧問料算定基準一覧(帳票ファイル取得)
'引　　数　：
'           strDay             I           基準日
'           strOutFName        O           出力ファイル名
'戻り値　　：処理結果
'機能説明　：顧問料算定基準一覧を出力するファイルを取得する。
'更新履歴　：2007/11/16 SRA             新規作成
'
'*************************************************************************
Public Function fncGetFile4000(ByVal strDay As String, _
                               ByRef strOutFName As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile4000"

On Error GoTo ErrSection

    Dim strDate         As String
    Dim strTime         As String

    strOutFName = ""
    
    fncGetFile4000 = False
    
    Call gfunc_GetSvDateTime(strDate, strTime)
       
    ' 出力帳票ファイル
    strOutFName = cPreFName
    strOutFName = strOutFName & "_" & strDate
    strOutFName = strOutFName & "_" & strTime
    strOutFName = strOutFName & "-(" & strDay
    strOutFName = strOutFName & ")"
    strOutFName = strOutFName & ".xls"
    
    fncGetFile4000 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetFile4000 = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：統計情報 顧問料算定基準一覧(帳票データ取得)
'引　　数　：
'           strKjnLastYmd      I           基準年月(YYYYMMDD)
'           inData()　         O           明細取得データ
'戻り値　　：処理結果
'機能説明　：顧問料算定基準一覧のデータを取得する。
'
'*************************************************************************
Public Function fncGetDBData4000( _
    ByVal strKjnLastYmd As String, _
    ByRef inData() As Variant _
) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetDBData4000"

On Error GoTo ErrSection

    Dim blnRet      As Boolean
    
    fncGetDBData4000 = False
    
    blnRet = False
    
    '顧問料算定基準一覧データ取得
    blnRet = fncGetKomonryohSanteiKijyunData(strKjnLastYmd, inData())
    If blnRet <> True Then
        Exit Function
    End If
    
    fncGetDBData4000 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetDBData4000 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

End Function


'*************************************************************************
'関数名　　：顧問料算定基準一覧 明細データ取得
'引　　数　：
'           strLastDay         I           基準年月末日(YYYYMMDD)
'           inData()           O           取得データ
'戻り値　　：処理結果
'機能説明　：顧問料算定基準一覧のデータを取得する
'更新履歴　：2007/11/16 SRA H.Anpo   新規
'
'*************************************************************************
Public Function fncGetKomonryohSanteiKijyunData( _
    ByVal strKjnLastYmd As String, _
    ByRef inData() As Variant _
    ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetKomonryohSanteiKijyunData"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    Dim lngColCnt   As Long
    
    fncGetKomonryohSanteiKijyunData = False

'*******************************************************
'* SQL文作成                                           *
'*******************************************************
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "         KVK.PRTCD                       PRTCD,"                     '口座番号
    strSql = strSql & "         TRIM(KVK.NAME_RJ)               NAME_RJ,"                   '口座略称
    strSql = strSql & "         TRIM(V301.NAME_J)               SINTAKU_BANK_CD,"           '信託銀行名
    strSql = strSql & "         KVK.SINTAKU_BANK_PRTCD          SINTAKU_BANK_PRTCD,"        '信託銀行口座番号
    strSql = strSql & "         KCV1.VALUE_NAME                 HEIZAN_DANMEN_KBN,"         '平残断面区分
    strSql = strSql & "         KCV2.VALUE_NAME                 MAE_ATO_BARAI_KBN,"         '前払い後払区分
    strSql = strSql & "         KCV3.VALUE_NAME                 ZEROEN_SEIKYUSHO_KBN,"      '0円請求書区分
    strSql = strSql & "         KCV4.VALUE_NAME                 KEISAN_HOHHOH,"             '計算方法
    strSql = strSql & "         KRT.RATE_CODE                   RATE_CODE,"                 '顧問料率コード
    strSql = strSql & "         TRIM(KR.RATE_NAME)              RATE_NAME,"                 '顧問料率名
    strSql = strSql & "         KCV5.VALUE_NAME                 PX_SHINTAKU_KBN,"           '基礎数値種類
    strSql = strSql & "         KCV6.VALUE_NAME                 KISO_SUCHI_KEISAN_HOHHOH"   '基礎数値計算方法
    strSql = strSql & " FROM"
    strSql = strSql & "         KYK_VW_KEIEI_CHOUHYOU           KVK,"                       '経営帳票VIEW
    strSql = strSql & "         KYK_SEIKYU_KIKAN                KSK,"                       '顧問料請求期間
    strSql = strSql & "         KYK_CODE_VALUE                  KCV1,"                      'コード値(平残断面区分 用)
    strSql = strSql & "         KYK_CODE_VALUE                  KCV2,"                      'コード値(前払い後払区分 用)
    strSql = strSql & "         KYK_CODE_VALUE                  KCV3,"                      'コード値(0円請求書区分 用)
    strSql = strSql & "         KYK_CODE_VALUE                  KCV4,"                      'コード値(計算方法 用)
    strSql = strSql & "         KYK_CODE_VALUE                  KCV5,"                      'コード値(基礎数値種類 用)
    strSql = strSql & "         KYK_CODE_VALUE                  KCV6,"                      'コード値(基礎数値計算方法 用)
    strSql = strSql & "         KYK_RATE                        KR,"                        '顧問料率
    strSql = strSql & "         VW_DT301D                       V301,"                      'コードブック
    strSql = strSql & "         ("
    strSql = strSql & "             SELECT  DISTINCT"
    strSql = strSql & "                     KRT2.KOHZA_NO                                       KOHZA_NO,"
    strSql = strSql & "                     KRT2.SEIKYU_KIKAN_ID                                SEIKYU_KIKAN_ID,"
    strSql = strSql & "                     KRT2.KEISAN_HOHHOH                                  KEISAN_HOHHOH,"
    strSql = strSql & "                     DECODE(KRT2.KEISAN_HOHHOH,'1',KRT2.RATE_CODE,'')    RATE_CODE,"
    strSql = strSql & "                     KRT2.PX_SHINTAKU_KBN                                PX_SHINTAKU_KBN,"
    strSql = strSql & "                     KRT2.KISO_SUCHI_KEISAN_HOHHOH                       KISO_SUCHI_KEISAN_HOHHOH,"
    strSql = strSql & "                     KRT2.RATE_TEKIYOH_KIKAN_FROM                        RATE_TEKIYOH_KIKAN_FROM"
    strSql = strSql & "             FROM"
    strSql = strSql & "                     KYK_RATE_TEKIYOH_KIKAN  KRT2"                   '料率適用期間
    strSql = strSql & "             WHERE"
    strSql = strSql & "                     KRT2.SHISAN_BUNRUI              = '0'"
    strSql = strSql & "             AND     KRT2.SAKUJO_FLG                 = '0'"
    strSql = strSql & "             AND     KRT2.RATE_TEKIYOH_KIKAN_FROM   <= '" & strKjnLastYmd & "'"
    strSql = strSql & "             AND     KRT2.RATE_TEKIYOH_KIKAN_TO     >= '" & strKjnLastYmd & "'"
    strSql = strSql & "         )                               KRT"
    strSql = strSql & " WHERE"
    strSql = strSql & "         KVK.PRTCD                       = KSK.KOHZA_NO"
    strSql = strSql & " AND     KVK.PRTCD                       = KRT.KOHZA_NO"
    strSql = strSql & " AND     KSK.SEIKYU_KIKAN_ID             = KRT.SEIKYU_KIKAN_ID"
    strSql = strSql & " AND     KSK.SAKUJO_FLG                  = '0'"
    strSql = strSql & " AND     KSK.SEIKYU_KIKAN_FROM          <= '" & strKjnLastYmd & "'"
    strSql = strSql & " AND     KSK.SEIKYU_KIKAN_TO            >= '" & strKjnLastYmd & "'"
'2008/02/06 Modified By H.Anpo Start
'PH4総合テスト障害表項番24対応
'    strSql = strSql & " AND     TRIM(KVK.SINTAKU_BANK_CD)       = TRIM(V301.CD_1)"
'    strSql = strSql & " AND     V301.CD_KBN                     = '15'"
    strSql = strSql & " AND     KVK.SINTAKU_BANK_CD       = V301.CD_1(+)"
    strSql = strSql & " AND     V301.CD_KBN                  (+)= '15'"
'2008/02/06 Modified By H.Anpo Start
    strSql = strSql & " AND     KRT.RATE_CODE                   = KR.RATE_CODE(+)"
    strSql = strSql & " AND     KSK.HEIZAN_DANMEN_KBN           = KCV1.CODE_VALUE (+)"
    strSql = strSql & " AND     KCV1.CODE_NO                 (+)= '30'"                     'コード値(平残断面区分 用)
    strSql = strSql & " AND     KCV1.SAKUJO_FLG              (+)= '0'"
    strSql = strSql & " AND     KSK.MAE_ATO_BARAI_KBN           = KCV2.CODE_VALUE (+)"
    strSql = strSql & " AND     KCV2.CODE_NO                 (+)= '31'"                     'コード値(前払い後払区分 用)
    strSql = strSql & " AND     KCV2.SAKUJO_FLG              (+)= '0'"
    strSql = strSql & " AND     KSK.ZEROEN_SEIKYUSHO_KBN        = KCV3.CODE_VALUE (+)"
    strSql = strSql & " AND     KCV3.CODE_NO                 (+)= '32'"                     'コード値(0円請求書区分 用)
    strSql = strSql & " AND     KCV3.SAKUJO_FLG              (+)= '0'"
    strSql = strSql & " AND     KRT.KEISAN_HOHHOH               = KCV4.CODE_VALUE (+)"
    strSql = strSql & " AND     KCV4.CODE_NO                 (+)= '8'"                      'コード値(計算方法 用)
    strSql = strSql & " AND     KCV4.SAKUJO_FLG              (+)= '0'"
    strSql = strSql & " AND     KRT.PX_SHINTAKU_KBN             = KCV5.CODE_VALUE (+)"
    strSql = strSql & " AND     KCV5.CODE_NO                 (+)= '6'"                      'コード値(基礎数値種類 用)
    strSql = strSql & " AND     KCV5.SAKUJO_FLG              (+)= '0'"
    strSql = strSql & " AND     KRT.KISO_SUCHI_KEISAN_HOHHOH    = KCV6.CODE_VALUE (+)"
    strSql = strSql & " AND     KCV6.CODE_NO                 (+)= '2'"                      'コード値(基礎数値計算方法 用)
    strSql = strSql & " AND     KCV6.SAKUJO_FLG              (+)= '0'"
    strSql = strSql & " AND     KVK.KIJYUN_YMD                  = '" & strKjnLastYmd & "'"  '基準日(月末日)
    strSql = strSql & " AND     KVK.SETTEI_KBN                  = '1'"                      '口座開設区分 1:国内本開設
    strSql = strSql & " ORDER BY"
    strSql = strSql & "         KVK.PRTCD,"
    strSql = strSql & "         KRT.RATE_TEKIYOH_KIKAN_FROM"
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, inData, lngCol, lngRow)
    If lngRow > 0 Then
        fncGetKomonryohSanteiKijyunData = True
    Else
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "")
    End If
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetKomonryohSanteiKijyunData = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
End Function


'*************************************************************************
'関数名　　：統計情報 顧問料算定基準一覧(帳票作成)
'引　　数　：
'           strOutFName        I           出力ファイル名
'           strKjnLastYmd      I           基準日(月末日)
'           inData()　         I           顧問料算定基準一覧データ
'戻り値　　：処理結果
'機能説明　：統計情報 顧問料算定基準一覧のBOOKを作成する。
'更新履歴　：2007/12/04 SRA Y.Azuma    変更     (PH4)印刷エラー対策
'
'*************************************************************************
Public Function fncMakeBook4000( _
    ByVal strOutFName As String, _
    ByVal strKjnLastYmd As String, _
    ByRef inData() As Variant _
    ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeBook4000"
    
On Error GoTo ErrSection

    Dim lngRowCnt       As Long
    Dim strDesRange     As String
    Dim vntArray2()     As Variant
    Dim lngRet          As Long
    Dim lngStartRowIchiran  As Long
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    fncMakeBook4000 = False
    
Application.DisplayAlerts = False
    
    '============================================
    ' 顧問料算定基準一覧
    '============================================
    '配列の添え字の階層を変更 array(X,Y) -> array(Y,X)
    lngRet = fncKsCmnArrayIdxChange(inData, vntArray2)
    
    lngRowCnt = UBound(inData, 2)
    
    With Workbooks(Workbooks.Count).Worksheets("顧問料算定基準一覧")
    
        'ヘッダー部の編集
        '基準日
        .Range("基準日").Value = strKjnLastYmd
    
        '明細部の編集
        lngStartRowIchiran = .Range("一覧_明細TOP").Row
        strDesRange = .Cells(lngStartRowIchiran, .Range("一覧_明細TOP").Column).Address & ":" & .Cells(lngStartRowIchiran + lngRowCnt, .Range("一覧_終了列").Column).Address
    
        '行を追加
        .Rows(lngStartRowIchiran & ":" & lngStartRowIchiran).Copy
        .Rows(lngStartRowIchiran & ":" & lngStartRowIchiran + lngRowCnt).Insert Shift:=xlDown
        'シートにデータをセット
        .Range(strDesRange) = vntArray2

        '不要な行を削除
        .Range(lngStartRowIchiran + lngRowCnt + 1 & ":" & lngStartRowIchiran + lngRowCnt + 1).Delete

        'カーソル位置を移動する。
        .Activate     ' シートが２個以上の場合要る。
        .Range("A1").Activate
    End With
    
    '式を値に変換
    fncForMulaToValueInAllSheets Workbooks(Workbooks.Count)
    
    ActiveWindow.ScrollRow = 1
    ActiveWindow.ScrollColumn = 1
    
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
    Workbooks(Workbooks.Count).Save
'    Workbooks(Workbooks.Count).Close SaveChanges:=True
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
    
Application.DisplayAlerts = True
    
    fncMakeBook4000 = True
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Erase vntArray2
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

Application.DisplayAlerts = True

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook4000 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
    Erase vntArray2
   
End Function






