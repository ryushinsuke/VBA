Attribute VB_Name = "basClickRunBtn4500"
Option Explicit

'*************************************************************************
'プロジェクト名：新契約管理システム・業務帳票作成指示
'オブジェクト名：basClickRunBtn4500
'機能概要　：統計情報 専務会報告 残高推移
'更新履歴　：2007/11/05 SRA 安保     新規作成
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn4500"

Private Const cBaseFile = gc帳票Base45
Private Const cPreFName = gc帳票名45

'取得する基準日の数
Private Const cNenYmdCnt = 5                        '年比較用       5ヶ月
Private Const c4hankiYmdCnt = 6                     '4半期比較用    6ヶ月
Private Const cYmdCnt = cNenYmdCnt + c4hankiYmdCnt  '年 + 4半期     11ヶ月

'取得データ構造体
Private Type typIndata
    KijyunYmd           As Variant  '基準日(月末日：YYYYMMDD)
    MeisaiCnt           As Long     '明細レコード数
    Meisai()            As Variant  '明細
End Type

'*************************************************************************
'関数名　　：統計情報 専務会報告(帳票作成)
'引　　数　：
'           objTarget        I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2007/11/05 SRA             新規作成
'更新履歴　：2007/12/04 SRA Y.Azuma    変更     (PH4)印刷エラー対策
'
'*************************************************************************
Public Function fncClickRunBtn4500(ByRef objTarget As Object) As Boolean
    
    Const PROCEDURE_NAME As String = "fncClickRunBtn4500"

On Error GoTo ErrSection

    Dim strDay          As String
    Dim strOutFName     As String
    Dim strOutFNameOnly As String
    
    Dim inData(cYmdCnt) As typIndata    'データ取得用配列
    Dim strKjnLastYmd   As String       '基準月末日
    Dim wDate           As Date         '日付編集用WORK
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    fncClickRunBtn4500 = False
   
    With objTarget

        'マウスポインターをWAITにする
        ThisWorkbook.Application.Cursor = xlWait

        ' 作成選択の場合
        If .chkMake.Value = True Then
        
            strDay = Trim(.Range("基準年月4500").Value)

            '基準年月の月末日取得
            wDate = Mid(strDay, 1, 4) & "/" & Mid(strDay, 5, 2) & "/01"
            wDate = DateAdd("m", 1, wDate)
            wDate = DateAdd("d", -1, wDate)
            strKjnLastYmd = Format(wDate, "YYYYMMDD")
            
            '対象の基準日を取得
            If fncGetKijyunbi4500(strKjnLastYmd, inData()) = False Then
                GoTo RtnErrSection
            End If
            
            ' データ取得
            If fncGetDBData4500(inData()) = False Then
                GoTo RtnErrSection
            End If

            ' 帳票ファイル取得
            If fncGetFile4500(strDay, strOutFName) = False Then
                GoTo RtnErrSection
            End If
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            strOutFNameOnly = strOutFName
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------

            ' フォルダをつける。
            strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName

            ' ファイルコピー
            If fncCopyBook(cBaseFile, strOutFName) = False Then
                GoTo RtnErrSection
            End If

            ' ファイル作成
            If fncMakeBook4500(strOutFName, strKjnLastYmd, inData()) = False Then
                ' ファイル削除
                Call fncDeleteFile(strOutFName)

                GoTo RtnErrSection
            End If

'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            If .chkPrint.Value = True Then
                Call ToukeiChouhyouPrint(strOutFNameOnly)
            End If
            
            Workbooks(strOutFNameOnly).Close SaveChanges:=True
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        
        End If
    
        ' 印刷選択の場合
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        If .chkMake.Value = False And .chkPrint.Value = True Then
'        If .chkPrint.Value = True Then
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
    'マウスポインターを戻す
    ThisWorkbook.Application.Cursor = xlDefault
    
    End With
   
    fncClickRunBtn4500 = True
    Erase inData
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                     *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション                              *
'*******************************************************
RtnErrSection:
                
    'マウスポインターを戻す
    ThisWorkbook.Application.Cursor = xlDefault
                
    fncClickRunBtn4500 = False
    Erase inData
    
End Function

'*************************************************************************
'関数名　　：統計情報 専務会報告(帳票ファイル取得)
'引　　数　：
'           strDay             I           基準日
'           strOutFName        O           出力ファイル名
'戻り値　　：処理結果
'機能説明　：残高推移を出力するファイルを取得する。
'更新履歴　：2007/10/11 SRA             新規作成
'
'*************************************************************************
Public Function fncGetFile4500(ByVal strDay As String, _
                               ByRef strOutFName As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile4500"

On Error GoTo ErrSection

    Dim strDate         As String
    Dim strTime         As String

    strOutFName = ""
    
    fncGetFile4500 = False
    
    Call gfunc_GetSvDateTime(strDate, strTime)
       
    ' 出力帳票ファイル
    strOutFName = cPreFName
    strOutFName = strOutFName & "_" & strDate
    strOutFName = strOutFName & "_" & strTime
    strOutFName = strOutFName & "-(" & strDay
    strOutFName = strOutFName & ")"
    strOutFName = strOutFName & ".xls"
    
    fncGetFile4500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetFile4500 = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：統計情報 専務会報告(対象基準日取得)
'引　　数　：
'           strKjnLastYmd      I           基準日の月末日(YYYYMMDD)
'           inData()           O           取得データ
'戻り値　　：処理結果
'機能説明　：inData構造体の基準日項目をセットする。
'
'*************************************************************************
Public Function fncGetKijyunbi4500( _
    ByVal strKjnLastYmd As String, _
    ByRef inData() As typIndata _
) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetKijyunbi4500"

On Error GoTo ErrSection

    Dim lngCnt      As Long
    Dim lngYear     As Long
    Dim wDate       As Date

    fncGetKijyunbi4500 = False
    
    '**************************
    '年比較用の基準日を取得
    '**************************
    '①過去5年の3月末を取得
    wDate = Mid(strKjnLastYmd, 1, 4) & "/" & Mid(strKjnLastYmd, 5, 2) & "/01"
    wDate = DateAdd("m", -1, wDate)
    '直近の3月末を取得
    Do While (Month(wDate) <> 3)
        wDate = DateAdd("m", -1, wDate)
    Loop
    lngYear = Year(wDate)
   
    '過去5年の3月末を取得
    For lngCnt = cNenYmdCnt To 1 Step -1
        inData(lngCnt).KijyunYmd = lngYear & "0331"
        lngYear = lngYear - 1
    Next lngCnt
    
    '②過去6ヶ月の4半期末日を取得(基準月も含む)
    wDate = Mid(strKjnLastYmd, 1, 4) & "/" & Mid(strKjnLastYmd, 5, 2) & "/01"
    '基準日の翌月の１日を取得
    wDate = DateAdd("m", 1, wDate)
    For lngCnt = c4hankiYmdCnt To 1 Step -1
        '翌月の１日から１日を引き、末日を取得
        inData(cNenYmdCnt + lngCnt).KijyunYmd = Format(DateAdd("d", -1, wDate), "YYYYMMDD")
        '３ヶ月前を取得
        wDate = DateAdd("m", -3, wDate)
    Next lngCnt
    
    fncGetKijyunbi4500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

End Function



'*************************************************************************
'関数名　　：統計情報 専務会報告(帳票データ取得)
'引　　数　：
'           strData()          O           取得データ
'戻り値　　：処理結果
'機能説明　：残高推移のデータを取得する。
'
'*************************************************************************
Public Function fncGetDBData4500( _
    ByRef inData() As typIndata _
) As Boolean

    Const PROCEDURE_NAME As String = "fncGetDBData4500"

On Error GoTo ErrSection

    Dim lngCnt      As Long
    Dim strWhere    As String
    Dim blnRet      As Boolean

    fncGetDBData4500 = False

    '基準日の検索条件文字列を編集
    strWhere = " IN ("
    For lngCnt = 1 To cYmdCnt
        If lngCnt > 1 Then
            strWhere = strWhere & ","
        End If
        strWhere = strWhere & "'" & inData(lngCnt).KijyunYmd & "'"
    Next lngCnt
    strWhere = strWhere & ")"
    
    'データ取得
    blnRet = fncGetZandakaSuiiData(strWhere, inData())
    If blnRet <> True Then
        Exit Function
    End If

    fncGetDBData4500 = True

    Exit Function

'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetDBData4500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

End Function


'*************************************************************************
'関数名　　：残高推移データ取得
'引　　数　：
'           strWhere           I           基準日のWHERE条件 " IN ('YYYYMMDD','YYYYMMDD',..)"
'           strData()          O           取得データ
'戻り値　　：処理結果
'機能説明　：残高推移のデータを取得する
'更新履歴　：2007/10/24 SRA H.Anpo   新規
'
'*************************************************************************
Public Function fncGetZandakaSuiiData(ByVal strWhere As String, ByRef inData() As typIndata) As Boolean

    Const PROCEDURE_NAME As String = "fncGetZandakaSuiiData"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    Dim lngColCnt   As Long

    Dim lngCnt      As Long
    Dim lngCnt2     As Long
    Dim lngCnt3     As Long

    fncGetZandakaSuiiData = False

'*******************************************************
'* SQL文作成                                           *
'*******************************************************

    strSql = ""
    strSql = strSql & " SELECT "
    strSql = strSql & "     KIJYUN_YMD,"                            '基準日
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KOKU_SONOTA',DATA_CNT,"
    strSql = strSql & "             'KOKU_NEN_KOU',DATA_CNT,"
    strSql = strSql & "             'KOKU_NEN_SI',DATA_CNT,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KOKU_ALL_CNT,"                        '国内_ALL_件数
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KOKU_NEN_KOU',DATA_CNT,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KOKU_NEN_KOU_CNT,"                    '国内_公的年金_件数
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KOKU_NEN_SI',DATA_CNT,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KOKU_NEN_SI_CNT,"                     '国内_私的年金_件数
    strSql = strSql & " "
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KOKU_SONOTA',JIKA,"
    strSql = strSql & "             'KOKU_NEN_KOU',JIKA,"
    strSql = strSql & "             'KOKU_NEN_SI',JIKA,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KOKU_ALL_JIKA,"                       '国内_ALL_時価
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KOKU_NEN_KOU',JIKA,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KOKU_NEN_KOU_JIKA,"                   '国内_公的年金_時価
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KOKU_NEN_SI',JIKA,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KOKU_NEN_SI_JIKA,"                    '国内_私的年金_時価
    strSql = strSql & " "
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KAI_SONOTA',DATA_CNT,"
    strSql = strSql & "             'KAI_NEN',DATA_CNT,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KAI_ALL_CNT,"                         '海外_ALL_件数
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KAI_NEN',DATA_CNT,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KAI_NEN_CNT,"                         '海外_年金_件数
    strSql = strSql & " "
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KAI_SONOTA',JIKA,"
    strSql = strSql & "             'KAI_NEN',JIKA,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KAI_ALL_JIKA,"                        '海外_ALL_時価
    strSql = strSql & "     SUM("
    strSql = strSql & "         DECODE(DATA_TYPE, "
    strSql = strSql & "             'KAI_NEN',JIKA,"
    strSql = strSql & "             0"
    strSql = strSql & "         ) "
    strSql = strSql & "     ) KAI_NEN_JIKA"                         '海外_年金_時価
    strSql = strSql & " FROM"
    strSql = strSql & " ("
    strSql = strSql & "     SELECT"
    strSql = strSql & "             KIJYUN_YMD,"                    '基準日
    strSql = strSql & "             SETTEI_KBN,"                    '口座開設区分
    strSql = strSql & "             DECODE(DATA_TYPE,"
    strSql = strSql & "                 '101','KOKU_NEN_KOU',"      '公的年金 (共済組合)
    strSql = strSql & "                 '102','KOKU_NEN_KOU',"      '公的年金 (その他)
    strSql = strSql & "                 '103','KOKU_NEN_SI',"       '私的年金 (厚年)
    strSql = strSql & "                 '104','KOKU_NEN_SI',"       '私的年金 (国年)
    strSql = strSql & "                 '105','KOKU_NEN_SI',"       '私的年金 (適年)
    strSql = strSql & "                 '106','KOKU_NEN_SI',"       '私的年金 (その他)
    strSql = strSql & "                 '113','KOKU_NEN_SI',"       '私的年金（規約型）
    strSql = strSql & "                 '114','KOKU_NEN_SI',"       '私的年金（基金型）
    strSql = strSql & "                 '201','KAI_NEN',"           '年金（米国）
    strSql = strSql & "                 '202','KAI_NEN',"           '年金（英国）
    strSql = strSql & "                 '203','KAI_NEN',"           '年金（欧州除英国）
    strSql = strSql & "                 '204','KAI_NEN',"           '年金（その他）（海外）
    strSql = strSql & "             DECODE(TRIM(SETTEI_KBN),'1','KOKU_SONOTA','2','KAI_SONOTA','')" '上記以外
    strSql = strSql & "             ) DATA_TYPE,"                   'データタイプ
    strSql = strSql & "             COUNT(*) DATA_CNT,"             '件数
    strSql = strSql & "             SUM("
    strSql = strSql & "             DECODE(TRIM(SETTEI_KBN),'1',JPY_JIKA_TOTAL_KOKUNAI,'2',JPY_JIKA_TOTAL_KAIGAI,0) / 100"
    strSql = strSql & "             ) JIKA"                         '時価
    strSql = strSql & "     FROM"
    strSql = strSql & "             KYK_VW_KEIEI_CHOUHYOU KVKC"     '経営帳票VIEW
    strSql = strSql & "     WHERE"
    strSql = strSql & "             KVKC.KIJYUN_YMD " & strWhere    '基準日
    strSql = strSql & "     AND     KVKC.SETTEI_KBN IN ('1','2')"   '口座開設区分
    strSql = strSql & "     GROUP BY"
    strSql = strSql & "             KIJYUN_YMD,"
    strSql = strSql & "             SETTEI_KBN,"
    strSql = strSql & "             DATA_TYPE"
    strSql = strSql & " )"
    strSql = strSql & " GROUP BY"
    strSql = strSql & "     KIJYUN_YMD"
    strSql = strSql & " ORDER BY"
    strSql = strSql & "     KIJYUN_YMD"
    strSql = strSql & " "
    
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    If lngRow > 0 Then
        '取得したデータをinDataの配列に格納
        For lngCnt = 1 To cYmdCnt
            inData(lngCnt).MeisaiCnt = 0
            For lngCnt2 = 0 To lngRow - 1
                If Trim(vData(0, lngCnt2)) = Trim(inData(lngCnt).KijyunYmd) Then
                    ReDim inData(lngCnt).Meisai(10)
                    For lngCnt3 = 1 To 10
                        inData(lngCnt).Meisai(lngCnt3) = vData(lngCnt3, lngCnt2)
                    Next lngCnt3
                    inData(lngCnt).MeisaiCnt = 1
                End If
            Next lngCnt2
        Next lngCnt
        
        fncGetZandakaSuiiData = True
    Else
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "")
    End If

    Erase vData

    Exit Function

'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetZandakaSuiiData = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase vData

End Function


'*************************************************************************
'関数名　　：統計情報 専務会報告(帳票作成)
'引　　数　：
'           strOutFName        I           出力ファイル名
'           strKjnLastYmd      I           基準日(月末日)
'           inData()           I           出力データ
'戻り値　　：処理結果
'機能説明　：統計情報 専務会報告のBOOKを作成する。
'更新履歴　：2007/12/04 SRA Y.Azuma    変更     (PH4)印刷エラー対策
'
'*************************************************************************
Public Function fncMakeBook4500( _
    ByVal strOutFName As String, _
    ByVal strKjnLastYmd As String, _
    ByRef inData() As typIndata _
    ) As Boolean

    Const PROCEDURE_NAME As String = "fncMakeBook4500"

On Error GoTo ErrSection

    Dim lngCnt          As Long
    Dim wDate           As Date
    Dim strWk           As String
    Dim strSvZougen     As String

    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)

    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName

    fncMakeBook4500 = False

Application.DisplayAlerts = False

    '============================================
    ' 残高推移シート編集
    '============================================
    With Workbooks(Workbooks.Count).Worksheets("残高推移")
    
        strSvZougen = ""
        For lngCnt = 0 To cYmdCnt - 1
            '基準日
            wDate = Format(inData(lngCnt + 1).KijyunYmd, "####/##/##")
            strWk = Format(wDate, "ggge年m月末")
            .Range("基準日").Offset(0, lngCnt * 2) = strWk
        
            '増減比タイトル
            .Range("増減比").Offset(0, lngCnt * 2) = strSvZougen & "比増減"
            strSvZougen = Format(wDate, "e/m")
            
            If inData(lngCnt + 1).MeisaiCnt = 0 Then
                '件数、時価
                .Range("国内_ALL_件数").Offset(0, lngCnt * 2) = 0
                .Range("国内_公的年金_件数").Offset(0, lngCnt * 2) = 0
                .Range("国内_私的年金_件数").Offset(0, lngCnt * 2) = 0
                .Range("国内_ALL_時価").Offset(0, lngCnt * 2) = 0
                .Range("国内_公的年金_時価").Offset(0, lngCnt * 2) = 0
                .Range("国内_私的年金_時価").Offset(0, lngCnt * 2) = 0
                .Range("海外_ALL_件数").Offset(0, lngCnt * 2) = 0
                .Range("海外_年金_件数").Offset(0, lngCnt * 2) = 0
                .Range("海外_ALL_時価").Offset(0, lngCnt * 2) = 0
                .Range("海外_年金_時価").Offset(0, lngCnt * 2) = 0
            Else
                '件数、時価
                .Range("国内_ALL_件数").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(1)
                .Range("国内_公的年金_件数").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(2)
                .Range("国内_私的年金_件数").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(3)
                .Range("国内_ALL_時価").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(4)
                .Range("国内_公的年金_時価").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(5)
                .Range("国内_私的年金_時価").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(6)
                .Range("海外_ALL_件数").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(7)
                .Range("海外_年金_件数").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(8)
                .Range("海外_ALL_時価").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(9)
                .Range("海外_年金_時価").Offset(0, lngCnt * 2) = inData(lngCnt + 1).Meisai(10)
            End If
            
        Next lngCnt
        
        '式を値に変換
        If fncForMulaToValueInAllSheets(Workbooks(Workbooks.Count)) = False Then
            GoTo ErrSection
        End If
        
        ' カーソル位置を移動する。
        .Activate     ' シートが２個以上の場合要る。
        .Range("A1").Activate
    End With

    ActiveWindow.ScrollRow = 1
    ActiveWindow.ScrollColumn = 1

'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
    Workbooks(Workbooks.Count).Save
'    Workbooks(Workbooks.Count).Close SaveChanges:=True
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------

Application.DisplayAlerts = True

    fncMakeBook4500 = True

    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)

    Exit Function

'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

Application.DisplayAlerts = True

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook4500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

End Function




