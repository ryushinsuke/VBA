Attribute VB_Name = "basClickRunBtn1500"
Option Explicit

'*************************************************************************
'プロジェクト名：新契約管理システム・業務帳票作成指示
'オブジェクト名：basClickRunBtn1500
'機能概要　：メイン画面
'更新履歴　：2006/11/28 SRA T.Sato      新規作成
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn1500"

Private Const cSTART_RAW = 7
Private Const cBaseFile = gc帳票Base5
Private Const cPreFName = gc帳票名5


'*************************************************************************
'関数名　　：顧問料率一覧表(帳票作成)
'引　　数　：
'           objTarget       I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2006/11/28 SRA             新規作成
'
'*************************************************************************
Public Function fncClickRunBtn1500(ByRef objTarget As Object) As Boolean
    
    Const PROCEDURE_NAME As String = "fncClickRunBtn1500"

On Error GoTo ErrSection

    Dim strKikanFrom    As String
    Dim strKikanTo      As String
    Dim strOutFName     As String
    Dim strData()       As String
    Dim lngRow          As Long
    Dim lngRowCnt       As Long
   
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    fncClickRunBtn1500 = False
   
    With objTarget

        ' 作成選択の場合
        If .chkMake.Value = True Then
        
            strKikanFrom = Trim(.Range("料率適用期間from").Value)
            strKikanTo = Trim(.Range("料率適用期間to").Value)
            
            ' データ取得
            If fncGetDBData1500(strKikanFrom, strKikanTo, strData()) = False Then
                GoTo RtnErrSection
            End If
        
            ' 帳票ファイル取得
            If fncGetFile1500(strKikanFrom, strKikanTo, strOutFName) = False Then
                GoTo RtnErrSection
            End If
        
            ' フォルダをつける。
            strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName
            
            ' ファイルコピー
            If fncCopyBook(cBaseFile, strOutFName) = False Then
                GoTo RtnErrSection
            End If
            
            ' 帳票作成（顧問料率一覧表(2)）
            If fncMakeBook1500_2(strKikanFrom, strKikanTo, _
                                 strOutFName, strData()) = False Then
                ' ファイル削除
                fncDeleteFile (strOutFName)

                GoTo RtnErrSection
            End If
            
            ' 帳票作成（顧問料率一覧表）
            If fncMakeBook1500_1(strKikanFrom, strKikanTo, _
                                 strOutFName, strData()) = False Then
                ' ファイル削除
                fncDeleteFile (strOutFName)

                GoTo RtnErrSection
            End If

        End If
    
        ' 印刷選択の場合
        If .chkPrint.Value = True Then
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
    End With
   
    fncClickRunBtn1500 = True
    Erase strData
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                     *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション                              *
'*******************************************************
RtnErrSection:
                
    fncClickRunBtn1500 = False
    Erase strData
    
End Function

'*************************************************************************
'関数名　　：顧問料率一覧表(帳票ファイル取得)
'引　　数　：
'           strKikanFrom       I        料率適用期間From
'           strKikanTo         I        料率適用期間To
'           strOutFName        O        出力ファイル名
'戻り値　　：処理結果
'機能説明　：顧問料率一覧表を出力するファイルを取得する。
'更新履歴　：2006/11/28 SRA             新規作成
'
'*************************************************************************
Public Function fncGetFile1500(ByVal strKikanFrom As String, _
                               ByVal strKikanTo As String, _
                               ByRef strOutFName As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile1500"

On Error GoTo ErrSection

    Dim strDate         As String
    Dim strTime         As String

    strOutFName = ""
    
    fncGetFile1500 = False
    
    Call gfunc_GetSvDateTime(strDate, strTime)
       
    ' 出力帳票ファイル
    strOutFName = cPreFName
    strOutFName = strOutFName & "_" & strDate
    strOutFName = strOutFName & "_" & strTime
    strOutFName = strOutFName & "-(" & strKikanFrom
    strOutFName = strOutFName & "-" & strKikanTo
    strOutFName = strOutFName & ")"
    strOutFName = strOutFName & ".xls"
    
    fncGetFile1500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetFile1500 = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：顧問料率一覧表(帳票データ取得)
'引　　数　：
'           strKikanFrom      I         料率適用期間From
'           strKikanTo        I         料率適用期間To
'           strData()         O         取得データ
'戻り値　　：処理結果
'機能説明　：顧問料率一覧表のデータを取得する。
'更新履歴　：2006/11/28 SRA             新規作成
'
'*************************************************************************
Public Function fncGetDBData1500(ByVal strKikanFrom As String, _
                                 ByVal strKikanTo As String, _
                                 ByRef strData() As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetDBData1500"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    Dim lngColCnt   As Long

    Dim intNo       As Integer
    
    fncGetDBData1500 = False
    
'*******************************************************
'* SQL文作成                                           *
'*******************************************************

    strSql = ""
'------------<Modify Start Ichikawa 2007/10/29 (PH4)> -----------------------
'    strSql = strSql & "SELECT RYORITSU_CD,"
'    strSql = strSql & "       RYORITSU_MEI,"
'    strSql = strSql & "       PK_NAME.SF_TUUKA_NAME(kk33.ryo_tuuka),"
'
'    For intNo = 1 To 20
'    strSql = strSql & "       KINGAKU_FROM_" & Mid(intNo + 100, 2, 2) & ","
'    strSql = strSql & "       KINGAKU_TO_" & Mid(intNo + 100, 2, 2) & ","
'    strSql = strSql & "       RITSU_GAKU_" & Mid(intNo + 100, 2, 2) & ","
'    strSql = strSql & "       RITSU_" & Mid(intNo + 100, 2, 2) & ","
'    strSql = strSql & "       PK_NAME.SF_KUBUN_EDIT(KINGAKU_FROM_" & Mid(intNo + 100, 2, 2) & ",KINGAKU_TO_" & Mid(intNo + 100, 2, 2) & ",RYO_TUUKA) KINGAKU_HANI_" & Mid(intNo + 100, 2, 2) & ","
'    strSql = strSql & "       PK_NAME.SF_RITSU_EDIT(RITSU_" & Mid(intNo + 100, 2, 2) & ",RITSU_GAKU_" & Mid(intNo + 100, 2, 2) & ") RITSU_KBN_" & Mid(intNo + 100, 2, 2)
'    If intNo <> 20 Then
'        strSql = strSql & ","
'    End If
'    Next intNo
'    strSql = strSql & "  FROM KK33"
'    strSql = strSql & " WHERE RYORITSU_CD IN (SELECT RATE_CODE"
'    strSql = strSql & "                         FROM KYK_RATE_TEKIYOH_KIKAN"
'    strSql = strSql & "                        WHERE RATE_TEKIYOH_KIKAN_FROM <= " & strKikanTo
'    strSql = strSql & "                          AND RATE_TEKIYOH_KIKAN_TO   >= " & strKikanFrom
'    strSql = strSql & "                          AND SAKUJO_FLG               = '0'"
'    strSql = strSql & "                        GROUP BY RATE_CODE)"
'    strSql = strSql & " ORDER BY RYORITSU_CD"
    
    
    strSql = strSql & "SELECT" & vbCrLf
    strSql = strSql & "    RATE_CODE," & vbCrLf
    strSql = strSql & "    RATE_NAME," & vbCrLf
    strSql = strSql & "    RYO_TUUKA," & vbCrLf
    
    For intNo = 1 To 20
    strSql = strSql & "    KINGAKU_FROM_" & Mid(intNo + 100, 2, 2) & "," & vbCrLf
    strSql = strSql & "    KINGAKU_TO_" & Mid(intNo + 100, 2, 2) & "," & vbCrLf
    strSql = strSql & "    RITSU_GAKU_" & Mid(intNo + 100, 2, 2) & "," & vbCrLf
    strSql = strSql & "    RITSU_" & Mid(intNo + 100, 2, 2) & "," & vbCrLf
    strSql = strSql & "    ''," & vbCrLf
    strSql = strSql & "    ''"
    If intNo <> 20 Then
        strSql = strSql & "," & vbCrLf
    Else
        strSql = strSql & vbCrLf
    End If
    Next intNo
    strSql = strSql & "FROM" & vbCrLf
    strSql = strSql & "    KYK_RATE" & vbCrLf
    strSql = strSql & "WHERE" & vbCrLf
    strSql = strSql & "    RATE_CODE IN (" & vbCrLf
    strSql = strSql & "        SELECT" & vbCrLf
    strSql = strSql & "            RATE_CODE" & vbCrLf
    strSql = strSql & "        FROM" & vbCrLf
    strSql = strSql & "            KYK_RATE_TEKIYOH_KIKAN" & vbCrLf
    strSql = strSql & "        WHERE" & vbCrLf
    strSql = strSql & "            RATE_TEKIYOH_KIKAN_FROM <= " & strKikanTo & vbCrLf
    strSql = strSql & "        AND RATE_TEKIYOH_KIKAN_TO   >= " & strKikanFrom & vbCrLf
    strSql = strSql & "        AND SAKUJO_FLG               = '0'" & vbCrLf
    strSql = strSql & "        GROUP BY" & vbCrLf
    strSql = strSql & "            RATE_CODE)" & vbCrLf
    strSql = strSql & "ORDER BY" & vbCrLf
    strSql = strSql & "    RATE_CODE" & vbCrLf
'------------<Modify End   Ichikawa 2007/10/29 (PH4)> -----------------------
    
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    If lngRow > 0 Then
        ' 動的配列変数に対するメモリ領域の再割り当てを行います
        ReDim strData(lngRow, lngCol)

        '取得データを配列に設定
        For lngRowCnt = 0 To lngRow - 1
            For lngColCnt = 0 To UBound(strData, 2) - 1

'------------<Modify Start Ichikawa 2007/10/29 (PH4)> -----------------------
'                strData(lngRowCnt, lngColCnt) = CStr(vData(lngColCnt, lngRowCnt))

                '料率段階文字列編集
                If CStr(vData(lngColCnt, lngRowCnt)) = "" Then
                    If lngColCnt Mod 2 = 1 Then
                        strData(lngRowCnt, lngColCnt) = func_RyorituDankaiEdit(CDbl(strData(lngRowCnt, lngColCnt - 4)), CDbl(strData(lngRowCnt, lngColCnt - 3)), CStr(strData(lngRowCnt, 2)))
                    Else
                        '料率コード料率編集
                        If strData(lngRowCnt, lngColCnt - 2) > 0 Then
                            strData(lngRowCnt, lngColCnt) = strData(lngRowCnt, lngColCnt - 2)
                        Else
                            strData(lngRowCnt, lngColCnt) = strData(lngRowCnt, lngColCnt - 3) / 10
                        End If
                    End If
                Else
                    strData(lngRowCnt, lngColCnt) = CStr(vData(lngColCnt, lngRowCnt))
                End If
'------------<Modify End   Ichikawa 2007/10/29 (PH4)> -----------------------

            Next
        Next lngRowCnt
        fncGetDBData1500 = True
    Else
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "")
    End If
    
    Erase vData
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetDBData1500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase vData
    Erase strData
   
End Function

'*************************************************************************
'関数名　　：顧問料率一覧表(帳票作成)
'引　　数　：
'           strKikanFrom      I         料率適用期間From
'           strKikanTo        I         料率適用期間To
'           strOutFName       I         出力ファイル名
'           strData()         I         出力データ
'戻り値　　：処理結果
'機能説明　：顧問料率一覧表の作成を取得する。
'更新履歴　：2006/11/28 SRA             新規作成
'更新履歴　：2007/11/01 SRA Y.Azuma    変更     (PH4)
'
'*************************************************************************
Public Function fncMakeBook1500_1(ByVal strKikanFrom As String, _
                                  ByVal strKikanTo As String, _
                                  ByVal strOutFName As String, _
                                  ByRef strData() As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeBook1500_1"

On Error GoTo ErrSection

    Dim lngRowCnt       As Long
    Dim lngRow          As Long
    
    Dim intCnt          As Integer
    Dim lngDataCnt      As Long
    Dim intKingakuFrom  As Integer
    Dim intKingakuTo    As Integer
    Dim intKingakuHani  As Integer
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    lngRow = UBound(strData, 1)
    
    fncMakeBook1500_1 = False
    
    With Workbooks(Workbooks.Count).Worksheets(1)
        
        ' 一覧出力
        
        ' 料率適用期間
        .Range("料率適用期間１").Value = "(料率適用期間：" & _
                                         Format(strKikanFrom, "@@@@/@@/@@") & "〜" & _
                                         Format(strKikanTo, "@@@@/@@/@@") & ")"
        lngRowCnt = 0
        For lngDataCnt = 0 To lngRow - 1
            .Cells(cSTART_RAW + lngRowCnt, 1).NumberFormatLocal = "@"           ' 料率コード（文字列）
            .Cells(cSTART_RAW + lngRowCnt, 1).HorizontalAlignment = xlRight     ' 料率コード（右詰め）
            .Cells(cSTART_RAW + lngRowCnt, 1).Value = strData(lngDataCnt, 0)    ' 料率コード
        
            .Cells(cSTART_RAW + lngRowCnt, 2).NumberFormatLocal = "@"           ' 料率名（文字列に編集）
            .Cells(cSTART_RAW + lngRowCnt, 2).Value = strData(lngDataCnt, 1)    ' 料率名
            
            intKingakuFrom = 3  '金額FROM
            intKingakuTo = 4    '金額TO
            intKingakuHani = 7  '金額範囲
            For intCnt = 1 To 20
                '金額FROM <> 0 又は　金額TO <> 0の場合、出力
                If strData(lngDataCnt, intKingakuFrom) <> 0 Or strData(lngDataCnt, intKingakuTo) <> 0 Then
                
                    .Cells(cSTART_RAW + lngRowCnt, 3).Value = strData(lngDataCnt, intKingakuHani)    ' 金額範囲
                    If strData(lngDataCnt, intKingakuHani + 1) < 1 Then
                        ' "年" + 料率区分 + "%"
                        .Cells(cSTART_RAW + lngRowCnt, 5).Value = "年" & Format(strData(lngDataCnt, intKingakuHani + 1), "0.0000") & "%"
                    Else
                        ' 料率区分 + "万" + 通貨名称
'------------<Modify Start azuma 2007/11/01 (PH4)> -----------------------
                        .Cells(cSTART_RAW + lngRowCnt, 5).Value = Format(strData(lngDataCnt, intKingakuHani + 1), "0.0") & "万" & _
                                                                  func_GetTukaName(strData(lngDataCnt, 2))
'                        .Cells(cSTART_RAW + lngRowCnt, 5).Value = Format(strData(lngDataCnt, intKingakuHani + 1), "0.0") & "万" & _
'                                                                  strData(lngDataCnt, 2)
'------------<Modify End   azuma 2007/11/01 (PH4)> -----------------------
                    End If
                    
                    lngRowCnt = lngRowCnt + 1
                
                End If
                
                intKingakuFrom = intKingakuFrom + 6
                intKingakuTo = intKingakuTo + 6
                intKingakuHani = intKingakuHani + 6
                
            Next intCnt
            
            lngRowCnt = lngRowCnt + 1
        
        Next lngDataCnt
         
        ' カソル位置を異動する。
        .Activate     ' シートが２個以上の場合要る。
        .Range("A1").Activate
       
    End With

    ActiveWindow.ScrollRow = 1
    ActiveWindow.ScrollColumn = 1
    
    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook1500_1 = True
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook1500_1 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
End Function

'*************************************************************************
'関数名　　：顧問料率一覧表(2)(帳票作成)
'引　　数　：
'           strKikanFrom      I         料率適用期間From
'           strKikanTo        I         料率適用期間To
'           strOutFName       I         出力ファイル名
'           strData()         I         出力データ
'戻り値　　：処理結果
'機能説明　：顧問料率一覧表の作成を取得する。
'更新履歴　：2006/11/28 SRA             新規作成
'        　：2007/10/29 SRA T.Ichikawa  変更     (PH4)SQLを修正(新テーブルを参照する)
'更新履歴　：2007/11/01 SRA Y.Azuma     変更     (PH4)
'
'*************************************************************************
Public Function fncMakeBook1500_2(ByVal strKikanFrom As String, _
                                  ByVal strKikanTo As String, _
                                  ByVal strOutFName As String, _
                                  ByRef strData() As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeBook1500_2"

On Error GoTo ErrSection

    Dim lngRowCnt       As Long
    Dim lngRow          As Long
    
    Dim intCnt          As Integer
    Dim lngDataCnt      As Long
    Dim intKingakuFrom  As Integer
    Dim intKingakuTo    As Integer
    Dim intRitsuGaku    As Integer
    Dim intRitsu        As Integer
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    lngRow = UBound(strData, 1)
    
    fncMakeBook1500_2 = False
    
    With Workbooks(Workbooks.Count).Worksheets(2)
        
        ' 一覧出力
        
        ' 料率適用期間
        .Range("料率適用期間２").Value = "(料率適用期間：" & _
                                         Format(strKikanFrom, "@@@@/@@/@@") & "〜" & _
                                         Format(strKikanTo, "@@@@/@@/@@") & ")"
        lngRowCnt = 0
        For lngDataCnt = 0 To lngRow - 1
            .Cells(cSTART_RAW + lngRowCnt, 1).NumberFormatLocal = "@"           ' 料率コード（文字列）
            .Cells(cSTART_RAW + lngRowCnt, 1).HorizontalAlignment = xlRight     ' 料率コード（右詰め）
            .Cells(cSTART_RAW + lngRowCnt, 1).Value = strData(lngDataCnt, 0)    ' 料率コード
            
            .Cells(cSTART_RAW + lngRowCnt, 2).Value = strData(lngDataCnt, 1)    ' 料率名
            .Cells(cSTART_RAW + lngRowCnt, 2).Value = strData(lngDataCnt, 1)    ' 料率名
            
            intKingakuFrom = 3  '金額FROM
            intKingakuTo = 4    '金額TO
            intRitsuGaku = 5    '料率金額
            intRitsu = 6        '料率
            For intCnt = 1 To 20
                '金額FROM <> 0 又は　金額TO <> 0の場合、出力
                If strData(lngDataCnt, intKingakuFrom) <> 0 Or strData(lngDataCnt, intKingakuTo) <> 0 Then
                    
                    ' 金額FROM * 10000000
                    .Cells(cSTART_RAW + lngRowCnt, 3).Value = strData(lngDataCnt, intKingakuFrom) * 10000000
'------------<Modify 1兆円超対応20080218 2008/03/05 SRA市川 Start > ----------------------------------------
'                    If strData(lngDataCnt, intKingakuTo) < 99999 Then
                    If strData(lngDataCnt, intKingakuTo) < 9999999 Then
'------------<Modify 1兆円超対応20080218 2008/03/05 SRA市川 End   > ----------------------------------------
                        ' 金額TO * 10000000
                        .Cells(cSTART_RAW + lngRowCnt, 4).Value = strData(lngDataCnt, intKingakuTo) * 10000000
                    End If
                    
                    If strData(lngDataCnt, intRitsu) <> 0 Then
                        ' 料率 /100　(0.00000%に編集）
                        .Cells(cSTART_RAW + lngRowCnt, 5).Value = strData(lngDataCnt, intRitsu) / 100
                        .Cells(cSTART_RAW + lngRowCnt, 5).NumberFormatLocal = "0.00000%"
                    End If
                    If strData(lngDataCnt, intRitsuGaku) <> 0 Then
                        ' 料率金額 * 1000　(#,###,###0 + 通貨名称に編集）
'                        .Cells(cSTART_RAW + lngRowCnt, 6).Value = Format(strData(lngDataCnt, intRitsuGaku) * 1000, "#,###,###0") & " " & strData(lngDataCnt, 2)
'                        .Cells(cSTART_RAW + lngRowCnt, 6).HorizontalAlignment = xlRight
                        .Cells(cSTART_RAW + lngRowCnt, 6).Value = strData(lngDataCnt, intRitsuGaku) * 1000
'------------<Modify Start azuma 2007/11/01 (PH4)> -----------------------
                        .Cells(cSTART_RAW + lngRowCnt, 6).NumberFormatLocal = "#,##0_ " & func_GetTukaName(strData(lngDataCnt, 2))
''                        .Cells(cSTART_RAW + lngRowCnt, 6).NumberFormatLocal = "#,##0_ " & strData(lngDataCnt, 2)
'------------<Modify End   azuma 2007/11/01 (PH4)> -----------------------
                    End If
                    
                    lngRowCnt = lngRowCnt + 1
                
                End If
                
                intKingakuFrom = intKingakuFrom + 6
                intKingakuTo = intKingakuTo + 6
                intRitsuGaku = intRitsuGaku + 6
                intRitsu = intRitsu + 6
                
            Next intCnt
            
            lngRowCnt = lngRowCnt + 1
        
        Next lngDataCnt
         
        ' カソル位置を異動する。
        .Activate     ' シートが２個以上の場合要る。
        .Range("A1").Activate
       
    End With

    ActiveWindow.ScrollRow = 1
    ActiveWindow.ScrollColumn = 1
    
    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook1500_2 = True
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook1500_2 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
End Function

