Attribute VB_Name = "basUserCommon"
Option Explicit

'*******************************************************
'* ユーザログイン画面                              　  *
'* 　ＯＫボタン/パスワード変更ボタン押下共通処理   　  *
'*******************************************************
Public Function UserCommon(ByRef argPass_YukoKgn As String, _
                           ByRef argPass_KgnUmu As String, _
                           ByRef argPass_YukoTuti As String, _
                           ByRef argLock_Cnt As String, _
                           ByRef argLock_Umu As String, _
                           ByRef argPass_Rireki As String, _
                           ByRef argLockKbn As String, _
                           ByRef argKengenKbn As String, _
                           ByRef argErr_Count As String, _
                           ByRef argSedai_no As String, _
                           ByRef argShoki_pswd_flg As String, _
                           ByRef argPassTohroku_ymd As String, _
                           ByRef strSysDate As String, _
                           ByRef Conn As ADODB.Connection, _
                           ByRef RS As ADODB.Recordset) As String
    On Error GoTo ErrProc

    Dim Ret                As String

    'チェックＮＧの場合は、1(False)を返す
    '戻り値を"1"で初期化する
    UserCommon = "1"

    'ユーザID未入力チェック-----------------------------------------------------------------------------------
    If Trim(UserCertification.txt_UsrId) = "" Then
        MsgBox "ユーザIDを入力してください。", vbCritical
        'ユーザIDテキストボックスにセットフォーカス
        UserCertification.txt_UsrId.SelStart = 0
        UserCertification.txt_UsrId.SelLength = Len(UserCertification.txt_UsrId.Text)
        UserCertification.txt_UsrId.SetFocus
        Exit Function
    End If
    '----------------------------------------------------------------------------------------------------------
    
    'パスワード未入力チェック----------------------------------------------------------------------------------
    If Trim(UserCertification.txt_UsrPass) = "" Then
        MsgBox "パスワードを入力してください。", vbCritical
        'パスワードテキストボックスにセットフォーカス
        UserCertification.txt_UsrPass.SelStart = 0
        UserCertification.txt_UsrPass.SelLength = Len(UserCertification.txt_UsrPass.Text)
        UserCertification.txt_UsrPass.SetFocus
        Exit Function
    End If
    '----------------------------------------------------------------------------------------------------------
    
    '入力IDの存在チェック(PW変更押下時と共通処理)--------------------------------------------------------------
    Ret = UserInfoGet(Trim(UserCertification.txt_UsrId), _
                      argKengenKbn, _
                      argLockKbn, _
                      argErr_Count, _
                      Conn, _
                      RS)
    If Ret = "No Register" Then
        'UserInfoGetからの戻り値が"No Register"の場合、入力されたユーザIDがテーブルに登録されていない
        MsgBox "ユーザIDが違います。", vbCritical
        
        '登録されていないUSERIDがログインしようとしたことをログに残す
        '※引数2番目の"0"はERR_COUNT：("0"を渡す)
        '※引数3番目の"2"はログ区分 ：(登録ユーザ以外のログイン)
        Ret = UserLog_Out(UserCertification.txt_UsrId, _
                          "0", _
                          "2", _
                          Conn, _
                          RS)
        'ログテーブルへの登録に失敗した場合は、処理を終了する
        If Ret = "False" Then Exit Function
        
        'ユーザIDテキストボックスにセットフォーカス
        UserCertification.txt_UsrId.SelStart = 0
        UserCertification.txt_UsrId.SelLength = Len(UserCertification.txt_UsrId.Text)
        UserCertification.txt_UsrId.SetFocus
        Exit Function
    
    ElseIf Ret = "False" Then
        'ユーザID存在チェック中、システムエラー発生
        Exit Function
    End If
    '----------------------------------------------------------------------------------------------------------
    
    'ログインパラメータを取得する(PW変更押下時と共通処理)------------------------------------------------------
    Ret = ParamInfoGet(argPass_YukoKgn, _
                       argPass_KgnUmu, _
                       argPass_YukoTuti, _
                       argLock_Cnt, _
                       argLock_Umu, _
                       argPass_Rireki, _
                       Conn, _
                       RS)
    'ログインパラメータが何らかの理由(システムエラー)で取得できなかったら処理終了
    If Ret = "False" Then Exit Function
    '----------------------------------------------------------------------------------------------------------
    
    'ロック区分チェック----------------------------------------------------------------------------------------
    If argKengenKbn <> "A" And _
       argLockKbn <> "0" And _
       argLock_Umu = "1" Then
        '権限区分が"A"(管理者)でなく、
        'ログインミスを規定回数以上、
        'ロックアウト有無フラグが"1"(あり)の場合 、
        'ユーザIDは使用不可
        '※権限区分が"A"(管理者)の場合は、strLockKbn = "1"(規定回数以上のログインミス)をしていても、ロックはかからない。
        MsgBox "ユーザがロックされています｡ ", vbCritical
        
        'ロックされているUSERIDがログインしようとしたことをログに残す
        '※引数2番目の"0"はERR_COUNT：("0"を渡す)
        '※引数3番目の"3"はログ区分 ：(ロックユーザのログイン)
        Ret = UserLog_Out(UserCertification.txt_UsrId, _
                          "0", _
                          "4", _
                          Conn, _
                          RS)
        'ログテーブルへの登録に失敗した場合は、処理を終了する
        If Ret = "False" Then Exit Function
        Exit Function
    End If
    '----------------------------------------------------------------------------------------------------------
    
    'パスワード最新世代番号と登録日取得------------------------------------------------------------------------
    Ret = UserInfoGet2(Trim(UserCertification.txt_UsrId), _
                       argSedai_no, _
                       argShoki_pswd_flg, _
                       argPassTohroku_ymd, _
                       Conn, _
                       RS)
    'ユーザ情報が何らかの理由(システムエラー)で取得できなかったら処理終了
    If Ret = "False" Then Exit Function
    
    'SEによるパスワード登録ミスチェック
    If argSedai_no = "登録失敗" Or _
       argShoki_pswd_flg = "登録失敗" Then
        MsgBox "ＳＥによるパスワード登録作業が誤っている可能性があります。システム管理者へご連絡ください。", vbExclamation
        Exit Function
    End If
    '----------------------------------------------------------------------------------------------------------
    
    'パスワード存在チェック------------------------------------------------------------------------------------
    '入力されたパスワードを暗号化し、テーブル登録されている暗号パスワードと照合する
    Ret = Pswd_Check(Trim(UserCertification.txt_UsrId), _
                     argSedai_no, _
                     Trim(UserCertification.txt_UsrPass), _
                     Conn, _
                     RS)
    'Falseが返された場合は照合不整合
    If Ret = "False" Then
        
'------------<連結障害対応 20080912 SRA東 START> -----------------------
        'パスワードの不一致
        MsgBox "パスワードが違います。", vbCritical
'------------<連結障害対応 20080912 SRA東 END  > -----------------------

        'パスワード入力ミスしたことをログに残す
        '※引数3番目の"1"はログ区分：(パスワード入力ミス)
        Ret = UserLog_Out(Trim(UserCertification.txt_UsrId), _
                          argErr_Count, _
                          "1", _
                          Conn, _
                          RS)
        'ログ出力が何らかの理由(システムエラー)で失敗したら処理終了
        If Ret = "False" Then Exit Function
        
        'ErrCount_Upを1カウントアップしてテーブル登録
        argErr_Count = argErr_Count + 1
        Ret = ErrCount_Up(argErr_Count, _
                          argLock_Cnt, _
                          Trim(UserCertification.txt_UsrId), _
                          argKengenKbn, _
                          argLock_Umu, _
                          Conn, _
                          RS)
        'エラーカウントUPが何らかの理由(システムエラー)で失敗したら処理終了
        If Ret = "False" Then Exit Function
        
        
        '規定回数以上のログインミスをしたためユーザロックしたことをログに残す
        If argErr_Count >= argLock_Cnt And _
           argKengenKbn = "U" And _
           argLock_Umu = "1" Then
            MsgBox "ユーザをロックします。", vbExclamation
            '※引数3番目の"3"はログ区分：(ユーザをロック)
            Ret = UserLog_Out(Trim(UserCertification.txt_UsrId), _
                              "0", _
                              "3", _
                              Conn, _
                              RS)
            'ログ出力が何らかの理由(システムエラー)で失敗したら処理終了
            If Ret = "False" Then Exit Function
        End If
        
'------------<連結障害対応 20080912 SRA東 START> -----------------------
'ロック時のメッセージ表示順を変更（ロックメッセージの前に移動）
'''        'パスワードの不一致
'''        MsgBox "パスワードが違います。", vbCritical
'------------<連結障害対応 20080912 SRA東 END  > -----------------------
        
        'パスワードテキストボックスにセットフォーカス
        UserCertification.txt_UsrPass.SelStart = 0
        UserCertification.txt_UsrPass.SelLength = Len(UserCertification.txt_UsrPass.Text)
        UserCertification.txt_UsrPass.SetFocus
        Exit Function
    
    '"SYSTEM ERROR"が返された場合はSQLエラーが発生したので処理終了
    ElseIf Ret = "SYSTEM ERROR" Then
        Exit Function
    End If
    '----------------------------------------------------------------------------------------------------------
    
    'システム日付の取得------------------------------------------------------------------------
    Ret = DateGet(strSysDate, _
                  Conn, _
                  RS)
    'ユーザ情報が何らかの理由(システムエラー)で取得できなかったら処理終了
    If Ret = "False" Then Exit Function
    '----------------------------------------------------------------------------------------------------------

    'チェックＯＫの場合は、-1(True)を返す
    UserCommon = "-1"

    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "UserCommon", Err.Number, _
                       "ユーザログインチェック処理が失敗しました｡ " & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*******************************************************
'* DB接続文字列作成                                　  *
'*******************************************************
Public Function DBAccessString(ByRef argDBConnect As String) As String

    On Error GoTo ErrProc
    
    Dim strODBCConn As String
    Dim strUserName As String
    Dim strPassWord As String
    
    '戻り値Falseで初期化
    DBAccessString = "False"
    
    'DB接続文字列作成
    '非表示の"設定"シートに書かれている情報を読み取り、接続文字列を作成する
    With ThisWorkbook.Sheets("設定")
        strODBCConn = .Range("ODBC")
        strUserName = .Range("USERNAME")
        strPassWord = .Range("PASSWORD")
    End With

    argDBConnect = ""
    argDBConnect = argDBConnect & "DSN=" & strODBCConn & ";"
    argDBConnect = argDBConnect & "UID=" & strUserName & ";"
    argDBConnect = argDBConnect & "PWD=" & strPassWord & ";"
    argDBConnect = argDBConnect & "EXC=T"

    '戻り値Trueへ更新
    DBAccessString = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "DBAccessString", Err.Number, _
                       "DB接続文字列の作成が失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*******************************************************
'* DB接続                                          　  *
'*******************************************************
Public Function DBOpen(ByVal argDBConnect As String, _
                       ByVal Conn As ADODB.Connection) As String

    On Error GoTo ErrProc
    
    '戻り値Falseで初期化
    DBOpen = "False"
    
    'DB接続
    Conn.ConnectionString = argDBConnect
    Conn.Open

    '戻り値Trueへ更新
    DBOpen = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "DBOpen", Err.Number, _
                       "DB接続失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*******************************************************
'* DB切断                                          　  *
'*******************************************************
Public Function DBClose(ByRef Conn As ADODB.Connection, _
                        ByRef RS As ADODB.Recordset) As String
    
    On Error GoTo ErrProc
    
    '戻り値Falseで初期化
    DBClose = "False"
    
    'DB切断
    Set RS = Nothing
    Conn.Close
    Set Conn = Nothing

    '戻り値Trueへ更新
    DBClose = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "DBClose", Err.Number, _
                       "DB切断失敗しました。" & vbCrLf & Err.Description)

    On Error GoTo 0

End Function

'*******************************************************
'* ブックを閉じる                                  　  *
'*******************************************************
Public Sub Workbook_Close(ByRef Conn As ADODB.Connection, _
                          ByRef RS As ADODB.Recordset)
    'ユーザ認証画面でｷｬﾝｾﾙ押下またはﾛｸﾞｲﾝ規定回数以上の失敗

    Set Conn = Nothing
    Set RS = Nothing
    
    '数式バーとステータスバーを復帰する
    Application.DisplayFormulaBar = True
    Application.DisplayStatusBar = True
    
    'ワークブックを閉じる
    ThisWorkbook.Close
    
End Sub

'*******************************************************
'* パラメータ情報を取得                            　  *
'*******************************************************
Public Function ParamInfoGet(ByRef argPass_YukoKgn As String, _
                             ByRef argPass_KgnUmu As String, _
                             ByRef argPass_YukoTuti As String, _
                             ByRef argLock_Cnt As String, _
                             ByRef argLock_Umu As String, _
                             ByRef argPass_Rireki As String, _
                             ByVal Conn As ADODB.Connection, _
                             ByVal RS As ADODB.Recordset) As String

    Dim Ret                 As String  '関数からの戻り値(True/False)
    Dim strSql              As String  'パラメータ取得SQL文
    Dim i                   As Long    'ループカウンタ
    
    ParamInfoGet = "False"
    
    '1～7のループは、パラメータ行が"010"～"070"のため("060"は欠番)
    For i = 1 To 7
        
        'パラメータ取得SQL作成
        strSql = ""
        strSql = strSql & " FROM KYK_USER_KANRI_PARAM_ZOKUSEI"
        strSql = strSql & " WHERE"
        
        Select Case i
            Case 1      '"パスワード有効期限"
                strSql = "SELECT NUM_PARAM " & strSql
                strSql = strSql & "    PARAMETER_CD = '010'"
                Ret = ParamGet1(strSql, _
                                i, _
                                argPass_YukoKgn, _
                                Conn, _
                                RS)
            Case 2      '"パスワード期限有無"
                strSql = "SELECT STR_PARAM " & strSql
                strSql = strSql & "    PARAMETER_CD = '020'"
                Ret = ParamGet1(strSql, _
                                i, _
                                argPass_KgnUmu, _
                                Conn, _
                                RS)
            Case 3      '"パスワード有効期限通知日数"
                strSql = "SELECT NUM_PARAM " & strSql
                strSql = strSql & "    PARAMETER_CD = '030'"
                Ret = ParamGet1(strSql, _
                                i, _
                                argPass_YukoTuti, _
                                Conn, _
                                RS)
            Case 4      '"ロックアウト回数"
                strSql = "SELECT NUM_PARAM " & strSql
                strSql = strSql & "    PARAMETER_CD = '040'"
                Ret = ParamGet1(strSql, _
                                i, _
                                argLock_Cnt, _
                                Conn, _
                                RS)
            Case 5      '"ロックアウト有無"
                strSql = "SELECT STR_PARAM " & strSql
                strSql = strSql & "    PARAMETER_CD = '050'"
                Ret = ParamGet1(strSql, _
                                i, _
                                argLock_Umu, _
                                Conn, _
                                RS)
            Case 7      '"同一パスワードのチェックをする履歴数"
                strSql = "SELECT NUM_PARAM " & strSql
                strSql = strSql & "    PARAMETER_CD = '070'"
                Ret = ParamGet1(strSql, _
                                i, _
                                argPass_Rireki, _
                                Conn, _
                                RS)
        End Select
    
        '戻り値がFalseの場合は、システムエラーによりパラメータ取得失敗により処理を終了する
        If Ret = "False" Then Exit Function
    
    Next
    
    'ログインパラメータに何も設定されていない場合はメッセージで表示する
    If argPass_YukoKgn = "" Or _
       argPass_KgnUmu = "" Or _
       argPass_YukoTuti = "" Or _
       argLock_Cnt = "" Or _
       argLock_Umu = "" Or _
       argPass_Rireki = "" Then
        MsgBox "パラメータテーブルが設定されていません。", vbCritical
        ParamInfoGet = "False"
        Exit Function
    End If
    
    ParamInfoGet = "True"

End Function

'*******************************************************
'* ユーザ情報を取得                                　  *
'*******************************************************
Public Function UserInfoGet(ByVal argInputUID As String, _
                            ByRef argKengenKbn As String, _
                            ByRef argLockKbn As String, _
                            ByRef argErr_Count As String, _
                            ByVal Conn As ADODB.Connection, _
                            ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc
    
    Dim strSql As String

    '戻り値をFalseで初期化
    UserInfoGet = "False"
    
    strSql = ""
    strSql = strSql & " select kyk_login_user_zokusei.user_id,                                    "
    strSql = strSql & "        kyk_login_user_zokusei.kengen_kbn,                                 "
    strSql = strSql & "        kyk_login_user_zokusei.lock_kbn,                                   "
    strSql = strSql & "        kyk_login_user_zokusei.pswd_machigai_cnt                           "
    strSql = strSql & " from   kyk_login_user_zokusei,                                            "
    strSql = strSql & "        kyk_login_pswd_zokusei                                             "
    strSql = strSql & " where  kyk_login_user_zokusei.user_id = '" & argInputUID & "'"
    strSql = strSql & " and    kyk_login_pswd_zokusei.user_id = '" & argInputUID & "'"
    
    'SQL文実行
    'レコードセットオープン
    RS.Open strSql, Conn, adOpenForwardOnly, adLockOptimistic, adCmdText

    If RS.EOF = True Then
        '入力されたIDがテーブルに登録されていない場合、処理終了
        '"No Register"文字列を呼び元へ返す
        UserInfoGet = "No Register"
        'レコードセットクローズ
        RS.Close
        Exit Function
    End If
    
    Do Until RS.EOF = True
        
        'kyk_login_pswd_zokuseiのSEDAI_NO行取得されるが、何行取得しても同じデータとなる
        'NULLだった場合は、システムでユーザ登録した際、登録ミスがあったと考えられる。
        argErr_Count = Trim(IIf(IsNull(RS("pswd_machigai_cnt").Value), "登録失敗", RS("pswd_machigai_cnt").Value))
        argLockKbn = Trim(IIf(IsNull(RS("lock_kbn").Value), "登録失敗", RS("lock_kbn").Value))
        argKengenKbn = Trim(IIf(IsNull(RS("kengen_kbn").Value), "登録失敗", RS("kengen_kbn").Value))
        RS.MoveNext
        
    Loop

    '戻り値Trueへ更新
    UserInfoGet = "True"
            
    'レコードセットクローズ
    RS.Close

    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    If RS.State = 1 Then
        'RS.State = 1(オープン状態)のときはレコードセットクローズする
        RS.Close
    End If

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "UserInfoGet", Err.Number, _
                       "ユーザデータ取得処理失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*******************************************************
'* ユーザ情報を取得2                               　  *
'*******************************************************
Public Function UserInfoGet2(ByVal argInputUID As String, _
                             ByRef argSedai_no As String, _
                             ByRef argShoki_pswd_flg As String, _
                             ByRef argPassTohroku_ymd As String, _
                             ByVal Conn As ADODB.Connection, _
                             ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc
    
    Dim strSql As String

    '戻り値をFalseで初期化
    UserInfoGet2 = "False"
    
    strSql = ""
    strSql = strSql & " select  b.user_id,                                                  "
    strSql = strSql & "         a.sedai_no,                                                 "
    strSql = strSql & "         b.shoki_pswd_flg,                                           "
    strSql = strSql & "         TO_CHAR(c.saishu_pswd_kohshin_ymd,'yyyy/mm/dd') kohshin_ymd "
    strSql = strSql & " from    (select user_id,                                            "
    strSql = strSql & "                 MAX(sedai_no) sedai_no                              "
    strSql = strSql & "          from   kyk_login_pswd_zokusei                              "
    strSql = strSql & "          where  user_id = '" & argInputUID & "'"
    strSql = strSql & "          group  by  user_id) a,                                     "
    strSql = strSql & "         kyk_login_pswd_zokusei b,                                   "
    strSql = strSql & "         kyk_login_user_zokusei c                                    "
    strSql = strSql & " where   b.user_id = a.user_id                                       "
    strSql = strSql & " and     b.sedai_no = a.sedai_no                                     "
    strSql = strSql & " and     b.user_id = c.user_id                                       "
    
    'SQL文実行
    'レコードセットオープン
    RS.Open strSql, Conn, adOpenForwardOnly, adLockOptimistic, adCmdText
    
    Do Until RS.EOF = True
        
        'NULLだった場合は、システムでユーザ登録した際、登録ミスがあったと考えられる。
        argSedai_no = Trim(IIf(IsNull(RS("sedai_no").Value), "登録失敗", RS("sedai_no").Value))
        argShoki_pswd_flg = Trim(IIf(IsNull(RS("shoki_pswd_flg").Value), "登録失敗", RS("shoki_pswd_flg").Value))
        '更新日時はNULLであってもかまわない
        argPassTohroku_ymd = Trim(IIf(IsNull(RS("kohshin_ymd").Value), "", RS("kohshin_ymd").Value))
        RS.MoveNext
        
    Loop

    '戻り値Trueへ更新
    UserInfoGet2 = "True"
            
    'レコードセットクローズ
    RS.Close

    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    If RS.State = 1 Then
        'RS.State = 1(オープン状態)のときはレコードセットクローズする
        RS.Close
    End If

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "UserInfoGet2", Err.Number, _
                       "ユーザデータ取得処理失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'**********************************************************************************************************
'* 入力されたパスワードを暗号化して、テーブル登録されている暗号パスワードと照合する                       *
'**********************************************************************************************************
Public Function Pswd_Check(ByVal argInputUID As String, _
                           ByVal argSedai_no As String, _
                           ByRef argUserPass As String, _
                           ByVal Conn As ADODB.Connection, _
                           ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc
    
    Dim strSql As String

    '戻り値をFalseで初期化
    Pswd_Check = "False"
       
    strSql = ""
    strSql = strSql & "DECLARE"
    strSql = strSql & "    a_user_id             VARCHAR2(64) := '" & argInputUID & "';"
    strSql = strSql & "    a_sedai               NUMBER := " & argSedai_no & ";"
    strSql = strSql & "    a_user_pswd           VARCHAR2(64) := '" & argUserPass & "';"
    strSql = strSql & "    key_str               VARCHAR2(8) := 'namnamna';"
    strSql = strSql & "    encrypted_str         VARCHAR2(64);"
    strSql = strSql & "    val_sec_password      VARCHAR2(64);"
    strSql = strSql & "    key_not_found         EXCEPTION;"
    strSql = strSql & "    rec_cnt               NUMBER;"
    strSql = strSql & "    err_num               NUMBER;"
    strSql = strSql & "    err_msg               VARCHAR2(100);"
    strSql = strSql & "BEGIN"
    strSql = strSql & "    val_sec_password := RPAD(a_user_pswd, 64, ' ');"
    strSql = strSql & "    dbms_obfuscation_toolkit.DESEncrypt( input_string => val_sec_password, key_string => key_str, encrypted_string => encrypted_str );"
    strSql = strSql & "    SELECT count(*) INTO rec_cnt FROM kyk_login_pswd_zokusei "
    strSql = strSql & "        WHERE  user_id  = a_user_id "
    strSql = strSql & "          AND  sedai_no = a_sedai "
    strSql = strSql & "          AND  pswd = encrypted_str ;"
    strSql = strSql & "    IF rec_cnt = 0 THEN"
    strSql = strSql & "        RAISE key_not_found;"
    strSql = strSql & "    END IF;"
    strSql = strSql & "END;"

    'SQL文実行
    'レコードセットオープン
    Conn.Execute strSql

    'エラーへ飛ばなければ、照合成功
    Pswd_Check = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション(エラーへ飛んだ場合は照合不整合)　  *
'*******************************************************
ErrProc:
    
    'SQLエラーと、単なるパスワード入力ミスとでエラー処理を分岐する
    If Err.Number = -2147217900 Then
        'SQLエラー(SQL文が何らかの原因で書き換わった時のみ通過するロジック)
        Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "Pswd_Check", Err.Number, _
                           "パスワード暗号化SQLエラー" & vbCrLf & Err.Description)
        Pswd_Check = "SYSTEM ERROR"
    Else
        'パスワード入力エラーの場合は何もしない
    End If
    
    On Error GoTo 0

End Function

'*************************************************************************************
'*ログインパラメータを取得する                                                       *
'*************************************************************************************
Public Function ParamGet1(ByVal argstrSql As String, _
                          ByVal argParam_Line As Long, _
                          ByRef argParam_Kbn As String, _
                          ByVal Conn As ADODB.Connection, _
                          ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc
    
    '戻り値をFalseで初期化
    ParamGet1 = "False"

    'SQL文実行
    'レコードセットオープン
    RS.Open argstrSql, Conn, adOpenForwardOnly, adLockOptimistic, adCmdText

    Do Until RS.EOF = True
        Select Case argParam_Line
            Case 1, 3, 4, 7  '1,3,4,7は数値パラメータ
                argParam_Kbn = IIf(IsNull(RS("num_param").Value), "0", RS("num_param").Value)
            Case 2, 5        '2,5は文字パラメータ
                argParam_Kbn = IIf(IsNull(RS("str_param").Value), "NULL", RS("str_param").Value)
        End Select
        RS.MoveNext
    Loop

    '戻り値Trueへ更新
    ParamGet1 = "True"

    'レコードセットクローズ
    RS.Close
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    If RS.State = 1 Then
        'RS.State = 1(オープン状態)のときはレコードセットクローズする
        RS.Close
    End If

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "ParamGet1", Err.Number, _
                       "パラメータ取得処理失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*************************************************************************************
'*ログインパラメータを取得する④　"同一パスワードのチェックをする履歴数"             *
'*************************************************************************************
Public Function ParamGet4(ByRef argPass_Rireki As String, _
                          ByVal Conn As ADODB.Connection, _
                          ByVal RS As ADODB.Recordset) As String
    
    On Error GoTo ErrProc
    
    Dim strSql As String

    '戻り値をFalseで初期化
    ParamGet4 = "False"

    strSql = ""
    strSql = strSql & " SELECT NUM_PARAM,STR_PARAM "
    strSql = strSql & " FROM   KYK_USER_KANRI_PARAM_ZOKUSEI "
    strSql = strSql & " WHERE  PARAMETER_CD = '070' "         '070=同一パスワードのチェックをする履歴数
    
    'SQL文実行
    'レコードセットオープン
    RS.Open strSql, Conn, adOpenForwardOnly, adLockOptimistic, adCmdText

    Do Until RS.EOF = True
        'もし、テーブル内容がNULLの場合、数値パラメータには"0"をセットする
        argPass_Rireki = IIf(IsNull(RS("num_param").Value), "0", RS("num_param").Value)
        RS.MoveNext
    Loop

    '戻り値Trueへ更新
    ParamGet4 = "True"
            
    'レコードセットクローズ
    RS.Close
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    If RS.State = 1 Then
        'RS.State = 1(オープン状態)のときはレコードセットクローズする
        RS.Close
    End If

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "ParamGet4", Err.Number, _
                       "パラメータ取得処理失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*************************************************************************************
'*パスワード入力を誤ったので、ERR_COUNTを1UPする                       　　　　　　　*
'*argLock_Count回目の失敗であれば、SEC_ROCK_KBNを"1"へ更新する　　　　　　　　　　　 *
'*************************************************************************************
Public Function ErrCount_Up(ByVal argErr_Count As String, _
                            ByVal argLock_Count As String, _
                            ByVal argUSER_ID As String, _
                            ByVal argKengenKbn As String, _
                            ByVal argLock_Umu As String, _
                            ByVal Conn As ADODB.Connection, _
                            ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc

    Dim strSql As String

    '戻り値をFalseで初期化
    ErrCount_Up = "False"
    
    strSql = ""
    strSql = strSql & " update kyk_login_user_zokusei "
    strSql = strSql & " set pswd_machigai_cnt = '" & argErr_Count & "',"
    strSql = strSql & "     kohshin_ymd       = sysdate "
    'パスワード入力をargLock_Count回失敗した場合、SEC_ROCK_KBNを"1"(ロック)へ更新する
    If argErr_Count >= argLock_Count And _
       argKengenKbn = "U" And _
       argLock_Umu = "1" Then
        strSql = strSql & " , lock_kbn = '1'"
        strSql = strSql & " , lock_ymd = sysdate"
    End If
    strSql = strSql & " where user_id = '" & argUSER_ID & "'"

    'SQL文実行
    Conn.Execute strSql

    '戻り値Trueへ更新
    ErrCount_Up = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "ErrCount_Up", Err.Number, _
                       "ERR COUNTUP処理失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0

End Function

'*************************************************************************************
'*ユーザID、パスワードが正常であることが確認できたのでERR_COUNTをクリアする　　　　　*
'*************************************************************************************
Public Function ErrCOUNT_Clear(ByVal argUSER_ID As String, _
                               ByVal Conn As ADODB.Connection, _
                               ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc
    
    Dim strSql As String

    '戻り値をFalseで初期化
    ErrCOUNT_Clear = "False"
    
    strSql = ""
    strSql = strSql & " update kyk_login_user_zokusei "
    strSql = strSql & " set pswd_machigai_cnt = '0',"
    strSql = strSql & "     saishu_login_ymd = sysdate "
    strSql = strSql & " where user_id = '" & argUSER_ID & "'"

    'SQL文実行
    Conn.Execute strSql

    '戻り値Trueへ更新
    ErrCOUNT_Clear = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "ErrCOUNT_Clear", Err.Number, _
                       "ErrCOUNT_Clear処理失敗しました。" & vbCrLf & Err.Description)
    On Error GoTo 0
    
End Function

'*************************************************************************************
'*ユーザの操作履歴をログテーブルに出力する                                 　　　　　*
'*************************************************************************************
Public Function UserLog_Out(ByVal argUSER_ID As String, _
                            ByVal argErr_Count As String, _
                            ByVal argLog_Kbn As String, _
                            ByVal Conn As ADODB.Connection, _
                            ByVal RS As ADODB.Recordset) As String
    On Error GoTo ErrProc
    
    '戻り値をFalseで初期化
    UserLog_Out = "False"
    
    Dim strSql        As String
    Dim strLogMessage As String
    Dim strlog_seq    As String

    'kyk_user_kanri_logのlog_seqを取得する
    strSql = ""
    strSql = strSql & " select Max(log_seq) log_seq "
    strSql = strSql & " from   kyk_user_kanri_log "
    strSql = strSql & " where  substr(log_ymd_hms,1,10) = substr(sysdate,1,10) "

    'SQL文実行
    'レコードセットオープン
    RS.Open strSql, Conn, adOpenForwardOnly, adLockOptimistic, adCmdText

    If RS.EOF = True Then
        strlog_seq = "0"
    Else
        '同一日時に発生するログの場合、log_seqを1カウントアップする。
        strlog_seq = IIf(IsNull(RS("log_seq").Value), "0", RS("log_seq").Value + 1)
    End If

    'レコードセットクローズ
    RS.Close

    'argLog_Kbnにより、テーブルに出力するメッセージを変える
    Select Case argLog_Kbn
    Case "1"
        strLogMessage = "パスワード入力ミス" & (CInt(argErr_Count) + 1) & "回目"
    Case "2"
        strLogMessage = "登録ユーザ以外のログイン"
    Case "3"
        strLogMessage = "ユーザをロック"
    Case "4"
        strLogMessage = "ロックユーザのログイン"
    Case "5"
        strLogMessage = "新パスワード登録"
    End Select
            
    strSql = ""
    strSql = strSql & " insert into kyk_user_kanri_log "
    strSql = strSql & " (log_ymd_hms, "
    strSql = strSql & "  log_seq, "
    strSql = strSql & "  sousa_user_id, "
    strSql = strSql & "  log_msg, "
    strSql = strSql & "  kohshin_pgm_id) "
    strSql = strSql & " VALUES( sysdate ,"
    strSql = strSql & "  '" & strlog_seq & "',"
    strSql = strSql & "  '" & argUSER_ID & "',"
    strSql = strSql & "  '" & strLogMessage & "',"
    strSql = strSql & "  '国内顧問料計算システム　契約管理メニュー') "

    'SQL文実行
    Conn.Execute strSql

    '戻り値Trueへ更新
    UserLog_Out = "True"
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    If RS.State = 1 Then
        'RS.State = 1(オープン状態)のときはレコードセットクローズする
        RS.Close
    End If

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "UserLog_Out", Err.Number, _
                       "UserLog_Out処理失敗しました。" & vbCrLf & Err.Description)

    On Error GoTo 0

End Function

'*************************************************************************************
'*システム日付を取得する　                                                           *
'*************************************************************************************
Public Function DateGet(ByRef strSysDate As String, _
                        ByVal Conn As ADODB.Connection, _
                        ByVal RS As ADODB.Recordset) As String
    
    On Error GoTo ErrProc
    
    Dim strSql As String

    '戻り値をFalseで初期化
    DateGet = "False"

    strSql = ""
    strSql = strSql & " SELECT TO_CHAR(SYSDATE,'YYYY/MM/DD') AS SYS_DATE "
    strSql = strSql & " FROM   DUAL "
    
    'SQL文実行
    'レコードセットオープン
    RS.Open strSql, Conn, adOpenForwardOnly, adLockOptimistic, adCmdText

    Do Until RS.EOF = True
        'もし、テーブル内容がNULLの場合、数値パラメータには"0"をセットする
        strSysDate = RS("SYS_DATE").Value
        RS.MoveNext
    Loop

    '戻り値Trueへ更新
    DateGet = "True"
            
    'レコードセットクローズ
    RS.Close
    
    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    If RS.State = 1 Then
        'RS.State = 1(オープン状態)のときはレコードセットクローズする
        RS.Close
    End If

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "DateGet", Err.Number, _
                       "システム日付取得処理に失敗しました。" & vbCrLf & Err.Description)

    On Error GoTo 0

End Function

'***************************************************
'*管理者Bookをオープンする　                       *
'***************************************************
Public Function ManagementBookOpen() As String

    On Error GoTo ErrProc
    
    Dim xlApp As Object
    
    '戻り値をFalseで初期化
    ManagementBookOpen = "False"
    
    Set xlApp = CreateObject("excel.application")
    'メッセージを非表示にする
    Application.DisplayAlerts = False
    
    With xlApp
        .Visible = True
        .Workbooks.Open FileName:=Application.ThisWorkbook.Path & "\ユーザアカウント管理.xls", Password:="nam", UpdateLinks:=3
    End With
    
    xlApp.Quit
    Set xlApp = Nothing
    'メッセージを表示にする
    Application.DisplayAlerts = True

    '戻り値Trueへ更新
    ManagementBookOpen = "True"

    Exit Function

'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrProc:

    Call subErrProcess(ThisWorkbook.Name & "." & "basUserCommon", "ManagementBookOpen", Err.Number, _
                       "管理者メニューのオープンに失敗しました。" & vbCrLf & Err.Description)

    On Error GoTo 0

End Function

'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gblnDbBeginTrans
'機能            :   ﾄﾗﾝｻﾞｸｼｮﾝを開始し、ﾄﾗﾝｻﾞｸｼｮﾝ開始ﾌﾗｸﾞをｵﾝにする
'
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/19         K.Furuya<SRA>      新規作成
'===============================================================================
Public Function gblnDbBeginTrans( _
                                 ByVal Conn As ADODB.Connection, _
                                 ByVal RS As ADODB.Recordset) As Boolean
    Dim sMsg                As String

    gblnDbBeginTrans = False
    
    On Error GoTo gblnDbBeginTransError
    
    'ﾄﾗﾝｻﾞｸｼｮﾝを開始する
    Conn.BeginTrans
    
    
    gblnDbBeginTrans = True
    
    Exit Function

gblnDbBeginTransError:
    
End Function

'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gblnDbRollback
'機能            :   ﾛｰﾙﾊﾞｯｸを実行し、ﾄﾗﾝｻﾞｸｼｮﾝ開始ﾌﾗｸﾞをｵﾌにする
'
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/19         K.Furuya<SRA>      新規作成
'===============================================================================
Public Function gblnDbRollback( _
                               ByVal Conn As ADODB.Connection, _
                               ByVal RS As ADODB.Recordset) As Boolean
    
    gblnDbRollback = False
    
    On Error GoTo gblnDbRollbackError
    
    'ﾄﾗﾝｻﾞｸｼｮﾝをﾛｰﾙﾊﾞｯｸする
    Conn.RollbackTrans
    
    gblnDbRollback = True
    
    Exit Function

gblnDbRollbackError:

End Function

'===============================================================================
'ﾌﾟﾛｼｰｼﾞｬ名称    :   gblnDbCommitTrans
'機能            :   ｺﾐｯﾄを実行し、ﾄﾗﾝｻﾞｸｼｮﾝ開始ﾌﾗｸﾞをｵﾌにする
'
'日付               担当　              修正内容
'-------------------------------------------------------------------------------
'2008/05/19         k.Furuya<SRA>        新規作成
'===============================================================================
Public Function gblnDbCommitTrans( _
                                  ByVal Conn As ADODB.Connection, _
                                  ByVal RS As ADODB.Recordset) As Boolean
    Dim sMsg                As String

    gblnDbCommitTrans = False
    
    On Error GoTo gblnDbCommitTransError
    
    'ﾄﾗﾝｻﾞｸｼｮﾝをｺﾐｯﾄする
    Conn.CommitTrans
    
    gblnDbCommitTrans = True
    
    Exit Function

gblnDbCommitTransError:

End Function
