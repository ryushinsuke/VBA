Attribute VB_Name = "basCheck"
Option Explicit

'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー算出システム・月次未払計上
'
'オブジェクト名：basCheck
'
'機能概要　：チェック機能
'
'更新履歴　：2008/07/02 SRA 吉井        新規作成
'
'*************************************************************************

Private Const MODULE_NAME As String = "basCheck"

'*************************************************************************
'
'関数名　　：日付チェック
'
'引　数　　：ARG1   I   Target      String 入力テキスト
'　　　　　：ARG2   I   Msg1        String メッセージ出力時の項目名
'
'戻り値　　：Long       エラー番号
'
'機能説明　：YYYYMM形式の日付チェックを行う。
'
'更新履歴　：2008/07/03 SRA 吉井        新規作成
'
'*************************************************************************
Public Function fncChkDateFormat(ByVal Target As String, ByVal Msg1 As String) As Long

    Const PROCEDURE_NAME As String = "fncChkDateFormat"

On Error GoTo ErrHandler
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkDateFormat = 0
    
    Select Case Len(Target)
        Case LEN_NULL
            '24000 未入力です。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 24000, Msg1, , True
            fncChkDateFormat = 24000
            Exit Function
            
        Case LEN_DATE
            If IsDate(Format(Target & "01", "####/##/##")) Then
            Else
                '形式不正
                '24001 日付として認識できません。YYYYMM形式で入力して下さい。
                fncKsCmnMessageDisp MSGLVL_WARNNING, 24001, Msg1, , True
                fncChkDateFormat = 24001
                Exit Function
            End If
            
        Case Else
            '桁数不正
            '24002 入力桁数が不正です。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 24002, Msg1, MSG2_LEN_KIJYUNBI, True
            fncChkDateFormat = 24002
            Exit Function
    End Select
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    fncChkDateFormat = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function

'*************************************************************************
'
'関数名　　：フォルダチェック
'
'引　数　　：ARG1   I   Target      String 入力テキスト
'　　　　　：ARG2   I   Msg1        String メッセージ出力時の項目名
'
'戻り値　　：Long       エラー番号
'
'機能説明　：金額のチェックを行う。
'
'更新履歴　：2008/07/03 SRA 吉井        新規作成
'
'*************************************************************************
Public Function fncChkFolder(ByVal Target As String, ByVal Msg1 As String) As Long

    Const PROCEDURE_NAME As String = "fncChkFolder"

On Error GoTo ErrHandler
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkFolder = 0
    
    If Trim(Target) = "" Then
        '24006 未入力です。
        fncKsCmnMessageDisp MSGLVL_WARNNING, 24006, Msg1, , True
        fncChkFolder = 24006
        Exit Function
    End If
    
    If Dir(CStr(Target), vbDirectory) = "" Then
        '形式不正
        '24003 入力に誤りがあります。
        fncKsCmnMessageDisp MSGLVL_WARNNING, 24003, Msg1, , True
        fncChkFolder = 24003
        Exit Function
    End If

    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    fncChkFolder = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function
