Attribute VB_Name = "basIni"
Option Explicit

'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー算出システム・月次未払計上
'
'オブジェクト名：basIni
'
'機能概要　：アプリケーション初期処理
'
'更新履歴　：2008/07/02 SRA 吉井        新規作成
'
'*************************************************************************

Private Const MODULE_NAME As String = "basIni"

'*************************************************************************
'
'関数名　　：起動処理
'
'引　数　　：なし
'
'戻り値　　：なし
'
'機能説明　：アプリケーション起動処理
'
'更新履歴　：2008/07/02 SRA 吉井        新規作成
'
'*************************************************************************
Public Sub subInit()
Attribute subInit.VB_ProcData.VB_Invoke_Func = "i\n14"

    Const PROCEDURE_NAME As String = "subInit"

On Error GoTo ErrHandler
    
    Dim lngRet  As Long
    Dim strMsg  As String
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Application.DisplayAlerts = False
    Application.ScreenUpdating = False
    
    Do
        lngRet = fncIniBook
        If lngRet <> 0 Then
            Exit Do
        End If
        
        strMsg = vbNullString
    
    Loop Until True
    
    Application.ScreenUpdating = True
    
    If strMsg <> vbNullString Then
        'MsgBox strMsg, vbExclamation, MSG_TTL_SYS
        '1 システムエラー
        fncKsCmnMessageDisp MSGLVL_FATAL, 1, 1, , True
    End If
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Sub

ErrHandler:
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Sub

'*************************************************************************
'
'関数名　　：ブックの初期化
'
'引　数　　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：リリースされた状態に戻す。
'
'更新履歴　：2008/07/03 SRA 吉井        新規作成
'
'*************************************************************************
Public Function fncIniBook() As Long

    Const PROCEDURE_NAME As String = "fncIniBook"

On Error GoTo ErrHandler
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    '初期設定
    With Sheets(SHEET_MAIN)
        .chkKeisan.Value = True                     '計算チェックボックスはＯＮ
        .Range(RNG_KIJYUNBI).Value = vbNullString   '基準年月
        .Range(RNG_GI_NO).Value = vbNullString      '外部委託先
        .Range(RNG_FOLDER).Value = INITVAL_FOLDER   '保存フォルダはデフォルト値セット
        .Range(RNG_KIJYUNBI).Select                 '基準年月を初期項目選択
    End With

    '外部委託先のデータ取得（コンボ項目の初期化）
    If fncGet_GI_INFO = False Then
        Exit Function
    End If
    
    '外部委託先のセル範囲名前定義
    Call subNameDefine
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function

'*************************************************************************
'
'関数名　　：コード値テーブル抽出処理（契約区分 = 外部委託先）
'
'引　数　　：なし
'
'戻り値　　：Boolean（True:異常なし False:異常検出）
'
'機能説明　：コード値テーブルからコンボ項目を取得し「data」シートに記入
'
'更新履歴　：2008/07/03 SRA吉井   新規作成
'
'*************************************************************************
Private Function fncGet_GI_INFO() As Boolean

    Const PROCEDURE_NAME As String = "fncGet_GI_INFO"

On Error GoTo ErrHandler
    
    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String
    Const strConnErrCd As String = "この操作を実行する"
    Dim blnRet      As Boolean
    
    fncGet_GI_INFO = False

    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "       CODE_VALUE || ':' || VALUE_NAME"
    strSql = strSql & " FROM  KYK_CODE_VALUE"
    strSql = strSql & " WHERE CODE_NO = '56' AND SAKUJO_FLG = '0'"
    strSql = strSql & " ORDER BY CODE_VALUE"

    'データ取得
    strErrCode = strConnErrCd
    blnRet = gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    If Not blnRet Then
        If strErrCode = strConnErrCd Then   'DB接続エラー
        '接続失敗(メッセージテーブルは使用不可注意)
            Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "fncGet_GI_INFO", 0, _
            "ＤＢ接続エラー。設定を確認してください。")
            Exit Function
        Else                                'その他エラー
            Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "fncGet_GI_INFO", 0, _
            "システムエラー。管理者に連絡してください。")
            Exit Function
        End If
    End If
    
    With ThisWorkbook.Worksheets(SHEET_DATA)
        .Activate
        '「外部委託先」のデータセル範囲をクリア
        .Range("A2:A65536").ClearContents
        '取得データをセルに設定
        For lngCnt = 0 To lngRow - 1
             .Cells(3 + lngCnt, 1).Value = vData(0, lngCnt)
        Next lngCnt
    End With
    
    Erase vData
    
    fncGet_GI_INFO = True
    
    Exit Function

ErrHandler:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    Erase vData
End Function

'*************************************************************************
'
'関数名　　：外部委託先のセル範囲名前定義設定
'
'引　数　　：なし
'
'戻り値　　：なし
'
'機能説明　：外部委託先のセル範囲名前定義を再設定する。
'
'更新履歴　：2008/07/03 SRA吉井   新規作成
'
'*************************************************************************
Private Sub subNameDefine()

    Dim rowcnt  As Long

    '外部委託先のセル範囲の確認
    For rowcnt = 3 To 65536
        If ThisWorkbook.Worksheets(SHEET_DATA).Cells(rowcnt, 1).Value = "" Then
            rowcnt = rowcnt - 1
            Exit For
        End If
    Next rowcnt
    
    '外部委託先の名前定義の再設定
    ThisWorkbook.Names.Add Name:="外部委託先コードリスト", _
    RefersToR1C1:="=data!R2C1:R" & rowcnt & "C1"

End Sub
