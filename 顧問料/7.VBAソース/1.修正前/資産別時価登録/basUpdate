Attribute VB_Name = "basUpdate"
Option Explicit

'*************************************************************************
'
'プロジェクト名：契約管理システム・資産別時価登録
'
'オブジェクト名：basUpdate
'
'機能概要　：   時価情報更新
'
'更新履歴　：2006/07/06 SRA H.Anpo      新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "basUpdate"

'資産時価レコード
Private Type TypeRecKsj
    Kohza_no            As String   '口座番号
    Shisan_bunrui       As String   '資産分類
    Shisan_code         As String   '資産コード
    Kijun_ymd           As Long     '基準年月
    jika                As Currency '時価額
    Tohroku_ymd         As Date     '登録年月日
    Kohshin_ymd         As Date     '更新年月日
    Kohshin_pgm_id      As String   '更新プログラムID
    Kohshin_tantohsha   As String   '更新担当者
    Sakujo_flg          As String   '削除フラグ
End Type
Private mrecKsj As TypeRecKsj


'*************************************************************************
'関数名　　：時価情報チェック
'
'引　　数　：ARG1   O   ErrRow      long    エラー発生行番号
'　　　　　：ARG2   O   ErrCol      long    エラー発生列番号
'
'戻り値　　：エラー番号
'
'機能説明　：時価情報の基準日、時価額の妥当性をチェックする
'
'更新履歴　：2006/07/06 SRA H.Anpo      新規作成
'
'*************************************************************************
Public Function fncChkJikaInfo(ByRef ErrRow As Long, _
                            ByRef ErrCol As Long) As Long

    Const PROCEDURE_NAME As String = "fncChkJikaInfo"

    On Error GoTo ErrHandler
    
    Dim lngRow  As Long
    Dim lngCol  As Long
    Dim TopRow  As Long
    Dim lngCnt  As Long
    Dim lngRet  As Long
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkJikaInfo = 0
    
    '変更チェック
    '検索・表示ボタン押下後、口座番号、基準日、組入資産選択行が
    '変更されたかをチェックする
    lngRet = fncChkDataChange(ErrRow, ErrCol)
    If lngRet <> 0 Then
        fncChkJikaInfo = lngRet
        Exit Function
    End If
    
    With Sheets(SHEET_MAIN)
        TopRow = fncKsCmnSearchDataTopRow(2)
        lngRow = TopRow
'=== 2006/12/21 Modified By H.Anpo Start
'        Do While .Cells(lngRow, COLN_LINKCELL) <> vbNullString
        Do While .Cells(lngRow, COLN_DATA_FLAG) <> vbNullString
'=== 2006/12/21 Modified By H.Anpo End
            
            '時価額チェック
            lngRet = fncChkJikagakuFormat(.Cells(lngRow, COLN_J_JIKAGAKU), MSG1_JIKA_JIKA)
            If lngRet <> 0 Then
                ErrRow = lngRow
                ErrCol = COLN_J_JIKAGAKU
                fncChkJikaInfo = lngRet
                Exit Function
            End If
            
            lngRow = lngRow + 1
        Loop
    End With
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
ErrHandler:
    fncChkJikaInfo = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function



'*************************************************************************
'関数名　　：更新処理
'
'引　　数　：ARG1   I   Kouza_no    Strinng 口座番号
'　　　　　：ARG2   I   Sisan_bun   Strinng 資産分類
'　　　　　：ARG3   I   Sisan_cd    Strinng 資産コード
'
'戻り値　　：エラー番号
'
'機能説明　：時価情報を基に資産時価テーブルを更新する
'
'更新履歴　：2006/07/06 SRA H.Anpo      新規作成
'更新履歴　：2008/01/23 SRA Y.Azuma     変更     (PH4)特殊文字バグ対応
'
'*************************************************************************
Public Function fncExecUpdate( _
                    ByVal Kouza_no As String, _
                    ByVal Sisan_bun As String, _
                    ByVal Sisan_cd As String) As Long

    Const PROCEDURE_NAME As String = "fncExecUpdate"

    On Error GoTo ErrHandler
    
    Dim strSql      As String
    Dim recSel      As ADODB.Recordset
    Dim lngRow      As Long
    
    Dim vntRec()    As Variant
    Dim lngCol      As Long
    Dim lngRecCnt   As Long
    Dim strErrCd    As String
    Dim blnRet      As Boolean
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncExecUpdate = 0
        
    'トランザクション開始
    fncKsCmnBeginTrans
        
    '時価情報データの先頭行取得
    lngRow = fncKsCmnSearchDataTopRow(2)
        
    With mrecKsj

'=== 2006/12/21 Modified By H.Anpo Start
'        Do While Sheets(SHEET_MAIN).Cells(lngRow, COLN_LINKCELL) <> vbNullString
        Do While Sheets(SHEET_MAIN).Cells(lngRow, COLN_DATA_FLAG) <> vbNullString
'=== 2006/12/21 Modified By H.Anpo End
        
            .Kohza_no = Kouza_no                        '口座番号
            .Shisan_bunrui = Sisan_bun                  '資産分類
            .Shisan_code = Mid(Sisan_cd, 2, 4)          '資産コード
'            .jika = Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_JIKAGAKU)) '時価額
            .Kijun_ymd = Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_KIJYUNBI)  '基準年月日
            .Tohroku_ymd = Format(Now, "YYYY/MM/DD")    '登録年月日
            .Kohshin_ymd = Format(Now, "YYYY/MM/DD")    '更新年月日
            .Kohshin_pgm_id = PROGRAM_ID                '更新プログラムID
            .Kohshin_tantohsha = USER_ID                '更新担当者
                                                        
            If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_JIKAGAKU)) = vbNullString Then
                .Sakujo_flg = "1"                       '削除フラグ
            Else
                .jika = Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_JIKAGAKU)) '時価額
                .Sakujo_flg = "0"                       '削除フラグ
            End If
            
            'ODBCエラーのコードをセット
            '[Oracle][ODBC][Ora]ORA-00054: リソース・ビジー、NOWAITが指定されていました。
            strErrCd = ODBC_ERRCD_RESOURCE_BUSY
            
            strSql = ""
            strSql = strSql & " SELECT"
            strSql = strSql & "     KSJ.*　"
            strSql = strSql & " FROM"
            strSql = strSql & "     KYK_SHISAN_BETSU_SHINTAKU_JIKA KSJ "
            strSql = strSql & " WHERE"
'------------<Modify Start azuma 2008/01/23 (PH4)> ★★★☆-----------------------
'            strSql = strSql & "     KSJ.KOHZA_NO        = '" & .Kohza_no & "'"
            strSql = strSql & "     KSJ.KOHZA_NO        = '" & func_ChkQuoteshon(.Kohza_no, "'") & "'"
'------------<Modify End   azuma 2008/01/23 (PH4)> ★★★☆-----------------------
            strSql = strSql & " AND KSJ.SHISAN_BUNRUI   = '" & .Shisan_bunrui & "'"
            strSql = strSql & " AND KSJ.SHISAN_CODE     = '" & .Shisan_code & "'"
            strSql = strSql & " AND KSJ.KIJUN_YMD       = '" & .Kijun_ymd & "'"
            strSql = strSql & " FOR UPDATE NOWAIT"
'                strSql = strSql & " FOR UPDATE "
            blnRet = fncKsCmnDoExequteSql(mainmenu.gcSELECT, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
            If blnRet = False Then
                If strErrCd = ODBC_ERRCD_RESOURCE_BUSY Then
                    'リソース・ビジー
                    '3023 他のユーザーにより更新されています。
                    fncKsCmnMessageDisp MSGLVL_WARNNING, 3023, MSG1_JIKAINF, "", True
                    fncExecUpdate = 3023
                Else
                    fncExecUpdate = strErrCd
                End If
            
                'ロールバック
                GoTo RollBackExit
            End If
            
            If lngRecCnt > 1 Then
                '更新対象データが資産時価に複数件ある
                '3003 該当データが複数件存在します。
                fncKsCmnMessageDisp MSGLVL_WARNNING, 3003, MSG1_JIKAINF, MSG2_MANY_ROWS_SJ, True
                fncExecUpdate = 3003
                'ロールバック
                GoTo RollBackExit
            End If
        
            If lngRecCnt = 0 Then
                If Not Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_JIKAGAKU)) = vbNullString Then
                    strSql = ""
                    strSql = strSql & " INSERT INTO"
                    strSql = strSql & "     KYK_SHISAN_BETSU_SHINTAKU_JIKA ("
                    strSql = strSql & "         KOHZA_NO, "
                    strSql = strSql & "         SHISAN_BUNRUI,"
                    strSql = strSql & "         SHISAN_CODE, "
                    strSql = strSql & "         KIJUN_YMD,"
                    strSql = strSql & "         JIKA,"
                    strSql = strSql & "         TOHROKU_YMD,"
                    strSql = strSql & "         KOHSHIN_YMD,"
                    strSql = strSql & "         KOHSHIN_PGM_ID,"
                    strSql = strSql & "         KOHSHIN_TANTOHSHA,"
                    strSql = strSql & "         SAKUJO_FLG"
                    strSql = strSql & "     ) VALUES("
'------------<Modify Start azuma 2008/01/23 (PH4)> ★★★☆-----------------------
'                    strSql = strSql & "         '" & .Kohza_no & "',"
                    strSql = strSql & "         '" & func_ChkQuoteshon(.Kohza_no, "'") & "',"
'------------<Modify End   azuma 2008/01/23 (PH4)> ★★★☆-----------------------
                    strSql = strSql & "         '" & .Shisan_bunrui & "',"
                    strSql = strSql & "         '" & .Shisan_code & "',"
                    strSql = strSql & "          " & .Kijun_ymd & ","
                    strSql = strSql & "          " & .jika & ","
                    strSql = strSql & "         TO_CHAR(SYSDATE,'YYYY/MM/DD'),"
                    strSql = strSql & "         TO_CHAR(SYSDATE,'YYYY/MM/DD'),"
                    strSql = strSql & "         '" & .Kohshin_pgm_id & "',"
                    strSql = strSql & "         '" & .Kohshin_tantohsha & "',"
                    strSql = strSql & "         '" & .Sakujo_flg & "'"
                    strSql = strSql & "     )"
                End If
                blnRet = fncKsCmnDoExequteSql(mainmenu.gcINSERT, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
                If blnRet = False Then
                    fncExecUpdate = strErrCd
                    'ロールバック
                    GoTo RollBackExit
                End If
            
            Else
                strSql = ""
                strSql = strSql & " UPDATE KYK_SHISAN_BETSU_SHINTAKU_JIKA KSJ"
                strSql = strSql & " SET"
                If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_JIKAGAKU)) = vbNullString Then
                    strSql = strSql & "     KSJ.JIKA                = NULL,"
                Else
                    strSql = strSql & "     KSJ.JIKA                = " & .jika & ","
                End If
                strSql = strSql & "     KSJ.KOHSHIN_YMD         = TO_CHAR(SYSDATE,'YYYY/MM/DD'),"
                strSql = strSql & "     KSJ.KOHSHIN_PGM_ID      = '" & .Kohshin_pgm_id & "',"
                strSql = strSql & "     KSJ.KOHSHIN_TANTOHSHA   = '" & .Kohshin_tantohsha & "',"
                strSql = strSql & "     KSJ.SAKUJO_FLG          = '" & .Sakujo_flg & "'"
                strSql = strSql & " WHERE"
'------------<Modify Start azuma 2008/01/23 (PH4)> ★★★☆-----------------------
'                strSql = strSql & "     KSJ.KOHZA_NO            = '" & .Kohza_no & "'"
                strSql = strSql & "     KSJ.KOHZA_NO            = '" & func_ChkQuoteshon(.Kohza_no, "'") & "'"
'------------<Modify End   azuma 2008/01/23 (PH4)> ★★★☆-----------------------
                strSql = strSql & " AND KSJ.SHISAN_BUNRUI       = '" & .Shisan_bunrui & "'"
                strSql = strSql & " AND KSJ.SHISAN_CODE         = '" & .Shisan_code & "'"
                strSql = strSql & " AND KSJ.KIJUN_YMD           = '" & .Kijun_ymd & "'"
                blnRet = fncKsCmnDoExequteSql(mainmenu.gcUPDATE, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
                If blnRet = False Then
                    fncExecUpdate = strErrCd
                    'ロールバック
                    GoTo RollBackExit
                End If
            End If
            
            lngRow = lngRow + 1
        Loop
    End With
    
    'コミット
    fncKsCmnCommitTrans
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
ErrHandler:
    fncExecUpdate = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
RollBackExit:
    'ロールバック
    fncKsCmnRollbackTrans
End Function



'*************************************************************************
'関数名　　：更新ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：更新ボタン処理
'
'更新履歴　：2006/07/06 SRA H.Anpo    新規作成
'            2006/11/22 SRA T.Sato    口座トータル更新対応
'
'*************************************************************************
Function fncUpdate() As Long
    
    Const PROCEDURE_NAME As String = "fncUpdate"
    
On Error GoTo ErrorHandler

    Dim lngRet      As Long
    Dim lngRow      As Long
    Dim lngCol      As Long
    Dim strErrMsg   As String
    Dim strTitle    As String
    
    Dim strKouzaNo  As String
    Dim strSisanBun As String
    Dim strSisanCd  As String

    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)

    fncUpdate = 0
    
    '入力チェック
    lngRet = fncChkJikaInfo(lngRow, lngCol)
    If lngRet <> 0 Then
        Sheets(SHEET_MAIN).Cells(lngRow, lngCol).Select
        fncUpdate = lngRet
        Exit Function
    End If
    
    '更新処理
    With Sheets(SHEET_MAIN)
        strKouzaNo = .Range(RNG_SV_KOUZA_NO).Value
        lngRet = fncChkOptionButton(lngRow)
        If lngRet <> 0 Then
            fncUpdate = lngRet
            Exit Function
        End If
        strSisanBun = .Cells(lngRow, COLN_SHISAN_BUNRUI_CODE)
        strSisanCd = .Cells(lngRow, COLN_SHISAN_CODE)
    End With
    
'***** UPDATE T.Sato 2006/11/22 *****
'口座トータルも更新可能に変更
'    If (strSisanBun = "0" And strSisanCd <> "00000") Or _
'       (strSisanBun = "A") Or _
'       (strSisanBun = "B") _
'    Then
    If (strSisanBun = "0") Or _
       (strSisanBun = "A") Or _
       (strSisanBun = "B") _
    Then
'***** UPDATE T.Sato End *****
        '3020 更新確認
        If MsgBox(fncKsCmnMessageDisp(MSGLVL_QUESTION, 3020, "", , False), vbQuestion + vbYesNo, "確認") = vbNo Then
            Exit Function
        End If
    
        '更新実行
        lngRet = fncExecUpdate( _
                    strKouzaNo, _
                    strSisanBun, _
                    strSisanCd)
        If lngRet <> 0 Then
            fncUpdate = lngRet
            Exit Function
        End If
        
        '3004 正常終了メッセージ
        fncKsCmnMessageDisp MSGLVL_INFO, 3004, "", , True
    Else
        '3022 更新対象の資産ではありません。
        fncKsCmnMessageDisp MSGLVL_WARNNING, 3022, "", MSG2_UP_SISAN, True
        fncUpdate = 3022
        Exit Function
    End If
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrorHandler:
    fncUpdate = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    
End Function


