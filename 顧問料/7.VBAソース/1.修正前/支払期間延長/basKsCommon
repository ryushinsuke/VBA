Attribute VB_Name = "basKsCommon"
Option Explicit

'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー計算システム
'
'オブジェクト名：basKsCommon
'
'機能概要　：プロジェクト内共通モジュール
'
'更新履歴　：2008/06/17 SRA T.Ichikawa      新規作成
'
'*************************************************************************
Const MODULE_NAME = "basKsCommon"

'*************************************************************************
'プロジェクト内定数
'*************************************************************************
'=======================================
'シート名称の定義
'=======================================
Public Const MAIN_SHEETNAME = "main"
Public Const TEMPLATE_SHEETNAME = "Template"
Public Const DATA_SHEETNAME = "Data"

'=======================================
'チェックボックスの定義
'=======================================
Public Const mCELLNAME = "B"                                        'チェックボックスの表示位置
Public Const mLINKCELLNAME = "AD"                                   'チェックボックスリンクセルの位置(チェックボックスの値を格納)

'=======================================
'コート値テーブルのコードNO定義
'=======================================
Public Const mC_CODENO_KEIYAKU_KBN = "56"                           '契約区分
Public Const mC_CODENO_SHOUHI_ZEI_FLG = "58"                        '消費税フラグ
Public Const mC_CODENO_GENSEN_ZEI_FLG = "59"                        '源泉税フラグ
Public Const mC_CODENO_NENKAN_NISSU_KBN = "7"                       '年間日数区分

'=======================================
'Templateシートの列番号定義
'=======================================
Public Const mC_DATASHEET_COLNO_KEIYAKU_KBN = 1                     '契約区分
Public Const mC_DATASHEET_COLNO_END = 1                             '最終列番号

'=======================================
'Templateシートのリストの名前定義
'=======================================
Public Const mC_NAME_LIST_KEIYAKU_KBN = "契約区分リスト"            '契約区分リスト
    
'=======================================
'初期表示値の定義
'=======================================
Public Const mC_INIT_VALUE_GINO = ""                          '外部委託先の初期値

'*************************************************************************
'関数名　　：明細行追加(複数行追加)メイン処理
'
'引　　数　：TargetSheets  I            対象シート
'            iAddRowCnt    I            追加行数
'            iSrcRow       I            複写元ROW
'
'戻り値　　：Long       エラー番号
'
'機能説明　：指定された明細行の追加を行う。
'
'更新履歴　：2008/06/17 SRA T.Ichikawa    新規作成
'
'*************************************************************************
Public Function gKsC_fncKsCmnInsSumRow( _
                    TargetSheets As Object, _
                    iAddRowCnt As Long, _
                    iSrcRow As Long) As Long
    
On Error GoTo ErrorHandler

    Dim lngRowCnt   As Long

    gKsC_fncKsCmnInsSumRow = 0
    
    With TargetSheets
        ThisWorkbook.Sheets(TEMPLATE_SHEETNAME).Rows(iSrcRow & ":" & iSrcRow).Copy
        .Select
        .Rows(.Range("先頭行").Row + 1 & ":" & .Range("先頭行").Row + iAddRowCnt).Insert Shift:=xlDown
        ThisWorkbook.Application.CutCopyMode = False
    
        For lngRowCnt = .Range("先頭行").Row + 1 To Range("先頭行").Row + iAddRowCnt
            'チェックボックスの追加
            Call fncCmnSetCheckBox(TargetSheets, mCELLNAME & lngRowCnt, mLINKCELLNAME & lngRowCnt)
        Next lngRowCnt
        
        TargetSheets.Cells(lngRowCnt - 1, 2).Select
    
    End With
    
    Exit Function

ErrorHandler:
    gKsC_fncKsCmnInsSumRow = Err.Number
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gKsC_fncKsCmnInsSumRow", Err.Number, Err.Description)
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    On Error GoTo 0
    
End Function

'*************************************************************************
'関数名　　：明細行削除メイン処理
'
'引　　数　：TargetSheets I             対象シート
'
'戻り値　　：Long       エラー番号
'
'機能説明　：明細行の一括削除を行う。
'
'更新履歴　：2008/06/17 SRA T.Ichikawa    新規作成
'
'*************************************************************************
Public Function gKsC_fncKsCmnCheckRowDelete(TargetSheets As Object) As Long

On Error GoTo ErrorHandler

    gKsC_fncKsCmnCheckRowDelete = 0
        
    '全削除
    'チェックボックスの削除処理(一括削除)
    Call fncCmnDelCheckBox(TargetSheets)
    '明細行削除(一括削除)
    Call fncKsCmnDeleteRow(TargetSheets)
        
    Exit Function

ErrorHandler:
    gKsC_fncKsCmnCheckRowDelete = Err.Number
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gKsC_fncKsCmnCheckRowDelete", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：明細行削除処理
'
'引　　数　：TargetSheets I             対象シート
'
'戻り値　　：Long       エラー番号
'
'機能説明　：明細行の削除を行う。
'
'更新履歴　：2008/06/17 SRA T.Ichikawa    新規作成
'
'*************************************************************************
Private Function fncKsCmnDeleteRow(TargetSheets As Object) As Long
On Error GoTo ErrorHandler

    fncKsCmnDeleteRow = 0
    
    With TargetSheets

        '全行削除
        If .Range("最終行").Row - .Range("先頭行").Row > 1 Then
            '明細行の削除処理
            ThisWorkbook.Application.CutCopyMode = False
            .Rows(.Range("先頭行").Row + 1 & ":" & .Range("最終行").Row - 1).Delete Shift:=xlUp
        
            TargetSheets.Cells(.Range("最終行").Row, 2).Select
        End If
    
    End With

    Exit Function

ErrorHandler:
    fncKsCmnDeleteRow = Err.Number
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "fncKsCmnDeleteRow", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：チェックボックスの作成
'
'引　　数　：objTarget    I         対象シート
'  　　  　：CellName     I         作成対象セル ex) "B30"
'            LinkCellName I         リンクセル   ex) "AD30"
'
'戻り値　　：Long       エラー番号
'
'機能説明　：指定された行にチェックボックスを作成する
'
'更新履歴　：2008/06/17 SRA T.Ichikawa    新規作成
'
'*************************************************************************
Public Function fncCmnSetCheckBox(ByRef objTarget As Object _
                                , ByVal CellName As String _
                                , ByVal LinkCellName As String) As Long
    
On Error GoTo ErrorHandler
    fncCmnSetCheckBox = 0
    
    Dim lngZoom As Long
    Dim objX    As Object
    
    '倍率変更時対応
    '元の倍率を退避
    lngZoom = ThisWorkbook.Application.ActiveWindow.Zoom
    '倍率を100%に設定
    ThisWorkbook.Application.ActiveWindow.Zoom = 100
    
    '指定セルにチェックボックスを作成
    With objTarget.Range(CellName)
        Set objX = objTarget.Shapes.AddFormControl( _
            xlCheckBox, _
            .Left + 3, _
            .Top - 1, _
            10.5, _
            10)
        With objX.OLEFormat.Object
            .Display3DShading = True
            .Caption = ""
            .LinkedCell = LinkCellName
            .PrintObject = False
        End With
    End With

    '初期値をチェックありに設定
    objTarget.Range(LinkCellName) = True

    '倍率を元の倍率に設定
    ThisWorkbook.Application.ActiveWindow.Zoom = lngZoom
    
    Exit Function

ErrorHandler:
    fncCmnSetCheckBox = Err.Number
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "fncCmnSetCheckBox", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：チェックボックスの削除
'
'引　　数　：objTarget    I             対象シート
'
'戻り値　　：Long       エラー番号
'
'機能説明　：チェックボックスを削除する
'
'更新履歴　：2008/06/17 SRA T.Ichikawa    新規作成
'
'*************************************************************************
Public Function fncCmnDelCheckBox(ByRef objTarget As Object) As Long
Dim c As Object
    
On Error GoTo ErrorHandler
    
    fncCmnDelCheckBox = 0
    
    With objTarget
        For Each c In .Shapes
            If c.Type = msoFormControl Then
                'チェックボックスコントロールの場合
                If c.FormControlType = xlCheckBox Then
                    'チェックボックスを全削除
                    c.Delete
                End If
            End If
        Next
    End With

    Exit Function

ErrorHandler:
    fncCmnDelCheckBox = Err.Number
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "fncCmnDelCheckBox", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
End Function

