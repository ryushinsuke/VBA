Attribute VB_Name = "bas40Torikomi_5"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料計算システム・請求書作成指示
'
'オブジェクト名：bas40Torikomi_5
'
'機能概要　：特殊計算パターン請求書作成サブ・フェーズ３．５対応
'
'更新履歴　：2007/07/31 SRA 東       新規作成
'
'*************************************************************************
Const MODULE_NAME = "bas40Torikomi_5"


'*************************************************************************
'関数名　　：日次計算書のデータシート行追加関数
'
'引　　数　：
'           strFileName     I       対象ファイル名
'           lngRateMdfSeq   I       料率変更番号
'           lngSanteiCnt    I       算定資産順
'           Sisan()         I       資産配列の
'           lngSisan        I       資産配列の添え字
'           strKohzaList()  I       処理対象口座配列配列
'           lngErrorCnt     O       エラー件数
'
'戻り値　　：なし
'
'機能説明　：
'
'更新履歴　：2007/07/31 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub sub_Shintakujika_KeisanSho(ByVal strFileName As String _
                         , ByVal lngRateMdfSeq As Long _
                         , ByVal lngSanteiCnt As Long _
                         , ByRef Sisan() As Variant _
                         , ByVal lngSisan As Long _
                         , ByRef strKohzaList() As String _
                         , ByVal lngKKCnt As Long _
                         , ByRef lngErrorCnt As Long)
                     
On Error GoTo ErrHandler
                     
    Const PROCEDURE_NAME = "sub_Shintakujika_KeisanSho"
    Dim lngStartRow         As Long
    Dim lngEndRow           As Long
    Dim strKeisanKekka()     As String       '計算基礎数値のデータ格納配列
    Dim blnZentai           As Boolean      '適用期間内の合計で出力する場合
    Dim lngRet              As Long

'*******************************************************
'* 関数開始　                                        　*
'*******************************************************

    blnZentai = True


                    
    
    '「日次計算書」の貼付
    '−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−
    '行増幅数の取得
    '行増幅数の取得(基準月)
    lngRet = func_GetKeisanKekka(Sisan() _
                        , lngSisan _
                        , strKohzaList() _
                        , lngKKCnt _
                        , strKeisanKekka())
    If lngRet = 0 Then
        'エラー情報構造体セット
        With gusrErr
            .ModuleId = MODULE_NAME         'モージュールID
            .Procedure = PROCEDURE_NAME     'プロシージャID
            .MsgCode = ERR_MSGCD_VB         'メッセージ区分
            .ErrNum = Err.Number            'システムエラーコード
            .ErrDescript = Err.Description  'システムエラー内容
            .KohzaNo = strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO)
            .MsgNaiyou = "顧問料計算結果テーブルのデータ取得結果０件"   'メッセージ内容(ブックファイル名)
            .MsgSyousai = "日次計算書で顧問料計算結果の取得結果が０件か又は" & vbCrLf & _
                          "顧問料計算結果取得関数(func_GetKeisanKekka)でシステムエラーが発生しています。" & vbCrLf & _
                          "システム管理者に連絡して下さい。" & vbCrLf & _
                          "テンプレート　：" & DATASHEET_NICHIJI_KSAN_SHO & vbCrLf & _
                          "テーブル　　　：KYK_KOMONRYOH_KEISAN_KEKKA" & vbCrLf & _
                          "口座番号　　　：" & strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO) & vbCrLf & _
                          "請求期間ID　　：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYU_KIKAN_ID) & vbCrLf & _
                          "資産分類　　　：" & Sisan(lngSisan, SISANLIST_SHISAN_BUNRUI) & vbCrLf & _
                          "資産コード　　：" & Sisan(lngSisan, SISANLIST_SHISAN_CODE) & vbCrLf & _
                          "日付種類　　　：" & strKohzaList(0, KOHZALIST_HIZUKE_SHURUI) & vbCrLf & _
                          "計算ＴＯ日　　：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYUSHO_KIKAN_TO) & vbCrLf & _
                          "計算ＦＲＯＭ日：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYUSHO_KIKAN_FROM)                              'メッセージ詳細
            
            'エラーシート書き込み
            
            Call fncWriteErrSheet(ERROR_SHEETNAME)
        End With
        lngErrorCnt = lngErrorCnt + 1
        Exit Sub
    End If
    
    '「日次計算書」の貼付け
    Call sub_AddSheetItemRow(strFileName _
                           , DATASHEET_NICHIJI_KSAN_SHO _
                           , lngKKCnt _
                           , DATASHEET_INC_KIKAN & DATASHEET_INC_XXX _
                           , DATASHEET_INC_KIKAN & Format(lngRateMdfSeq, "000") _
                           , "" _
                           , lngStartRow _
                           , lngEndRow)
    
    
    '　行増幅と値設定
    Call sub_RowIncrement(strFileName _
                        , lngStartRow _
                        , lngEndRow _
                        , DATASHEET_INC_DANKAI & DATASHEET_INC_XX _
                        , strKeisanKekka())
    
    '条件への貼付け
    Call sub_SetJoukenData(strFileName, Sisan(), lngSisan, lngStartRow, blnZentai)
    
    '−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−

    Erase strKeisanKekka
    
    Exit Sub

ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "sub_Shintakujika_KeisanSho", Err.Number, Err.Description)
    On Error GoTo 0
    
    Erase strKeisanKekka

'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub

'*************************************************************************
'関数名　　：顧問料計算結果取得関数
'
'引　　数　：
'           Sisan()         I       資産配列の
'           lngSisan        I       資産配列の添え字
'           strKohzaList()  I       処理対象口座配列配列
'           lngKKCnt        I       処理対象口座配列配列の添え字
'           strData()       O       合成料率の取得結果
'
'戻り値　　：件数
'
'機能説明　：日次計算書用に顧問料計算結果テーブルから該当する計算日を取得する。
'
'更新履歴　：2007/07/31 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Function func_GetKeisanKekka(ByRef Sisan() As Variant _
                           , ByVal lngSisan As Long _
                           , ByRef strKohzaList() As String _
                           , ByVal lngKKCnt As Long _
                           , ByRef strData() As String) As Long
On Error GoTo ErrHandler

    Const PROCEDURE_NAME = "func_GetKeisanKekka"
    
    Dim strSql      As String
    Dim vdata()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    Dim lngColCnt   As Long
    
    Const ItemMax = 1           '取得データ項目数最大値

'*******************************************************
'* 関数開始　                                        　*
'*******************************************************

    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     KOMONRYOH_KEISAN_KIJUN_YMD"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_KOMONRYOH_KEISAN_KEKKA"
    strSql = strSql & " WHERE"
    strSql = strSql & "     KOHZA_NO        = '" & strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO) & "'"
    strSql = strSql & " AND SEIKYU_KIKAN_ID =  " & strKohzaList(lngKKCnt, KOHZALIST_SEIKYU_KIKAN_ID)
    strSql = strSql & " AND SHISAN_BUNRUI   = '" & Sisan(lngSisan, SISANLIST_SHISAN_BUNRUI) & "'"
    strSql = strSql & " AND SHISAN_CODE     = '" & Sisan(lngSisan, SISANLIST_SHISAN_CODE) & "'"
    strSql = strSql & " AND RATE_TEKIYOH_KIKAN_ID = '" & Sisan(lngSisan, SISANLIST_RATE_KIKAN_ID) & "'"
    strSql = strSql & " AND HIZUKE_SHURUI   = '" & strKohzaList(lngKKCnt, KOHZALIST_HIZUKE_SHURUI) & "'"
    strSql = strSql & " AND KEISAN_TO_YMD   = '" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYUSHO_KIKAN_TO) & "'"
    strSql = strSql & " AND KEISAN_FROM_YMD = '" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYUSHO_KIKAN_FROM) & "'"
    strSql = strSql & " ORDER BY KOMONRYOH_KEISAN_KIJUN_YMD"
    

    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vdata, lngCol, lngRow)
    
    
    If lngRow = 0 Then
    'データなし
        Erase strData
        func_GetKeisanKekka = 0
        Exit Function
    Else
    'データあり
        ReDim strData(lngRow - 1, ItemMax - 1)
        For lngRowCnt = 0 To lngRow - 1
            For lngColCnt = 0 To ItemMax - 1
                strData(lngRowCnt, lngColCnt) = vdata(lngColCnt, lngRowCnt)
            Next lngColCnt
        Next lngRowCnt
        
        func_GetKeisanKekka = lngRow

    End If

    Erase vdata

    Exit Function

ErrHandler:
'Debug.Print strSql

    func_GetKeisanKekka = 0

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetKeisanKekka", Err.Number, Err.Description)
    On Error GoTo 0
    
    Erase vdata
    
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：日次料率データシート作成関数
'
'引　　数　：
'           strFileName     I       対象ファイル名
'           lngRateMdfSeq   I       料率変更番号
'           Sisan()         I       資産配列の
'           lngSisan        I       資産配列の添え字
'           strKohzaList()  I       処理対象口座配列配列
'           lngKKCnt        I       処理対象口座配列配列の添え字
'           lngErrorCnt     O       エラー件数
'
'戻り値　　：なし
'
'機能説明　：日次料率のデータシートを作成する関数
'
'更新履歴　：2007/07/31 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub sub_NichijiRate(ByVal strFileName As String _
                             , ByVal lngRateMdfSeq As Long _
                             , ByRef Sisan() As Variant _
                             , ByVal lngSisan As Long _
                             , ByRef strKohzaList() As String _
                             , ByVal lngKKCnt As Long _
                             , ByRef lngErrorCnt As Long)
On Error GoTo ErrHandler
    
    Const PROCEDURE_NAME = "sub_NichijiRate"
                             
    Dim lngRet              As Long
    Dim strSheetName        As String       'シート名　(行増幅時使用)
    Dim strOffSet           As String       'オフセット(行増幅時使用)
    Dim strChikanMae        As String
    Dim strChikanAto        As String
    Dim strCell             As String
    Dim lngStartRow         As Long         '追加した行の始まり
    Dim lngEndRow           As Long         '追加した行の終わり
    Dim strData()           As String       '料率段階のデータ格納配列


'*******************************************************
'* 関数開始　                                        　*
'*******************************************************

    '「日次料率」の貼付
    '−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−
    '＠段階数分の行増幅
    lngRet = sub_Ryoritsu(CStr(Sisan(lngSisan, SISANLIST_RATE_CODE)), strKohzaList(), lngKKCnt, strData())
    
    If lngRet = 0 Then
        'エラー情報構造体セット
        With gusrErr
            .ModuleId = MODULE_NAME         'モージュールID
            .Procedure = PROCEDURE_NAME     'プロシージャID
            .MsgCode = ERR_MSGCD_VB         'メッセージ区分
            .ErrNum = Err.Number            'システムエラーコード
            .ErrDescript = Err.Description  'システムエラー内容
            .KohzaNo = strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO)
            .MsgNaiyou = "料率段階取得結果が０件"   'メッセージ内容(ブックファイル名)
            .MsgSyousai = "料率段階取得結果が０件または" & vbCrLf & _
                          "日次料率の料率段階取得(sub_Ryoritsu)関数でシステムエラーが発生しています。" & vbCrLf & _
                          "システム管理者に連絡して下さい。" & vbCrLf & _
                          "テンプレート　：" & DATASHEET_NICHIJI_RATE & vbCrLf & _
                          "テーブル　    ：KYK_RATE" & vbCrLf & _
                          "料率コード　  ：" & Sisan(lngSisan, SISANLIST_RATE_CODE) & vbCrLf & _
                          "口座番号      ：" & strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO) & vbCrLf & _
                          "請求期間ID    ：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYU_KIKAN_ID) & vbCrLf & _
                          "料率適用期間ID：" & Sisan(lngSisan, SISANLIST_RATE_KIKAN_ID)                              'メッセージ詳細
            
            'エラーシート書き込み
            
            Call fncWriteErrSheet(ERROR_SHEETNAME)
        End With
        
        lngErrorCnt = lngErrorCnt + 1
        
        Erase strData
        
        Exit Sub
    End If
    
    
    strSheetName = DATASHEET_NICHIJI_RATE  '日次料率
    strOffSet = ""
    strCell = DATASHEET_SHEETHEAD_COL
    strChikanMae = DATASHEET_INC_KIKAN & DATASHEET_INC_XXX
    strChikanAto = DATASHEET_INC_KIKAN & Format(lngRateMdfSeq, "000")
    Call sub_AddSheetItemRow(strFileName _
                           , strSheetName _
                           , lngKKCnt _
                           , strChikanMae _
                           , strChikanAto _
                           , strOffSet _
                           , lngStartRow _
                           , lngEndRow, strCell)
    
    '　行増幅と値設定
    Call sub_RowIncrement(strFileName _
                        , lngStartRow _
                        , lngEndRow _
                        , DATASHEET_INC_NICHIJI_RATE_DANKAI & DATASHEET_INC_XX _
                        , strData())
    
    '条件への貼付け
    Call sub_SetJoukenData(strFileName _
                         , Sisan() _
                         , lngSisan _
                         , lngStartRow _
                         , True)
    
    '−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−
    
    Erase strData
                
    Exit Sub

ErrHandler:
On Error Resume Next
    With gusrErr
        .ModuleId = MODULE_NAME         'モージュールID
        .Procedure = PROCEDURE_NAME     'プロシージャID
        .MsgCode = ERR_MSGCD_VB         'メッセージ区分
        .ErrNum = Err.Number            'システムエラーコード
        .ErrDescript = Err.Description  'システムエラー内容
        .KohzaNo = strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO)
        .MsgNaiyou = "日次料率データシート作成関数でシステムエラー"        'メッセージ内容(ブックファイル名)
        'メッセージ詳細
        .MsgSyousai = "「日次料率」の作成(sub_NichijiRate)でシステムエラーが発生しました。" & _
             vbCrLf & "システム管理者に連絡して下さい。" & _
             vbCrLf & "作成中書類名   :" & DATASHEET_NICHIJI_RATE & _
             vbCrLf & "ファイル名     :" & strFileName & _
             vbCrLf & "口座番号       :" & strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO) & _
             vbCrLf & "請求期間ID     :" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYU_KIKAN_ID) & _
             vbCrLf & "資産分類       :" & Sisan(lngSisan, SISANLIST_SHISAN_BUNRUI) & _
             vbCrLf & "資産コード     :" & Sisan(lngSisan, SISANLIST_SHISAN_CODE)
        'エラーシート書き込み
        Call fncWriteErrSheet(ERROR_SHEETNAME)
    End With
    
    lngErrorCnt = lngErrorCnt + 1
    
    Erase strData

End Sub

'*************************************************************************
'関数名　　：顧客指定Ｂ計算書・料率段階のデータシート行追加関数
'
'引　　数　：
'           strFileName     I       対象ファイル名
'           lngRateMdfSeq   I       料率変更番号
'           Sisan()         I       資産配列の
'           lngSisan        I       資産配列の添え字
'           strKohzaList()  I       処理対象口座配列配列
'           lngKKCnt        I       処理対象口座配列配列の添え字
'           lngErrorCnt     O       エラーカウンタ
'           strKsanShoName  I       作成する計算書名
'
'戻り値　　：なし
'
'機能説明　：「顧客指定Ｂ計算書・料率段階」の行貼り付けと行増幅
'
'更新履歴　：2007/07/31 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub sub_B_KsanShoRateDankai(ByVal strFileName As String _
                         , ByVal lngRateMdfSeq As Long _
                         , ByRef Sisan() As Variant _
                         , ByVal lngSisan As Long _
                         , ByRef strKohzaList() As String _
                         , ByVal lngKKCnt As Long _
                         , ByRef lngErrorCnt As Long _
                         , ByVal strKsanShoName As String)
                     
On Error GoTo ErrHandler
                     
    Const PROCEDURE_NAME = "sub_B_KsanShoRateDankai"
    Dim lngStartRow         As Long
    Dim lngEndRow           As Long
    Dim strKeisanMeisai()   As String       '顧問料計算結果明細のデータ格納配列
    Dim lngRet              As Long

'*******************************************************
'* 関数開始　                                        　*
'*******************************************************

            
    
    If Trim(strKsanShoName) <> "" Then


            '＠段階数分の行増幅
            lngRet = func_GetKeisanMeisai(Sisan() _
                                , lngSisan _
                                , strKohzaList() _
                                , lngKKCnt _
                                , strKeisanMeisai() _
                                , False)
            If lngRet = 0 Then
                'エラー情報構造体セット
                With gusrErr
                    .ModuleId = MODULE_NAME         'モージュールID
                    .Procedure = PROCEDURE_NAME     'プロシージャID
                    .MsgCode = ERR_MSGCD_VB         'メッセージ区分
                    .ErrNum = Err.Number            'システムエラーコード
                    .ErrDescript = Err.Description  'システムエラー内容
                    .KohzaNo = strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO)
                    .MsgNaiyou = "計算結果明細の検索結果０件"   'メッセージ内容(ブックファイル名)
                    .MsgSyousai = strKsanShoName & "で計算結果明細の検索結果が０件又は" & vbCrLf & _
                                  "顧問料計算結果明細取得関数(func_GetKeisanMeisai)でシステムエラーが発生してます。" & vbCrLf & _
                                  "システム管理者に連絡して下さい。" & vbCrLf & _
                                  "テンプレート　：" & strKsanShoName & vbCrLf & _
                                  "テーブル　　　：KYK_KOMONRYOH_KEISAN_MEISAI" & vbCrLf & _
                                  "口座番号　　　：" & strKohzaList(lngKKCnt, KOHZALIST_KOHZA_NO) & vbCrLf & _
                                  "請求期間ID　　：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYU_KIKAN_ID) & vbCrLf & _
                                  "資産分類　　　：0" & vbCrLf & _
                                  "資産コード　　：0000" & vbCrLf & _
                                  "計算FROM日　　：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYUSHO_KIKAN_FROM) & vbCrLf & _
                                  "計算TO日　　　：" & strKohzaList(lngKKCnt, KOHZALIST_SEIKYUSHO_KIKAN_TO) & vbCrLf & _
                                  "料率適用期間ID：" & Sisan(lngSisan, SISANLIST_RATE_KIKAN_ID)                              'メッセージ詳細
                    
                    'エラーシート書き込み
                    
                    Call fncWriteErrSheet(ERROR_SHEETNAME)
                End With
                lngErrorCnt = lngErrorCnt + 1
                Exit Sub
            End If
            
            Call sub_AddSheetItemRow(strFileName _
                                   , strKsanShoName _
                                   , lngKKCnt _
                                   , DATASHEET_INC_KIKAN & DATASHEET_INC_XXX _
                                   , DATASHEET_INC_KIKAN & Format(lngRateMdfSeq, "000") _
                                   , "" _
                                   , lngStartRow _
                                   , lngEndRow)
                                 
            '　行増幅と値設定
            Call sub_RowIncrement(strFileName _
                                , lngStartRow _
                                , lngEndRow _
                                , DATASHEET_INC_DANKAI & DATASHEET_INC_XX _
                                , strKeisanMeisai() _
                                , -1 _
                                , strKsanShoName)
            
            
            '＠条件への貼付け
            Call sub_SetJoukenData(strFileName _
                                 , Sisan() _
                                 , lngSisan _
                                 , lngStartRow _
                                 , True)

    End If
    
    
    Exit Sub

ErrHandler:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "sub_B_KsanShoRateDankai", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub


