Attribute VB_Name = "bas30Shinki"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料計算システム・計算方法
'
'オブジェクト名：bas30Shinki
'
'機能概要　：新規処理
'
'更新履歴　：2006/07/04 SRA 東       新規作成
'更新履歴　：2007/03/14 SRA H.Anpo   PH3対応
'
'*************************************************************************

Const MODULE_NAME = "bas30Shinki"



'*************************************************************************
'関数名　　：新規ボタン押下処理関数
'
'引　　数　：
'           lngType       I       追加対象
'                           (1:一般資産 2:月末顧問料調整資産 3:T21調整資産
'戻り値　　：なし
'
'機能説明　：新規ボタン押下じの行追加処理
'
'更新履歴　：2006/07/07 SRA Y.Azuma    新規作成
'更新履歴　：2007/01/12 SRA Y.Azuma    変更     (要望)基礎数値種類のデフォルト表示
'更新履歴　：2007/03/14 SRA H.Anpo     PH3対応
'
'*************************************************************************
Public Sub gsub_NewClick(ByVal lngType As Long)
    
    Dim lngAddRow As Long       '追加開始行番号
    Dim lngSrcRow As Long       '複写元ROW

    '入力チェック
    If gfuncKeisan_NewInputCheck(ThisWorkbook.Worksheets(MAIN_SHEETNAME)) = False Then
        Exit Sub
    End If
    
    '追加行番号取得
    lngAddRow = fncKsCmnSearchNewRow(ThisWorkbook.Worksheets(MAIN_SHEETNAME), lngType)
    
    Call mainmenu.gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), False)        'シート保護解除
    
    '複写元ROWの判別
    Select Case lngType
    Case 1
    '一般資産
        lngSrcRow = 5
    Case 2
    '月末顧問料調整資産
        lngSrcRow = 11
    Case 3
    'T21調整資産
        lngSrcRow = 17
    Case Else
        Call gfunc_ErrorMsg(4, 1300, "", "不正な追加対象が指定されました。")
        Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), True)         'シート保護
        Exit Sub
    End Select

    '行追加
    Call gKsC_fncKsCmnInsRow(ThisWorkbook.Worksheets(MAIN_SHEETNAME), lngAddRow, lngSrcRow)
    
'------------<Modify Start azuma 2007/01/12 > -----------------------
    With ThisWorkbook.Worksheets(MAIN_SHEETNAME)
Dim lngT21StartRow As Long
        'T21の開始行を調べる
        lngT21StartRow = fncKsCmnSearchDataTopRow(ThisWorkbook.Worksheets(MAIN_SHEETNAME), 3)
        
        '一般と調整資産だけ”２：信託”を初期値として設定する。
        If lngT21StartRow > lngAddRow Then
            .Range(mPX_SHINTAKU_KBN & lngAddRow).Value = func_SearchComboItem(ThisWorkbook.Worksheets(CHECK_SHEETNAME), 2, "基礎数値種類リスト")  '信託
        End If
    End With
'------------<Modify End   azuma 2007/01/12 > -----------------------

    '印刷ボタン無効
    ThisWorkbook.Worksheets(MAIN_SHEETNAME).cmdPrint.Enabled = False
    Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), True)         'シート保護
    
    '資産保有期間のデフォルト設定
    Call gsubKeisan_SetDefaltTerm(ThisWorkbook.Worksheets(MAIN_SHEETNAME), lngAddRow)

'*** 2007/03/14 PH3対応 Added By H.Anpo Start
    If lngType = 1 And Mid(ThisWorkbook.Worksheets(MAIN_SHEETNAME).Range("平残断面区分"), 1, 1) = "2" Then
        Call gsubKeisan_SetDefaltDanmen(ThisWorkbook.Worksheets(MAIN_SHEETNAME), lngAddRow)
    End If
'*** 2007/03/14 PH3対応 Added By H.Anpo End

End Sub

'*************************************************************************
'関数名　　：入力チェック処理(新規時)
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2006/07/05 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Function gfuncKeisan_NewInputCheck(ByRef TargetSheets As Object) As Boolean

    gfuncKeisan_NewInputCheck = False

    With TargetSheets
    
        '口座名称存在(口座名称の必須チェックで代用)
        If Trim(.Range("口座名称").Value) = "" Then
        'Error
            '"口座番号が未入力"
            Call gfunc_ErrorMsg(3, 1301, "", "検索ボタンを押下して下さい。")
            Exit Function
        End If
        
    
        '顧問料請求期間チェック
        If gfunc_CheckKikan(.Range("顧問請求From"), .Range("顧問請求To")) = False Then
            Exit Function
        End If
        
    End With
    
    gfuncKeisan_NewInputCheck = True

End Function


'*************************************************************************
'関数名　　：新規時の保有期間初期設定
'
'引　　数　：
'           TargetSheets   O       対象シート
'           lngAddRow      I       設定ROW
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2006/07/07 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub gsubKeisan_SetDefaltTerm(ByRef TargetSheets As Object, ByVal lngAddRow As Long)

    With TargetSheets
        .Cells(lngAddRow, 3).Value = .Range("顧問請求From")
        .Cells(lngAddRow, 5).Value = .Range("顧問請求To")

    End With


End Sub


'*************************************************************************
'関数名　　：断面系時の初期設定
'
'引　　数　：
'           TargetSheets   O       対象シート
'           lngAddRow      I       設定ROW
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/03/26 SRA H.Anpo    新規作成
'
'*************************************************************************
Public Sub gsubKeisan_SetDefaltDanmen(ByRef TargetSheets As Object, ByVal lngAddRow As Long)

    With TargetSheets
        .Cells(lngAddRow, 7).Value = func_SearchComboItem(ThisWorkbook.Worksheets(DATA_SHEETNAME), "00000", "資産リスト")
        If Mid(.Cells(lngAddRow - 1, 7).Value, 1, 5) = "00000" Then
            '追加の場合の初期値セット(上の行の設定値を引継ぐ)
            .Cells(lngAddRow, 3).Value = ""
            .Cells(lngAddRow, 15).Value = .Cells(lngAddRow - 1, 15).Value                   '料率コード
            .Cells(lngAddRow, 19).Value = .Cells(lngAddRow - 1, 19).Value                   '基礎数値種類
            .Cells(lngAddRow, 22).Value = func_SearchComboItem(ThisWorkbook.Worksheets(CHECK_SHEETNAME), 8, "基礎数値計算方法リスト") '基礎数値計算方法(8:期中(追加))
            .Cells(lngAddRow, 26).Value = .Cells(lngAddRow - 1, 26).Value                   '顧問料計算方法
            .Cells(lngAddRow, 3).Select                                                     '資産保有期間FROMにカーソル移動
        Else
            '期初の場合の初期値セット
            .Cells(lngAddRow, 22).Value = func_SearchComboItem(ThisWorkbook.Worksheets(CHECK_SHEETNAME), 7, "基礎数値計算方法リスト") '基礎数値計算方法(7:期初)
            .Cells(lngAddRow, 19).Value = func_SearchComboItem(ThisWorkbook.Worksheets(CHECK_SHEETNAME), 4, "基礎数値種類リスト")    '基礎数値種類(4:元本)
        End If
    End With


End Sub

