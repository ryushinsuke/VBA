Attribute VB_Name = "bas10Initialize"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料計算システム・請求情報登録
'
'オブジェクト名：bas10Initialize
'
'機能概要　：初期処理
'
'更新履歴　：2006/08/15 SRA T.Sato       新規作成
'
'*************************************************************************
Const MODULE_NAME = "bas10Initialize"


'*************************************************************************
'関数名　　：請求日登録画面の初期化メイン
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：初期起動時に行われる初期処理のメイン
'
'更新履歴　：2006/08/15 SRA T.Sato    新規作成
'
'*************************************************************************
Public Sub g_subInitialize()

    '画面の初期化
    Call gCom01_SheetProtect(Worksheets(MAIN_SHEETNAME), False)
    Worksheets(MAIN_SHEETNAME).Range("口座番号").Value = ""
    Worksheets(MAIN_SHEETNAME).Range("信託銀行コード").Value = ""
    Worksheets(MAIN_SHEETNAME).Range("請求書期間From").Value = ""
    Worksheets(MAIN_SHEETNAME).Range("請求書期間To").Value = ""
    Worksheets(MAIN_SHEETNAME).Range("請求日").Value = ""
    
    Call g_subIniMeisai(Worksheets(MAIN_SHEETNAME))   '明細部
    
    'コンボ項目の初期化
    Call IniCombo
    
    'コマンドボタンの表示・非表示の設定
    Call gfunc_VisibleCommandBtn(True)

End Sub


'*************************************************************************
'関数名　　：請求情報登録画面のクリア処理(明細部)
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：明細部行を全て削除する
'
'更新履歴　：2006/08/15 SRA T.Sato    新規作成
'
'*************************************************************************
Public Sub g_subIniMeisai(ByRef TargetSheets As Object)
        
    Call gCom01_SheetProtect(TargetSheets, False)
    
    '明細行削除
    Call gKsC_fncKsCmnRowAllDelete(ThisWorkbook.Worksheets(MAIN_SHEETNAME))
    
    Call gCom01_SheetProtect(TargetSheets, True)

End Sub


'*************************************************************************
'関数名　　：コンボ項目の初期化処理
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：コンボ項目に信託銀行、掛け目コードを設定する
'
'更新履歴　：2006/08/15 SRA T.Sato    新規作成
'
'*************************************************************************
Private Sub IniCombo()

    '信託銀行データ取得
    If funcGetSGinko = False Then
        Call MsgBox("データ取得失敗(信託銀行データ)")
        Exit Sub
    End If
    
    '掛け目データ取得
    If funcGetKakeme = False Then
        Call MsgBox("データ取得失敗(掛け目データ)")
        Exit Sub
    End If

End Sub

'*************************************************************************
'関数名　　：コードブックテーブル抽出処理
'
'引　　数　：無し
'
'戻り値　　：Boolean
'
'機能説明　：コードブックテーブルから信託銀行コード・名称（コンボ項目）を
'           取得し「data」シートに設定
'
'更新履歴　：2006/08/15 SRA T.Sato    新規作成
'更新履歴　：2007/01/17 SRA Y.Azuma    変更     (仕変)総No.185 信託銀行リストのコード入力対応
'更新履歴　：2007/05/31 SRA Y.Azuma    変更     (PH3)連結　起動中のキー入力エラー対策
'
'*************************************************************************
Private Function funcGetSGinko() As Boolean
       
On Error GoTo ErrHandler
    
    Dim strsql      As String
    Dim vdata()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngCnt      As Long
    
    funcGetSGinko = False
    
'------------<Modify Start azuma 2007/01/17 > -----------------------
'    strSql = ""
'    strSql = strSql & " SELECT"
'    strSql = strSql & "     TRIM(CD_1)||':'||TRIM(NAME_J)"
'    strSql = strSql & " FROM"
'    strSql = strSql & "     VW_DT301D"
'    strSql = strSql & " WHERE"
'    strSql = strSql & "     CD_KBN ='15'"
'    strSql = strSql & " ORDER BY "
'    strSql = strSql & "     CD_1 "
    
    Call gsub_CreateSql_ShintakuBk(strsql)

'------------<Modify End   azuma 2007/01/17 > -----------------------
    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strsql, vdata, lngCol, lngRow)
    
'------------<Modify Start azuma 2007/05/31 (PH3)> -----------------------
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Activate
'------------<Modify End   azuma 2007/05/31 (PH3)> -----------------------
    
    '「信託銀行」のデータセル範囲をクリア
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("A2:A65536").ClearContents
    
    '取得データをセルに設定
    For lngCnt = 0 To lngRow - 1
'------------<Modify Start azuma 2007/01/17 > -----------------------
         ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(2 + lngCnt + 1, 1).Value = vdata(0, lngCnt)
'         ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(2 + lngCnt, 1).Value = vData(0, lngCnt)
'------------<Modify End   azuma 2007/01/17 > -----------------------
    Next lngCnt
    
    '信託銀行コードリスト再定義
'------------<Modify Start azuma 2007/01/17 > -----------------------
    ThisWorkbook.Names.Add Name:="信託銀行コードリスト", _
     RefersToR1C1:="=Data!R2C1:R" & lngRow + 2 & "C1"
'    ThisWorkbook.Names.Add Name:="信託銀行コードリスト", _
'     RefersToR1C1:="=Data!R2C1:R" & lngRow + 1 & "C1"
'------------<Modify End   azuma 2007/01/17 > -----------------------
    
    '取得データクリア
    Erase vdata
    
    funcGetSGinko = True
    
    Exit Function
ErrHandler:

    Erase vdata
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "funcGetSGinko", Err.Number, Err.Description)

End Function

'*************************************************************************
'関数名　　：掛け目テーブル抽出処理
'
'引　　数　：無し
'
'戻り値　　：Boolean
'
'機能説明　：掛け目テーブルから掛け目コードを
'           取得し「data」シートに設定
'
'更新履歴　：2006/08/15 SRA T.Sato    新規作成
'更新履歴　：2006/12/08 SRA Y.Azuma   変更     (PH2)掛け目テーブル変更対応
'更新履歴　：2007/05/31 SRA Y.Azuma    変更     (PH3)連結　起動中のキー入力エラー対策
'
'*************************************************************************
Private Function funcGetKakeme() As Boolean
       
On Error GoTo ErrHandler
    
    Dim strsql      As String
    Dim vdata()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngCnt      As Long
    
    funcGetKakeme = False
    
'------------<Modify Start azuma 2006/12/08 > -----------------------
    strsql = ""
    strsql = strsql & " SELECT"
    strsql = strsql & "     KAKEME_CD"
    strsql = strsql & " FROM"
    strsql = strsql & "     KYK_KAKEME_NAME"
    strsql = strsql & " WHERE"
    strsql = strsql & "     SAKUJO_FLG      = '0'"
    strsql = strsql & " ORDER BY"
    strsql = strsql & "     KAKEME_CD"
    strsql = strsql & " " '    strSql = ""
'    strSql = strSql & " SELECT"
'    strSql = strSql & "     KAKEME_CD"
'    strSql = strSql & " FROM"
'    strSql = strSql & "     KYK_KAKEME"
'    strSql = strSql & " ORDER BY "
'    strSql = strSql & "     KAKEME_CD "
'------------<Modify End   azuma 2006/12/08 > -----------------------

    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strsql, vdata, lngCol, lngRow)
    
'------------<Modify Start azuma 2007/05/31 (PH3)> -----------------------
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Activate
'------------<Modify End   azuma 2007/05/31 (PH3)> -----------------------
    
    '「掛け目」のデータセル範囲をクリア
    ThisWorkbook.Worksheets(DATA_SHEETNAME).Range("C2:C65536").ClearContents
    
    '取得データをセルに設定
    For lngCnt = 0 To lngRow - 1
         ThisWorkbook.Worksheets(DATA_SHEETNAME).Cells(2 + lngCnt, 3).Value = vdata(0, lngCnt)
    Next lngCnt
    
    '掛け目コードリスト再定義
'------------<Modify Start T.Ichikawa 2007/05/08 > -----------------------
    If lngCnt <> 0 Then
        ThisWorkbook.Names.Add Name:="掛け目コードリスト", _
         RefersToR1C1:="=Data!R2C3:R" & lngRow + 1 & "C3"
    Else
        ThisWorkbook.Names.Add Name:="掛け目コードリスト", _
         RefersToR1C1:="=Data!R2C3:R" & 2 & "C3"
    End If
'------------<Modify Start T.Ichikawa 2007/05/08 > -----------------------
    
    '取得データクリア
    Erase vdata
    
    
    funcGetKakeme = True
    
    Exit Function
ErrHandler:

    Erase vdata
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "funcGetKakeme", Err.Number, Err.Description)

End Function

'*************************************************************************
'関数名　　：コマンドボタンの表示・非表示設定
'
'引　　数　：
'           blnType       I       表示：True、非表示：False
'
'戻り値　　：なし
'
'機能説明　：コマンドボタン表示・非表示の設定をする。
'
'更新履歴　：2006/08/15 SRA T.Sato    新規作成
'
'*************************************************************************
Public Sub gfunc_VisibleCommandBtn(blnType As Boolean)

'------------<Modify Start azuma 2007/05/31 (PH3)> -----------------------
    ThisWorkbook.Worksheets(MAIN_SHEETNAME).Activate
'------------<Modify End   azuma 2007/05/31 (PH3)> -----------------------

    With Worksheets(MAIN_SHEETNAME)
    
        .cmdInsert.Visible = blnType
        .cmdDelete.Visible = blnType
        
        .cmdSearch.Visible = blnType
        .cmdUpdate.Visible = blnType
        .cmdAuto.Visible = blnType
                
    End With

End Sub
