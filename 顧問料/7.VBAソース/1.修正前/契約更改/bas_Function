Attribute VB_Name = "bas_Function"
Option Explicit

'===============================================================================
'
' <NAME>    関数　その他
'
'-------------------------------------------------------------------------------
' <VERSION> 01-00 : 2006/08/06 IIDA
'
'===============================================================================
'-------------------------------------------------------------------------------
' <FUNCTION>  Fn_ChngFixStr ： 固定長文字列変換
'
' <INPUT>　　 v_str : 文字列  v_strLng : 調整バイト数
' <OUTPUT>    変換後文字列
'-------------------------------------------------------------------------------
Public Function Fn_ChngFixStr(ByVal v_str As String, ByVal v_strLng As Integer) As String
  
Dim strJituCntByte         As Integer '実バイト数
Dim strJituCnt             As Integer '実文字数
Dim strWk                  As String
   '
   strWk = v_str
   strJituCnt = Len(v_str)
   Do
        strJituCntByte = Fn_GetHalfStr(strWk)
        
        If strJituCntByte = v_strLng Then
              Fn_ChngFixStr = strWk
              Exit Function
        End If
        
        If strJituCntByte < v_strLng Then
              Fn_ChngFixStr = strWk & Space$(v_strLng - strJituCntByte)
              Exit Function
        Else
              strJituCnt = strJituCnt - 1
              strWk = Left$(strWk, strJituCnt)
        End If
  Loop

End Function
'-------------------------------------------------------------------------------
' <FUNCTION>  Fn_GetHalfStr ： 半角換算文字数取得
'
' <INPUT>　　 v_str : 文字列
' <OUTPUT>    変換後文字数
'-------------------------------------------------------------------------------
Public Function Fn_GetHalfStr(ByVal v_str As String) As Integer

Dim i As Integer
  
    Fn_GetHalfStr = 0

    For i = 1 To Len(v_str)
       If Asc(Mid$(v_str, i, 1)) < 0 Then
          Fn_GetHalfStr = Fn_GetHalfStr + 2
       Else
          Fn_GetHalfStr = Fn_GetHalfStr + 1
       End If
    Next
  
End Function

'-------------------------------------------------------------------------------
' <FUNCTION>  Fn_ChngStr ： 文字列を変換方法によって変換する
'
' <INPUT>　　 v_str : 文字列  v_type : 変換方法
'
'             v_type 0:NULL→空白へ 1:"0"
'
' <OUTPUT>    変換後文字列
'-------------------------------------------------------------------------------
Public Function Fn_ChngStr(ByVal v_str As Variant, Optional v_type As Integer = 0) As String
    Dim strbuf As String
    
    strbuf = ""
    If IsNull(v_str) = True Then
        Select Case v_type
            Case 0
                strbuf = ""
            Case 1
                strbuf = "0"
        End Select
        Fn_ChngStr = strbuf
    Else
        Fn_ChngStr = v_str
    End If
    

End Function




