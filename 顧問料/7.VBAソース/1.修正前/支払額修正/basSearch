Attribute VB_Name = "basSearch"
Option Explicit

'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー算出システム・支払額修正
'
'オブジェクト名：basSearch
'
'機能概要　：支払情報、外部委託支払計算情報検索
'
'更新履歴　：2008/06/18 SRA吉井   新規作成
'
'*************************************************************************

Private Const MODULE_NAME As String = "basSearch"

'一覧表示欄取得レコード
Private Enum eIchiranRec
    eGI_SHIHARAI_KIKAN_ID           '支払期間ID
    eSHIHARAI_FLG                   '支払フラグ
    eSHOHIZEI_FLG                   '消費税フラグ
    eSHOHIZEI_RITSU                 '消費税率
    eSHUUSEIGAKU_SAIYOU_FLG         '修正額採用フラグ
    eGI_SHIHARAI_KIKAN_FROM         '支払期間FROM
    eGI_SHIHARAI_KIKAN_TO           '支払期間TO
    eSHIHARAI_YOTEI_YMD             '支払予定日
    eSTATUS                         'ステータス
    eSHOHIZEI_KAZEI_GO_GAKU         '課税後外部委託支払計算額
    eSHOHIZEI_GAKU                  '消費税額
    eKAZEI_GO_SHUUSEIGAKU           '課税後外部委託支払計算修正額
    eSHOHIZEI_SHUUSEIGAKU           '消費税修正額
End Enum

'*************************************************************************
'
'関数名　　：検索ボタン処理
'
'引　数　　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：入力フィールドの初期化
'
'更新履歴　：2008/06/20 SRA吉井   新規作成
'
'*************************************************************************
Function fncSearch() As Long

    Const PROCEDURE_NAME As String = "fncSearch"

On Error GoTo ErrorHandler

    Dim lngRet          As Long
    Dim strSoufubi      As String
    Dim lngRowCnt       As Long
    Dim lngRow          As Long
    Dim vntRec()        As Variant
    Dim lngCnt          As Long

    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)

    fncSearch = 0

    '入力チェック
    With Sheets(SHEET_MAIN)
        Application.ScreenUpdating = False
        '一覧表示欄 削除
        fncKsCmnClearField 1

        '送付日
        If .Range(RNG_SOUFUBI).Value = vbNullString Then
            '送付日が未入力の場合、DBのSYSDATEをセット
            lngRet = fncGetDbSysdate(strSoufubi)
            If lngRet <> 0 Then
                fncSearch = lngRet
                Exit Function
            End If
            .Range(RNG_SOUFUBI).Value = strSoufubi
        Else
            '送付日の形式をチェック
            lngRet = fncChkDateFormat(.Range(RNG_SOUFUBI).Value, MSG1_SOUFUBI)
            If lngRet <> 0 Then
                .Range(RNG_SOUFUBI).Select
                fncSearch = lngRet
                Exit Function
            End If
        End If

        '一覧表示欄取得
        lngRet = fncIchiranRead(Left(.Range(RNG_GI_NO).Value, 1), .Range(RNG_SOUFUBI).Value, vntRec, lngRowCnt)
        If lngRet <> 0 Then
            fncSearch = lngRet
            Exit Function
        End If

        If lngRowCnt > 0 Then

            '一覧表示欄 行追加
            fncKsCmnAddNrow lngRowCnt

            'データセット
            lngRow = LIST_DATA_TOP_ROW
            For lngCnt = 0 To lngRowCnt - 1

                .Cells(lngRow, COLN_GI_SHIHARAI_ID) = vntRec(eIchiranRec.eGI_SHIHARAI_KIKAN_ID, lngCnt)
                .Cells(lngRow, COLN_SHIHARAI_FLG) = vntRec(eIchiranRec.eSHIHARAI_FLG, lngCnt)
                .Cells(lngRow, COLN_SHOHIZEI_FLG) = vntRec(eIchiranRec.eSHOHIZEI_FLG, lngCnt)
                .Cells(lngRow, COLN_SHOHIZEI_RITSU) = vntRec(eIchiranRec.eSHOHIZEI_RITSU, lngCnt)

                '修正額採用
                If vntRec(eIchiranRec.eSHUUSEIGAKU_SAIYOU_FLG, lngCnt) = "1" Then
                    .Cells(lngRow, COLN_J_CHK_DEFAULT).Value = "TRUE"
                    .Cells(lngRow, COLN_J_CHK_FLG).Value = "TRUE"
                Else
                    .Cells(lngRow, COLN_J_CHK_DEFAULT).Value = "FALSE"
                    .Cells(lngRow, COLN_J_CHK_FLG).Value = "FALSE"
                End If

                .Cells(lngRow, COLN_GI_SHIHARAI_KIKAN_FROM) = vntRec(eIchiranRec.eGI_SHIHARAI_KIKAN_FROM, lngCnt)
                .Cells(lngRow, COLN_GI_SHIHARAI_KIKAN_TO) = vntRec(eIchiranRec.eGI_SHIHARAI_KIKAN_TO, lngCnt)
                .Cells(lngRow, COLN_SHIHARAI_YOTEIBI) = vntRec(eIchiranRec.eSHIHARAI_YOTEI_YMD, lngCnt)
                .Cells(lngRow, COLN_STATUS) = fnc_SearchComboItem(vntRec(eIchiranRec.eSTATUS, lngCnt), "ステータスリスト", 1)
                .Cells(lngRow, COLN_SHIHARAI_GAKU) = vntRec(eIchiranRec.eSHOHIZEI_KAZEI_GO_GAKU, lngCnt)
                .Cells(lngRow, COLN_SHOHIZEI_GAKU) = vntRec(eIchiranRec.eSHOHIZEI_GAKU, lngCnt)
                .Cells(lngRow, COLN_SHIHARAI_SHUSEI_GAKU) = vntRec(eIchiranRec.eKAZEI_GO_SHUUSEIGAKU, lngCnt)
                .Cells(lngRow, COLN_SHOHIZEI_SHUSEI_GAKU) = vntRec(eIchiranRec.eSHOHIZEI_SHUUSEIGAKU, lngCnt)

                '支払済の時は修正額採用と課税後外部委託支払計算修正額セルの背景色を白、入力不可にする
                If vntRec(eIchiranRec.eSTATUS, lngCnt) = "2" Then
                    .Cells(lngRow, COLN_DATA_TOP).Select
                    Selection.Interior.ColorIndex = xlNone
                    .Cells(lngRow, COLN_SHIHARAI_SHUSEI_GAKU).Select
                    With Selection
                        .Interior.ColorIndex = xlNone
                        .Locked = True
                        .Borders(xlEdgeLeft).ColorIndex = xlAutomatic
                        .Borders(xlEdgeTop).ColorIndex = xlAutomatic
                        .Borders(xlEdgeBottom).ColorIndex = xlAutomatic
                        .Borders(xlEdgeRight).ColorIndex = xlAutomatic
                    End With
                End If

                lngRow = lngRow + 1
            Next

            '更新ボタンを押せるようにする
            .cmdUpdate.Enabled = True

            Application.ScreenUpdating = True

        Else
            '22005 該当データが存在しません。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 22005, MSG1_ICHIRAN_DISP, , True
            fncSearch = 22005
            Exit Function

        End If

        '入力された外部委託先番号・送付日を保存
        .Range(RNG_SV_GI_NO).Value = .Range(RNG_GI_NO).Value
        .Range(RNG_SV_SOUFUBI).Value = .Range(RNG_SOUFUBI).Value

    End With

    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)

    Exit Function

ErrorHandler:
    Application.ScreenUpdating = True
    fncSearch = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function

'*************************************************************************
'
'関数名　　：一覧表示欄取得
'
'引　数　　：ARG1   I   GI_no       String 外部委託先番号(1桁)
'　　　　　：ARG2   I   Soufubi     String 送付日("YYYYMMDD")
'　　　　　：ARG5   O   vntRec      Variant データ配列
'
'戻り値　　：Long   エラー番号
'
'機能説明　：一覧表示情報を取得する
'
'更新履歴　：2008/06/20 SRA吉井   新規作成
'
'*************************************************************************
Function fncIchiranRead(GI_no As String, _
                        Soufubi As String, _
                        vntRec() As Variant, _
                        lngRow As Long) As Long

    Const PROCEDURE_NAME As String = "fncIchiranRead"

On Error GoTo ErrHandler
    
    Dim strRet      As String
    Dim strSql      As String
    Dim lngCol      As Long
    Dim strErrCd    As String
    Dim blnRet      As Boolean
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncIchiranRead = 0
 
    strSql = ""
    strSql = strSql & "SELECT" & vbCrLf
    strSql = strSql & "    A.GI_SHIHARAI_KIKAN_ID," & vbCrLf
    strSql = strSql & "    A.SHIHARAI_FLG," & vbCrLf
    strSql = strSql & "    B.SHOHIZEI_FLG," & vbCrLf
    strSql = strSql & "    B.SHOHIZEI_RITSU," & vbCrLf
    strSql = strSql & "    C.SHUUSEIGAKU_SAIYOU_FLG," & vbCrLf
    strSql = strSql & "    B.GI_SHIHARAI_KIKAN_FROM," & vbCrLf
    strSql = strSql & "    B.GI_SHIHARAI_KIKAN_TO," & vbCrLf
    strSql = strSql & "    A.SHIHARAI_YOTEI_YMD," & vbCrLf
    strSql = strSql & "    A.STATUS," & vbCrLf
    strSql = strSql & "    C.SHOHIZEI_KAZEI_GO_GAKU," & vbCrLf
    strSql = strSql & "    C.SHOHIZEI_GAKU," & vbCrLf
    strSql = strSql & "    C.KAZEI_GO_SHUUSEIGAKU," & vbCrLf
    strSql = strSql & "    C.SHOHIZEI_SHUUSEIGAKU" & vbCrLf
    strSql = strSql & "FROM" & vbCrLf
    strSql = strSql & "    KYK_GI_SHIHARAI_KANRI A," & vbCrLf
    strSql = strSql & "    KYK_GI_SHIHARAI_KIKAN B," & vbCrLf
    strSql = strSql & "    KYK_GI_SHIHARAI_KEISAN_SUM C" & vbCrLf
    strSql = strSql & "WHERE" & vbCrLf
    strSql = strSql & "    A.GI_NO = B.GI_NO" & vbCrLf
    strSql = strSql & "AND B.GI_NO = C.GI_NO" & vbCrLf
    strSql = strSql & "AND A.GI_SHIHARAI_KIKAN_ID = B.GI_SHIHARAI_KIKAN_ID" & vbCrLf
    strSql = strSql & "AND B.GI_SHIHARAI_KIKAN_ID = C.GI_SHIHARAI_KIKAN_ID" & vbCrLf
    strSql = strSql & "AND A.SHIHARAI_FLG = C.SHIHARAI_FLG" & vbCrLf
    strSql = strSql & "AND B.GI_SHIHARAI_KIKAN_TO = C.KEISAN_TO_YMD" & vbCrLf
    strSql = strSql & "AND A.GI_NO = '" & GI_no & "'" & vbCrLf
    strSql = strSql & "AND A.SHIHARAI_FLG = '1'" & vbCrLf     '支払フラグが支払
    strSql = strSql & "AND A.SOUFU_YMD = " & Soufubi & vbCrLf
    strSql = strSql & "AND A.SAKUJO_FLG = '0'" & vbCrLf
    strSql = strSql & "AND B.SAKUJO_FLG = '0'" & vbCrLf
    strSql = strSql & "AND C.SAKUJO_FLG = '0'" & vbCrLf
    strSql = strSql & "ORDER BY" & vbCrLf
    strSql = strSql & "    B.GI_SHIHARAI_KIKAN_FROM," & vbCrLf
    strSql = strSql & "    B.GI_SHIHARAI_KIKAN_TO" & vbCrLf
    
    blnRet = fncKsCmnDoExequteSql(MainMenu.gcSELECT, strSql, vntRec, lngCol, lngRow, strErrCd)
    If blnRet = False Then
        fncIchiranRead = strErrCd
        Exit Function
    End If
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    fncIchiranRead = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function
