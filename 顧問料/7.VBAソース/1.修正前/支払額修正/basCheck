Attribute VB_Name = "basCheck"
Option Explicit

'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー算出システム・支払額修正
'
'オブジェクト名：basCheck
'
'機能概要　：チェック機能
'
'更新履歴　：2008/06/18 SRA吉井   新規作成
'
'*************************************************************************

Private Const MODULE_NAME As String = "basCheck"

'*************************************************************************
'
'関数名　　：日付チェック
'
'引　数　　：ARG1   I   Target      String 入力テキスト
'　　　　　：ARG2   I   Msg1        String メッセージ出力時の項目名
'
'戻り値　　：エラー番号
'
'機能説明　：YYYYMMDD形式の日付チェックを行う。
'
'更新履歴　：2008/06/20 SRA吉井   新規作成
'
'*************************************************************************
Public Function fncChkDateFormat(ByVal Target As String, _
                                 ByVal Msg1 As String) As Long

    Const PROCEDURE_NAME As String = "fncChkDateFormat"

On Error GoTo ErrHandler
    
    Dim strRet  As String
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Select Case Len(Target)
        Case LEN_DATE
            If IsDate(Format(Target, "####/##/##")) Then
            Else
                '形式不正
                '22003 日付として認識できません。YYYYMMDD形式で入力して下さい。
                fncKsCmnMessageDisp MSGLVL_WARNNING, 22003, Msg1, , True
                fncChkDateFormat = 22003
                Exit Function
            End If
            
        Case Else
            '桁数不正
            '22006 入力桁数が不正です。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 22006, Msg1, "YYYYMMDD形式で入力して下さい。", True
            fncChkDateFormat = 22006
            Exit Function
    End Select
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    fncChkDateFormat = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function

'*************************************************************************
'
'関数名　　：金額チェック
'
'引　数　　：ARG1   I   Target      String 入力テキスト
'　　　　　：ARG2   I   Msg1        String メッセージ出力時の項目名
'
'戻り値　　：Long       エラー番号
'
'機能説明　：金額のチェックを行う。
'
'更新履歴　：2008/06/18 SRA吉井   新規作成
'
'*************************************************************************
Public Function fncChkShuseiGakuFormat(ByVal Target As String, _
                                       ByVal Msg1 As String) As Long

    Const PROCEDURE_NAME As String = "fncChkShuseiGakuFormat"

On Error GoTo ErrHandler
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkShuseiGakuFormat = 0
    
    If Trim(Target) = vbNullString Then GoTo EndProcess
    
    If Not gkyk_IsNumeric(Trim(Format(Target, "#0"))) Then
        '形式不正
        '22004 数値で入力して下さい。
        fncKsCmnMessageDisp MSGLVL_WARNNING, 22004, Msg1, MSG2_NOT_NUMERIC, True
        fncChkShuseiGakuFormat = 22004
        Exit Function
    End If

    If Len(Trim(Format(Target, "#"))) > LEN_CURRENCY Then
        '22006 １５桁以内の金額を入力して下さい｡
        fncKsCmnMessageDisp MSGLVL_WARNNING, 22006, Msg1, MSG2_LEN_CURRENCY, True
        fncChkShuseiGakuFormat = 22006
        Exit Function
    End If
    
EndProcess:
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    fncChkShuseiGakuFormat = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function

'*************************************************************************
'
'関数名　　：項目変更チェック
'
'引　数　　：ARG1   O   ErrRow      long    エラーセルの行
'　　　　　：ARG2   O   ErrCol      long    エラーセルの列
'
'戻り値　　：0:正常終了(変更なし)、0以外:エラー番号
'
'機能説明　：最初の入力・選択値と更新実行時の値が変更されて
'           いないかをチェックする
'
'更新履歴　：2008/06/20 SRA吉井   新規作成
'
'*************************************************************************
Public Function fncChkDataChange(ByRef ErrRow As Long, _
                                 ByRef ErrCol As Long) As Long

    Const PROCEDURE_NAME As String = "fncChkDataChange"

On Error GoTo ErrHandler
    
    Dim lngRet      As Long
    Dim lngSelRow   As Long
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkDataChange = 0
    
    With Sheets(SHEET_MAIN)
        '外部委託先番号
        If .Range(RNG_GI_NO).Value <> .Range(RNG_SV_GI_NO).Value Then
            ErrRow = .Range(RNG_GI_NO).Row
            ErrCol = .Range(RNG_GI_NO).Column
            '22009 検索時の情報と異なります。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 22009, MSG1_GI_SAKI, MSG2_CHG_GI_NO, True
            fncChkDataChange = 22009
            Exit Function
        End If
        
        '送付日
        If Trim(.Range(RNG_SOUFUBI).Value) <> Trim(.Range(RNG_SV_SOUFUBI).Value) Then
            ErrRow = .Range(RNG_SOUFUBI).Row
            ErrCol = .Range(RNG_SOUFUBI).Column
            '22009 検索時の情報と異なります。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 22009, MSG1_SOUFUBI, MSG2_CHG_SOUFUBI, True
            fncChkDataChange = 22009
            Exit Function
        End If
        
    End With
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrHandler:
    fncChkDataChange = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function
