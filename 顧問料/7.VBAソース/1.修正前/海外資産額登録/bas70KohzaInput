Attribute VB_Name = "bas70KohzaInput"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料・海外資産額登録
'
'オブジェクト名：bas70KohzaInput
'
'機能概要　：口座番号入力時処理
'
'更新履歴　：2007/11/30 SRA 東       新規作成
'
'*************************************************************************

Const MODULE_NAME = "bas70KohzaInput"

'*************************************************************************
'関数名　　：口座情報取得処理
'
'引　　数　：
'           TargetSheets        I/O     シート
'           lngRow              I       対象行
'           strYYYYMM           I       基準月
'           strKohzaNo          I       口座番号
'
'戻り値　　：Boolean
'
'機能説明　：海外資産額テーブル情報を取得する
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'更新履歴　：2008/01/22 SRA Y.Azuma    変更     (PH4)特殊文字バグ対応
'
'*************************************************************************
Public Function func_GetKohzaInfo(ByRef TargetSheets As Object _
                                , ByVal lngDetailRow As Long _
                                , ByVal strYYYYMM As String _
                                , ByVal strKohzaNo As String _
                                , ByRef lngRow As Long) As Boolean
On Error GoTo ErrorSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim strErrCode  As String
    Dim strDate     As String
    
    func_GetKohzaInfo = False

    '基準年月の月末日取得
    strDate = func_GetLastDay(strYYYYMM)
    
    '口座番号のゼロパディング
    strKohzaNo = Right(String(7, "0") & strKohzaNo, 7)
    
    strSql = ""
    strSql = strSql & " SELECT V01.PRTCD        KOHZA_NO"
    strSql = strSql & "       ,V01.NAME_RE      NAME_RE"
    strSql = strSql & "       ,V01.MNGCD        MNGCD"
    strSql = strSql & "       ,''               SHISAN_GAKU"
    strSql = strSql & "       ,CASE WHEN KKZ.KEIYAKUGAI_FROM <= '" & strDate & "' THEN"
    strSql = strSql & "            CASE WHEN KKZ.KEIYAKUGAI_TO >= '" & strDate & "' THEN"
    strSql = strSql & "                '対象外' "
    strSql = strSql & "            ELSE "
    strSql = strSql & "                '対象' "
    strSql = strSql & "            END "
    strSql = strSql & "        ELSE "
    strSql = strSql & "            '対象'"
    strSql = strSql & "        END              KEIYAKU_TAISHOH"
    strSql = strSql & "       ,'0'              KAKUTEI_FLG"
    strSql = strSql & "       ,''               TOHROKU_YMD"
    strSql = strSql & "   FROM VW_DT001D              V01"
    strSql = strSql & "       ,KYK_KOHZA_ZOKUSEI      KKZ"
'------------<Modify Start azuma 2008/01/22 (PH4)> ★★★☆-----------------------
    strSql = strSql & "  WHERE V01.PRTCD          = '" & func_ChkQuoteshon(strKohzaNo, "'") & "'"
'    strSql = strSql & "  WHERE V01.PRTCD          = '" & strKohzaNo & "'"
'------------<Modify End   azuma 2008/01/22 (PH4)> ★★★☆-----------------------
    strSql = strSql & "    AND (V01.CALLOFF_DATE  = '0' OR "
    strSql = strSql & "         V01.CALLOFF_DATE >= '" & strDate & "')"
    strSql = strSql & "    AND V01.PRTCD          = KKZ.KOHZA_NO(+)"
    strSql = strSql & "    AND KKZ.SAKUJO_FLG(+)  = '0'"

    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        Exit Function
    End If
    
'    blnProcessing = True
    If lngRow > 0 Then
        Call gCom01_SheetProtect(Worksheets(MAIN_SHEETNAME), False)
        With TargetSheets
            .Cells(lngDetailRow, .Range("口座番号").Column).Value = CStr(vData(EmGetDetailInfo.KOHZA_NO, 0))
            .Cells(lngDetailRow, .Range("口座名称").Column).Value = CStr(Trim(vData(EmGetDetailInfo.NAME_RE, 0)))
            .Cells(lngDetailRow, .Range("通貨").Column).Value = CStr(Trim(vData(EmGetDetailInfo.MNGCD, 0)))
            .Cells(lngDetailRow, .Range("契約カウント").Column).Value = CStr(vData(EmGetDetailInfo.KEIYAKU_TAISHOH, 0))
        End With
        Call gCom01_SheetProtect(Worksheets(MAIN_SHEETNAME), True)
    Else
        Call gCom01_SheetProtect(Worksheets(MAIN_SHEETNAME), False)
        With TargetSheets
            .Cells(lngDetailRow, .Range("口座名称").Column).Value = ""
            .Cells(lngDetailRow, .Range("通貨").Column).Value = ""
            .Cells(lngDetailRow, .Range("契約カウント").Column).Value = ""
        End With
        Call gCom01_SheetProtect(Worksheets(MAIN_SHEETNAME), True)
    End If
'    blnProcessing = False
    
    func_GetKohzaInfo = True
    
    
    Exit Function

ErrorSection:
    func_GetKohzaInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(MODULE_NAME, "func_GetKohzaInfo", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
'    Erase vData
    lngRow = 0

End Function


