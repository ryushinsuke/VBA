Attribute VB_Name = "bas30Kensaku"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料・海外資産額登録
'
'オブジェクト名：bas30Kensaku
'
'機能概要　：検索ボタン処理
'
'更新履歴　：2007/11/30 SRA 東       新規作成
'
'*************************************************************************

Const MODULE_NAME = "bas30Kensaku"



'*************************************************************************
'関数名　　：検索処理メイン処理
'
'引　　数　：
'           objTarget        I           対象のシート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Sub gsub_KensakuMain(ByRef objTarget As Object)

On Error GoTo ErrorSection

    Dim blnRet As Boolean
    
    blnRet = False
    
    
    With objTarget
    
        '明細部の初期化
        Call g_subIniScreenD(objTarget)   '明細部
        
        '入力チェック処理(検索時)
        blnRet = gfuncHeaderInputCheck(objTarget)
        
        If blnRet = False Then
            Call g_subIniScreenD(objTarget)   '明細部
            Exit Sub
        End If
        
        blnRet = False
        
        '検索処理とデータ設定
        blnRet = gfuncKaigaiShisan_DoSearch(objTarget)
        
        If blnRet = False Then
            
            'システムエラー
            Call gfunc_ErrorMsg(4, 16005, "", "")
            
            .cmdUp.Enabled = False
            .cmdDel.Enabled = False

            Call g_subIniScreenD(objTarget)   '明細部
            Exit Sub
        End If
        
        .Range("基準月").Select
    End With
    
    Exit Sub
    
ErrorSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gsub_KensakuMain", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Call g_subRateCodeIniScreenH(objTarget)   'ヘッダー部
    Call g_subIniScreenD(objTarget)   '明細部

End Sub


'*************************************************************************
'関数名　　：入力チェック処理
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Function gfuncHeaderInputCheck(ByRef TargetSheets As Object) As Boolean

On Error GoTo ErrorSection
    Dim strDate      As String
    
    gfuncHeaderInputCheck = False
    strDate = ""
    
'１．【基準月】
    strDate = Trim(TargetSheets.Range("基準月").Value)
    
    '①必須チェック
    If strDate = "" Then
        Call gfunc_ErrorMsg(3, 16001, "基準月", "")
        TargetSheets.Range("基準月").Select
        Exit Function
    End If

    
    '②桁数ﾁｪｯｸ
    If Len(strDate) <> 6 Then
        Call gfunc_ErrorMsg(3, 16002, "基準月", "YYYYMM形式の6桁で入力して下さい。")
        TargetSheets.Range("基準月").Select
        Exit Function
    End If
    
    strDate = strDate & "01"
    '③妥当チェック
    If gfunc_CheckDate(strDate) = False Then
        Call gfunc_ErrorMsg(3, 16003, "基準月", "")
        TargetSheets.Range("基準月").Select
        Exit Function
    End If
    
    gfuncHeaderInputCheck = True


    Exit Function
    
ErrorSection:

    gfuncHeaderInputCheck = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfuncHeaderInputCheck", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------


End Function

    

    
    

'*************************************************************************
'関数名　　：海外資産額検索処理
'
'引　　数　：
'           TargetSheets       I       シート
'
'戻り値　　：
'
'機能説明　：
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'          : 2010/04/15 SRA T.mizutani 海外資産額登録画面の過去資産額の表示改善
'
'*************************************************************************
Public Function gfuncKaigaiShisan_DoSearch(ByRef TargetSheets As Object) As Boolean
On Error GoTo ErrorSection
    
    Dim blnRet              As Boolean
    Dim strDate             As String
    Dim vData()             As Variant  '海外資産額テーブル取得データ
    Dim lngCnt              As Long     '海外資産額テーブル取得データ数
    Dim lngAddRow           As Long     '追加開始行番号
    Dim blnTaishou          As Boolean
    
'------------<Add Start T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
    Dim strDateZen          As String   '基準前月末日
'------------<Add End T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
    
    Erase vData
    
    gfuncKaigaiShisan_DoSearch = False
    
    blnTaishou = False
'*******************************************************
'* 関数開始　                                        　*
'*******************************************************

    '検索キー情報取得(基準年月の月末日)
    strDate = func_GetLastDay(TargetSheets.Range("基準月").Value)

    '海外資産額テーブル検索(基準年月と完全一致するデータの検索)
    lngCnt = 0
    blnRet = func_GetKaigaiInfo(strDate, vData, lngCnt)
                                   
    If blnRet = False Then
        Erase vData
        Exit Function
    End If
        
    If lngCnt <= 0 Then
'------------<Add Start T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
'    '直近の海外資産額取得
    '前月の海外資産額取得
    
    '基準年月の前月末日の取得
    strDateZen = func_GetZenLastDay(TargetSheets.Range("基準月").Value)
'------------<Add End T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
    
        'テーブル検索
        lngCnt = 0

'------------<Modify Start T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
'        blnRet = func_GetKaigaiLastestInfo(strDate, vData, lngCnt)
        blnRet = func_GetKaigaiLastestInfo(strDateZen, strDate, vData, lngCnt)
'------------<Modify End T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
                                       
        If blnRet = False Then
            Erase vData
            Exit Function
        End If
        blnTaishou = True
    End If
    
    
    If lngCnt > 0 Then
        '取得データの表示
        '情報の画面設定
        blnRet = func_SetKaigaiShisanInfo(TargetSheets, vData, lngCnt, blnTaishou)
        If blnRet = False Then
            Erase vData
            Exit Function
        End If
        
        With TargetSheets
            .cmdUp.Enabled = True
            .cmdDel.Enabled = True
        End With
    Else
    
        Call gfunc_ErrorMsg(3, 16004, "海外資産額", "")
        TargetSheets.Range("基準月").Select
        
        With TargetSheets
            .cmdUp.Enabled = True
            .cmdDel.Enabled = True
'            '追加行番号取得
'            lngAddRow = fncKsCmnSearchNewRow(TargetSheets)
'
'            '新規行追加
'            Call gCom01_SheetProtect(TargetSheets, False)
'            Call gKsC_fncKsCmnInsRow(ThisWorkbook.Worksheets(MAIN_SHEETNAME), lngAddRow, LIST_COPY_NEW_ROW)
'            Call gCom01_SheetProtect(TargetSheets, True)
            
        End With
    End If
    
    '取得データをメモリーから破棄
    Erase vData
    
    gfuncKaigaiShisan_DoSearch = True

    Exit Function

ErrorSection:

    gfuncKaigaiShisan_DoSearch = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfuncKaigaiShisan_DoSearch", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
    Erase vData

End Function



'*************************************************************************
'関数名　　：海外資産額テーブル情報取得処理
'
'引　　数　：
'           strDate         I       基準月
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：海外資産額テーブル情報を取得する
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Public Function func_GetKaigaiInfo(ByVal strDate As String _
                               , ByRef vData() As Variant _
                               , ByRef lngRow As Long) As Boolean
On Error GoTo ErrorSection

    Dim strSql      As String
    Dim lngCol      As Long
    Dim strErrCode  As String
    
    func_GetKaigaiInfo = False

    strSql = ""
    strSql = strSql & " SELECT A.KOHZA_NO       KOHZA_NO"
    strSql = strSql & "       ,B.NAME_RE        NAME_RE"
    strSql = strSql & "       ,B.MNGCD          MNGCD"
    strSql = strSql & "       ,A.SHISAN_GAKU    SHISAN_GAKU"
    strSql = strSql & "       ,CASE WHEN C.KEIYAKUGAI_FROM <= '" & strDate & "' THEN"
    strSql = strSql & "            CASE WHEN C.KEIYAKUGAI_TO >= '" & strDate & "' THEN"
    strSql = strSql & "                '対象外' "
    strSql = strSql & "            ELSE "
    strSql = strSql & "                '対象' "
    strSql = strSql & "            END "
    strSql = strSql & "        ELSE "
    strSql = strSql & "            '対象'"
    strSql = strSql & "        END              KEIYAKU_TAISHOH"
    strSql = strSql & "       ,A.KAKUTEI_FLG    KAKUTEI_FLG"
    strSql = strSql & "       ,A.KOHSHIN_YMD    TOHROKU_YMD"
    strSql = strSql & "   FROM " & TBL_KAIGAI_SHISAN_GAKU & " A"
    strSql = strSql & "       ,VW_DT001D              B"
    strSql = strSql & "       ,KYK_KOHZA_ZOKUSEI      C"
    strSql = strSql & "  WHERE A.KOHZA_NO      = B.PRTCD"
    strSql = strSql & "    AND A.SAKUJO_FLG    = '0'"
    strSql = strSql & "    AND A.KIJYUN_YMD    = '" & strDate & "'"
    strSql = strSql & "    AND (B.CALLOFF_DATE = '0' OR B.CALLOFF_DATE >= '" & strDate & "')"
    strSql = strSql & "    AND A.KOHZA_NO      = C.KOHZA_NO(+)"
    strSql = strSql & "    AND C.SAKUJO_FLG(+) = '0'"
    strSql = strSql & "  ORDER BY A.KOHZA_NO  "

    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        lngRow = 0
        Exit Function
    End If
    
    func_GetKaigaiInfo = True
    
    Exit Function

ErrorSection:
    func_GetKaigaiInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetKaigaiInfo", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
'    Erase vData
    lngRow = 0


End Function

'*************************************************************************
'関数名　　：前月末海外資産情報取得処理
'
'引　　数　：
'           strDateZen      I       基準月前月末日
'           strDate         I       基準月月末日
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：基準月前月の海外資産額を取得する
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'          : 2010/04/15 SRA T.mizutani 海外資産額登録画面の過去資産額の表示改善
'
'*************************************************************************
'Public Function func_GetKaigaiLastestInfo(ByVal strDate As String _
'                               , ByRef vData() As Variant _
'                               , ByRef lngRow As Long) As Boolean
Public Function func_GetKaigaiLastestInfo(ByVal strDateZen As String _
                               , ByVal strDate As String _
                               , ByRef vData() As Variant _
                               , ByRef lngRow As Long) As Boolean
On Error GoTo ErrorSection
    Dim strSql      As String
    Dim lngCol      As Long
    Dim strErrCode  As String
    
    func_GetKaigaiLastestInfo = False

    strSql = ""
    strSql = strSql & " SELECT KSG.KOHZA_NO     KOHZA_NO"
    strSql = strSql & "       ,V01.NAME_RE      NAME_RE"
    strSql = strSql & "       ,V01.MNGCD        MNGCD"
    strSql = strSql & "       ,KSG.SHISAN_GAKU  SHISAN_GAKU"
    strSql = strSql & "       ,CASE WHEN KKZ.KEIYAKUGAI_FROM <= '" & strDate & "' THEN"
    strSql = strSql & "            CASE WHEN KKZ.KEIYAKUGAI_TO >= '" & strDate & "' THEN"
    strSql = strSql & "                '対象外' "
    strSql = strSql & "            ELSE "
    strSql = strSql & "                '対象' "
    strSql = strSql & "            END "
    strSql = strSql & "        ELSE "
    strSql = strSql & "            '対象'"
    strSql = strSql & "        END              KEIYAKU_TAISHOH"
    strSql = strSql & "       ,'0'              KAKUTEI_FLG"
    strSql = strSql & "       ,''               TOHROKU_YMD"
    strSql = strSql & "   FROM " & TBL_KAIGAI_SHISAN_GAKU & "  KSG"
    strSql = strSql & "       ,(SELECT A.KOHZA_NO        KOHZA_NO"
'------------<Modify Start T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
'    strSql = strSql & "               ,MAX(A.KIJYUN_YMD) KIJYUN_YMD"
    strSql = strSql & "               ,A.KIJYUN_YMD KIJYUN_YMD"
'------------<Modify End T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
    strSql = strSql & "           FROM " & TBL_KAIGAI_SHISAN_GAKU & " A"
    strSql = strSql & "               ,VW_DT001D              B"
    strSql = strSql & "          WHERE A.KOHZA_NO      = B.PRTCD"
'------------<Modify Start T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
'    strSql = strSql & "            AND A.KIJYUN_YMD   <= '" & strDate & "'"
    strSql = strSql & "            AND A.KIJYUN_YMD   = '" & strDateZen & "'"
'------------<Modify End T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
    strSql = strSql & "            AND A.SAKUJO_FLG    = '0'"
    strSql = strSql & "            AND (B.CALLOFF_DATE = '0' OR B.CALLOFF_DATE >= '" & strDate & "')"
'------------<Modify Start T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
'    strSql = strSql & "          GROUP BY A.KOHZA_NO)    TKZ"
    strSql = strSql & "        )    TKZ"
'------------<Modify End T.Mizutani 2010/4/15 (海外資産額登録画面の過去資産額の表示改善)> ------------
    strSql = strSql & "       ,VW_DT001D              V01"
    strSql = strSql & "       ,KYK_KOHZA_ZOKUSEI      KKZ"
    strSql = strSql & "  WHERE KSG.KOHZA_NO      = TKZ.KOHZA_NO"
    strSql = strSql & "    AND KSG.KIJYUN_YMD    = TKZ.KIJYUN_YMD"
    strSql = strSql & "    AND KSG.KOHZA_NO      = V01.PRTCD"
    strSql = strSql & "    AND KSG.SAKUJO_FLG    = '0'"
    strSql = strSql & "    AND KSG.KOHZA_NO      = KKZ.KOHZA_NO(+)"
    strSql = strSql & "    AND KKZ.SAKUJO_FLG(+) = '0'"
    strSql = strSql & "  ORDER BY KSG.KOHZA_NO  "
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        lngRow = 0
        Exit Function
    End If
    
    func_GetKaigaiLastestInfo = True
    
    Exit Function

ErrorSection:
    func_GetKaigaiLastestInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetKaigaiLastestInfo", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
'    Erase vData
    lngRow = 0


End Function






'*************************************************************************
'関数名　　：ワークシートに取得情報を設定する
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           vdata()         I       設定データ
'           lngRow          I       設定データ件数
'           blnTaishou      I       対象チェック(True:ON、Flase:OFF)
'
'戻り値　　：Boolean
'
'機能説明　：処理対象ワークシートにデータを設定する。
'
'更新履歴　：2007/11/30 SRA Y.Azuma    新規作成
'
'*************************************************************************
Private Function func_SetKaigaiShisanInfo(ByRef TargetSheets As Object _
                                   , ByVal vData As Variant _
                                   , ByVal lngRow As Long _
                                   , ByVal blnTaishou As Boolean) As Boolean
On Error GoTo ErrorSection

    Dim lngCnt          As Long
    Dim lngDankai       As Long
    Dim lngAddRow       As Long
    Dim strLinkValue    As String


    func_SetKaigaiShisanInfo = False
    
    If lngRow > 0 Then
        
        For lngCnt = 0 To lngRow - 1
            
            '行追加
            '追加行番号取得
            lngAddRow = fncKsCmnSearchNewRow(ThisWorkbook.Worksheets(MAIN_SHEETNAME))
            
            '行追加
            Call gKsC_fncKsCmnInsRow(ThisWorkbook.Worksheets(MAIN_SHEETNAME), lngAddRow, LIST_COPY_SEARCH_ROW)
            
            With TargetSheets
            
                lngDankai = lngAddRow - .Range("対象").Row
            
                '対象の設定
                If blnTaishou = True Then
                    strLinkValue = CHECK_ON
                Else
                    strLinkValue = CHECK_OFF
                End If
                .Range("対象リンクセル").Offset(lngDankai, 0) = strLinkValue
                
                .Range("口座番号").Offset(lngDankai, 0) = CStr(vData(EmGetDetailInfo.KOHZA_NO, lngCnt - 0))
                .Range("口座名称").Offset(lngDankai, 0) = CStr(Trim(vData(EmGetDetailInfo.NAME_RE, lngCnt - 0)))
                .Range("通貨").Offset(lngDankai, 0) = CStr(Trim(vData(EmGetDetailInfo.MNGCD, lngCnt - 0)))
                .Range("資産額").Offset(lngDankai, 0) = func_Format(CStr(vData(EmGetDetailInfo.SHISAN_GAKU, lngCnt - 0)), .Range("資産額").Column)
                .Range("契約カウント").Offset(lngDankai, 0) = CStr(vData(EmGetDetailInfo.KEIYAKU_TAISHOH, lngCnt - 0))
                
                If CStr(vData(EmGetDetailInfo.KAKUTEI_FLG, lngCnt - 0)) = "1" Then
                    strLinkValue = CHECK_ON
                Else
                    strLinkValue = CHECK_OFF
                End If
                .Range("確定リンクセル").Offset(lngDankai, 0) = strLinkValue
                
                .Range("更新年月日").Offset(lngDankai, 0) = CStr(Trim(vData(EmGetDetailInfo.TOHROKU_YMD, lngCnt - 0)))
            
            End With
            
        Next lngCnt
    Else
    '処理なし
    End If

    func_SetKaigaiShisanInfo = True

    Exit Function

ErrorSection:
    func_SetKaigaiShisanInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_SetKaigaiShisanInfo", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function


