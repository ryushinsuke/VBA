Attribute VB_Name = "basSearch"
Option Explicit

'*************************************************************************
'
'プロジェクト名：契約管理システム・請求額修正
'
'オブジェクト名：basSearch
'
'機能概要　：請求情報、顧問料情報検索
'
'更新履歴　：2007/04/17 SRA T.Ichikawa  新規作成
'          ：2009/07/16 SRA T.Mizutani  請求額修正画面における消費税額修正
'
'*************************************************************************
Private Const MODULE_NAME As String = "basSearch"
Public gintShouhi_Zei As Integer

'一覧表示欄取得レコード
Private Enum eIchiranRec
    eSEIKYU_KIKAN_ID                '請求期間ID
    eSHUUSEIGAKU_SAIYOU_FLG         '修正額採用フラグ
    eSEIKYUSHO_KIKAN_FROM           '請求書期間FROM
    eSEIKYUSHO_KIKAN_TO             '請求書期間TO
    eHIZUKE_SHURUI                  '日付種類
    eNYUKIN_YOTEI_YMD               '入金予定日
    eSTATUS                         'ステータス
    eKAZEI_ATO_KONKAI_SEIKYU_GAKU   '課税後今回請求額
    eSHOHIZEI_GAKU                  '消費税額
    eKAZEI_ATO_SEIKYU_SHUUSEIGAKU   '課税後請求顧問料修正額
    eSHOHIZEI_SHUUSEIGAKU           '消費税修正額
End Enum

'*************************************************************************
'関数名　　：検索ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：入力フィールドの初期化
'
'更新履歴　：2007/04/17 SRA T.Ichikawa  新規作成
'          ：2009/07/16 SRA T.Mizutani  請求額修正画面における消費税額修正
'
'*************************************************************************
Function fncSearch() As Long

    Const PROCEDURE_NAME As String = "fncSearch"

On Error GoTo ErrorHandler

    Dim lngRet          As Long
    Dim strKouza_no     As String
    Dim strKouza_name   As String
    Dim strSeikyubi     As String
    Dim lngRowCnt       As Long
    Dim lngRow          As Long
    Dim vntRec()        As Variant
    Dim vntRec_Tax()    As Variant
    Dim lngCnt          As Long
    Dim lngTaxCnt       As Long

    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)

    fncSearch = 0

    '入力チェック
    With Sheets(SHEET_MAIN)
        Application.ScreenUpdating = False
        '一覧表示欄 削除
        fncKsCmnClearField 1

        '口座番号
        lngRet = fncChkKouzaNoFormat(.Range(RNG_KOUZA_NO).Value)
        If lngRet <> 0 Then
            .Range(RNG_KOUZA_NM).Value = vbNullString
            .Range(RNG_KOUZA_NO).Select
            fncSearch = lngRet
            Exit Function
        Else
            strKouza_no = Format(.Range(RNG_KOUZA_NO).Value, "0000000")
        End If

        '請求日
        If .Range(RNG_SEIKYUBI).Value = vbNullString Then
            '請求日が未入力の場合、DBのSYSDATEをセット
            lngRet = fncGetDbSysdate(strSeikyubi)
            If lngRet <> 0 Then
                fncSearch = lngRet
                Exit Function
            End If
            .Range(RNG_SEIKYUBI).Value = strSeikyubi
        Else
            '請求日の形式をチェック
            lngRet = fncChkDateFormat(.Range(RNG_SEIKYUBI).Value, MSG1_SEIKYUBI)
            If lngRet <> 0 Then
                .Range(RNG_SEIKYUBI).Select
                fncSearch = lngRet
                Exit Function
            End If
        End If

        '口座番号情報取得
        lngRet = fncChkKouzaNoInfo( _
                    strKouza_no, _
                    strKouza_name)

        If lngRet = 0 Then
            '口座名称表示
            .Range(RNG_KOUZA_NM).Value = strKouza_name

        Else
            .Range(RNG_KOUZA_NM).Value = vbNullString
            .Range(RNG_KOUZA_NO).Select
            fncSearch = lngRet
            Exit Function
        End If

        '一覧表示欄取得
        lngRet = fncIchiranRead(strKouza_no, .Range(RNG_SEIKYUBI).Value, vntRec, lngRowCnt)
        If lngRet <> 0 Then
            fncSearch = lngRet
            Exit Function
        End If

        If lngRowCnt > 0 Then

            '消費税率取得
            lngRet = fncShouhizeiGet(vntRec(eIchiranRec.eSEIKYUSHO_KIKAN_FROM, 0), vntRec_Tax, lngTaxCnt)
            If lngRet <> 0 Then
                fncSearch = lngRet
                Exit Function
            ElseIf lngTaxCnt = 0 Then
                '13014 消費税率の取得に失敗しました。
                fncKsCmnMessageDisp MSGLVL_WARNNING, 13014, MSG1_ICHIRAN_DISP, , True
                fncSearch = 13014
                Exit Function
            End If

            gintShouhi_Zei = vntRec_Tax(0, 0)

            '一覧表示欄 行追加
            fncKsCmnAddNrow lngRowCnt

            'データセット
            lngRow = LIST_DATA_TOP_ROW
            For lngCnt = 0 To lngRowCnt - 1

                .Cells(lngRow, COLN_SEIKYU_ID) = vntRec(eIchiranRec.eSEIKYU_KIKAN_ID, lngCnt)

                '修正額採用
                If vntRec(eIchiranRec.eSHUUSEIGAKU_SAIYOU_FLG, lngCnt) = "1" Then
                    .Cells(lngRow, COLN_DATA_TOP).Value = "TRUE"
                    .Cells(lngRow, COLN_J_CHK_DEFAULT).Value = "TRUE"
                    .Cells(lngRow, COLN_J_CHK_FLG).Value = "TRUE"
                Else
                    .Cells(lngRow, COLN_DATA_TOP).Value = "FALSE"
                    .Cells(lngRow, COLN_J_CHK_DEFAULT).Value = "FALSE"
                    .Cells(lngRow, COLN_J_CHK_FLG).Value = "FALSE"
                End If

                .Cells(lngRow, COLN_SEIKYU_SHO_KIKAN_FROM) = vntRec(eIchiranRec.eSEIKYUSHO_KIKAN_FROM, lngCnt)
                .Cells(lngRow, COLN_SEIKYU_SHO_KIKAN_TO) = vntRec(eIchiranRec.eSEIKYUSHO_KIKAN_TO, lngCnt)
                .Cells(lngRow, COLN_KAMI_SHIMO_KBN) = fnc_SearchComboItem(vntRec(eIchiranRec.eHIZUKE_SHURUI, lngCnt), "上下期区分リスト", 1)
                .Cells(lngRow, COLN_HIZUKE_SHURUI) = vntRec(eIchiranRec.eHIZUKE_SHURUI, lngCnt)
                .Cells(lngRow, COLN_NYUKIN_YOTEIBI) = vntRec(eIchiranRec.eNYUKIN_YOTEI_YMD, lngCnt)
                .Cells(lngRow, COLN_STATUS) = fnc_SearchComboItem(vntRec(eIchiranRec.eSTATUS, lngCnt), "ステータスリスト", 1)
                .Cells(lngRow, COLN_SEIKYU_GAKU) = vntRec(eIchiranRec.eKAZEI_ATO_KONKAI_SEIKYU_GAKU, lngCnt)
                .Cells(lngRow, COLN_SHOHIZEI_GAKU) = vntRec(eIchiranRec.eSHOHIZEI_GAKU, lngCnt)
                .Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU) = vntRec(eIchiranRec.eKAZEI_ATO_SEIKYU_SHUUSEIGAKU, lngCnt)
                
                '------------<Modify Start T.Mizutani 2009/07/16>-----------------------
                .Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU) = vntRec(eIchiranRec.eSHOHIZEI_SHUUSEIGAKU, lngCnt)
                '------------<Modify End T.Mizutani 2009/07/16>-----------------------

                '入金済の時は修正額採用と課税後請求顧問料修正額セルの背景色を白、入力不可にする
                If vntRec(eIchiranRec.eSTATUS, lngCnt) = "2" Then
                    .Cells(lngRow, COLN_DATA_TOP).Select
                    Selection.Interior.ColorIndex = xlNone
                    .Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Select
                    With Selection
                        .Interior.ColorIndex = xlNone
                        .Locked = True
                        .Borders(xlEdgeLeft).ColorIndex = xlAutomatic
                        .Borders(xlEdgeTop).ColorIndex = xlAutomatic
                        .Borders(xlEdgeBottom).ColorIndex = xlAutomatic
                        .Borders(xlEdgeRight).ColorIndex = xlAutomatic
                    End With
                    
                    '------------<Modify Start T.Mizutani 2009/07/16>-----------------------
                    '消費税修正額セルも背景色を白、入力不可にする
                    .Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Select
                    With Selection
                        .Interior.ColorIndex = xlNone
                        .Locked = True
                        .Borders(xlEdgeLeft).ColorIndex = xlAutomatic
                        .Borders(xlEdgeTop).ColorIndex = xlAutomatic
                        .Borders(xlEdgeBottom).ColorIndex = xlAutomatic
                        .Borders(xlEdgeRight).ColorIndex = xlAutomatic
                    End With
                    '------------<Modify Start T.Mizutani 2009/07/16>-----------------------
                    
                End If

                lngRow = lngRow + 1
            Next

            '更新ボタンを押せるようにする
            .cmdUpdate.Enabled = True

            Application.ScreenUpdating = True

        Else
            '13005 該当データが存在しません。
            fncKsCmnMessageDisp MSGLVL_WARNNING, 13005, MSG1_ICHIRAN_DISP, , True
            fncSearch = 13005
            Exit Function

        End If

        '入力された口座番号・請求日を保存
        .Range(RNG_SV_KOUZA_NO).Value = strKouza_no
        .Range(RNG_SV_SEIKYUBI).Value = .Range(RNG_SEIKYUBI).Value

    End With

    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)

    Exit Function

ErrorHandler:
    Application.ScreenUpdating = True
    fncSearch = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)

End Function

