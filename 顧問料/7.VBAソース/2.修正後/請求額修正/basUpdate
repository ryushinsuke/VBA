Attribute VB_Name = "basUpdate"
Option Explicit

'*************************************************************************
'
'プロジェクト名：契約管理システム・請求額修正
'
'オブジェクト名：basUpdate
'
'機能概要　：   顧問料情報更新
'
'更新履歴　：2007/04/17 SRA T.Ichikawa  新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "basUpdate"

'顧問料情報レコード
Private Type TypeRecKsj
    Kohza_no            As String   '口座番号
    Seikyu_id           As String   '請求期間ID
    Hizuke_shurui       As String   '日付種類
    Keisan_Frombi       As Long     '計算FROM日
    Shuuseigaku_saiyou_flg  As Currency '修正額採用フラグ
    Kazei_Mae_Shuseigaku    As Currency '課税前今回請求修正額
    Shohizei_Shuuseigaku    As Currency '消費税修正額
    Kazei_Ato_Shuseigaku    As Currency '課税後今回請求修正額
    Kohshin_ymd         As Date     '更新年月日
    Kohshin_pgm_id      As String   '更新プログラムID
    Kohshin_tantohsha   As String   '更新担当者
    Sakujo_flg          As String   '削除フラグ
End Type
Private mrecKsj As TypeRecKsj


'*************************************************************************
'関数名　　：一覧表示欄チェック
'
'引　　数　：ARG1   O   TopRow      long    一覧表示欄の開始行番号
'          ：ARG2   O   ErrRow      long    エラー発生行番号
'　　　　　：ARG3   O   ErrCol      long    エラー発生列番号
'
'戻り値　　：エラー番号
'
'機能説明　：一覧表示欄の課税後請求顧問料修正額、消費税修正額の妥当性をチェックする
'
'更新履歴　：2007/04/17 SRA T.Ichikawa  新規作成
'          ：2009/07/16 SRA T.Mizutani  請求額修正画面における消費税額修正
'
'*************************************************************************
Public Function fncChkIchiranDisp(ByRef TopRow As Long, ByRef ErrRow As Long, _
                            ByRef ErrCol As Long) As Long

    Const PROCEDURE_NAME As String = "fncChkIchiranDisp"

    On Error GoTo ErrHandler
    
    Dim lngRow  As Long
    Dim lngCol  As Long
    Dim lngCnt  As Long
    Dim lngRet  As Long
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkIchiranDisp = 0
    
    '変更チェック
    '検索ボタン押下後、口座番号、請求日が変更されたかをチェックする
    lngRet = fncChkDataChange(ErrRow, ErrCol)
    If lngRet <> 0 Then
        fncChkIchiranDisp = lngRet
        Exit Function
    End If
    
    With Sheets(SHEET_MAIN)
        TopRow = fncKsCmnSearchDataTopRow
        lngRow = TopRow
        
        Do While .Cells(lngRow, COLN_DATA_FLAG) <> vbNullString
            
            '------------<Modify Start T.Mizutani 2009/07/16>-----------------------
            'NULLチェック (請求顧問料修正額)
            If .Cells(lngRow, COLN_J_CHK_FLG).Value = True And _
                Trim(.Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Value) = vbNullString Then
                ErrRow = lngRow
                ErrCol = COLN_SEIKYU_SHUSEI_GAKU
                '13002 未入力です。
                fncKsCmnMessageDisp MSGLVL_WARNNING, 13002, MSG1_SEIKYU_SHUSEI_GAKU, , True
                fncChkIchiranDisp = 13002
                Exit Function
            End If
            
            'NULLチェック (消費税修正額)
            If .Cells(lngRow, COLN_J_CHK_FLG).Value = True And _
                Trim(.Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value) = vbNullString Then
                ErrRow = lngRow
                ErrCol = COLN_SHOHIZEI_SHUUSEIGAKU
                '13002 未入力です。
                fncKsCmnMessageDisp MSGLVL_WARNNING, 13002, MSG1_SHOHIZEI_SHUSEI_GAKU, , True
                fncChkIchiranDisp = 13002
                Exit Function
            End If
            
            '採用フラグがOFFの場合のNULLチェック
            If .Cells(lngRow, COLN_J_CHK_FLG).Value = False Then
            
                If (Trim(.Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Value) = vbNullString And _
                    Trim(.Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value) <> vbNullString) _
                    Or _
                    (Trim(.Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Value) <> vbNullString And _
                    Trim(.Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value) = vbNullString) Then
                    
                    ErrRow = lngRow
                    ErrCol = COLN_SEIKYU_SHUSEI_GAKU
                    '13015 入力に誤りがあります。
                    fncKsCmnMessageDisp MSGLVL_WARNNING, 13015, MSG1_SHUSEI_GAKU, MSG2_CHK_SHUSEI_GAKU, True
                    fncChkIchiranDisp = 13015
                    Exit Function
                End If
                
            End If
            '------------<Modify End T.Mizutani 2009/07/16>-----------------------
            
            '課税後請求顧問料修正額チェック
            lngRet = fncChkShuseiGakuFormat(.Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU), MSG1_SEIKYU_SHUSEI_GAKU)
            If lngRet <> 0 Then
                ErrRow = lngRow
                ErrCol = COLN_SEIKYU_SHUSEI_GAKU
                fncChkIchiranDisp = lngRet
                Exit Function
            End If
            
            '------------<Modify Start T.Mizutani 2009/07/16>-----------------------
            '消費税修正額チェック
            lngRet = fncChkShuseiGakuFormat(.Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU), MSG1_SHOHIZEI_SHUSEI_GAKU)
            If lngRet <> 0 Then
                ErrRow = lngRow
                ErrCol = COLN_SHOHIZEI_SHUUSEIGAKU
                fncChkIchiranDisp = lngRet
                Exit Function
            End If
            
            '修正額の大小チェック
            If Trim(.Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Value) <> vbNullString And _
                Trim(.Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value) <> vbNullString Then
                
                '消費税修正額が正の場合のチェック
                If .Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value > 0 Then
                    
                    If .Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Value < .Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value Then
                        ErrRow = lngRow
                        ErrCol = COLN_SEIKYU_SHUSEI_GAKU
                        '13015 入力に誤りがあります。
                        fncKsCmnMessageDisp MSGLVL_WARNNING, 13015, MSG1_SHUSEI_GAKU, MSG2_SHUSEI_GAKU_NG1, True
                        fncChkIchiranDisp = 13015
                        Exit Function
                    End If
                    
                '消費税修正額が負の場合のチェック
                ElseIf .Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value < 0 Then
                    
                    If .Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU).Value > .Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU).Value Then
                        ErrRow = lngRow
                        ErrCol = COLN_SEIKYU_SHUSEI_GAKU
                        '13015 入力に誤りがあります。
                        fncKsCmnMessageDisp MSGLVL_WARNNING, 13015, MSG1_SHUSEI_GAKU, MSG2_SHUSEI_GAKU_NG2, True
                        fncChkIchiranDisp = 13015
                        Exit Function
                    End If
                    
                '消費税修正額が0の場合
                Else
                    '処理しない
                    
                End If
                
            End If
            '------------<Modify End T.Mizutani 2009/07/16>-----------------------
            
            lngRow = lngRow + 1
        Loop
    End With
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
ErrHandler:
    fncChkIchiranDisp = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function

'*************************************************************************
'関数名　　：更新処理
'
'引　　数　：ARG1   I   Kouza_no        Strinng 口座番号
'　　　　　：ARG2   I   Seikyu_id       Strinng 請求期間ID
'　　　　　：ARG3   I   Hizuke_Shurui   Strinng 日付種類
'
'戻り値　　：エラー番号
'
'機能説明　：時価情報を基に資産時価テーブルを更新する
'
'更新履歴　：2007/04/17 SRA T.Ichikawa  新規作成
'
'*************************************************************************
Public Function fncExecUpdate( _
                    ByVal Kouza_no As String, _
                    ByVal Seikyu_id As String, _
                    ByVal Hizuke_shurui As String) As Long

    Const PROCEDURE_NAME As String = "fncExecUpdate"

    On Error GoTo ErrHandler
    
    Dim strSql      As String
    Dim recSel      As ADODB.Recordset
    Dim lngRow      As Long
    
    Dim vntRec()    As Variant
    Dim lngCol      As Long
    Dim lngRecCnt   As Long
    Dim strErrCd    As String
    Dim blnRet      As Boolean
    Dim lngUpdCnt   As Long     '更新件数カウント用(0件の場合エラーメッセージを出力する)
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncExecUpdate = 0
    
    lngUpdCnt = 0
        
    'トランザクション開始
    fncKsCmnBeginTrans
        
    '基礎数値情報データの先頭行取得
    lngRow = fncKsCmnSearchDataTopRow
        
    With mrecKsj

        Do While Sheets(SHEET_MAIN).Cells(lngRow, COLN_DATA_FLAG) <> vbNullString
        
            If Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_CHK_DEFAULT) <> False Or Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_CHK_FLG) <> False Then
            
                '入金済チェック
                If Sheets(SHEET_MAIN).Cells(lngRow, COLN_STATUS) = "入金済" Then
                    '13013 入金済のデータは更新できません。
                    fncKsCmnMessageDisp MSGLVL_WARNNING, 13013, MSG1_ICHIRAN_DISP, "", True
                    fncExecUpdate = 13013
                    'ロールバック
                    GoTo RollBackExit
                End If
            
                .Kohza_no = Kouza_no    '口座番号
                .Seikyu_id = Seikyu_id  '請求期間ID
                .Hizuke_shurui = Hizuke_shurui  '日付種類
                .Keisan_Frombi = Sheets(SHEET_MAIN).Cells(lngRow, COLN_SEIKYU_SHO_KIKAN_FROM)  '計算FROM日
                '修正額採用フラグ
                If Sheets(SHEET_MAIN).Cells(lngRow, COLN_J_CHK_FLG) = True Then
                    .Shuuseigaku_saiyou_flg = "1"
                Else
                    .Shuuseigaku_saiyou_flg = "0"
                End If
                
                
                '課税後請求顧問料修正額がブランクの場合
                If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU)) = vbNullString Then
                    .Kazei_Mae_Shuseigaku = 0   '課税前今回請求修正額
                    .Kazei_Ato_Shuseigaku = 0   '課税後今回請求修正額
                Else
                    .Kazei_Mae_Shuseigaku = Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU)) - Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU)) '課税前今回請求修正額
                    .Kazei_Ato_Shuseigaku = Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU)) '課税後今回請求修正額
                End If
                
                '消費税修正額がブランクの場合
                If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU)) = vbNullString Then
                    .Shohizei_Shuuseigaku = 0
                Else
                    .Shohizei_Shuuseigaku = Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU))
                End If
                
                .Kohshin_ymd = Format(Now, "YYYY/MM/DD")    '更新年月日
                .Kohshin_pgm_id = PROGRAM_ID                '更新プログラムID
                .Kohshin_tantohsha = USER_ID                '更新担当者
                .Sakujo_flg = "0"                           '削除フラグ
            
                'ODBCエラーのコードをセット
                '[Oracle][ODBC][Ora]ORA-00054: リソース・ビジー、NOWAITが指定されていました。
                strErrCd = ODBC_ERRCD_RESOURCE_BUSY
                
                strSql = ""
                strSql = strSql & "SELECT" & vbCrLf
                strSql = strSql & "    *" & vbCrLf
                strSql = strSql & "FROM" & vbCrLf
                strSql = strSql & "    KYK_KOMONRYOH_SUM" & vbCrLf
                strSql = strSql & "WHERE" & vbCrLf
                strSql = strSql & "    KOHZA_NO = '" & .Kohza_no & "'" & vbCrLf
                strSql = strSql & "AND SEIKYU_KIKAN_ID = '" & .Seikyu_id & "'" & vbCrLf
                strSql = strSql & "AND HIZUKE_SHURUI = '" & .Hizuke_shurui & "'" & vbCrLf
                strSql = strSql & "AND KEISAN_FROM_YMD = " & .Keisan_Frombi & vbCrLf
                strSql = strSql & "AND SAKUJO_FLG = '0'" & vbCrLf
                strSql = strSql & "FOR UPDATE NOWAIT" & vbCrLf
    
                blnRet = fncKsCmnDoExequteSql(MainMenu.gcSELECT, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
                If blnRet = False Then
                    If strErrCd = ODBC_ERRCD_RESOURCE_BUSY Then
                        'リソース・ビジー
                        '13010 他のユーザーにより更新されています。
                        fncKsCmnMessageDisp MSGLVL_WARNNING, 13010, MSG1_ICHIRAN_DISP, "", True
                        fncExecUpdate = 13010
                    Else
                        fncExecUpdate = strErrCd
                    End If
                
                    'ロールバック
                    GoTo RollBackExit
                End If
                
                If lngRecCnt > 1 Then
                    '更新対象データが顧問料合計に複数件ある
                    fncKsCmnMessageDisp MSGLVL_WARNNING, 13008, MSG1_ICHIRAN_DISP, MSG2_MANY_ROWS_SJ, True
                    fncExecUpdate = 13008
                    'ロールバック
                    GoTo RollBackExit
                End If
            
                If lngRecCnt = 0 Then
                    '13011 他ユーザにより削除されています。
                    fncKsCmnMessageDisp MSGLVL_WARNNING, 13011, MSG1_ICHIRAN_DISP, "", True
                    fncExecUpdate = 13011
                    'ロールバック
                    GoTo RollBackExit
                
                Else
                
                    strSql = ""
                    strSql = strSql & "UPDATE KYK_KOMONRYOH_SUM" & vbCrLf
                    strSql = strSql & "SET" & vbCrLf
                    strSql = strSql & "    KOHSHIN_YMD = TO_CHAR(SYSDATE,'YYYY/MM/DD')," & vbCrLf
                    strSql = strSql & "    KOHSHIN_PGM_ID      = '" & .Kohshin_pgm_id & "'," & vbCrLf
                    strSql = strSql & "    KOHSHIN_TANTOHSHA   = '" & .Kohshin_tantohsha & "'," & vbCrLf
                    
                    If .Shuuseigaku_saiyou_flg = False Then
                        strSql = strSql & "    SHUUSEIGAKU_SAIYOU_FLG = '0'," & vbCrLf
                    Else
                        strSql = strSql & "    SHUUSEIGAKU_SAIYOU_FLG = '1'," & vbCrLf
                    End If
                    
                    If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU)) = vbNullString Then
                        strSql = strSql & "    KAZEI_MAE_SEIKYU_SHUUSEIGAKU = NULL," & vbCrLf
                    Else
                        strSql = strSql & "    KAZEI_MAE_SEIKYU_SHUUSEIGAKU = " & .Kazei_Mae_Shuseigaku & "," & vbCrLf
                    End If
                    
                    If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SHOHIZEI_SHUUSEIGAKU)) = vbNullString Then
                        strSql = strSql & "    SHOHIZEI_SHUUSEIGAKU = NULL," & vbCrLf
                    Else
                        strSql = strSql & "    SHOHIZEI_SHUUSEIGAKU = " & .Shohizei_Shuuseigaku & "," & vbCrLf
                    End If
                    
                    If Trim(Sheets(SHEET_MAIN).Cells(lngRow, COLN_SEIKYU_SHUSEI_GAKU)) = vbNullString Then
                        strSql = strSql & "    KAZEI_ATO_SEIKYU_SHUUSEIGAKU = NULL" & vbCrLf
                    Else
                        strSql = strSql & "    KAZEI_ATO_SEIKYU_SHUUSEIGAKU = " & .Kazei_Ato_Shuseigaku & vbCrLf
                    End If
                    
                    strSql = strSql & "WHERE" & vbCrLf
                    strSql = strSql & "    KOHZA_NO = '" & .Kohza_no & "'" & vbCrLf
                    strSql = strSql & "AND SEIKYU_KIKAN_ID = '" & .Seikyu_id & "'" & vbCrLf
                    strSql = strSql & "AND HIZUKE_SHURUI = '" & .Hizuke_shurui & "'" & vbCrLf
                    strSql = strSql & "AND KEISAN_FROM_YMD = " & .Keisan_Frombi & vbCrLf
                    
                    blnRet = fncKsCmnDoExequteSql(MainMenu.gcUPDATE, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
                    If blnRet = False Then
                        fncExecUpdate = strErrCd
                        'ロールバック
                        GoTo RollBackExit
                    End If
                    
                    lngUpdCnt = lngUpdCnt + 1
                End If
                
            End If
            
            lngRow = lngRow + 1
        Loop
    End With
    
    '更新件数が0の場合
    If lngUpdCnt = 0 Then
        '13012 更新対象のデータがありません。
        fncKsCmnMessageDisp MSGLVL_WARNNING, 13012, MSG1_ICHIRAN_DISP, "", True
        fncExecUpdate = 13012
        'ロールバック
        GoTo RollBackExit
    End If
    
    'コミット
    fncKsCmnCommitTrans
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
ErrHandler:
    fncExecUpdate = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
RollBackExit:
    'ロールバック
    fncKsCmnRollbackTrans
End Function

'*************************************************************************
'関数名　　：更新ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：更新ボタン処理
'
'更新履歴　：2007/04/17 SRA T.Ichikawa  新規作成
'
'*************************************************************************
Function fncUpdate() As Long
    
    Const PROCEDURE_NAME As String = "fncUpdate"
    
On Error GoTo ErrorHandler

    Dim lngRet      As Long
    Dim lngRow      As Long
    Dim lngErrRow   As Long
    Dim lngErrCol   As Long
    Dim strErrMsg   As String
    
    Dim strKouzaNo  As String
    Dim strSeikyuId As String
    Dim strHizukeShurui As String

    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)

    fncUpdate = 0
    
    '入力チェック
    lngRet = fncChkIchiranDisp(lngRow, lngErrRow, lngErrCol)
    If lngRet <> 0 Then
        Sheets(SHEET_MAIN).Cells(lngErrRow, lngErrCol).Select
        fncUpdate = lngRet
        Exit Function
    End If
    
    '更新処理
    With Sheets(SHEET_MAIN)
        strKouzaNo = .Range(RNG_SV_KOUZA_NO).Value
        strSeikyuId = .Cells(lngRow, COLN_SEIKYU_ID)
        strHizukeShurui = .Cells(lngRow, COLN_HIZUKE_SHURUI)
    End With
    
    '13001 更新確認
    If MsgBox(fncKsCmnMessageDisp(MSGLVL_QUESTION, 13001, "", , False), vbQuestion + vbYesNo, "確認") = vbNo Then
        Exit Function
    End If

    '更新実行
    lngRet = fncExecUpdate( _
                strKouzaNo, _
                strSeikyuId, _
                strHizukeShurui)
                
    If lngRet <> 0 Then
        fncUpdate = lngRet
        Exit Function
    End If
    
    '13000 正常終了メッセージ
    fncKsCmnMessageDisp MSGLVL_INFO, 13000, "", , True
    
    '顧問料情報を再表示させる
    Call fncSearch
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrorHandler:
    fncUpdate = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    
End Function


