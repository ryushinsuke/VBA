VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー計算システム
'
'オブジェクト名：外部委託先情報登録画面
'
'機能概要　：メイン画面
'
'更新履歴　：2008/06/03 SRA H.Anpo      新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "main"



'*************************************************************************
'関数名　　：組入れ情報の新規ボタン・イベントプロシージャ
'
'引　　数　：
'
'戻り値　　：
'
'機能説明　：新規ボタン押下時の処理
'
'更新履歴　：2008/06/9  SRA H.Anpo     新規作成
'
'*************************************************************************
Private Sub cmdAdd_Click()

    ThisWorkbook.Application.ScreenUpdating = False
    
    Call gsub_NewClick

    ThisWorkbook.Application.ScreenUpdating = True
    
End Sub

'*************************************************************************
'関数名　　：組入れ情報の削除ボタン・イベントプロシージャ
'
'引　　数　：
'
'戻り値　　：
'
'機能説明　：削除ボタン押下時の処理
'
'更新履歴　：2008/06/9  SRA H.Anpo     新規作成
'
'*************************************************************************
Private Sub cmdDel_Click()

    ThisWorkbook.Application.ScreenUpdating = False
    
    Call gsub_DelClick

    ThisWorkbook.Application.ScreenUpdating = True

End Sub


'*************************************************************************
'関数名　　：印刷ボタン・イベントプロシージャ
'
'引　　数　：
'
'戻り値　　：
'
'機能説明　：印刷ボタン押下時の処理
'
'更新履歴　：2008/06/9  SRA H.Anpo     新規作成
'
'*************************************************************************
Private Sub cmdPrint_Click()

On Error GoTo Error_Section

    ThisWorkbook.Application.Cursor = xlWait
    ThisWorkbook.Application.ScreenUpdating = False
    
    
    Dim strNowString As String
    Dim strDefaultString As String
    strNowString = fucGetAllString
    strDefaultString = ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時文字列").Value
    If strNowString <> strDefaultString Then
        ThisWorkbook.Worksheets(MAIN_SHEETNAME).cmdPrint.Enabled = False
        ThisWorkbook.Application.Cursor = xlDefault
        ThisWorkbook.Application.ScreenUpdating = True
        Call gfunc_ErrorMsg(3, 18002, "", "")
        Exit Sub
    End If
    
    With ThisWorkbook.Worksheets(MAIN_SHEETNAME).PageSetup

        '印刷範囲を1ページに収まるよう設定
        .Zoom = 76
        .FitToPagesWide = 1

        '印刷の向きを横に設定
        .Orientation = xlLandscape

        '印刷のサイズをＡ４に設定
        .PaperSize = xlPaperA4
        
        'フッターに検索時の支払期間ID表示
        .RightFooter = "(" & ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時支払期間ID") & ")"
    End With
            
    '承認印エリアの表示
    Call gfunc_VisibleSignArea(True)
    
    Call gfunc_VisibleCommandBtn(False)
    
'    Worksheets(MAIN_SHEETNAME).PrintPreview
    ThisWorkbook.Worksheets(MAIN_SHEETNAME).PrintOut
    
    
    Call gfunc_VisibleCommandBtn(True)
    
    '承認印エリアの非表示
    Call gfunc_VisibleSignArea(False)
    
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault

    Exit Sub

Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdPrint_Click", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.Cursor = xlDefault
    ThisWorkbook.Application.ScreenUpdating = True
    
    '印刷ボタンの無効化
    cmdPrint.Enabled = True


End Sub

'*************************************************************************
'関数名　　：検索ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：検索ボタンを押下された場合の処理
'
'更新履歴　：2008/06/05 SRA H.Anpo    新規作成
'
'*************************************************************************
Private Sub cmdSearch_Click()

On Error GoTo Error_Section

    '検索メイン
    ThisWorkbook.Application.ScreenUpdating = False
    ThisWorkbook.Application.Cursor = xlWait

    Call gsub_KensakuMain(ThisWorkbook.Worksheets(MAIN_SHEETNAME))

    ThisWorkbook.Application.Cursor = xlDefault
    ThisWorkbook.Application.ScreenUpdating = True

    Exit Sub

Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdSearch_Click", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault

End Sub


'*************************************************************************
'関数名　　：更新ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：更新ボタンを押下された場合の処理
'
'更新履歴　：2008/06/05 SRA H.Anpo    新規作成
'
'*************************************************************************
Private Sub cmdUp_Click()

On Error GoTo Error_Section

    Dim blnRet      As Boolean
    Dim Kumiire()   As Variant      '組入れ情報配列
    Dim strMsg      As String
    Dim lngRet      As Long
    Dim lngKikanID  As Long         '採番　　請求期間ID
    
    Erase Kumiire
    
    blnRet = False

    ThisWorkbook.Application.Cursor = xlWait
    
    '入力チェック処理(更新時)
    blnRet = gfuncKeisan_UpdInputCheck(ThisWorkbook.Worksheets(MAIN_SHEETNAME), Kumiire)

    If blnRet = False Then
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Kumiire
        Exit Sub
    End If
       
    '最終処理確認
    strMsg = gfunc_ErrorMsg(2, 18003, "", "", False)
    lngRet = MsgBox(strMsg, vbQuestion + vbOKCancel, ThisWorkbook.Application.ActiveWorkbook.Name)
    If lngRet = vbCancel Then
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Kumiire
        Exit Sub
    End If
    
    '更新処理
    blnRet = gfuncKeisan_DoUpdate(ThisWorkbook.Worksheets(MAIN_SHEETNAME), Kumiire, lngKikanID)
    
    If blnRet = False Then
        '更新エラー
        Call gfunc_ErrorMsg(1, 18004, "", "", True)
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Kumiire
        Exit Sub
    Else
    
    '更新成功
'''        '検索時の請求期間IDに登録した請求期間IDで上書きする。
'''        ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時請求期間ID").Value2 = lngKikanId
'''
'''        Dim strDate As String
'''        Dim strTime As String
'''        'サーバ日付の取得
'''        Call gfunc_GetSvDateTime(strDate, strTime)
'''
'''        'データ取得日時の設定
'''        Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), False)
'''        ThisWorkbook.Worksheets(MAIN_SHEETNAME).Range("データ取得日時").Value2 = gfunc_ymdFormat(strDate) & " " _
'''            & Mid(strTime, 1, 2) & ":" & Mid(strTime, 3, 2) & ":" & Mid(strTime, 5, 2)
'''        Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), True)
'''
'''        '印刷ボタンの有効化
'''        cmdPrint.Enabled = True
    
        '画面クリア処理
        ThisWorkbook.Application.ScreenUpdating = False
        
        Call g_subKeisanIniScreenH(ThisWorkbook.Worksheets(MAIN_SHEETNAME))   'ヘッダー部
        Call g_subKeisanIniScreenD(ThisWorkbook.Worksheets(MAIN_SHEETNAME))   '明細部
    
        '印刷ボタンの無効化
        cmdPrint.Enabled = False
    
'''        '検索処理
'''        Call gsub_KensakuMain(ThisWorkbook.Worksheets(MAIN_SHEETNAME))
    
        ThisWorkbook.Application.ScreenUpdating = True
    
        'メッセージ
        Call gfunc_ErrorMsg(1, 18000, "", "", True)
    End If
    
    ThisWorkbook.Application.Cursor = xlDefault
    
    Erase Kumiire
    
    Exit Sub
Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdUp_Click", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault
    Erase Kumiire

End Sub

'*************************************************************************
'関数名　　：セル変更処理・イベントプロシージャ
'
'引　　数　：
'
'戻り値　　：なし
'
'機能説明　：セル変更後の書式設定
'
'更新履歴　：2008/06/05 SRA H.Anpo    新規作成
'
'*************************************************************************
Private Sub Worksheet_Change(ByVal Target As Range)
On Error Resume Next
    
    Dim blnDataFlg  As Boolean
    Dim strResult   As String
    
    '日付のフォーマット
    With Target
        If Target = Range("支払期間") Or _
           Target = Range("支払期間FROM") Or Target = Range("支払期間TO") Or _
           Target = Range("契約期間FROM") Or Target = Range("契約期間TO") Or _
           Target = Range("送付日") Or Target = Range("支払予定日") Or _
           ((Target.Column = Range("計算期間FROM").Column Or Target.Column = Range("計算期間TO").Column) And _
           (Target.Row > Range("先頭行").Row And Target.Row < Range("最終行").Row)) _
           Then
            '日付形式(YYYY/MM/DD,YYYY/M/D)で入力された値を(YYYYMMDD)形式に変換する
            '　　※セルを日付型(YYYYMMDD)にすると"/"区切りで入力しないと####となる為の対策
            If IsDate(Target.Value) = True Then
                Target.Value = CStr(Format(Target.Value, "YYYYMMDD"))
            End If
        End If
    End With

    '口座番号のフォーマット
    With Target
        If ((Target.Column = Range("口座番号").Column) And _
           (Target.Row > Range("先頭行").Row And Target.Row < Range("最終行").Row)) _
           Then
            '口座番号を０埋めする
            If Len(Trim(Target.Value)) < 7 Then
                If IsNumeric(Trim(Target.Value)) Then
                    Target.Value = Format(Target.Value, "0000000")
                End If
            End If
        End If
    End With

    'リスト項目の検索
    With ThisWorkbook.Sheets(MAIN_SHEETNAME)
        If Target.Row > .Range("先頭行").Row And Target.Row < .Range("最終行").Row Then
            '料率コード、運用報酬料率コード
            If Target.Column = .Range("料率コード").Column Or Target.Column = .Range("運用報酬料率コード").Column Then
                strResult = gKsC_func_CodeListSearch(Target.Text, ThisWorkbook.Name, DATA_SHEETNAME, mC_NAME_LIST_RATE, blnDataFlg)
            End If
        End If
        If Trim(strResult) <> "" Then
            Target.Value = strResult
        End If
    End With
        
'''    If mainmenu.gOperationStatus <> "cmdSearch_Click" Then
'''        gfuncKeisan_FilterNameList ThisWorkbook.Worksheets(MAIN_SHEETNAME), Target
'''    End If

End Sub

'*************************************************************************
'関数名　　：料率コード補充ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：料率コード補充ボタンが押下された場合の処理
'
'更新履歴　：2010/06/22 SRA T.Mizutani    新規作成
'
'*************************************************************************
Private Sub cmdRateSet_Click()

On Error GoTo Error_Section
    
    Dim blnRet      As Boolean
    Dim Kumiire()   As Variant      '補充対象の組入れ情報配列
    Dim strMsg      As String
    Dim lngRet      As Long
    
    Erase Kumiire
    
    blnRet = False
    
    ThisWorkbook.Application.Cursor = xlWait
    
    '入力チェック処理(料率コード補充ボタン押下時)
    blnRet = gfunc_RateSetInputCheck(ThisWorkbook.Worksheets(MAIN_SHEETNAME), Kumiire)
    
    If blnRet = False Then
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Kumiire
        Exit Sub
    End If
    
    '補充実行確認
    strMsg = gfunc_ErrorMsg(2, 18551, "", "", False)
    
    lngRet = MsgBox(strMsg, vbQuestion + vbOKCancel, ThisWorkbook.Application.ActiveWorkbook.Name)
    
    If lngRet = vbCancel Then
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Kumiire
        Exit Sub
    End If
    
    'メイン処理
    blnRet = gfunc_DoRateSet(ThisWorkbook.Worksheets(MAIN_SHEETNAME), Kumiire)
    
    If blnRet = False Then
        'エラーが発生
        Call gfunc_ErrorMsg(3, 18552, "", "", True)
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Kumiire
        Exit Sub
    End If
    
    'メッセージ
    Call gfunc_ErrorMsg(1, 18000, "", "", True)
    
    ThisWorkbook.Application.Cursor = xlDefault
    
    Erase Kumiire
    
    Exit Sub
    
Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdRateSet_Click", Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.Cursor = xlDefault
    Erase Kumiire

End Sub

