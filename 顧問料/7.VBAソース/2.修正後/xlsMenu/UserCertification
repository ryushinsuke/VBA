VERSION 5.00
Begin {C62A69F0-16DC-11CE-9E98-00AA00574A4F} UserCertification 
   Caption         =   "ログイン"
   ClientHeight    =   1905
   ClientLeft      =   45
   ClientTop       =   435
   ClientWidth     =   6000
   OleObjectBlob   =   "UserCertification.frx":0000
   StartUpPosition =   1  'オーナー フォームの中央
End
Attribute VB_Name = "UserCertification"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False

Option Explicit

'DB接続用変数
    Private Conn                As New ADODB.Connection        '# Connection
    Private RS                  As New ADODB.Recordset         '# Recordset

'パラメータ情報
    Private strPass_YukoKgn     As String   'パスワード有効期限
    Private strPass_KgnUmu      As String   'パスワード期限有無
    Private strPass_YukoTuti    As String   'パスワード有効期限通知日数
    Private strLock_Cnt         As String   'ロックアウト回数
    Private strLock_Umu         As String   'ロックアウト有無
    Private strPass_Rireki      As String   '同一パスワードのチェックをする履歴数
    
'ユーザ情報
    Private strLockKbn          As String   'ユーザのロック区分
    Private strKengenKbn        As String   'ユーザの権限区分
    Private strErr_Count        As String   'ユーザのログインエラー回数
    Private strSedai_no         As String   'ユーザの世代No
    Private strShoki_pswd_flg   As String   '初期パスワードフラグ
    Private strPassTohroku_ymd  As String   '最新パスワード登録日時
    Private strSysDate          As String   'サーバ日付
    Private Const gc_NoData = "ORA-06510"   'ユーザー定義の例外コード '<----------- Modify azuma 2007/12/25 ★★★☆'

'*******************************************************
'* ＯＫボタン押下時                                　  *
'* 更新履歴 2013/04/18 GUT 劉  設定シートのログインユーザーの設定処理の追加
'*******************************************************
Private Sub cmb_OK_Click()
    
    Dim Ret                 As String
    Dim strPassKgnDate      As String   'パスワード期限日
    Dim strPassTutiDate     As String   'パスワード有効通知日
    
    '【ＯＫ】/【パスワード変更】ボタン押下時共通処理--------------------------------------------------------------
    Ret = UserCommon(strPass_YukoKgn, _
                     strPass_KgnUmu, _
                     strPass_YukoTuti, _
                     strLock_Cnt, _
                     strLock_Umu, _
                     strPass_Rireki, _
                     strLockKbn, _
                     strKengenKbn, _
                     strErr_Count, _
                     strSedai_no, _
                     strShoki_pswd_flg, _
                     strPassTohroku_ymd, _
                     strSysDate, _
                     Conn, _
                     RS)
    'UserCommonからの戻り値が"1"(False)の場合、ユーザID、パスワードチェックにて入力不備
    'またはシステムエラーが発生したため、処理を終了する
    If Ret = "1" Then Exit Sub
    '--------------------------------------------------------------------------------------------------------------

    '権限区分チェック(権限区分が"U"だったら、チェックロジックを実行する)
    If strKengenKbn = "U" Then
        
        '初期パスワードチェック------------------------------------------------------------------------------------
        If strShoki_pswd_flg = "1" Then
            '初期パスワードフラグが"1"(初期パスワードでのログイン)の場合、
            'パスワード変更画面へ遷移する
            MsgBox "初期パスワードでのログインです。 パスワードの変更を行ってください。", vbCritical
            'パスワード変更画面をオープンする
            UserPassWordChg.Show
            Exit Sub
        End If
        '----------------------------------------------------------------------------------------------------------
        
        'パスワード登録期限以内チェック----------------------------------------------------------------------------
        If strPass_KgnUmu = "1" And _
           DateAdd("d", strPass_YukoKgn, strPassTohroku_ymd) <= strSysDate Then
            'パスワード期限有無フラグが"1"(期限あり) かつ
            'パスワード登録日時から規定日数以上経過している場合、パスワードの変更を促す
            MsgBox "パスワードが有効期限を過ぎています。パスワードを変更してください。", vbCritical
            'パスワード変更画面をオープンする
            UserPassWordChg.Show
            Exit Sub
        End If
        '----------------------------------------------------------------------------------------------------------
        
        'パスワード有効期限通知日数チェック------------------------------------------------------------------------
        strPassKgnDate = Left(DateAdd("d", strPass_YukoKgn, strPassTohroku_ymd), 10)
        strPassTutiDate = DateAdd("d", -(strPass_YukoTuti), strPassKgnDate)
        If strPass_KgnUmu = "1" And _
           strPassTutiDate <= strSysDate Then
            'パスワード期限有無フラグが"1"(期限あり) かつ パスワード有効通知日数以内の場合、パスワード更新メッセージを表示する
'------------<連結障害対応 20080912 SRA東 START> -----------------------
'''            If MsgBox("パスワードが " & strPassKgnDate & " で有効期限となります。パスワード変更を行いますか？", _
'''                vbOKCancel + vbQuestion) = vbOK Then
            If MsgBox("パスワードが " & strPassKgnDate & " で期限切れとなります。パスワード変更を行いますか？", _
                vbOKCancel + vbQuestion) = vbOK Then
'------------<連結障害対応 20080912 SRA東 END  > -----------------------
                    'パスワード変更画面をオープンする
                    UserPassWordChg.Show
                    Exit Sub
            End If
        End If
        '----------------------------------------------------------------------------------------------------------
    End If
    
    'ユーザID/パスワード共に正常であることが確認できた
    '入力されたユーザIDのERR_COUNTを0に戻す
    '最終ログイン日時をシステム日時で更新する------------------------------------------------------------------
    Ret = ErrCOUNT_Clear(Trim(UserCertification.txt_UsrId), _
                         Conn, _
                         RS)
    If Ret = "False" Then Exit Sub
    '----------------------------------------------------------------------------------------------------------
    
    '権限区分チェック(権限区分が"A"だったら、管理者用Book(別ブック)へ遷移する)---------------------------------
    If strKengenKbn = "A" Then
        '管理者用Book(別ブック)ｵｰﾌﾟﾝ
        '※当ファイル(xlsMenu.xls)と同じフォルダに用意しておく必要がある
        ManagementBookOpen
        Workbook_Close Conn, RS
        Exit Sub
    End If
    '------------<顧問バック更改対応 2013/04/18 GUT劉 START> ------------
    ThisWorkbook.Worksheets("設定").Range("LOGIN_USER").Value = Trim(UserCertification.txt_UsrId.Value)
    '------------<顧問バック更改対応 2013/04/18 GUT劉 END> ------------
    '----------------------------------------------------------------------------------------------------------

    'ユーザ認証画面を閉じる
    Unload UserCertification

End Sub

'*******************************************************
'* パスワード変更ボタン押下時                      　  *
'*******************************************************
Private Sub cmd_PwdChg_Click()

    Dim Ret                 As String
    
    '【ＯＫ】/【パスワード変更】ボタン押下時共通処理-----------------------------------------------------------
    Ret = UserCommon(strPass_YukoKgn, _
                     strPass_KgnUmu, _
                     strPass_YukoTuti, _
                     strLock_Cnt, _
                     strLock_Umu, _
                     strPass_Rireki, _
                     strLockKbn, _
                     strKengenKbn, _
                     strErr_Count, _
                     strSedai_no, _
                     strShoki_pswd_flg, _
                     strPassTohroku_ymd, _
                     strSysDate, _
                     Conn, _
                     RS)
    'UserCommonからの戻り値が"1"(False)の場合、ユーザID、パスワードチェックにて入力不備
    'またはシステムエラーが発生したため、処理を終了する
    If Ret = "1" Then Exit Sub
    '----------------------------------------------------------------------------------------------------------
    
    'パスワード変更画面をオープンする
    UserPassWordChg.Show

End Sub

'*******************************************************
'* キャンセルボタン押下時                          　  *
'*******************************************************
Private Sub cmd_Cancel_Click()
    If MsgBox("ワークブックを終了しますか？", vbOKCancel + vbExclamation) = vbOK Then
        Workbook_Close Conn, RS
    Else
        'ユーザIDテキストボックスにセットフォーカス
        txt_UsrId.SelStart = 0
        txt_UsrId.SelLength = Len(txt_UsrId.Text)
        txt_UsrId.SetFocus
    End If
End Sub

'*******************************************************
'* エンターキー押下時                              　  *
'*******************************************************
Private Sub txt_UsrPass_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    'KeyCodeが13番(エンターキー)が押下されたときは、
    'パスワード入力完了とみなし、OKボタン押下処理を実行する
    If KeyCode = 13 Then
        cmb_OK_Click
    End If
End Sub

'*******************************************************
'* 画面オープン処理                                　  *
'*******************************************************
Private Sub UserForm_Initialize()
    
    Dim strDBConnect As String
    Dim Ret          As String
    
    'DB接続文字列作成
    Ret = DBAccessString(strDBConnect)
    If Ret = False Then
        '設定シートが無い等、何らかの異常があったときは処理を終了する
        MsgBox "ワークブックを終了します", vbInformation
        Workbook_Close Conn, RS
    End If
    
    'DB接続
    Ret = DBOpen(strDBConnect, Conn)
    If Ret = False Then
        'DB接続失敗時は処理を終了する
        MsgBox "ワークブックを終了します", vbInformation
        Workbook_Close Conn, RS
    End If

End Sub

'*******************************************************
'* 画面クローズ処理                                　  *
'*******************************************************
Private Sub UserForm_QueryClose(Cancel As Integer, CloseMode As Integer)
    
    Dim Ret          As String
    
    'CloseModeが1の時はユーザIDとパスワードが正常に入力され、
    'フォームクローズするだけなので、ワークブックの終了はしない
    If CloseMode = 0 Then
        'CloseModeが0の時、右上のXボタンが押下された
        If MsgBox("ワークブックを終了しますか？", vbOKCancel + vbExclamation) = vbOK Then
                        
            'DB切断
            Ret = DBClose(Conn, RS)
            If Ret = "False" Then
                ' DB切断失敗
                MsgBox "ワークブックを終了します", vbInformation
                Workbook_Close Conn, RS
            End If
            
            'ワークブック終了
            Workbook_Close Conn, RS
        
        Else
            Cancel = True
        End If
    End If
End Sub
