Attribute VB_Name = "basUpdate"
Option Explicit
'*************************************************************************
'
'プロジェクト名：外部委託ライセンスフィー計算システム・外部委託基礎数値修正
'
'オブジェクト名：basUpdate
'
'機能概要　    ：基礎数値情報更新
'
'更新履歴　    ：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "basUpdate"


'*************************************************************************
'関数名　　：基礎数値情報チェック
'
'引　　数　：ARG1   O   ErrRow      long    エラー発生行番号
'　　　　　：ARG2   O   ErrCol      long    エラー発生列番号
'
'戻り値　　：エラー番号
'
'機能説明　：基礎数値情報の基準日、基礎数値修正額の妥当性をチェックする
'
'更新履歴　：2008/06/20 SRA H.Anpo  新規作成
'
'*************************************************************************
Public Function fncChkKisoSuchiInfo( _
                    ByRef ErrRow As Long, _
                    ByRef ErrCol As Long, _
                    ByRef MsgLvl As Long _
                    ) As Long

    Const PROCEDURE_NAME As String = "fncChkKisoSuchiInfo"

    On Error GoTo ErrHandler
    
    Dim lngRow  As Long
    Dim TopRow  As Long
    Dim lngRet  As Long
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncChkKisoSuchiInfo = 0
    
    '変更チェック
    '検索・表示ボタン押下後、外部委託先、口座番号、基準日、組入資産選択行が
    '変更されたかをチェックする
    lngRet = fncChkDataChange(ErrRow, ErrCol, MsgLvl)
    If lngRet <> 0 Then
        fncChkKisoSuchiInfo = lngRet
        Exit Function
    End If
    
    With ThisWorkbook.Sheets(SHEET_MAIN)
        TopRow = fncKsCmnSearchDataTopRow(2)
        lngRow = TopRow
        
        Do While .Cells(lngRow, COLN_DATA_FLAG) <> vbNullString
            
            '基礎数値修正額チェック
            lngRet = fncChkKisoSuchiFormat(CStr(.Cells(lngRow, .Range("計算基礎数値修正額").Column).Value), MSG1_KISOSUCHI_INFO, MsgLvl)
            If lngRet <> 0 Then
                ErrRow = lngRow
                ErrCol = .Range("計算基礎数値修正額").Column
                fncChkKisoSuchiInfo = lngRet
                Exit Function
            End If
            
            lngRow = lngRow + 1
        Loop
    End With
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
ErrHandler:
    MsgLvl = MSGLVL_FATAL
    fncChkKisoSuchiInfo = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
End Function



'*************************************************************************
'関数名　　：更新処理
'
'引　　数　：なし
'
'戻り値　　：エラー番号
'
'機能説明　：時価情報を基に外部委託資産時価テーブルを更新する
'
'更新履歴　：2008/06/20 SRA H.Anpo  新規作成
'更新履歴　：2008/08/29 SRA Y.Azuma    変更   連結障害対応(No.21)
'
'*************************************************************************
Public Function fncExecUpdate() As Long

    Const PROCEDURE_NAME As String = "fncExecUpdate"

    On Error GoTo ErrHandler
    
    Dim strSql      As String
    Dim lngRow      As Long
    Dim strKohzaNo  As String
    
    Dim vntRec()    As Variant
    Dim lngCol      As Long
    Dim lngRecCnt   As Long
    Dim strErrCd    As String
    Dim blnRet      As Boolean
    Dim lngUpdCnt   As Long     '更新件数カウント用(0件の場合エラーメッセージを出力する)
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)
    
    fncExecUpdate = 0
    
    lngUpdCnt = 0
        
    '************************************
    '* トランザクション開始
    '************************************
    fncKsCmnBeginTrans
        
    '基礎数値情報データの先頭行取得
    lngRow = fncKsCmnSearchDataTopRow(2)
        
    With ThisWorkbook.Sheets(SHEET_MAIN)

        Do While .Cells(lngRow, COLN_DATA_FLAG) <> vbNullString
        
            If .Cells(lngRow, COLN_J_CHK_DEFAULT) <> False Or .Cells(lngRow, COLN_J_CHK_FLG) <> False Then
            
                '************************************
                '* 更新対象行のロック
                '************************************
                'ODBCエラーのコードをセット
                '[Oracle][ODBC][Ora]ORA-00054: リソース・ビジー、NOWAITが指定されていました。
                strErrCd = ODBC_ERRCD_RESOURCE_BUSY
                
                strKohzaNo = func_ChkQuoteshon(Format(Trim(.Range("口座番号").Value), "0000000"), "'")
                
                strSql = ""
                strSql = strSql & " SELECT"
                strSql = strSql & "     KSJ.*　"
                strSql = strSql & " FROM"
                strSql = strSql & "     KYK_GI_SHISAN_JIKA KSJ "
                strSql = strSql & " WHERE"
                strSql = strSql & "     KSJ.KOHZA_NO            = '" & strKohzaNo & "'"
'------------<Modify Start azuma 2008/08/29 > 連結障害対応(No.21)-----------------------
                strSql = strSql & " AND KSJ.SHISAN_JIKA_ISSCD   = '" & func_ChkQuoteshon(.Cells(lngRow, .Range("資産時価銘柄コード").Column), "'") & "'"
'''                strSql = strSql & " AND KSJ.SHISAN_JIKA_ISSCD   = '" & .Cells(lngRow, .Range("資産時価銘柄コード").Column) & "'"
'------------<Modify End   azuma 2008/08/29 > 連結障害対応(No.21)-----------------------
                strSql = strSql & " AND KSJ.KIJUN_YMD           = '" & .Cells(lngRow, .Range("基礎数値基準日").Column) & "'"
                strSql = strSql & " AND KSJ.SAKUJO_FLG          = '0'"
                strSql = strSql & " FOR UPDATE NOWAIT"
    
                blnRet = fncKsCmnDoExequteSql(mainmenu.gcSELECT, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
                If blnRet = False Then
                    If strErrCd = ODBC_ERRCD_RESOURCE_BUSY Then
                        'リソース・ビジー
                        '21019 他のユーザーにより更新されています。
                        fncKsCmnMessageDisp MSGLVL_WARNNING, 21019, MSG1_KISOINF, "", True
                        fncExecUpdate = 21019
                    Else
                        fncExecUpdate = strErrCd
                    End If
                
                    'ロールバック
                    GoTo RollBackExit
                End If
                
                If lngRecCnt = 0 Then
                    '21020 他ユーザにより削除されています。
                    fncKsCmnMessageDisp MSGLVL_WARNNING, 21020, MSG1_KISOINF, "", True
                    fncExecUpdate = 21020
                    'ロールバック
                    GoTo RollBackExit
                
                Else
                
                    '************************************
                    '* 更新(UPDATE)実行
                    '************************************
                    strSql = ""
                    strSql = strSql & " UPDATE KYK_GI_SHISAN_JIKA KSJ"
                    strSql = strSql & " SET"
                    strSql = strSql & "     KSJ.KOHSHIN_YMD         = TO_CHAR(SYSDATE,'YYYY/MM/DD'),"
                    strSql = strSql & "     KSJ.KOHSHIN_PGM_ID      = '" & PROGRAM_ID & "',"
                    strSql = strSql & "     KSJ.KOHSHIN_TANTOHSHA   = '" & USER_ID & "',"
                    
                    If Trim(.Cells(lngRow, .Range("計算基礎数値修正額").Column)) = vbNullString Then
                        strSql = strSql & "     KSJ.KISO_SUCHI_MOD      = NULL,"
                    Else
                        strSql = strSql & "     KSJ.KISO_SUCHI_MOD      = " & Trim(.Cells(lngRow, .Range("計算基礎数値修正額").Column)) & ","
                    End If
                    
                    If Trim(.Cells(lngRow, COLN_J_CHK_FLG)) = "False" Then
                        strSql = strSql & "     KSJ.SHUUSEIGAKU_SAIYOU_FLG = '0'"
                    Else
                        strSql = strSql & "     KSJ.SHUUSEIGAKU_SAIYOU_FLG = '1'"
                    End If
                    
                    strSql = strSql & " WHERE"
                    strSql = strSql & "     KSJ.KOHZA_NO            = '" & strKohzaNo & "'"
'------------<Modify Start azuma 2008/08/29 > 連結障害対応(No.21)-----------------------
                    strSql = strSql & " AND KSJ.SHISAN_JIKA_ISSCD   = '" & func_ChkQuoteshon(.Cells(lngRow, .Range("資産時価銘柄コード").Column), "'") & "'"
'''                    strSql = strSql & " AND KSJ.SHISAN_JIKA_ISSCD   = '" & .Cells(lngRow, .Range("資産時価銘柄コード").Column) & "'"
'------------<Modify End   azuma 2008/08/29 > 連結障害対応(No.21)-----------------------
                    strSql = strSql & " AND KSJ.KIJUN_YMD           = '" & .Cells(lngRow, .Range("基礎数値基準日").Column) & "'"
                    strSql = strSql & " AND KSJ.SAKUJO_FLG          = '0'"
                    
                    blnRet = fncKsCmnDoExequteSql(mainmenu.gcUPDATE, strSql, vntRec, lngCol, lngRecCnt, strErrCd)
                    If blnRet = False Then
                        fncExecUpdate = strErrCd
                        'ロールバック
                        GoTo RollBackExit
                    End If
                    
                    lngUpdCnt = lngUpdCnt + 1
                End If
                
            End If
            
            lngRow = lngRow + 1
        Loop
    End With
    
    '更新件数が0の場合
    If lngUpdCnt = 0 Then
        '21003 更新対象のデータがありません。
        fncKsCmnMessageDisp MSGLVL_WARNNING, 21003, MSG1_KISOINF, "", True
        fncExecUpdate = 21003
        'ロールバック
        GoTo RollBackExit
    End If
    
    '************************************
    '* トランザクションコミット
    '************************************
    fncKsCmnCommitTrans
        
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
ErrHandler:
    fncExecUpdate = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
RollBackExit:
    'ロールバック
    fncKsCmnRollbackTrans
End Function


'*************************************************************************
'関数名　　：更新ボタン処理
'
'引　　数　：なし
'
'戻り値　　：Long       エラー番号
'
'機能説明　：更新ボタン処理
'
'更新履歴　：2007/03/19 SRA T.Ichikawa  新規作成
'
'*************************************************************************
Function fncUpdate() As Long
    
    Const PROCEDURE_NAME As String = "fncUpdate"
    
On Error GoTo ErrorHandler

    Dim lngRet      As Long
    Dim lngRow      As Long
    Dim lngCol      As Long
    Dim lngMsgLvl   As Long
    
    Call subBeginProcess(MODULE_NAME, PROCEDURE_NAME)

    fncUpdate = 0
    
    With ThisWorkbook.Sheets(SHEET_MAIN)
    
        '************************************
        '* 入力チェック
        '************************************
        lngRet = fncChkKisoSuchiInfo(lngRow, lngCol, lngMsgLvl)
        If lngRet <> 0 Then
            If lngMsgLvl = MSGLVL_WARNNING Then
                .Cells(lngRow, lngCol).Select
            End If
            fncUpdate = lngRet
            Exit Function
        End If
    
    End With
       
    '************************************
    '* 更新確認
    '************************************
    '21001 更新確認
    If MsgBox(fncKsCmnMessageDisp(MSGLVL_QUESTION, 21001, "", , False), vbQuestion + vbYesNo, "確認") = vbNo Then
        Exit Function
    End If

    '************************************
    '* 更新実行
    '************************************
    lngRet = fncExecUpdate()
    If lngRet <> 0 Then
        fncUpdate = lngRet
        Exit Function
    End If
    
    '21004 正常終了メッセージ
    fncKsCmnMessageDisp MSGLVL_INFO, 21004, "", , True
    
    '************************************
    '* 画面再表示
    '************************************
    '基礎数値情報を再表示させる(表示処理を実行)
    Call fncDisp
    
    Call subEndProcess(MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function

ErrorHandler:
    fncUpdate = Err.Number
    Call subErrProcess(MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    
End Function

