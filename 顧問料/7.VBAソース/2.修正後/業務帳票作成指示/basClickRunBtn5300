Attribute VB_Name = "basClickRunBtn5300"
Option Explicit

'*************************************************************************
'プロジェクト名：新契約管理システム・業務帳票作成指示
'オブジェクト名：basClickRunBtn5300
'機能概要　：信託報酬支払い口座一覧
'更新履歴　：2008/04/22 SRA市川     新規作成
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn5300"

Private Const cBaseFile = gc帳票Base11
Private Const cPreFName = gc帳票名11

Private Const cTitle = "信託報酬支払い口座一覧"     '帳票タイトル
Private Const cDateTitle = "出力日："               '出力日

'信託報酬支払い口座一覧構造体
Private Type typShintaku
    KohzaNo             As String   '口座No.
    KokyakuName         As String   '顧客名
    AM_Name             As String   'AM
    PM_Name             As String   '主PM
    ShintakuBK          As String   '信託BK
    ShintakuNo          As String   '信託No.
    HikiotoshiBi        As String   '引落し日
    Shiharai            As String   '支払
End Type

'*************************************************************************
'関数名　　：信託報酬支払い口座一覧(帳票作成)
'引　　数　：
'           objTarget        I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2008/04/22 SRA市川     新規作成
'
'*************************************************************************
Public Function fncClickRunBtn5300(ByRef objTarget As Object) As Boolean
    
    Const PROCEDURE_NAME As String = "fncClickRunBtn5300"

On Error GoTo ErrSection

    Dim strOutFName         As String           '帳票ファイル名
    Dim strShintaku()       As typShintaku      '信託報酬支払い口座一覧
    Dim strDate             As String
    Dim strTime             As String
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    fncClickRunBtn5300 = False
   
    With objTarget

        ' 作成選択の場合
        If .chkMake.Value = True Then
            
            'システム日付を取得する
            strDate = Format(Date, "YYYY/MM/DD")
            
            'システム時間を取得する
            strTime = Format(Time, "HHMMSS")

            ' データ取得
            If fncGetDBData5300(Format(strDate, "YYYYMMDD"), strShintaku()) = False Then
                GoTo RtnErrSection
            End If
        
            ' 帳票ファイル取得
            If fncGetFile5300(Format(strDate, "YYYYMMDD"), strTime, strOutFName) = False Then
                GoTo RtnErrSection
            End If
        
            ' フォルダをつける。
            strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName
            
            ' ファイルコピー
            If fncCopyBook(cBaseFile, strOutFName) = False Then
                GoTo RtnErrSection
            End If
            
            ' ファイル作成
            If fncMakeBook5300(strDate, strOutFName, strShintaku()) = False Then
                ' ファイル削除
                Call fncDeleteFile(strOutFName)

                GoTo RtnErrSection
            End If
        End If
    
        ' 印刷選択の場合
        If .chkPrint.Value = True Then
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
    End With
   
    fncClickRunBtn5300 = True
    Erase strShintaku
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                    *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション                            *
'*******************************************************
RtnErrSection:
                
    fncClickRunBtn5300 = False
    Erase strShintaku
    
End Function

'*************************************************************************
'関数名　　：信託報酬支払い口座一覧(帳票ファイル取得)
'引　　数　：
'           strDate             I           サーバー日付
'           strTime             I           サーバー時間
'           strOutFName         O           出力ファイル名
'戻り値　　：処理結果
'機能説明　：信託報酬支払い口座一覧を出力するファイルを取得する。
'更新履歴　：2008/04/22 SRA市川     新規作成
'
'*************************************************************************
Public Function fncGetFile5300(ByVal strDate As String, ByVal strTime As String, ByRef strOutFName As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile5300"

On Error GoTo ErrSection

    strOutFName = ""
    
    fncGetFile5300 = False
       
    ' 出力帳票ファイル
    strOutFName = cPreFName
    strOutFName = strOutFName & "_" & strDate
    strOutFName = strOutFName & "_" & strTime
    strOutFName = strOutFName & ".xls"
    
    fncGetFile5300 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　 *
'*******************************************************
ErrSection:

    fncGetFile5300 = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：信託報酬支払い口座一覧(帳票データ取得)
'引　　数　：
'           strDay              I           サーバー日付
'           strShintaku()       O           取得データ
'戻り値　　：処理結果
'機能説明　：信託報酬支払い口座一覧のデータを取得する。
'更新履歴　：2008/04/22 SRA市川     新規作成
'
'*************************************************************************
Public Function fncGetDBData5300(ByVal strDay As String, ByRef strShintaku() As typShintaku) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetDBData5300"

On Error GoTo ErrSection

    Dim strSql              As String
    Dim blnRet              As Boolean
    Dim lngKohzaCnt         As Long
    
    fncGetDBData5300 = False
    
    blnRet = False
    
    '信託報酬支払い口座一覧を取得
    blnRet = fncGetShintaku(strDay, strShintaku(), lngKohzaCnt)
    
    If blnRet = False Then
        Exit Function
    End If
    
    '口座が1件も存在しない場合
    If lngKohzaCnt = 0 Then
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "")
        Exit Function
    End If

    fncGetDBData5300 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　 *
'*******************************************************
ErrSection:

    fncGetDBData5300 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase strShintaku
   
End Function

'*************************************************************************
'関数名　　：信託報酬支払い口座一覧取得
'引　　数　：
'           strDay              I           サーバー日付
'           strData()           O           取得データ
'戻り値　　：処理結果
'機能説明　：信託報酬支払い口座一覧のデータを取得する。
'更新履歴　：2008/04/22 SRA市川     新規作成
'        　：2013/04/10 GUT 趙 顧問バック更改対応
'
'*************************************************************************
Public Function fncGetShintaku(ByVal strDay As String, ByRef strData() As typShintaku, ByRef lngRow As Long) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetShintaku"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRowCnt   As Long
    
    fncGetShintaku = False
    
'*******************************************************
'* SQL文作成                                           *
'*******************************************************

    strSql = ""
    strSql = strSql & "SELECT" & vbCrLf
    strSql = strSql & "    VD1.PRTCD PRTCD," & vbCrLf
    strSql = strSql & "   (SELECT" & vbCrLf
    strSql = strSql & "        TRIM(V002.NAME_RK)" & vbCrLf
    strSql = strSql & "    FROM" & vbCrLf
    strSql = strSql & "        VW_DT002R V002" & vbCrLf
    strSql = strSql & "    WHERE" & vbCrLf
    strSql = strSql & "        V002.TEKIYOU_DATE = (SELECT" & vbCrLf
    strSql = strSql & "                                MAX(V2.TEKIYOU_DATE) TEKIYOU_DATE" & vbCrLf
    strSql = strSql & "                            FROM" & vbCrLf
    strSql = strSql & "                                VW_DT002R V2" & vbCrLf
    strSql = strSql & "                            WHERE" & vbCrLf
    strSql = strSql & "                                V2.TEKIYOU_DATE <= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "                            AND V2.PRTCD = VD1.PRTCD" & vbCrLf
    strSql = strSql & "                            )" & vbCrLf
    strSql = strSql & "    AND V002.PRTCD = VD1.PRTCD" & vbCrLf
    strSql = strSql & "   ) KOKYAKU_NAME," & vbCrLf
    strSql = strSql & "   (SELECT" & vbCrLf
    strSql = strSql & "        TRIM(V300.SEIMEI_KJ)" & vbCrLf
    strSql = strSql & "    FROM" & vbCrLf
'------------<顧問バック更改対応 2013/04/10 GUT趙 START> ------------
'    strSql = strSql & "        VW_DT003R V003," & vbCrLf
'    strSql = strSql & "        VW_DT300D V300" & vbCrLf
    strSql = strSql & "        KYK_AM_SHUTANTOHSHA V003," & vbCrLf
    strSql = strSql & "        KYK_TANTOHSHA       V300" & vbCrLf
'------------<顧問バック更改対応 2013/04/10 GUT趙 END> ------------
    strSql = strSql & "    WHERE" & vbCrLf
    strSql = strSql & "        V003.TANTOU_KBN    = '1'" & vbCrLf
    strSql = strSql & "    AND V003.TEKIYOU_FROM <= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND V003.TEKIYOU_TO   >= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND (V003.TANTOU_CD)  = (V300.TANTOU_CD)" & vbCrLf
    strSql = strSql & "    AND V300.TEKIYOU_FROM <= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND V300.TEKIYOU_TO   >= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND V003.PRTCD = VD1.PRTCD" & vbCrLf
    strSql = strSql & "   ) AM_NAME," & vbCrLf
    strSql = strSql & "   (SELECT" & vbCrLf
    strSql = strSql & "        TRIM(V300.SEIMEI_KJ)" & vbCrLf
    strSql = strSql & "    FROM" & vbCrLf
'------------<顧問バック更改対応 2013/04/10 GUT趙 START> ------------
'    strSql = strSql & "        VW_DT003R V003," & vbCrLf
'    strSql = strSql & "        VW_DT300D V300" & vbCrLf
    strSql = strSql & "        KYK_AM_SHUTANTOHSHA V003," & vbCrLf
    strSql = strSql & "        KYK_TANTOHSHA       V300" & vbCrLf
'------------<顧問バック更改対応 2013/04/10 GUT趙 END> ------------
    strSql = strSql & "    WHERE" & vbCrLf
    strSql = strSql & "        V003.TANTOU_KBN    = '2'" & vbCrLf
    strSql = strSql & "    AND V003.TEKIYOU_FROM <= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND V003.TEKIYOU_TO   >= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND (V003.TANTOU_CD)  = (V300.TANTOU_CD)" & vbCrLf
    strSql = strSql & "    AND V300.TEKIYOU_FROM <= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND V300.TEKIYOU_TO   >= VD1.KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    AND V003.PRTCD = VD1.PRTCD" & vbCrLf
    strSql = strSql & "   ) SHU_PM_NAME," & vbCrLf
    strSql = strSql & "   (SELECT" & vbCrLf
    strSql = strSql & "        TRIM(V301.NAME_J)" & vbCrLf
    strSql = strSql & "    FROM" & vbCrLf
    strSql = strSql & "        VW_DT301D  V301" & vbCrLf
    strSql = strSql & "    WHERE" & vbCrLf
    strSql = strSql & "        V301.CD_KBN        = '15'" & vbCrLf
    strSql = strSql & "    AND TRIM(V301.CD_1) = TRIM(VD1.SINTAKU_BANK_CD)" & vbCrLf
    strSql = strSql & "   ) SINTAKU_BANK_NAME," & vbCrLf
    strSql = strSql & "    TRIM(VD1.SINTAKU_BANK_PRTCD) SINTAKU_BANK_NO," & vbCrLf
    strSql = strSql & "    TRIM(KCV.VALUE_NAME) HIKIOTOSI_DATE," & vbCrLf
    strSql = strSql & "   (SELECT" & vbCrLf
    strSql = strSql & "        TRIM(KCV.value_name)" & vbCrLf
    strSql = strSql & "    FROM" & vbCrLf
    strSql = strSql & "        KYK_KOHZA_CODE KKC," & vbCrLf
    strSql = strSql & "        KYK_CODE_VALUE KCV" & vbCrLf
    strSql = strSql & "    WHERE" & vbCrLf
    strSql = strSql & "        KKC.CODE_NO    = '1025'" & vbCrLf
    strSql = strSql & "    AND KKC.CODE_NO    = KCV.CODE_NO" & vbCrLf
    strSql = strSql & "    AND KKC.CODE_VALUE = KCV.CODE_VALUE" & vbCrLf
    strSql = strSql & "    AND KKC.KOHZA_NO   = VD1.PRTCD" & vbCrLf
    strSql = strSql & "   ) SIHARAI" & vbCrLf
    strSql = strSql & "FROM" & vbCrLf
    strSql = strSql & "   (SELECT" & vbCrLf
    strSql = strSql & "        V1.PRTCD," & vbCrLf
    strSql = strSql & "        V1.SINTAKU_BANK_CD," & vbCrLf
    strSql = strSql & "        V1.SINTAKU_BANK_PRTCD," & vbCrLf
    strSql = strSql & "        " & strDay & " KIJYUN_YMD" & vbCrLf
    strSql = strSql & "    FROM" & vbCrLf
'=== 2008/10/14 海外口座の契約日の管理 SRA水谷 Start ===
'    strSql = strSql & "        VW_DT001D           V1" & vbCrLf
    strSql = strSql & "        KYK_VW_DT001D           V1" & vbCrLf
'=== 2008/10/14 海外口座の契約日の管理 SRA水谷 End ===
    strSql = strSql & "    WHERE" & vbCrLf
    strSql = strSql & "        (V1.CALLOFF_DATE = 0 OR V1.CALLOFF_DATE >= " & strDay & ")" & vbCrLf
    strSql = strSql & "    AND V1.SETTEI_DATE <= " & strDay & vbCrLf
'------------<顧問バック更改対応 2013/04/10 GUT趙 START> ------------
'    strSql = strSql & "    AND V1.DELT_FLG     = '0'" & vbCrLf
'    strSql = strSql & "    AND V1.SETTEI_KBN   = '1'" & vbCrLf
    strSql = strSql & "    AND V1.SETTEI_KBN    = '0'" & vbCrLf
    strSql = strSql & "    AND V1.GENGO         = '0'" & vbCrLf
'------------<顧問バック更改対応 2013/04/10 GUT趙 END> ------------
    strSql = strSql & "   ) VD1," & vbCrLf
    strSql = strSql & "    KYK_KOHZA_CODE                  KKC," & vbCrLf
    strSql = strSql & "    KYK_CODE_VALUE                  KCV" & vbCrLf
    strSql = strSql & "WHERE" & vbCrLf
    strSql = strSql & "    VD1.PRTCD      = KKC.KOHZA_NO" & vbCrLf
    strSql = strSql & "AND KKC.CODE_NO    = '1024'" & vbCrLf
    strSql = strSql & "AND KCV.CODE_NO    = '1024'" & vbCrLf
    strSql = strSql & "AND KKC.CODE_VALUE = KCV.CODE_VALUE" & vbCrLf
    strSql = strSql & "ORDER BY" & vbCrLf
    strSql = strSql & "    VD1.PRTCD," & vbCrLf
    strSql = strSql & "    KCV.CODE_VALUE" & vbCrLf

    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    
    ReDim strData(0)
    
    If lngRow > 0 Then
        ' 動的配列変数に対するメモリ領域の再割り当てを行います
        ReDim strData(lngRow - 1)
        
        '取得データを配列に設定
        For lngRowCnt = 0 To lngRow - 1
            With strData(lngRowCnt)
                .KohzaNo = CStr(vData(0, lngRowCnt))            '口座No.
                .KokyakuName = CStr(vData(1, lngRowCnt))        '顧客名
                .AM_Name = CStr(vData(2, lngRowCnt))            'AM
                .PM_Name = CStr(vData(3, lngRowCnt))            '主PM
                .ShintakuBK = CStr(vData(4, lngRowCnt))         '信託BK
                .ShintakuNo = CStr(vData(5, lngRowCnt))         '信託No.
                .HikiotoshiBi = CStr(vData(6, lngRowCnt))       '引落し日
                .Shiharai = CStr(vData(7, lngRowCnt))           '支払
            End With
        Next lngRowCnt
    End If
    
    fncGetShintaku = True
    
    Erase vData
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                　  *
'*******************************************************
ErrSection:

    fncGetShintaku = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase vData
   
End Function

'*************************************************************************
'関数名　　：信託報酬支払い口座一覧(帳票作成)
'引　　数　：
'           strDate             I           システム日付
'           strOutFName         I           出力ファイル名
'           strData()           I           出力データ
'戻り値　　：処理結果
'機能説明　：信託報酬支払い口座一覧を作成する。
'更新履歴　：2008/04/22 SRA市川     新規作成
'
'*************************************************************************
Public Function fncMakeBook5300(ByVal strDate As String, ByVal strOutFName As String, ByRef strData() As typShintaku) As Boolean
                    
    Const PROCEDURE_NAME As String = "fncMakeBook5300"

On Error GoTo ErrSection

    Dim lngRowCnt           As Long
    Dim lngStartRow         As Long     'データ出力開始行
    Dim lngDataMax          As Long     'データ件数
    Dim lngDataCnt          As Long
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    lngRowCnt = 0
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    '信託報酬支払い口座一覧配列よりデータ件数を取得
    lngDataMax = UBound(strData)
    fncMakeBook5300 = False
    
    With Workbooks(Workbooks.Count).Worksheets(1)
        
        '1件目の口座番号が空白ではない場合
        If Trim(strData(0).KohzaNo) <> "" Then
        
            'タイトル設定
            .Range("タイトル").Value = cTitle
            
            '出力日設定
            .Range("出力日").Value = cDateTitle & strDate
            
            '出力開始行を取得
            lngStartRow = .Range("明細_口座No.").Row
        
            ' 信託報酬支払い口座一覧を出力
            For lngDataCnt = 0 To lngDataMax

                '行を追加
                .Rows(lngStartRow + lngRowCnt & ":" & lngStartRow + lngRowCnt).Copy
                .Rows(lngStartRow + lngRowCnt & ":" & lngStartRow + lngRowCnt).Insert Shift:=xlDown

                lngRowCnt = lngRowCnt + 1

                'セルにデータを出力
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_口座No.").Column).Value = strData(lngDataCnt).KohzaNo
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_顧客名").Column).Value = strData(lngDataCnt).KokyakuName
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_AM").Column).Value = strData(lngDataCnt).AM_Name
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_主PM").Column).Value = strData(lngDataCnt).PM_Name
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_信託BK").Column).Value = strData(lngDataCnt).ShintakuBK
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_信託No.").Column).Value = strData(lngDataCnt).ShintakuNo
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_引落し日").Column).Value = strData(lngDataCnt).HikiotoshiBi
                .Cells(lngStartRow - 1 + lngRowCnt, .Range("明細_支払").Column).Value = strData(lngDataCnt).Shiharai
                
            Next lngDataCnt
        
        End If
                
        '行増幅用の空行を削除
        .Range(.Range("明細_口座No.").Row & ":" & .Range("明細_口座No.").Row + 2).Delete
        
        ' カーソル位置を移動する
        .Activate     ' シートが２個以上の場合要る
        .Range("A1").Activate
        
    End With
    
    ActiveWindow.ScrollRow = 1
    ActiveWindow.ScrollColumn = 1
    
    Workbooks(Workbooks.Count).Close SaveChanges:=True
    
    fncMakeBook5300 = True
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　 *
'*******************************************************
ErrSection:

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook5300 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
End Function
