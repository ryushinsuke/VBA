Attribute VB_Name = "basClickRunBtn5500"
Option Explicit

'*************************************************************************
'プロジェクト名：国内顧問料・業務帳票作成指示
'オブジェクト名：basClickRunBtn5500
'機能概要　：単月未収顧問料チェック表作成
'更新履歴　：2008/10/10 SRA Y.Azuma     新規作成
'          ：2010/05/25 SRA T.Mizutani  単月未収顧問料チェック表の仕様改善
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn5500"

Private Const cBaseFile = gc帳票Base55
Private Const cPreFName = gc帳票名55

Private Const cTitle = "単月未収顧問料チェック表"   'タイトル

'月構造体
Private Type typMonth
    Keisan_To_Ymd   As String   '計算TO日
    Result()        As Variant  '取得結果 (配列)
    Result_Row      As Variant  '取得件数
End Type

'取得結果構造体
Private Type typDbGet
    Kijun_Ymd       As String    '基準日
    Tougetsu        As typMonth  '当月構造体
    Zengetsu        As typMonth  '前月構造体
    ZenZengetsu      As typMonth '前々月構造体
End Type

'DB取得結果のインデックス
Private Enum nGetDb
    KOHZA_NO = 0            ' 0:口座番号
    NAME_RJ                 ' 1:口座名称
    KEISAN_KIKAN_FROM       ' 2:今期顧客計算期間FROM
    KEISAN_KIKAN_TO         ' 3:今期顧客計算期間TO
    Keisan_To_Ymd           ' 4:計算TO日
    TSUKI_NISU              ' 5:月日数
    NEN_NISU                ' 6:年日数
    NYUKIN                  ' 7:基準月末の入金額累計
    SEIKYUZUMI              ' 8:基準月末の未収請求済金額
    MISEIKYU                ' 9:基準月末の未収未未請求金額
    MISHU_RUIKEI            '10:基準月末の未収累計額
    SOU_RUIKEI              '11:基準月末の総累計額
    KISOSUCHI_TYPE          '12:基礎数値種類
    KEISAN_HOHO             '13:基礎数値計算方法
    BC_ZANDAKA              '14:基準月末の時価総額
'------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    SAIYOU_JIKA_TYPE        '15:基準月末のPX信託区分
    SAIYOU_JIKA_GAKU        '16:基準月末の計算基礎数値
'------------<Add End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
End Enum
'------------<Modify Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
'Private Const DBGET_INDEX_MAX = 14
Private Const DBGET_INDEX_MAX = 16
'------------<Modify End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------


'結果格納配列のインデックス
Private Enum nKekka
    Renban = 0              ' 0:連番(1～)
    KOHZA_NO                ' 1:口座番号
    Kohza_Name              ' 2:口座名称
    Ruikei_Tougetsu         ' 3:未収累計額・当月(A)
'------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    Hidari_Kakko            ' 4:未収累計額・当月(A)(当月入金額)
    Ruikei_Tougetsu_Nyukin  ' 5:未収累計額・当月(A)(当月入金額)
    Migi_Kakko              ' 6:未収累計額・当月(A)(当月入金額)
'------------<Add End T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------

'------------<Modify Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    Ruikei_Zengetsu         ' 7:未収累計額・前月(B)
    Ruikei_ZenZengetsu      ' 8:未収累計額・前々月(C)
    Tangetsu_Tougetsu       ' 9:未収単月・当月(D)
    Tougetsu_Nissu          '10:当月日数
    Tangetsu_Zengetsu       '11:未収単月・前月(E)
    Zengetsu_Nissu          '12:前月日数
    IchinichiAve_Tougetsu   '13:1日平均額・当月(F)
    IchinichiAve_Zengetsu   '14:1日平均額・前月(G)
    Ichinichi_Zougenritsu   '15:1日平均額・前月比(増減率)
    Check_Flg               '16:チェックフラグ
'------------<Modify End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------

'------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    SAIYOU_JIKA_TYPE        '17:採用時価種類・当月
    SAIYOU_JIKA_GAKU        '18:採用時価総額・当月
'------------<Add End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------

'------------<Modify Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    Kisosuuchi_Type         '19:基礎数値種類
    Kisosuuchi_Keisan_Type  '20:基礎数値計算方法
    Nav_Tougetsu            '21:時価総額・当月
    Nav_Zengetsu            '22:時価総額・前月
    Nav_ZenZengetsu         '23:時価総額・前々月
    Seikyu_Kikan_From       '24:顧問料請求期間・FROM
    Seikyu_Kikan_To         '25:顧問料請求期間・TO
    Seikyu_Kikan_Nissu      '26:顧問料請求期間・日数
'------------<Modify End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
End Enum

'------------<Modify Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
'Private Const KEKKA_INDEX_MAX = 21
Private Const KEKKA_INDEX_MAX = 26
'------------<Modify Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------



'*************************************************************************
'関数名　　：単月未収顧問料チェック表(帳票作成)
'引数　　　：
'           objTarget        I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'
'*************************************************************************
Public Function fncClickRunBtn5500(ByRef objTarget As Object) As Boolean

    Const PROCEDURE_NAME As String = "fncClickRunBtn5500"

On Error GoTo ErrSection

    Dim strOutFName     As String
    Dim strOutFNameOnly As String


    Dim strDate         As String   'システム日付(YYYY/MM/DD)
    Dim strTime         As String   'システム時間(HHMMSS)
    Dim strKijunFrom    As String   '画面.基準年月 & 01
    Dim strKijunDate    As String   '画面.基準年月
    Dim strTougetsu     As String   '当月末日(YYYYMMDD)
    Dim strZengetsu     As String   '前月末日(YYYYMMDD)
    Dim strZenZengetsu  As String   '前々月末日(YYYYMMDD)
    Dim inData          As typDbGet '単月未収顧問料チェック表データ取得用
    Dim vKekka()        As Variant  '結果格納配列

    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)

    fncClickRunBtn5500 = False

    With objTarget

        'マウスポインターをWAITにする
        ThisWorkbook.Application.Cursor = xlWait

        ' 作成選択の場合
        If .chkMake.Value = True Then
                        
            '画面の基準年月を取得する
            strKijunFrom = Trim(.Range("基準年月5500").Value) & "01"
            strKijunDate = Trim(.Range("基準年月5500").Value)
            
                        
            '当月、前月、前々月の月末日付の取得
            If fncGetGetsumatsuYmd5500(strKijunDate, strTougetsu, strZengetsu, strZenZengetsu) = False Then
                GoTo RtnErrSection
            End If
                        
            '取得結果構造体へのデータ設定
            With inData
                .Kijun_Ymd = strKijunDate
                .Tougetsu.Keisan_To_Ymd = strTougetsu
                .Zengetsu.Keisan_To_Ymd = strZengetsu
                .ZenZengetsu.Keisan_To_Ymd = strZenZengetsu
            End With
                        
                        
            '帳票データの取得
            If fncGetDBData5500(strTougetsu, strZengetsu, strZenZengetsu, inData, vKekka) = False Then
                GoTo RtnErrSection
            End If
        
        
            'チェック計算処理
            If fncCheckCalc5500(inData, vKekka) = False Then
                GoTo RtnErrSection
            End If
        
        
            'システム日付を取得する
            strDate = Format(Date, "YYYY/MM/DD")
            
            'システム時間を取得する
            strTime = Format(Time, "HHMMSS")
            
            ' 帳票ファイル名作成
            If fncGetFile5500(Format(strDate, "YYYYMMDD"), strTime, strKijunDate, strOutFNameOnly) = False Then
                GoTo RtnErrSection
            End If

            strOutFName = strOutFNameOnly
        
            ' フォルダをつける。
            strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName
            
            ' ファイルコピー
            If fncCopyBook(cBaseFile, strOutFName) = False Then
                GoTo RtnErrSection
            End If
            
            ' ファイル作成
            If fncMakeBook5500(strDate, strOutFName, inData, vKekka) = False Then
                ' ファイル削除
                Call fncDeleteFile(strOutFName)

                GoTo RtnErrSection
            End If
        End If
    
        ' 印刷のみ選択の場合
        If .chkPrint.Value = True Then
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
        'マウスポインターを戻す
        ThisWorkbook.Application.Cursor = xlDefault
    
    End With
   
   
    fncClickRunBtn5500 = True
    Erase vKekka
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                     *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション                              *
'*******************************************************
RtnErrSection:
                
    'マウスポインターを戻す
    ThisWorkbook.Application.Cursor = xlDefault
                
    fncClickRunBtn5500 = False
    Erase inData.Tougetsu.Result
    Erase inData.Zengetsu.Result
    Erase inData.ZenZengetsu.Result
    Erase vKekka
    
End Function
'*************************************************************************
'関数名　　：当月、前月、前々月の月末日付取得
'引数　　　：
'           strKijunDate        I           基準年月(YYYYMM)
'           strTougetsu         O           基準年月の月末日
'           strZengetsu         O           基準年月の前月末日
'           strZenZengetsu      O           基準年月の前々月末日
'
'戻り値　　：処理結果Boolean
'
'機能説明　：当月、前月、前々月の月末日付の取得する。
'
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'
'*************************************************************************
Public Function fncGetGetsumatsuYmd5500(ByVal strKijunDate As String _
                               , ByRef strTougetsu As String _
                               , ByRef strZengetsu As String _
                               , ByRef strZenZengetsu As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetGetsumatsuYmd5500"

On Error GoTo ErrSection
    
    Dim strSql              As String
    Dim vData()             As Variant
    Dim lngCol              As Long
    Dim lngRow              As Long
    
    fncGetGetsumatsuYmd5500 = False
    
    '基準年月の月末日取得
    strSql = ""
    strSql = strSql & " SELECT TO_CHAR(LAST_DAY('" & strKijunDate & "'||'01'),'YYYYMMDD')"
    strSql = strSql & "       ,TO_CHAR(ADD_MONTHS(LAST_DAY('" & strKijunDate & "'||'01'),-1),'YYYYMMDD')"
    strSql = strSql & "       ,TO_CHAR(ADD_MONTHS(LAST_DAY('" & strKijunDate & "'||'01'),-2),'YYYYMMDD')"
    strSql = strSql & "   FROM DUAL"
    
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    
    If lngRow > 0 Then
        strTougetsu = CStr(vData(0, 0))
        strZengetsu = CStr(vData(1, 0))
        strZenZengetsu = CStr(vData(2, 0))
    Else
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "基準年月の月末日取得")
        Erase vData
        Exit Function
    End If
    
    Erase vData
    fncGetGetsumatsuYmd5500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetGetsumatsuYmd5500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- エラーログ出力 End  ---------------------------------
   Erase vData
End Function


'*************************************************************************
'関数名　　：単月未収顧問料チェック表(帳票データ取得)
'引数　　　：
'           strTougetsu         I           基準年月の月末日
'           strZengetsu         I           基準年月の前月末日
'           strZenZengetsu      I           基準年月の前々月末日
'           inData              O           帳票用DB取得データ格納変数
'           vKekka()            O           帳票用出力用データ格納変数
'
'戻り値　　：処理結果Boolean
'
'機能説明　：単月未収顧問料チェック表のデータを取得する。
'
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'          ：2010/05/25 SRA T.Mizutani  単月未収顧問料チェック表の仕様改善
'
'*************************************************************************
Public Function fncGetDBData5500(ByVal strTougetsu As String _
                               , ByVal strZengetsu As String _
                               , ByVal strZenZengetsu As String _
                               , ByRef inData As typDbGet _
                               , ByRef vKekka() As Variant) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetDBData5500"

On Error GoTo ErrSection
    
    Dim lngKohzaCnt         As Long
    Dim strSql              As String
    Dim vData()             As Variant
    Dim lngCol              As Long
    Dim lngRow              As Long
    Dim lngCnt              As Long
    Dim blnRet              As Boolean
    
    
    fncGetDBData5500 = False
    
    
    '***当月のデータ取得***********************************************************************************************
    '取得SQLの作成
    If fncMakeSql(strTougetsu, strTougetsu, strSql) <> True Then
        Exit Function
    End If
    
    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    
    '取得件数の格納
    inData.Tougetsu.Result_Row = lngRow
    
    If lngRow > 0 Then
    
        '取得結果構造体への格納
        inData.Tougetsu.Result = vData
        
        '結果格納配列の作成
        ReDim vKekka(lngRow - 1, KEKKA_INDEX_MAX)
        
        For lngCnt = 0 To lngRow - 1
            
            '結果格納配列に格納(当月取得時に設定可能分)
            vKekka(lngCnt, nKekka.Renban) = lngCnt + 1 '連番(1～)
            vKekka(lngCnt, nKekka.KOHZA_NO) = vData(nGetDb.KOHZA_NO, lngCnt)                    '口座番号
            vKekka(lngCnt, nKekka.Kohza_Name) = vData(nGetDb.NAME_RJ, lngCnt)                   '口座名称
            vKekka(lngCnt, nKekka.Ruikei_Tougetsu) = vData(nGetDb.MISHU_RUIKEI, lngCnt)         '未収累計額・当月(A)
            vKekka(lngCnt, nKekka.Tougetsu_Nissu) = vData(nGetDb.TSUKI_NISU, lngCnt)            '当月日数
            vKekka(lngCnt, nKekka.Kisosuuchi_Type) = vData(nGetDb.KISOSUCHI_TYPE, lngCnt)       '基礎数値種類
            vKekka(lngCnt, nKekka.Kisosuuchi_Keisan_Type) = vData(nGetDb.KEISAN_HOHO, lngCnt)   '顧問料算定基準
            vKekka(lngCnt, nKekka.Nav_Tougetsu) = vData(nGetDb.BC_ZANDAKA, lngCnt)              '時価総額・当月
            If Trim(vData(nGetDb.KEISAN_KIKAN_FROM, lngCnt)) = vbNullString Then
                vKekka(lngCnt, nKekka.Seikyu_Kikan_From) = ""
            Else
                vKekka(lngCnt, nKekka.Seikyu_Kikan_From) = Mid(vData(nGetDb.KEISAN_KIKAN_FROM, lngCnt), 1, 4) & "/" _
                                                         & Mid(vData(nGetDb.KEISAN_KIKAN_FROM, lngCnt), 5, 2) & "/" _
                                                         & Mid(vData(nGetDb.KEISAN_KIKAN_FROM, lngCnt), 7, 2) '顧問料請求期間・FROM
            End If
            If Trim(vData(nGetDb.KEISAN_KIKAN_TO, lngCnt)) = vbNullString Then
                vKekka(lngCnt, nKekka.Seikyu_Kikan_To) = ""
            Else
                vKekka(lngCnt, nKekka.Seikyu_Kikan_To) = Mid(vData(nGetDb.KEISAN_KIKAN_TO, lngCnt), 1, 4) & "/" _
                                                         & Mid(vData(nGetDb.KEISAN_KIKAN_TO, lngCnt), 5, 2) & "/" _
                                                         & Mid(vData(nGetDb.KEISAN_KIKAN_TO, lngCnt), 7, 2) '顧問料請求期間・TO
            End If
            vKekka(lngCnt, nKekka.Seikyu_Kikan_Nissu) = vData(nGetDb.NEN_NISU, lngCnt)          '顧問料請求期間・日数
            
            '------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
            vKekka(lngCnt, nKekka.Hidari_Kakko) = "("                                           '左括弧
            vKekka(lngCnt, nKekka.Ruikei_Tougetsu_Nyukin) = vData(nGetDb.NYUKIN, lngCnt)        '未収累計額・当月(A)(当月入金額)
            vKekka(lngCnt, nKekka.Migi_Kakko) = ")"                                             '右括弧
            vKekka(lngCnt, nKekka.SAIYOU_JIKA_TYPE) = vData(nGetDb.SAIYOU_JIKA_TYPE, lngCnt)    '採用時価種類・当月
            vKekka(lngCnt, nKekka.SAIYOU_JIKA_GAKU) = vData(nGetDb.SAIYOU_JIKA_GAKU, lngCnt)    '時価総額総額・当月
            '------------<Add End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
            
        Next lngCnt
    
    Else
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "当月月末データ取得")
        Erase vData
        Exit Function
    End If
    
    
    '***前月のデータ取得***********************************************************************************************
    strSql = ""
    lngRow = 0
    lngCol = 0
    blnRet = False
    Erase vData
    '取得SQLの作成
    If fncMakeSql(strTougetsu, strZengetsu, strSql) <> True Then
        Erase vData
        Exit Function
    End If
    
    'データ取得
    blnRet = gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    
    If blnRet = True Then
        '取得件数の格納
        inData.Zengetsu.Result_Row = lngRow
        
        If lngRow > 0 Then
            '取得件数が一致しない場合の措置
            If inData.Tougetsu.Result_Row > lngRow Then
                ReDim Preserve inData.Zengetsu.Result(DBGET_INDEX_MAX, inData.Tougetsu.Result_Row - 1)
            End If
            
            '取得結果構造体への格納
            inData.Zengetsu.Result = vData
            
            For lngCnt = 0 To lngRow - 1
                '結果格納配列に格納(当月取得時に設定可能分)
                vKekka(lngCnt, nKekka.Ruikei_Zengetsu) = vData(nGetDb.MISHU_RUIKEI, lngCnt)         '未収累計額・前月(B)
                vKekka(lngCnt, nKekka.Zengetsu_Nissu) = vData(nGetDb.TSUKI_NISU, lngCnt)            '前月日数
                vKekka(lngCnt, nKekka.Nav_Zengetsu) = vData(nGetDb.BC_ZANDAKA, lngCnt)              '時価総額・前月
            Next lngCnt
        Else
            ' 0件でもそのまま続行する。
            ReDim inData.Zengetsu.Result(DBGET_INDEX_MAX, inData.Tougetsu.Result_Row - 1)
            For lngCnt = 0 To inData.Tougetsu.Result_Row - 1
                inData.Zengetsu.Result(nGetDb.KOHZA_NO, lngCnt) = inData.Tougetsu.Result(nGetDb.KOHZA_NO, lngCnt)
            Next lngCnt
        End If
    Else
        Erase vData
        Exit Function
    End If
    
    '***前々月のデータ取得*********************************************************************************************
    strSql = ""
    lngRow = 0
    lngCol = 0
    Erase vData
    blnRet = False
    '取得SQLの作成
    If fncMakeSql(strTougetsu, strZenZengetsu, strSql) <> True Then
        Erase vData
        Exit Function
    End If
    
    'データ取得
    blnRet = gclsDB.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow)
    
    If blnRet = True Then
        If lngRow > 0 Then
            '取得件数が一致しない場合の措置
            If inData.Tougetsu.Result_Row > lngRow Then
                ReDim Preserve inData.Zengetsu.Result(DBGET_INDEX_MAX, inData.Tougetsu.Result_Row - 1)
            End If
            
            '取得結果構造体への格納
            With inData
                .ZenZengetsu.Result_Row = lngRow
                .ZenZengetsu.Result = vData
            End With
                
                
            For lngCnt = 0 To lngRow - 1
                '結果格納配列に格納(当月取得時に設定可能分)
                vKekka(lngCnt, nKekka.Ruikei_ZenZengetsu) = vData(nGetDb.MISHU_RUIKEI, lngCnt)           '未収累計額・前々月(C)
                vKekka(lngCnt, nKekka.Nav_ZenZengetsu) = vData(nGetDb.BC_ZANDAKA, lngCnt)                '時価総額・前々月
            Next lngCnt
        Else
            ' 0件でもそのまま続行する。
            ReDim inData.ZenZengetsu.Result(DBGET_INDEX_MAX, inData.Tougetsu.Result_Row - 1)
            For lngCnt = 0 To inData.Tougetsu.Result_Row - 1
                inData.ZenZengetsu.Result(nGetDb.KOHZA_NO, lngCnt) = inData.Tougetsu.Result(nGetDb.KOHZA_NO, lngCnt)
            Next lngCnt
        End If
    Else
        Erase vData
        Exit Function
    End If
    
    
    Erase vData
    fncGetDBData5500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetDBData5500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- エラーログ出力 End  ---------------------------------
   Erase vData
   
End Function

'*************************************************************************
'関数名　　：単月未収顧問料チェック表データ取得
'引数　　　：
'           strTougetsu         I           当月月末日(yyyymmdd)
'           strKjnYmd           I           基準年月月末日(yyyymmdd)
'           strSql              O           SQL
'
'戻り値　　：処理結果
'
'機能説明　：単月未収顧問料チェック表のデータを取得する
'
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'          ：2010/05/25 SRA T.Mizutani  単月未収顧問料チェック表の仕様改善
'        　：2013/04/10 GUT 趙 顧問バック更改対応
'
'*************************************************************************
Public Function fncMakeSql(ByVal strTougetsu As String _
                         , ByVal strKjnYmd As String _
                         , ByRef strSql As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeSql"

On Error GoTo ErrSection

    
    fncMakeSql = False
    strSql = ""
'*******************************************************
'* SQL文作成                                           *
'*******************************************************

    strSql = ""
    strSql = strSql & " SELECT DISTINCT"
    strSql = strSql & "        MAIN.KOHZA_NO"                                                   '口座番号
    strSql = strSql & "       ,TRIM(V01.NAME_RJ)"                                               '口座名称
    strSql = strSql & "       ,MK.KEISAN_KIKAN_FROM"                                            '今期顧客計算期間FROM
    strSql = strSql & "       ,MK.KEISAN_KIKAN_TO"                                              '今期顧客計算期間TO
    strSql = strSql & "       ,MK.KEISAN_TO_YMD"                                                '計算TO日
    strSql = strSql & "       ,case when substr(V01.SETTEI_DATE,1,6) = '" & CStr(Mid(strKjnYmd, 1, 6)) & "' then"
    strSql = strSql & "             (TO_DATE(MK.KEISAN_TO_YMD) - TO_DATE(MK.KEISAN_KIKAN_FROM)+1)"
    strSql = strSql & "        else (TO_DATE(MK.KEISAN_TO_YMD) - TO_DATE(SUBSTR(MK.KEISAN_TO_YMD,1,6)||'01')+1) end TSUKI_NISU"     '基準月　日数
    strSql = strSql & "       ,(TO_DATE(MK.KEISAN_KIKAN_TO) - TO_DATE(MK.KEISAN_KIKAN_FROM)+1) NEN_NISU"                              '年間日数
    strSql = strSql & "       ,MSUM.NYUKIN"                                                     '基準月末の入金額累計
    strSql = strSql & "       ,MSUM.SEIKYUZUMI"                                                 '基準月末の未収請求済金額
    strSql = strSql & "       ,MSUM.MISEIKYU"                                                   '基準月末の未収未未請求金額
    strSql = strSql & "       ,(MSUM.SEIKYUZUMI + MSUM.MISEIKYU)             MISHU_RUIKEI"      '基準月末の未収累計額
    strSql = strSql & "       ,(MSUM.NYUKIN+MSUM.SEIKYUZUMI + MSUM.MISEIKYU) SOU_RUIKEI"        '基準月末の総累計額
    strSql = strSql & "       ,CV1.VALUE_NAME KISOSUCHI_TYPE"                                   '基礎数値種類
    strSql = strSql & "       ,CV2.VALUE_NAME KEISAN_HOHO"                                      '基礎数値計算方法
    strSql = strSql & "       ,NAV.BC_ZANDAKA / 1000000"                                        '基準月末の時価総額
    '------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    strSql = strSql & "       ,SAIYOU.SAIYOU_TYPE"                                              '基準月末の採用時価種類
    strSql = strSql & "       ,SAIYOU.SAIYOU_JIKA / 1000000"                                    '基準月末の採用時価総額（百万円）
    '------------<Add End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    strSql = strSql & "   FROM (SELECT DISTINCT C.KOHZA_NO"
    strSql = strSql & "           FROM KYK_MISHU_KANRI C"
    strSql = strSql & "          WHERE C.HIZUKE_SHURUI = '0'"
    strSql = strSql & "            AND C.KEISAN_TO_YMD = " & strTougetsu
    strSql = strSql & "            AND C.SAKUJO_FLG    = '0') MAIN"
    strSql = strSql & "      , KYK_MISHU_KANRI         MK"
    strSql = strSql & "      , VW_DT001D               V01"
    strSql = strSql & "      , (SELECT A.KOHZA_NO"
    strSql = strSql & "               ,A.KEISAN_TO_YMD"
    strSql = strSql & "               ,SUM(A.NAM_TOHGETSU_NYUKINGAKU_SUM) NYUKIN"
    strSql = strSql & "               ,SUM(A.KOMONRYOH_MISHU_SEIKYUZUMI)  SEIKYUZUMI"
    strSql = strSql & "               ,SUM(A.KOMONRYOH_MISHU_MISEIKYU)    MISEIKYU"
    strSql = strSql & "               ,count(*)                           NUM"
    strSql = strSql & "           FROM KYK_MISHU_KANRI A"
    strSql = strSql & "          WHERE A.SAKUJO_FLG='0'"
    strSql = strSql & "          GROUP BY"
    strSql = strSql & "                A.KOHZA_NO,"
    strSql = strSql & "                A.KEISAN_TO_YMD) MSUM"
    strSql = strSql & "      , KYK_RATE_TEKIYOH_KIKAN   RTK"
    strSql = strSql & "      ,(SELECT D.KOHZA_NO"
    strSql = strSql & "              ,D.SEIKYU_KIKAN_ID"
    strSql = strSql & "              ,D.SHISAN_BUNRUI"
    strSql = strSql & "              ,MAX(RATE_TEKIYOH_KIKAN_FROM) RATE_TEKIYOH_KIKAN_FROM"
    strSql = strSql & "          FROM KYK_RATE_TEKIYOH_KIKAN D"
    strSql = strSql & "         WHERE D.SHISAN_BUNRUI   ='0'"
    strSql = strSql & "           AND D.RATE_TEKIYOH_KIKAN_FROM <= " & strKjnYmd
    strSql = strSql & "           AND D.RATE_TEKIYOH_KIKAN_TO   >= " & strKjnYmd
    strSql = strSql & "           AND D.SAKUJO_FLG='0'"
    strSql = strSql & "         GROUP BY KOHZA_NO"
    strSql = strSql & "                 ,SEIKYU_KIKAN_ID"
    strSql = strSql & "                 ,SHISAN_BUNRUI) RTK2"
    strSql = strSql & "      , KYK_CODE_VALUE           CV1"
    strSql = strSql & "      , KYK_CODE_VALUE           CV2"
    strSql = strSql & "      , (SELECT V601.KEJO_DATE,"
    strSql = strSql & "                V601.PRTCD,"
    strSql = strSql & "                SUM(V601.BC_Y_ZAN * KSK.KAGEN_KBN) BC_ZANDAKA"
    strSql = strSql & "           FROM VW_KYK601R  V601"
    strSql = strSql & "               ,KYK_SHISAN_KANJOH_KAMOKU_CD  KSK"
    strSql = strSql & "          WHERE (   V601.DATE_ZOKUSEI_01 != ' '"
    strSql = strSql & "                 OR V601.DATE_ZOKUSEI_02 != ' '"
'------------<顧問バック更改対応 2013/04/10 GUT趙 START> ------------
'    strSql = strSql & "                 OR V601.DATE_ZOKUSEI_04 != ' '"
'    strSql = strSql & "                 OR V601.DATE_ZOKUSEI_05 != ' '"
'    strSql = strSql & "                 OR V601.DATE_ZOKUSEI_06 != ' '"
    strSql = strSql & "                 OR V601.DATE_ZOKUSEI_06 != ' ')"
'    strSql = strSql & "                 OR V601.DATE_ZOKUSEI_07 != ' ')"
'------------<顧問バック更改対応 2013/04/10 GUT趙 END> ------------
    strSql = strSql & "            AND KSK.SHISAN_BUNRUI       = '0'"
    strSql = strSql & "            AND KSK.SHISAN_CODE         = '0000'"
    strSql = strSql & "            AND KSK.GAIBU_CODE          = V601.KANJOU_CD"
    strSql = strSql & "            AND V601.KEJO_DATE          = " & strKjnYmd
    strSql = strSql & "          GROUP BY "
    strSql = strSql & "                V601.KEJO_DATE,"
    strSql = strSql & "                V601.PRTCD)       NAV"
    '------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    strSql = strSql & "      , (SELECT KS.KOHZA_NO"
    strSql = strSql & "               ,DECODE(RATE.KISO_SUCHI_KEISAN_HOHHOH, '6', null, '7', null, '8', null, '9', null,"
    strSql = strSql & "                  DECODE(RATE.KEISAN_HOHHOH, '1', CODE.VALUE_NAME, '2', CODE.VALUE_NAME, '3', null, '4', null, '5', null)) SAIYOU_TYPE"
    strSql = strSql & "               ,SUM(DECODE(RATE.KISO_SUCHI_KEISAN_HOHHOH, '6', null, '7', null, '8', null, '9', null,"
    strSql = strSql & "                  DECODE(RATE.KEISAN_HOHHOH, '1', KS.KEISAN_KISO_SUCHI, '2', KS.KEISAN_KISO_SUCHI, '3', KS.KEISAN_KISO_SUCHI, '4', null, '5', null))) SAIYOU_JIKA"
    strSql = strSql & "           FROM KYK_KEISAN_KISO_SUCHI KS"
    strSql = strSql & "               ,KYK_SHISAN_JIKA JIKA"
    strSql = strSql & "               ,(SELECT KOHZA_NO"
    strSql = strSql & "                       ,SEIKYU_KIKAN_ID"
    strSql = strSql & "                       ,SHISAN_BUNRUI"
    strSql = strSql & "                       ,DECODE(KEISAN_HOHHOH, '2', '0000', SHISAN_CODE) SHISAN_CODE"
    strSql = strSql & "                       ,RATE_TEKIYOH_KIKAN_ID"
    strSql = strSql & "                       ,KISO_SUCHI_KEISAN_HOHHOH"
    strSql = strSql & "                       ,KEISAN_HOHHOH"
    strSql = strSql & "                       ,MAX(RATE_TEKIYOH_KIKAN_FROM)"
    strSql = strSql & "                   FROM KYK_RATE_TEKIYOH_KIKAN"
    strSql = strSql & "                  WHERE SHISAN_BUNRUI = '0'"
    strSql = strSql & "                    AND RATE_TEKIYOH_KIKAN_FROM <= " & strTougetsu
    strSql = strSql & "                    AND RATE_TEKIYOH_KIKAN_TO >= " & strTougetsu
    strSql = strSql & "                    AND SAKUJO_FLG = '0'"
    strSql = strSql & "                  GROUP BY KOHZA_NO"
    strSql = strSql & "                          ,SEIKYU_KIKAN_ID"
    strSql = strSql & "                          ,SHISAN_BUNRUI"
    strSql = strSql & "                          ,DECODE(KEISAN_HOHHOH, '2', '0000', SHISAN_CODE)"
    strSql = strSql & "                          ,RATE_TEKIYOH_KIKAN_ID"
    strSql = strSql & "                          ,KISO_SUCHI_KEISAN_HOHHOH"
    strSql = strSql & "                          ,KEISAN_HOHHOH"
    strSql = strSql & "                ) RATE"
    strSql = strSql & "               ,KYK_CODE_VALUE CODE"
    strSql = strSql & "          WHERE RATE.KOHZA_NO              = KS.KOHZA_NO"
    strSql = strSql & "            AND RATE.SEIKYU_KIKAN_ID       = KS.SEIKYU_KIKAN_ID"
    strSql = strSql & "            AND RATE.SHISAN_BUNRUI         = KS.SHISAN_BUNRUI"
    strSql = strSql & "            AND RATE.SHISAN_CODE           = KS.SHISAN_CODE"
    strSql = strSql & "            AND RATE.RATE_TEKIYOH_KIKAN_ID = KS.RATE_TEKIYOH_KIKAN_ID"
    strSql = strSql & "            AND KS.KEISAN_TO_YMD           = " & strTougetsu
    strSql = strSql & "            AND KS.SAKUJO_FLG              = '0'"
    strSql = strSql & "            AND KS.KIJUN_YMD               = DECODE(RATE.KISO_SUCHI_KEISAN_HOHHOH, "
    strSql = strSql & "                                               '3', TO_CHAR(ADD_MONTHS(LAST_DAY(SUBSTR('" & strTougetsu & "', 1, 6) || '01'), -1), 'YYYYMMDD'), "
    strSql = strSql & "                                               '4', TO_CHAR(ADD_MONTHS(LAST_DAY(SUBSTR('" & strTougetsu & "', 1, 6) || '01'), -1), 'YYYYMMDD'), "
    strSql = strSql & "                                             " & strTougetsu & ")"
    strSql = strSql & "            AND KS.KOHZA_NO                = JIKA.KOHZA_NO"
    strSql = strSql & "            AND KS.SHISAN_BUNRUI           = JIKA.SHISAN_BUNRUI"
    strSql = strSql & "            AND KS.SHISAN_CODE             = JIKA.SHISAN_CODE"
    strSql = strSql & "            AND KS.KIJUN_YMD               = JIKA.KIJUN_YMD"
    strSql = strSql & "            AND JIKA.SAKUJO_FLG            = '0'"
    strSql = strSql & "            AND CODE.CODE_NO (+)           = '33'"
    strSql = strSql & "            AND CODE.CODE_VALUE (+)        = JIKA.PX_SHINTAKU_KBN"
    strSql = strSql & "            AND CODE.SAKUJO_FLG (+)        = '0'"
    strSql = strSql & "          GROUP BY KS.KOHZA_NO"
    strSql = strSql & "                  ,DECODE(RATE.KISO_SUCHI_KEISAN_HOHHOH, '6', null, '7', null, '8', null, '9', null,"
    strSql = strSql & "                     DECODE(RATE.KEISAN_HOHHOH, '1', CODE.VALUE_NAME, '2', CODE.VALUE_NAME, '3', null, '4', null, '5', null))"
    strSql = strSql & "        ) SAIYOU"
    '------------<Add End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    strSql = strSql & "  WHERE MAIN.KOHZA_NO                      = MK.KOHZA_NO(+)"
    strSql = strSql & "    AND MK.KEISAN_TO_YMD(+)                = " & strKjnYmd
    strSql = strSql & "    AND MK.HIZUKE_SHURUI(+)                ='0'"
    strSql = strSql & "    AND MK.KEISAN_KIKAN_FROM(+)           <= " & strKjnYmd
    strSql = strSql & "    AND MK.KEISAN_KIKAN_TO(+)             >= " & strKjnYmd
    strSql = strSql & "    AND MK.SAKUJO_FLG(+)                   ='0'"
    strSql = strSql & "    AND MAIN.KOHZA_NO                      = V01.PRTCD"
    strSql = strSql & "    AND (V01.CALLOFF_DATE                  = 0 "
    strSql = strSql & "      OR V01.CALLOFF_DATE                  > " & strTougetsu & ")"
    strSql = strSql & "    AND MAIN.KOHZA_NO                      = MSUM.KOHZA_NO(+)    "
    strSql = strSql & "    AND MSUM.KEISAN_TO_YMD(+)              = " & strKjnYmd
    strSql = strSql & "    AND MK.KOHZA_NO                        = RTK2.KOHZA_NO(+)"
    strSql = strSql & "    AND RTK2.KOHZA_NO                      = RTK.KOHZA_NO(+)"
    strSql = strSql & "    AND RTK2.SEIKYU_KIKAN_ID               = RTK.SEIKYU_KIKAN_ID(+)"
    strSql = strSql & "    AND RTK2.SHISAN_BUNRUI                 = RTK.SHISAN_BUNRUI(+)"
    strSql = strSql & "    AND RTK2.RATE_TEKIYOH_KIKAN_FROM       = RTK.RATE_TEKIYOH_KIKAN_FROM(+)"
    strSql = strSql & "    AND RTK.RATE_TEKIYOH_KIKAN_TO(+)      >= " & Mid(strKjnYmd, 1, 6) & "01"
    strSql = strSql & "    AND RTK.SHISAN_BUNRUI(+)               = '0'"
    strSql = strSql & "    AND RTK.SAKUJO_FLG(+)                  = '0'"
    strSql = strSql & "    AND CV1.CODE_NO(+)                     = '33'"
    strSql = strSql & "    AND CV1.SAKUJO_FLG(+)                  = '0'"
    strSql = strSql & "    AND RTK.PX_SHINTAKU_KBN                = CV1.CODE_VALUE(+)"
    strSql = strSql & "    AND CV2.CODE_NO(+)                     = '2'"
    strSql = strSql & "    AND CV2.SAKUJO_FLG(+)                  = '0'"
    strSql = strSql & "    AND RTK.KISO_SUCHI_KEISAN_HOHHOH       = CV2.CODE_VALUE(+)"
    strSql = strSql & "    AND MK.KOHZA_NO                        = NAV.PRTCD(+)"
    strSql = strSql & "    AND MK.KEISAN_TO_YMD                   = NAV.KEJO_DATE(+)"
    '------------<Add Start T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    strSql = strSql & "    AND MAIN.KOHZA_NO                      = SAIYOU.KOHZA_NO(+)"
    '------------<Add End   T.Mizutani 2010/05/25 (単月未収顧問料チェック表の仕様改善)> ------------
    strSql = strSql & "  ORDER BY MAIN.KOHZA_NO"
    
    fncMakeSql = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncMakeSql = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- エラーログ出力 End  ---------------------------------
   strSql = ""
End Function

'*************************************************************************
'関数名　　：単月未収計算チェック
'引数　　　：
'           strKijunDate        I           基準年月(YYYYMM)
'           strTougetsu         O           基準年月の月末日
'           strZengetsu         O           基準年月の前月末日
'           strZenZengetsu      O           基準年月の前々月末日
'
'戻り値　　：処理結果Boolean
'
'機能説明　：単月未収額、一日平均、増減率を求め判定をする。
'
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'
'*************************************************************************
Public Function fncCheckCalc5500(ByRef inData As typDbGet _
                               , ByRef vKekka() As Variant) As Boolean
    
    Const PROCEDURE_NAME As String = "fncCheckCalc5500"

On Error GoTo ErrSection

    Dim lngKohzaCnt     As Long
    Dim vTangetsu       As Variant  '当月未収単月 (変数)
    Dim vwSouRuikei     As Variant  '(ワーク)当月総累計
    Dim vwMishuRuikei   As Variant  '(ワーク)前月未収累計
    Dim vIchinichiAve   As Variant  '1日平均 (変数)
    Dim vZougenRitsu    As Variant  '増減率 (変数)
    Dim strFlg          As String   'チェックフラグ (変数)

    
    For lngKohzaCnt = 0 To inData.Tougetsu.Result_Row - 1
        vTangetsu = 0
        vIchinichiAve = 0
        vZougenRitsu = 0
        vwSouRuikei = 0
        vwMishuRuikei = 0
        strFlg = ""
        
        With inData
            '未収単月額､1日平均額の計算
            '***①当月単月の計算***************************************************************************************
            If IsNull(.Tougetsu.Result(nGetDb.SOU_RUIKEI, lngKohzaCnt)) Or _
               Trim(.Tougetsu.Result(nGetDb.SOU_RUIKEI, lngKohzaCnt)) = vbNullString Then
                vwSouRuikei = 0
            Else
                vwSouRuikei = .Tougetsu.Result(nGetDb.SOU_RUIKEI, lngKohzaCnt)
            End If
            If IsNull(.Zengetsu.Result(nGetDb.MISHU_RUIKEI, lngKohzaCnt)) Or _
               Trim(.Zengetsu.Result(nGetDb.MISHU_RUIKEI, lngKohzaCnt)) = vbNullString Then
                vwMishuRuikei = 0
            Else
                vwMishuRuikei = .Zengetsu.Result(nGetDb.MISHU_RUIKEI, lngKohzaCnt)
            End If
            vTangetsu = vwSouRuikei - vwMishuRuikei
            
            '②当月一日平均の計算
            If IsNull(.Tougetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt)) Or _
               Trim(.Tougetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt)) = vbNullString Then
                vIchinichiAve = 0
            ElseIf .Tougetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt) = 0 Then
                vIchinichiAve = 0
            Else
                vIchinichiAve = vTangetsu / .Tougetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt)
            End If
            
            '結果格納配列に格納する。
            vKekka(lngKohzaCnt, nKekka.Tangetsu_Tougetsu) = vTangetsu           '未収単月・当月(D)
            vKekka(lngKohzaCnt, nKekka.IchinichiAve_Tougetsu) = vIchinichiAve   '1日平均額・当月(F)

            
            vTangetsu = 0
            vIchinichiAve = 0
            vwSouRuikei = 0
            vwMishuRuikei = 0
            '***③前月単月の計算***************************************************************************************
            If IsNull(.Zengetsu.Result(nGetDb.SOU_RUIKEI, lngKohzaCnt)) Or _
               Trim(.Zengetsu.Result(nGetDb.SOU_RUIKEI, lngKohzaCnt)) = vbNullString Then
                vwSouRuikei = 0
            Else
                vwSouRuikei = .Zengetsu.Result(nGetDb.SOU_RUIKEI, lngKohzaCnt)
            End If
            If IsNull(.ZenZengetsu.Result(nGetDb.MISHU_RUIKEI, lngKohzaCnt)) Or _
               Trim(.ZenZengetsu.Result(nGetDb.MISHU_RUIKEI, lngKohzaCnt)) = vbNullString Then
                vwMishuRuikei = 0
            Else
                vwMishuRuikei = .ZenZengetsu.Result(nGetDb.MISHU_RUIKEI, lngKohzaCnt)
            End If
            vTangetsu = vwSouRuikei - vwMishuRuikei
            
            '④前月一日平均の計算
            If IsNull(.Zengetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt)) Or _
               Trim(.Zengetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt)) = vbNullString Then
                vIchinichiAve = 0
            ElseIf .Zengetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt) = 0 Then
                vIchinichiAve = 0
            Else
                vIchinichiAve = vTangetsu / .Zengetsu.Result(nGetDb.TSUKI_NISU, lngKohzaCnt)
            End If
            
            '結果格納配列に格納する。
            vKekka(lngKohzaCnt, nKekka.Tangetsu_Zengetsu) = vTangetsu            '未収単月・前月(E)
            vKekka(lngKohzaCnt, nKekka.IchinichiAve_Zengetsu) = vIchinichiAve    '1日平均額・前月(G)

            
            
            '***チェックフラグの判定***********************************************************************************
            '①前月比(増減率)の計算
            If IsNull(vKekka(lngKohzaCnt, nKekka.IchinichiAve_Zengetsu)) Or _
               Trim(vKekka(lngKohzaCnt, nKekka.IchinichiAve_Zengetsu)) = vbNullString Then
                 vZougenRitsu = 0
            ElseIf vKekka(lngKohzaCnt, nKekka.IchinichiAve_Zengetsu) = 0 Then
                vZougenRitsu = 0
            Else
                vZougenRitsu = (CDec(vKekka(lngKohzaCnt, nKekka.IchinichiAve_Tougetsu)) - CDec(vKekka(lngKohzaCnt, nKekka.IchinichiAve_Zengetsu))) _
                               * 100 / CDec(vKekka(lngKohzaCnt, nKekka.IchinichiAve_Zengetsu))
                vZougenRitsu = Round(vZougenRitsu, 1)
            End If
            
            '②チェックフラグの判定
            If vZougenRitsu > 30 Or vZougenRitsu < -30 Then
                strFlg = "1"
            Else
                strFlg = "0"
            End If
            
            '結果格納配列に格納する。
            vKekka(lngKohzaCnt, nKekka.Ichinichi_Zougenritsu) = vZougenRitsu    '1日平均額・前月比(増減率)
            vKekka(lngKohzaCnt, nKekka.Check_Flg) = strFlg                      'チェックフラグ

            
        End With
    
    Next lngKohzaCnt
    
    Set vTangetsu = Nothing
    Set vwSouRuikei = Nothing
    Set vwMishuRuikei = Nothing
    Set vIchinichiAve = Nothing
    Set vZougenRitsu = Nothing
    
    fncCheckCalc5500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncCheckCalc5500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- エラーログ出力 End  ---------------------------------
   
    Set vTangetsu = Nothing
    Set vwSouRuikei = Nothing
    Set vwMishuRuikei = Nothing
    Set vIchinichiAve = Nothing
    Set vZougenRitsu = Nothing
   
End Function


'*************************************************************************
'関数名　　：単月未収顧問料チェック表(帳票ファイル取得)
'引数　　　：
'           strDate            I           システム日付
'           strTime            I           システム時間
'           strKijunDate       I           画面.基準年月
'           strOutFNameOnly    O           出力ファイル名
'
'戻り値　　：処理結果
'
'機能説明　：単月未収顧問料チェック表を出力するファイルを取得する。
'
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'
'*************************************************************************
Public Function fncGetFile5500(ByVal strDate As String _
                             , ByVal strTime As String _
                             , ByVal strKijunDate As String _
                             , ByRef strOutFNameOnly As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile5500"

On Error GoTo ErrSection

    strOutFNameOnly = ""
    
    fncGetFile5500 = False
    
    ' 出力帳票ファイル
    strOutFNameOnly = cPreFName
    strOutFNameOnly = strOutFNameOnly & "_" & strDate
    strOutFNameOnly = strOutFNameOnly & "_" & strTime
    strOutFNameOnly = strOutFNameOnly & "-(" & strKijunDate
    strOutFNameOnly = strOutFNameOnly & ").xls"
    
    fncGetFile5500 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetFile5500 = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：単月未収顧問料チェック表(帳票作成)
'引数　　　：
'           strDate            I           システム日付
'           strOutFName        I           出力ファイル名
'           vKekka             I           DB取得データ
'           inData()           I           出力データ
'
'戻り値　　：処理結果
'
'機能説明　：単月未収顧問料チェック表のBOOKを作成する。
'
'更新履歴　：2008/10/10 SRA Y.Azuma    新規   単月未収顧問料チェック表
'
'*************************************************************************
Public Function fncMakeBook5500(ByVal strDate As String _
                              , ByVal strOutFName As String _
                              , ByRef inData As typDbGet _
                              , ByRef vKekka() As Variant) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeBook5500"

On Error GoTo ErrSection

    Dim lngRowCnt           As Long
    Dim strDesRange         As String
    Dim lngStartRow         As Long
    Dim strTougetsu         As String   '当月末日(YYYY/MM/DD)
    Dim strZengetsu         As String   '前月末日(YYYY/MM/DD)
    Dim strZenZengetsu      As String   '前々月末日(YYYY/MM/DD)
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    lngRowCnt = 0
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    fncMakeBook5500 = False
    
Application.DisplayAlerts = False
        
    '============================================
    ' 単月未収顧問料チェック表
    '============================================
    '配列の添え字の階層を変更 array(X,Y) -> array(Y,X)
'    lngRet = fncKsCmnArrayIdxChange(inData, vntArray)
    
    lngRowCnt = UBound(vKekka)
    
    With inData
        strTougetsu = Mid(inData.Tougetsu.Keisan_To_Ymd, 1, 4) & "/" _
                                   & Mid(inData.Tougetsu.Keisan_To_Ymd, 5, 2) & "/" _
                                   & Mid(inData.Tougetsu.Keisan_To_Ymd, 7, 2)
        strZengetsu = Mid(inData.Zengetsu.Keisan_To_Ymd, 1, 4) & "/" _
                                   & Mid(inData.Zengetsu.Keisan_To_Ymd, 5, 2) & "/" _
                                   & Mid(inData.Zengetsu.Keisan_To_Ymd, 7, 2)
        strZenZengetsu = Mid(inData.ZenZengetsu.Keisan_To_Ymd, 1, 4) & "/" _
                                   & Mid(inData.ZenZengetsu.Keisan_To_Ymd, 5, 2) & "/" _
                                   & Mid(inData.ZenZengetsu.Keisan_To_Ymd, 7, 2)
    End With
    
    With Workbooks(Workbooks.Count).Worksheets("単月未収顧問料チェック表")
        
        
        .Range("基準年月").Value = Mid(inData.Kijun_Ymd, 1, 4) & "/" _
                                 & Mid(inData.Kijun_Ymd, 5, 2) & "/01"
        .Range("当月月末日").Value = strTougetsu
        .Range("前月月末日").Value = strZengetsu
        .Range("前々月月末日").Value = strZenZengetsu
        .Range("未収単月・当月単月").Value = strTougetsu
        .Range("未収単月・前月単月").Value = strZengetsu
        .Range("一日平均額・当月単月").Value = strTougetsu
        .Range("一日平均額・前月単月").Value = strZengetsu
    
        lngStartRow = .Range("一覧_明細TOP").Row
        strDesRange = Cells(lngStartRow, .Range("NO").Column).Address & ":" & Cells(lngStartRow + lngRowCnt, .Range("顧問料請求期間・日数").Column).Address
    
        '行を追加
        .Rows(lngStartRow & ":" & lngStartRow).Copy
        .Rows(lngStartRow & ":" & lngStartRow + lngRowCnt).Insert Shift:=xlDown
        
        'シートにデータをセット
        .Range(strDesRange) = vKekka
         
        '不要な行を削除
        .Range(lngStartRow + lngRowCnt + 1 & ":" & lngStartRow + lngRowCnt + 1).Delete
        
        'カーソル位置を移動する。
        .Range("A1").Activate
    End With
    
    ActiveWindow.ScrollRow = 1
    ActiveWindow.ScrollColumn = 1
    
    Workbooks(Workbooks.Count).Close SaveChanges:=True
    
Application.DisplayAlerts = True
    
    fncMakeBook5500 = True
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

Application.DisplayAlerts = True

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook5500 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'---- エラーログ出力 End  ---------------------------------
   
End Function




