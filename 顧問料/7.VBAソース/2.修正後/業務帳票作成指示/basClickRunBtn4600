Attribute VB_Name = "basClickRunBtn4600"
Option Explicit

'*************************************************************************
'プロジェクト名：新契約管理システム・業務帳票作成指示
'オブジェクト名：basClickRunBtn4600
'機能概要　：部店別契約残高一覧
'更新履歴　：2007/11/19 SRA S.Yoshii     新規作成
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn4600"

Private Const cBaseFile = gc帳票Base46
Private Const cPreFName = gc帳票名46

'*************************************************************************
'関数名　　：部店別契約残高一覧表(帳票作成)
'引　　数　：
'           objTarget        I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2007/10/31 SRA             新規作成
'更新履歴　：2007/12/04 SRA Y.Azuma    変更     (PH4)印刷エラー対策
'
'*************************************************************************
Public Function fncClickRunBtn4600(ByRef objTarget As Object) As Boolean
    
    Const PROCEDURE_NAME As String = "fncClickRunBtn4600"

On Error GoTo ErrSection

    Dim strDay          As String
    Dim strOutFName     As String
    Dim strOutFNameOnly As String
    
    Dim inData1()       As Variant              '部店別契約残高一覧　　　データ取得用
    
    Dim strKjnFirstYmd  As String   '基準月初日WORK
    Dim strKjnLastYmd   As String   '基準月末日
    Dim strZenLastYmd   As String   '基準前月末日
    Dim wDate           As Date     '日付編集用WORK
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    fncClickRunBtn4600 = False
   
    With objTarget
    
        'マウスポインターをWAITにする
        ThisWorkbook.Application.Cursor = xlWait

        ' 作成選択の場合
        If .chkMake.Value = True Then
        
            strDay = Trim(.Range("基準年月4600").Value)

            '基準年月の月初日作成
            strKjnFirstYmd = strDay & "01"
            
            '基準年月の前月末日取得
            wDate = Mid(strKjnFirstYmd, 1, 4) & "/" & Mid(strKjnFirstYmd, 5, 2) & "/" & Mid(strKjnFirstYmd, 7, 2)
            wDate = DateAdd("d", -1, wDate)
            strZenLastYmd = Format(wDate, "YYYYMMDD")
            
            '基準年月の月末日取得
            wDate = Mid(strKjnFirstYmd, 1, 4) & "/" & Mid(strKjnFirstYmd, 5, 2) & "/" & Mid(strKjnFirstYmd, 7, 2)
            wDate = DateAdd("m", 1, wDate)
            wDate = DateAdd("d", -1, wDate)
            strKjnLastYmd = Format(wDate, "YYYYMMDD")

            ' 帳票データ取得
            If fncGetDBData4600(strKjnLastYmd, strKjnFirstYmd, strZenLastYmd, inData1()) = False Then
                GoTo RtnErrSection
            End If
        
            ' 帳票ファイル名取得
            If fncGetFile4600(strDay, strOutFName) = False Then
                GoTo RtnErrSection
            End If
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            strOutFNameOnly = strOutFName
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        
            ' フォルダをつける。
            strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName
            
            ' ファイルコピー
            If fncCopyBook(cBaseFile, strOutFName) = False Then
                GoTo RtnErrSection
            End If
            
            ' ファイル作成
            If fncMakeBook4600(strOutFName, strKjnLastYmd, strZenLastYmd, inData1()) = False Then
                ' ファイル削除
                Call fncDeleteFile(strOutFName)

                GoTo RtnErrSection
            End If

'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            If .chkPrint.Value = True Then
                Call ToukeiChouhyouPrint(strOutFNameOnly)
            End If
            
            Workbooks(strOutFNameOnly).Close SaveChanges:=True
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        
        End If
    
        ' 印刷選択の場合
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
        If .chkMake.Value = False And .chkPrint.Value = True Then
'        If .chkPrint.Value = True Then
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
        'マウスポインターを戻す
        ThisWorkbook.Application.Cursor = xlDefault
    
    End With
   
    fncClickRunBtn4600 = True
    Erase inData1
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                     *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション                              *
'*******************************************************
RtnErrSection:
                
    'マウスポインターを戻す
    ThisWorkbook.Application.Cursor = xlDefault
                
    fncClickRunBtn4600 = False
    Erase inData1
    
End Function

'*************************************************************************
'関数名　　：統計情報 部店別契約残高一覧(帳票データ取得)
'引　　数　：
'           strDay             I           基準年月(YYYYMM)
'           strData()          O           取得データ
'戻り値　　：処理結果
'機能説明　：部店別契約残高一覧表のデータを取得する。
'
'*************************************************************************
Public Function fncGetDBData4600( _
    ByVal strKjnLastYmd As String, _
    ByVal strKjnFirstYmd As String, _
    ByVal strZenLastYmd As String, _
    ByRef inData1() As Variant _
    ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetDBData4600"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    Dim blnRet      As Boolean
    
    fncGetDBData4600 = False
    
    blnRet = False
    
    '部店別契約残高一覧データ取得
    blnRet = fncGetButenBetsuKeiyakuZandakaIchiranData(strKjnLastYmd, strZenLastYmd, inData1())
    If blnRet <> True Then
        Exit Function
    End If
    
    fncGetDBData4600 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetDBData4600 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase vData
'    Erase strData
   
End Function
'*************************************************************************
'関数名　　：部店別契約残高一覧データ取得
'引　　数　：
'           strLastDay         I           基準年月末日(YYYYMMDD)
'           strData()          O           取得データ
'戻り値　　：処理結果
'機能説明　：部店別契約残高一覧のデータを取得する
'更新履歴　：2007/11/19 SRA S.Yoshii   新規
'　　　　　　2008/01/30 SRA S.Yoshii   変更　SQL ROLLUP無効化の対策
'          ：2013/04/11 GUT 梁賀松 顧問バック更改対応
'
'*************************************************************************
Public Function fncGetButenBetsuKeiyakuZandakaIchiranData( _
    ByVal strKjnLastYmd As String, _
    ByVal strZenLastYmd As String, _
    ByRef inData() As Variant _
    ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetButenBetsuKeiyakuZandakaIchiranData"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    
    fncGetButenBetsuKeiyakuZandakaIchiranData = False

'*******************************************************
'* SQL文作成                                           *
'*******************************************************

    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "         TRIM(V301X_2.NAME_J)            FUZOKU_NM,"
    strSql = strSql & "         TRIM(V301X.NAME_J)              BU_NM,"
    strSql = strSql & "         KCD.SUM_JIKA_TOU,"
    strSql = strSql & "         KCD.SUM_JIKA_ZEN,"
    strSql = strSql & "         KCD.SUM_ZOU_GEN"
    strSql = strSql & " FROM ("
    strSql = strSql & " SELECT"
'------------<Modify Start Yoshii 2008/01/30 (PH4)> ★★★☆-----------------------
    strSql = strSql & "         NVL(FUZOKU_CD,'　')             FUZOKU_CD,"
    strSql = strSql & "         NVL(BU_CD,'　')                 BU_CD,"
'    strSql = strSql & "         FUZOKU_CD,"
'    strSql = strSql & "         BU_CD,"
'------------<Modify End   Yoshii 2008/01/30 (PH4)> ★★★☆-----------------------
    strSql = strSql & "         SUM(JIKA_TOU)                   SUM_JIKA_TOU,"
    strSql = strSql & "         SUM(JIKA_ZEN)                   SUM_JIKA_ZEN,"
    strSql = strSql & "         SUM(JIKA_TOU) - SUM(JIKA_ZEN)   SUM_ZOU_GEN"
    strSql = strSql & " FROM ("
    strSql = strSql & " SELECT"
    strSql = strSql & "         V301.FUZOKU_CD                  FUZOKU_CD,"
    strSql = strSql & "         V301.CD_1                       BU_CD,"
    strSql = strSql & "        (KVC.JPY_JIKA_TOTAL_KOKUNAI/100) JIKA_TOU,"
    strSql = strSql & "         0                               JIKA_ZEN"
    strSql = strSql & " FROM"
    strSql = strSql & "         VW_DT301D               V301,"   '--コードブック
    strSql = strSql & "         KYK_VW_KEIEI_CHOUHYOU   KVC,"    '--国内顧問料 経営帳票中間VIEW"
    strSql = strSql & "         VW_DT301D               V301_2"  '--コードブック
    strSql = strSql & " WHERE"
    strSql = strSql & "         V301.CD_KBN     = '20'"          '--コード区分(部店コード)
    strSql = strSql & " AND     V301_2.CD_KBN   = '25'"          '--コード区分(本部コード)
    strSql = strSql & " AND     V301.FUZOKU_CD  = V301_2.CD_1"
    strSql = strSql & " AND     V301.CD_1       = KVC.SOUGOU_PRTCD_BUTEN_1(+)"
'------------<顧問バック更改対応 2013/04/11 GUT梁賀松 START> ------------
'    strSql = strSql & " AND     KVC.SETTEI_KBN  (+)= '1'"        '--口座開設区分 1：国内本開設"
    strSql = strSql & " AND     KVC.SETTEI_KBN  (+)= '0'"        '--口座開設区分 0：通常ポートフォリオ
    strSql = strSql & " AND     KVC.GENGO  (+)= '0'"             '--言語区分 0:日本語"
'------------<顧問バック更改対応 2013/04/11 GUT梁賀松 END> ------------
    strSql = strSql & " AND     KVC.KIJYUN_YMD  (+)= '" & strKjnLastYmd & "'"  '--基準年月月末日"
    strSql = strSql & " UNION"
    strSql = strSql & " SELECT"
    strSql = strSql & "         V301.FUZOKU_CD                  FUZOKU_CD,"
    strSql = strSql & "         V301.CD_1                       BU_CD,"
    strSql = strSql & "         0                               JIKA_TOU,"
    strSql = strSql & "        (KVC.JPY_JIKA_TOTAL_KOKUNAI/100) JIKA_ZEN"
    strSql = strSql & " FROM"
    strSql = strSql & "         VW_DT301D               V301,"   '--コードブック
    strSql = strSql & "         KYK_VW_KEIEI_CHOUHYOU   KVC,"    '--国内顧問料 経営帳票中間VIEW
    strSql = strSql & "         VW_DT301D               V301_2"  '--コードブック
    strSql = strSql & " WHERE"
    strSql = strSql & "         V301.CD_KBN     = '20'"          '--コード区分(部店コード)
    strSql = strSql & " AND     V301_2.CD_KBN   = '25'"          '--コード区分(本部コード)
    strSql = strSql & " AND     V301.FUZOKU_CD  = V301_2.CD_1"
    strSql = strSql & " AND     V301.CD_1       = KVC.SOUGOU_PRTCD_BUTEN_1(+)"
'------------<顧問バック更改対応 2013/04/11 GUT梁賀松 START> ------------
'    strSql = strSql & " AND     KVC.SETTEI_KBN  (+)= '1'"        '--口座開設区分 1：国内本開設
    strSql = strSql & " AND     KVC.SETTEI_KBN  (+)= '0'"        '--口座開設区分 0：通常ポートフォリオ
    strSql = strSql & " AND     KVC.GENGO  (+)= '0'"             '--言語区分 0:日本語"
'------------<顧問バック更改対応 2013/04/11 GUT梁賀松 END> ------------
    strSql = strSql & " AND     KVC.KIJYUN_YMD  (+)= '" & strZenLastYmd & "'"  '--基準年月前月末日"
    strSql = strSql & " )"
    strSql = strSql & " GROUP BY ROLLUP"
    strSql = strSql & "        (FUZOKU_CD,"
    strSql = strSql & "         BU_CD)"
    strSql = strSql & " )                               KCD,"    '--コード及び明細／小計／合計
    strSql = strSql & "         VW_DT301D               V301X,"  '--コードブック
    strSql = strSql & "         VW_DT301D               V301X_2" '--コードブック
    strSql = strSql & " WHERE"
    strSql = strSql & "         KCD.FUZOKU_CD   = V301X_2.CD_1(+)"
    strSql = strSql & " AND     V301X.CD_KBN    (+)= '20'"       '--コード区分(部店コード)
    strSql = strSql & " AND     KCD.BU_CD       = V301X.CD_1(+)"
    strSql = strSql & " AND     V301X_2.CD_KBN  (+)= '25'"       '--コード区分(本部コード)
    strSql = strSql & " ORDER BY"
    strSql = strSql & "         KCD.FUZOKU_CD,"                  '--本店コード
    strSql = strSql & "         KCD.BU_CD"                       '--部店コード

    'データ取得
    Call gclsDB.DoExequteSql(gcSELECT, strSql, inData, lngCol, lngRow)
    
    If lngRow > 0 Then
        fncGetButenBetsuKeiyakuZandakaIchiranData = True
    Else
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "（本部コード又は部店コードが未登録です。）")
    End If
    
    Erase vData
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetButenBetsuKeiyakuZandakaIchiranData = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase vData
   
End Function

'*************************************************************************
'関数名　　：統計情報 部店別契約残高一覧(帳票ファイル名取得)
'引　　数　：
'           strDay             I           基準日
'           strOutFName        O           出力ファイル名
'戻り値　　：処理結果
'機能説明　：部店別契約残高一覧表を出力するファイル名を取得する。
'更新履歴　：2007/10/31 SRA             新規作成
'
'*************************************************************************
Public Function fncGetFile4600(ByVal strDay As String, _
                               ByRef strOutFName As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile4600"

On Error GoTo ErrSection

    Dim strDate         As String
    Dim strTime         As String

    strOutFName = ""
    
    fncGetFile4600 = False
    
    Call gfunc_GetSvDateTime(strDate, strTime)
       
    ' 出力帳票ファイル
    strOutFName = cPreFName
    strOutFName = strOutFName & "_" & strDate
    strOutFName = strOutFName & "_" & strTime
    strOutFName = strOutFName & "-(" & strDay
    strOutFName = strOutFName & ")"
    strOutFName = strOutFName & ".xls"
    
    fncGetFile4600 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetFile4600 = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：統計情報 部店別契約残高一覧(帳票作成)
'引　　数　：
'           strOutFName        I           出力ファイル名
'           strKjnLastYmd      I           基準日(月末日)
'           strZenLastYmd      I           基準日(前月末日)
'           inData1()          I/O         出力データ
'戻り値　　：処理結果
'機能説明　：統計情報 部店別契約残高一覧のBOOKを作成する。
'更新履歴　：2007/12/04 SRA Y.Azuma    変更     (PH4)印刷エラー対策
'更新履歴　：2008/01/21 SRA S.Yoshii   変更     当月／前月残高がゼロの場合、ファイル作成の中止
'
'*************************************************************************
Public Function fncMakeBook4600( _
    ByVal strOutFName As String, _
    ByVal strKjnLastYmd As String, _
    ByVal strZenLastYmd As String, _
    ByRef inData1() As Variant _
    ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeBook4600"

On Error GoTo ErrSection

    Dim lngRowCnt       As Long
    Dim strDesRange     As String
    Dim vntArray2()     As Variant
    Dim lngRet          As Long
    
    Dim lngStartRowIchiran  As Long
    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    lngRowCnt = 0
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    fncMakeBook4600 = False
    
Application.DisplayAlerts = False
    
    
    '============================================
    ' 部店別契約残高一覧
    '============================================
    '配列の添え字の階層を変更 array(X,Y) -> array(Y,X)
    lngRet = fncKsCmnArrayIdxChange(inData1, vntArray2)
    
    lngRowCnt = UBound(inData1, 2)
'    strDesRange = "A" & lngStartRowIchiran & ":U" & lngStartRowIchiran + lngRowCnt
    
    With Workbooks(Workbooks.Count).Worksheets("部店別契約残高一覧")
    
        lngStartRowIchiran = .Range("一覧_明細TOP").Row
        strDesRange = Cells(lngStartRowIchiran, .Range("一覧_本部").Column).Address & ":" & Cells(lngStartRowIchiran + lngRowCnt, .Range("一覧_増減").Column).Address
    
        '行を追加
        .Rows(lngStartRowIchiran & ":" & lngStartRowIchiran).Copy
        .Rows(lngStartRowIchiran & ":" & lngStartRowIchiran + lngRowCnt).Insert Shift:=xlDown
        
        'シートにデータをセット
        .Range("当月残高") = Mid(strKjnLastYmd, 3, 2) & "/" & Mid(strKjnLastYmd, 5, 2) & "残高"
        .Range("前月残高") = Mid(strZenLastYmd, 3, 2) & "/" & Mid(strZenLastYmd, 5, 2) & "残高"
        .Range(strDesRange) = vntArray2
         
        '不要な行を削除
        .Range(lngStartRowIchiran + lngRowCnt + 1 & ":" & lngStartRowIchiran + lngRowCnt + 1).Delete
        
        'カーソル位置を移動する。
        .Activate
        .Range("A1").Activate
    End With
    
    ' 本部名のセル結合／整形及び合計等の文言追加
    If fncCellRemake4600(lngRowCnt) = False Then
        GoTo ErrSection
    End If
    
'------------<Modify Start azuma 2007/12/04 (PH4)> ★★★☆-----------------------
    Workbooks(Workbooks.Count).Save
'    Workbooks(Workbooks.Count).Close SaveChanges:=True
'------------<Modify End   azuma 2007/12/04 (PH4)> ★★★☆-----------------------
    
Application.DisplayAlerts = True
    
    ' 当月／前月残高にゼロ以上の有効データが存在？
    If vntArray2(lngRowCnt, 2) = 0 And vntArray2(lngRowCnt, 3) = 0 Then
        ' 該当データが存在しません。
        Call gfunc_ErrorMsg(gcMsgKbn_3, gcMsgId3001, cPreFName, "（当月残高及び前月残高の両方がゼロです。）")
'------------<Add Start Yoshii 2008/01/21 (PH4)> ★★★☆-----------------------
        Workbooks(Workbooks.Count).Close SaveChanges:=True
'------------<Add End   Yoshii 2008/01/21 (PH4)> ★★★☆-----------------------
        fncMakeBook4600 = False
        Exit Function
    Else
        fncMakeBook4600 = True
    End If
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

Application.DisplayAlerts = True

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook4600 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
End Function

'*************************************************************************
'関数名　　：部店別契約残高一覧表(表左側タイトルの整形)
'引　　数　：
'           lngRowCnt        I            出力行数
'戻り値　　：処理結果
'機能説明　：本部名のセル結合／整形及び合計等の文言追加
'更新履歴　：2007/11/21 SRA S.Yoshii       新規作成
'
'*************************************************************************
Function fncCellRemake4600(ByVal lngRowCnt As Long) As Boolean
    
    Const PROCEDURE_NAME As String = "fncCellRemake4600"

On Error GoTo ErrSection

    Dim i As Long
    Dim j As Long
    Dim MaxRow As Long
    
    fncCellRemake4600 = False

    '初期値設定
    MaxRow = lngRowCnt + 1
    j = 2

    With Range("一覧_本部")

        '本部名の結合／小計の配置
        For i = 2 To MaxRow
    
            If .Cells(i, 2) = "" Then
    
                .Cells(i, 2) = "＜計＞"
            
                With Range(.Cells(j, 1), .Cells(i, 1))
                    .HorizontalAlignment = xlCenter
                    .VerticalAlignment = xlCenter
                    .WrapText = True
                    .Orientation = xlVertical
                    .AddIndent = False
                    .IndentLevel = 0
                    .ShrinkToFit = False
                    .ReadingOrder = xlContext
                    .MergeCells = True
                End With
            
                j = i + 1
    
            End If
    
        Next i

        '最終行の整形
        .Cells(j, 1) = "総合計"
        .Cells(j, 1).Borders(xlEdgeRight).LineStyle = xlNone
    
    End With
    
    fncCellRemake4600 = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

Application.DisplayAlerts = True

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncCellRemake4600 = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

End Function




