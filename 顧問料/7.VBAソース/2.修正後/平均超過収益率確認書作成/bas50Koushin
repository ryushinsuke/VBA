Attribute VB_Name = "bas50Koushin"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料計算システム・平均超過収益率確認書作成
'
'オブジェクト名：bas50Koushin
'
'機能概要　：更新処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Const MODULE_NAME = "bas50Koushin"


Const UPDATE_USER = "APL001"
Const UPDATE_PGM_ID = "平均超過収益率確認書作成"


Public Type typRateTekiyohKikan
    KOHZA_NO                    As String
    SEIKYU_KIKAN_ID             As String   '検索時の請求期間ID
    SHISAN_BUNRUI               As String
    SHISAN_CODE                 As String
    RATE_TEKIYOH_KIKAN_ID       As String   '検索時の適用期間ID
    RATE_TEKIYOH_KIKAN_FROM     As String
    RATE_TEKIYOH_KIKAN_TO       As String
    RATE_TEKIYOH_KIKAN_NISSU    As String
    PX_SHINTAKU_KBN             As String
    KISO_SUCHI_KEISAN_HOHHOH    As String
    RATE_CODE                   As String
    KEISAN_HOHHOH               As String
    SHISAN_WARIAI               As String
    RATE_TEKIYOH_KIKAN_ID_NEW   As String   '登録時の適用期間ID
End Type

Public Const Sisan1_MaxCols = 9     'ローリング期間の入力エリア数

'*************************************************************************
'関数名　　：入力チェック処理(更新時)
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'           Sisan1()            O           ローリング期間   構造体
'
'戻り値　　：Boolean
'
'機能説明　：メインシートの更新時入力チェックを行い
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Public Function gfuncKeisan_UpdInputCheck(ByRef TargetSheets As Object _
                                        , ByRef Sisan1() As Variant) As Boolean
On Error GoTo ErrorSection


    Dim strKozaNo       As String
    Dim strKijun        As String
    Dim strDate         As String
    Dim strTime         As String
    Dim strCallOff_Date As String
    
    Dim lngIntegerLen   As Long
    Dim lngDecimalLen   As Long
    
    gfuncKeisan_UpdInputCheck = False
    strKozaNo = ""
    
    '口座番号の検索時同一チェック
    strKozaNo = gfunc_KozaNoFormat(Trim(TargetSheets.Range("口座番号")))
    If StrComp(ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時口座ＮＯ"), strKozaNo) <> 0 Then
        '"口座番号：検索時と口座番号が異なります。"
        Call gfunc_ErrorMsg(3, 27027, "口座番号", "")
        Exit Function
    End If
    
'1.口座番号の入力チェック
    strKozaNo = Trim(TargetSheets.Range("口座番号"))
    '口座番号の入力必須チェック
    If Trim(strKozaNo) = "" Then
        '口座番号が未入力の場合、エラーメッセージを表示
        Call gfunc_ErrorMsg(3, 27000, "口座番号", "7桁で入力して下さい。")
        Exit Function
    End If
    
    '口座番号の桁数チェック
    If Len(strKozaNo) >= 8 Then
        '8桁以上の入力がある場合、エラーメッセージを表示する
        Call gfunc_ErrorMsg(3, 27001, "口座番号", "7桁で入力して下さい。")
        Exit Function
    Else
        '8桁未満の場合は、口座番号の頭に0を設定する
        strKozaNo = gfunc_KozaNoFormat(strKozaNo)
        TargetSheets.Range("口座番号") = strKozaNo
    End If
    
    '口座の存在チェック
    If func_KozaNoExist(TargetSheets, strKozaNo, strCallOff_Date) = False Then
        '口座が存在しない場合は、エラーメッセージを表示する
        Call gfunc_ErrorMsg(3, 27003, "口座番号", "")
        blnLockFlg = True
        Exit Function
    End If
    
    
'2.基準日の入力チェック
    strKijun = Trim(TargetSheets.Range("基準日"))
    '基準日の入力有無を判定
    If Trim(strKijun) <> "" Then
        '基準日に入力値がある場合、入力日付妥当性チェックを行う
        If gfunc_CheckDate(strKijun) = False Then
            '入力値に誤りがある場合、エラーメッセージを表示
            Call gfunc_ErrorMsg(3, 27001, "基準日", "")
            Exit Function
        End If
    Else
        '基準日の入力がない場合、サーバ日付を取得し、基準日項目に反映する
        'サーバ日付取得
        Call gfunc_GetSvDateTime(strDate, strTime)
        If Trim(strDate) <> "" Then
            TargetSheets.Range("基準日") = Mid(strDate, 1, 4) & Mid(strDate, 5, 2) & Mid(strDate, 7, 2)
        Else
            TargetSheets.Range("基準日") = ""
        End If
        
    End If
    
'3.目標超過収益率の入力チェック
    If TargetSheets.chkMokuhyo = True Then
        '目標超過収益率の区分チェックがある場合
        '目標超過収益率の入力必須チェックを行う
        If Trim(TargetSheets.Range("目標超過収益率2")) = "" Then
            'エラーメッセージを表示
            Call gfunc_ErrorMsg(3, 27000, "目標超過収益率(%)", "")
            Exit Function
        Else
            '数値チェックを行う
            If gkyk_IsNumeric(TargetSheets.Range("目標超過収益率2"), "+-.") = False Then
                Call gfunc_ErrorMsg(3, 27019, "目標超過収益率(%)", "")
                Exit Function
            End If
            
            '変数の初期化
            lngIntegerLen = 0
            lngDecimalLen = 0
            Call sub_CheckIntDec(TargetSheets.Range("目標超過収益率2"), lngIntegerLen, lngDecimalLen)
            
            '整数部の桁数チェックを行う
            If lngIntegerLen > 3 Then
                '整数部が3桁超の場合、エラーメッセージを表示
                Call gfunc_ErrorMsg(3, 27008, "目標超過収益率(%)", "整数部は3桁以内で入力して下さい。")
                Exit Function
            End If
            '小数点部分の桁数チェック
            If lngDecimalLen > 6 Then
                Call gfunc_ErrorMsg(3, 27008, "目標超過収益率(%)", "小数点以下は6桁以内で入力して下さい。")
                Exit Function
            End If
        End If
    End If
    
'4.ローリング年数の入力チェック
    'ローリング年数必須チェック
    If Trim(TargetSheets.Range("ローリング年数")) = "" Then
        Call gfunc_ErrorMsg(3, 27000, "ローリング年数", "")
        Exit Function
    End If
    
    'ローリング年数数値チェック
    If gkyk_IsNumeric(TargetSheets.Range("ローリング年数")) = False Then
        Call gfunc_ErrorMsg(3, 27019, "ローリング年数", "")
        Exit Function
    End If
    
    'ローリング年数の妥当性チェック
    If CLng(TargetSheets.Range("ローリング年数")) <= 0 Then
        Call gfunc_ErrorMsg(3, 27028, "ローリング年数", "")
        Exit Function
    End If
    
    'ローリング年数の桁数チェック
    lngIntegerLen = 0
    lngDecimalLen = 0
    'ローリング年数の桁数を取得
    Call sub_CheckIntDec(TargetSheets.Range("ローリング年数"), lngIntegerLen, lngDecimalLen)
    
    '整数部の桁数チェックを行う
    If lngIntegerLen > 3 Then
        '整数部が3桁超の場合、エラーメッセージを表示
        Call gfunc_ErrorMsg(3, 27008, "ローリング年数", "3桁以内で入力して下さい。")
        Exit Function
    End If

          
'5.ローリング期間情報チェック
    If func_CheckShisan1(TargetSheets, Sisan1) = False Then
        Exit Function
    End If

'6.新規登録データの成功報酬期間重複チェック
    If func_CheckSeikoKikan(TargetSheets, Sisan1, TargetSheets.Range("口座番号")) = False Then
        '成功報酬期間期間のFROM、TOで重複したデータが存在する場合、エラーメッセージを表示
        Call gfunc_ErrorMsg(3, 27020, "成功報酬期間From,To", _
                    "同一口座番号で成功報酬期間の重複したデータが既に登録されています。")
        Exit Function
    End If

    gfuncKeisan_UpdInputCheck = True

    Exit Function
    
ErrorSection:

    gfuncKeisan_UpdInputCheck = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfuncKeisan_UpdInputCheck", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------


End Function

'*************************************************************************
'関数名　　：更新処理(メイン)
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'           Sisan1()            O           一般資産
'           lngId               O           請求期間ID(採番したもの)
'
'戻り値　　：Boolean
'
'機能説明　：更新
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Public Function gfuncKeisan_DoUpdate(ByRef TargetSheets As Object _
                                   , ByRef Sisan1() As Variant _
                                   , ByRef lngId As Long) As Boolean


On Error GoTo ErrorSection

    Dim lngKikanId          As Long         '採番　　請求期間ID
    Dim lngKikanIdFirst     As Long         '検索時　請求期間ID
    Dim strKohzaNo          As String       '口座番号
    Dim strTime             As String
    Dim strError            As String
    Dim lngWork             As Long
    Dim lngStatus           As Long         '更新済みチェックのステータス
    Dim lngMsgId            As Long         'エラーメッセージID
    Dim dblHeikinTyohka     As Double
    
    
    gfuncKeisan_DoUpdate = False
    
    '検索時の請求期間ID　取得
    lngKikanIdFirst = func_GetKikanIdFirst
    
    '口座番号　取得
    strKohzaNo = gfunc_KozaNoFormat(TargetSheets.Range("口座番号").Value2)
    
    'トランザクション開始
    cnAdo.BeginTrans
    
    '更新済みチェックと排他処理
    If func_UpdateCheck(TargetSheets, lngStatus, lngKikanIdFirst) = False Then
        
        If lngStatus = 1 Or lngStatus = 2 Then
            '0:正常,1:ロック,2:削除済,-1:その他
            Select Case lngStatus
                Case 1
                '他セッションからのロック
                    lngMsgId = 27021
                Case 2
                '削除済み
                    lngMsgId = 27022
            End Select
            
            'エラーメッセージ出力
            Call gfunc_ErrorMsg(3, lngMsgId, "", "")
        End If
        
        GoTo RollbackSection
        Exit Function
    End If
    
    '平均超過収益率計算処理
    Call sub_CalcSyuekiRitsu(TargetSheets, Sisan1)
    
    '論理削除処理
    If func_Del3Tables(strKohzaNo, Sisan1, strError) = False Then
        GoTo RollbackSection
        Exit Function
    End If
    
    
    '①対象メインテーブルへの更新
    '　①-1 請求期間ＩＤの採番
    If func_GetSeikohId(lngKikanId) = False Then
        GoTo RollbackSection
        Exit Function
    End If
    
    '　①-2 成功報酬期間へのInsert
    If func_InsSEIKOH_KIKAN(TargetSheets _
                           , lngKikanId _
                           , lngKikanIdFirst _
                           , Sisan1 _
                           , strError) = False Then
        GoTo RollbackSection
        Exit Function
    End If
    
    '　①-3 年ごと超過収益率へのInsert
    If func_InsNENGOTO_SYUEKI(TargetSheets _
                           , lngKikanId _
                           , lngKikanIdFirst _
                           , Sisan1 _
                           , dblHeikinTyohka _
                           , strError) = False Then
        GoTo RollbackSection
        Exit Function
    End If
    
    '　①-4 ローリング後超過収益率へのInsert
    If func_InsROLLING_KIKAN(TargetSheets _
                           , lngKikanId _
                           , lngKikanIdFirst _
                           , Sisan1 _
                           , dblHeikinTyohka _
                           , strError) = False Then
        GoTo RollbackSection
        Exit Function
    End If
    

    'コミット
    cnAdo.CommitTrans

    '登録に使用した請求期間IDを戻す
    lngId = lngKikanId
    
    gfuncKeisan_DoUpdate = True
    
    Exit Function
    
RollbackSection:
    'ロールバック
    cnAdo.RollbackTrans
    
    gfuncKeisan_DoUpdate = False
    Exit Function
    
ErrorSection:

    gfuncKeisan_DoUpdate = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "gfuncKeisan_DoUpdate", Err.Number, Err.Description)
    On Error GoTo 0
    On Error Resume Next
    'ロールバック
    cnAdo.RollbackTrans
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：平均超過収益率計算
'
'引　　数　：
'           TargetSheets        I       処理対象のシート
'           Sisan1()            O       ローリング期間の情報を保持している配列
'
'戻り値　　：なし
'
'機能説明　：平均超過収益率確認書出力に必要な項目の算出を行う
'            算出項目は以下
'            ・ローリングごと超過収益率
'            ・ローリングごとの年率換算後超過収益率
'            ・平均超過収益率
'            ・掛け目基礎数値
'
'更新履歴　：2011/02/14 SRA 矢口     新規作成
'
'*************************************************************************
Private Sub sub_CalcSyuekiRitsu(ByRef TargetSheets As Object _
                              , ByRef Sisan() As Variant)

On Error GoTo ErrorSection

    Dim lngCnt   As Long                   'ロープ処理のカウンタ
    Dim lngRolMAX As Long                  'ローリング年数

    Dim dblFundSyuekiRitsu As Double       'ファンド収益率
    Dim dblBenchmarkSyuekiRitsu As Double  'ベンチマーク収益率
    Dim dblMokuhyoTyokaRitsu As Double     '目標超過収益率

    Dim dblTyokaRitsu As Double            '算出後超過収益率
    Dim lngKeisanKikanSu As Long           '計算期間(月数)

    Dim dblNenRitsuTyoka As Double         '算出後年率換算後超過収益率
    Dim dblHukuriKanzan1 As Double         '複利換算計算用1(計算：1 + 超過収益率)
    Dim dblHukuriKanzan2 As Double         '複利換算計算用2(計算：複利換算計算用1 ^ (12 ÷ 計算期間)

    Dim dblSumTyokaRitsu As Double         'ローリング期間ごとの超過収益率合算値
    Dim dblHeikinTyokaRitsu As Double      '算出後平均超過収益率
    Dim dblKakemeKiso As Double            '算出後掛け目基礎数値


    '各変数の初期化
    lngCnt = 0
    lngRolMAX = 0

    dblFundSyuekiRitsu = 0
    dblBenchmarkSyuekiRitsu = 0
    dblMokuhyoTyokaRitsu = 0

    dblTyokaRitsu = 0
    lngKeisanKikanSu = 0

    dblNenRitsuTyoka = 0
    dblHukuriKanzan1 = 0
    dblHukuriKanzan2 = 0

    dblSumTyokaRitsu = 0
    dblHeikinTyokaRitsu = 0
    dblKakemeKiso = 0

    'ローリング期間の取得
    lngRolMAX = TargetSheets.Range("ローリング年数")
    '目標超過収益率の取得
    If TargetSheets.chkMokuhyo = True Then
        dblMokuhyoTyokaRitsu = CDbl(TargetSheets.Range("目標超過収益率2") / 100)
       '取得した目標超過収益率を格納
        Sisan(0, 0) = dblMokuhyoTyokaRitsu
    Else
        Sisan(0, 0) = Null
    End If

    '超過収益率、年率換算後超過収益率の算出
    'ローリング年数分、繰り返し処理を行う
    For lngCnt = 1 To lngRolMAX
        'ファンド収益率、ベンチマーク収益率の取得
        dblFundSyuekiRitsu = CDbl(Sisan(lngCnt, 5)) / 100
        dblBenchmarkSyuekiRitsu = CDbl(Sisan(lngCnt, 6)) / 100

        '超過収益率の算出
        dblTyokaRitsu = dblFundSyuekiRitsu - dblBenchmarkSyuekiRitsu

        '年率換算後超過収益率の算出
        '年率換算方法を判断し算出する
        Select Case Sisan(lngCnt, 3)
        
            '0:なしの場合
            Case 0
                dblNenRitsuTyoka = dblTyokaRitsu

            '1:月単利換算の場合
            Case 1
                '計算期間数の算出(月数)
                lngKeisanKikanSu _
                        = ((CLng(Mid(Sisan(lngCnt, 2), 1, 4)) * 12 + CLng(Mid(Sisan(lngCnt, 2), 5, 2))) _
                         - (CLng(Mid(Sisan(lngCnt, 1), 1, 4)) * 12 + CLng(Mid(Sisan(lngCnt, 1), 5, 2)))) + 1
                '年率換算後の算出
                dblNenRitsuTyoka = dblTyokaRitsu * (12 / lngKeisanKikanSu)

            '2:月複利換算の場合
            Case 2
                '計算期間数の算出(月数)
                lngKeisanKikanSu _
                        = ((CLng(Mid(Sisan(lngCnt, 2), 1, 4)) * 12 + CLng(Mid(Sisan(lngCnt, 2), 5, 2))) _
                         - (CLng(Mid(Sisan(lngCnt, 1), 1, 4)) * 12 + CLng(Mid(Sisan(lngCnt, 1), 5, 2)))) + 1
                '年率換算後の算出
                dblHukuriKanzan1 = 1 + dblTyokaRitsu
                dblHukuriKanzan2 = dblHukuriKanzan1 ^ (12 / lngKeisanKikanSu)
                dblNenRitsuTyoka = dblHukuriKanzan2 - 1

            '3:日単利換算の場合
            Case 3
                '計算期間数の算出(日数)
                lngKeisanKikanSu _
                        = (CDate(gfunc_YmdFormat(Sisan(lngCnt, 2))) - CDate(gfunc_YmdFormat(Sisan(lngCnt, 1)))) + 1
                '年率換算後の算出
                '年間日数の判定
                If Sisan(lngCnt, 4) = 1 Then
                    '年間日数が365日の場合
                    dblNenRitsuTyoka = dblTyokaRitsu * (365 / lngKeisanKikanSu)
                ElseIf Sisan(lngCnt, 4) = 2 Then
                    '年間日数が366日の場合
                    dblNenRitsuTyoka = dblTyokaRitsu * (366 / lngKeisanKikanSu)
                End If

            '4:日複利換算の場合
            Case 4
                '計算期間数の算出(日数)
                lngKeisanKikanSu _
                        = (CDate(gfunc_YmdFormat(Sisan(lngCnt, 2))) - CDate(gfunc_YmdFormat(Sisan(lngCnt, 1)))) + 1
                '年率換算後の算出
                dblHukuriKanzan1 = 1 + dblTyokaRitsu
                '年間日数の判定
                If Sisan(lngCnt, 4) = 1 Then
                    '年間日数が365日の場合
                    dblHukuriKanzan2 = dblHukuriKanzan1 ^ (365 / lngKeisanKikanSu)
                ElseIf Sisan(lngCnt, 4) = 2 Then
                    '年間日数が366日の場合
                    dblHukuriKanzan2 = dblHukuriKanzan1 ^ (366 / lngKeisanKikanSu)
                End If
                dblNenRitsuTyoka = dblHukuriKanzan2 - 1
        End Select

        '超過収益率の合算
        dblSumTyokaRitsu = dblSumTyokaRitsu + dblNenRitsuTyoka

        '各算出結果の格納
        'ファンド収益率
        Sisan(lngCnt, 5) = dblFundSyuekiRitsu
        'ベンチマーク収益率
        Sisan(lngCnt, 6) = dblBenchmarkSyuekiRitsu
        '算出後超過収益率
        Sisan(lngCnt, 7) = dblTyokaRitsu
        '算出後年率換算後超過収益率
        Sisan(lngCnt, 8) = dblNenRitsuTyoka
        '計算期間数
        Sisan(lngCnt, 9) = lngKeisanKikanSu
    Next lngCnt

    '平均超過収益率の算出
    dblHeikinTyokaRitsu = dblSumTyokaRitsu / CDbl(lngRolMAX)
    '掛け目基礎数値の算出
    dblKakemeKiso = dblHeikinTyokaRitsu - dblMokuhyoTyokaRitsu

    '算出結果の格納
    '平均超過収益率
    Sisan(0, 1) = dblHeikinTyokaRitsu
    '掛け目基礎数値
    Sisan(0, 2) = dblKakemeKiso

Exit Sub

ErrorSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "sub_CalcSyuekiRitsu", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub

'*************************************************************************
'関数名　　：成功報酬期間の既存登録重複チェック
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'           Sisan()             I           ローリング期間の構造体配列
'           strKozaNo           I           口座番号
'
'戻り値　　：Boolean
'
'機能説明　：新規データ登録時に成功報酬期間が既存データと重複していない
'            かをチェックする
'
'更新履歴　：2011/02/16 SRA 矢口    新規作成
'
'*************************************************************************
Private Function func_CheckSeikoKikan(ByRef TargetSheets As Object _
                                    , ByRef Sisan() As Variant _
                                    , ByRef strKozaNo As String) As Boolean
                                 
    Dim vData()                 As Variant  '取得データ
    Dim strSql                  As String   'SQL文
    Dim lngCol                  As Long     '取得カラム数
    Dim lngRow                  As Long     '取得データ数
    Dim lngCnt                  As Long
    Dim strErrCode              As String   'エラーコード
    Dim blnRet                  As Boolean  '返却値
    Dim lngKikanIdFirst         As Long     '検索時　成功報酬期間ID
    Dim lngSeikohKikanFrom      As Long      '成功報酬期間FROM
    Dim lngSeikohKikanTo        As Long      '成功報酬期間TO

    func_CheckSeikoKikan = False
    
    strErrCode = ""
    Erase vData

    '検索時成功報酬期間IDの取得
    lngKikanIdFirst = func_GetKikanIdFirst()
    '検索時に取得した成功報酬期間IDが存在しない(新規データ登録)場合に以下の処理を行う
    If lngKikanIdFirst = -1 Then
    
        '成功報酬期間FROMの取得
        lngSeikohKikanFrom = CLng(Sisan(1, 1))
        '成功報酬期間TOの取得
        lngSeikohKikanTo = CLng(Sisan(TargetSheets.Range("ローリング年数"), 2))
    
        strSql = ""
        strSql = strSql & " SELECT"
        strSql = strSql & "     count(*)"
        strSql = strSql & " FROM"
        strSql = strSql & "     KYK_SH_SEIKOH_HOHSHU_KIKAN"
        strSql = strSql & " WHERE"
        strSql = strSql & "     KOHZA_NO = '" & func_ChkQuoteshon(strKozaNo, "'") & "'  AND"
        strSql = strSql & "     SAKUJO_FLG = '0'      AND"
        strSql = strSql & "     (" & lngSeikohKikanFrom & " = SEIKOH_HOSYU_KIKAN_FROM"
        strSql = strSql & "      AND  " & lngSeikohKikanTo & " = SEIKOH_HOSYU_KIKAN_TO)"
        strSql = strSql & " "
    
        'データ取得
        blnRet = gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
        
        If blnRet Then
            If Trim(strErrCode) <> "" Then
                'Error
                Exit Function
            End If
        Else
            Exit Function
        End If
    
        '存在の確認
        If lngRow > 0 Then
            If CStr(vData(0, 0)) <> "0" Then
                'データがある場合期間が重複してるデータが存在している。
                Exit Function
            End If
        End If
        
        Erase vData
    End If
    
    func_CheckSeikoKikan = True
    
    Exit Function

ErrorSection:

    func_CheckSeikoKikan = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_CheckSeikoKikan", Err.Number, Err.Description)
    On Error GoTo 0
    
    Erase vData
    
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：ローリング期間情報チェック
'
'引　　数　：
'           TargetSheets        I           チェック対象のシート
'           Sisan()             O           ローリング期間の構造体配列
'
'戻り値　　：Boolean
'
'機能説明　：ローリング期間情報の更新時入力チェック
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_CheckShisan1(ByRef TargetSheets As Object _
                                 , ByRef Sisan() As Variant) As Boolean
On Error GoTo ErrorSection

    Dim lngCnt          As Long
    
    Dim blnRet          As Boolean      '
    
    Dim lngIntegerLen   As Long
    Dim lngDecimalLen   As Long
    
    Const Meisai = "ローリング期間　"

    func_CheckShisan1 = False
    
    ReDim Sisan(0)
    
    With TargetSheets
    
    'ローリング期間の構造体配列への取込とソート
    If func_Sisan1_To_Dim(TargetSheets, Sisan) = False Then
        Exit Function
    End If

'４．【ローリング年数】
    '③相関チェック（ローリング年番号）
    If UBound(Sisan) <> Trim(TargetSheets.Range("ローリング年数")) Then
        Call gfunc_ErrorMsg(3, 27009, "ローリング年数")
        Exit Function
    End If
        
'1.ローリング期間内の入力チェックを行う
    For lngCnt = 1 To UBound(Sisan)
        'ローリング期間Fromの入力必須チェック
        If Trim(Sisan(lngCnt, 1)) = "" Then
            '"未入力。"
            Call gfunc_ErrorMsg(3, 27000, "ローリング期間From ", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        If Trim(Sisan(lngCnt, 2)) = "" Then
            '"未入力。"
            Call gfunc_ErrorMsg(3, 27000, "ローリング期間To ", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
            
        '　①-1 日付の妥当チェック
        If gfunc_CheckDate(Sisan(lngCnt, 1)) = False Then
            '入力不正
            Call gfunc_ErrorMsg(3, 27023, "ローリング期間From ", "日付の妥当性に問題があります。" & vbCrLf & _
                                Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        If gfunc_CheckDate(Sisan(lngCnt, 2)) = False Then
            '入力不正
            Call gfunc_ErrorMsg(3, 27023, "ローリング期間To ", "日付の妥当性に問題があります。" & vbCrLf & _
                                Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
        
        '　①-2 大小チェック
        If Sisan(lngCnt, 1) > Sisan(lngCnt, 2) Then
            Call gfunc_ErrorMsg(3, 27002, "ローリング期間From/To", _
                                "ローリング期間Fromがローリング期間Toより先日付で指定されています。" _
                                & vbCrLf & Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
                
    '６．【年率換算方法】
        '　⑤-1 必須チェック
        If Trim(Sisan(lngCnt, 3)) = "" Then
            '"未入力。"
            Call gfunc_ErrorMsg(3, 27024, "年率換算方法 ", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
        
    '７．【年間日数】
        '　②-0 必須チェック
        If Trim(Sisan(lngCnt, 4)) = "" Then
            '"未入力。"
            Call gfunc_ErrorMsg(3, 27024, "年間日数 ", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
    '８．【ファンド収益率】
       '①ファンド収益率必須チェック
        If Trim(Sisan(lngCnt, 5)) = "" Then
            Call gfunc_ErrorMsg(3, 27000, "ファンド収益率", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
    
        '②ファンド収益率数値チェック
        If gkyk_IsNumeric(Replace(Sisan(lngCnt, 5), ",", "")) = False Then
            Call gfunc_ErrorMsg(3, 27019, "ファンド収益率", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
        lngIntegerLen = 0
        lngDecimalLen = 0
        Call sub_CheckIntDec(Trim(Sisan(lngCnt, 5)), lngIntegerLen, lngDecimalLen)
        
        '③整数部が3桁を超える場合、エラーメッセージを表示する。
        If lngIntegerLen > 3 Then
            Call gfunc_ErrorMsg(3, 27008, "ファンド収益率", "整数部は3桁以内で入力して下さい。")
            Exit Function
        End If
        
        '④小数部が6桁を超える場合、エラーメッセージを表示する。
        If lngDecimalLen > 6 Then
            Call gfunc_ErrorMsg(3, 27008, "ファンド収益率", "小数点以下は6桁以内で入力して下さい。")
            Exit Function
        End If
        
    '９．【ベンチマーク収益率】
        '①ベンチマーク収益率必須チェック
        If Trim(Sisan(lngCnt, 6)) = "" Then
            Call gfunc_ErrorMsg(3, 27000, "ベンチマーク収益率", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
        '②ベンチマーク収益率数値チェック
        If gkyk_IsNumeric(Replace(Sisan(lngCnt, 6), ",", "")) = False Then
            Call gfunc_ErrorMsg(3, 27019, "ベンチマーク収益率", Meisai & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
        lngIntegerLen = 0
        lngDecimalLen = 0
        Call sub_CheckIntDec(Trim(Sisan(lngCnt, 6)), lngIntegerLen, lngDecimalLen)
        
        '③整数部が3桁を超える場合、エラーメッセージを表示する。
        If lngIntegerLen > 3 Then
            Call gfunc_ErrorMsg(3, 27008, "ベンチマーク収益率", "整数部は3桁以内で入力して下さい。")
            Exit Function
        End If
        
        '④小数部が6桁を超える場合、エラーメッセージを表示する。
        If lngDecimalLen > 6 Then
            Call gfunc_ErrorMsg(3, 27008, "ベンチマーク収益率", "小数点以下は6桁以内で入力して下さい。")
            Exit Function
        End If
    
    Next lngCnt
   
    For lngCnt = 2 To UBound(Sisan)
       
        '前行FromとカレントFromが一致しない場合
        '前行Toのプラス１日とカレントFromが一致しない(不連続か重複)NG
        If CStr(Sisan(lngCnt, 1)) < CStr(Replace(DateAdd("d", 1, CDate(gfunc_YmdFormat(Sisan(lngCnt - 1, 2)))), "/", "")) Then
            Call gfunc_ErrorMsg(3, 27010, "", "重複行：　" & Sisan(lngCnt - 1, 0) & "行目と" & Sisan(lngCnt, 0) & "行目")
            Exit Function
        ElseIf CStr(Sisan(lngCnt, 1)) > CStr(Replace(DateAdd("d", 1, CDate(gfunc_YmdFormat(Sisan(lngCnt - 1, 2)))), "/", "")) Then
            Call gfunc_ErrorMsg(3, 27011, "", "不連続行：　" & Sisan(lngCnt - 1, 0) & "行目と" & Sisan(lngCnt, 0) & "行目")
            Exit Function
        End If
        
    Next lngCnt

    
    End With


    func_CheckShisan1 = True
    
    Exit Function

ErrorSection:

    func_CheckShisan1 = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_CheckShisan1", Err.Number, Err.Description)
    On Error GoTo 0

    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    


End Function


'*************************************************************************
'関数名　　：明細行開始終了位置取得処理
'
'引　　数　：
'           TargetSheets    I       対象ワークシート
'           iKind           I       対象(1:ローリング期間
'           lngSart         O       明細開始Row
'           lngEnd          O       明細終了Row
'
'戻り値　　：なし
'
'機能説明　：明細行の開始、終了行番号を返す
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Public Sub sub_SearchStartEndRow(ByRef TargetSheets As Object, ByVal iKind As Long, _
                                  ByRef lngStart As Long, ByRef lngEnd As Long)
    Dim lngKindCnt As Long
    Dim lngCnt As Long

On Error GoTo ErrorHandler

    If iKind < 1 Or iKind > 1 Then
        Exit Sub
    End If
    
    lngCnt = 25
    lngKindCnt = 0
    
    With TargetSheets
        Do While (1)
            If .Cells(lngCnt, 2) = "対象" Then
                lngKindCnt = lngKindCnt + 1
                lngStart = lngCnt + 2
                lngCnt = lngCnt + 2
            End If
            
            If lngKindCnt = iKind And .Range(mLINKCELLNAME & lngCnt) = vbNullString Then
                lngEnd = lngCnt - 1
                Exit Sub
            End If
            
            lngCnt = lngCnt + 1
        Loop
    End With
    
    Exit Sub

ErrorHandler:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "sub_SearchStartEndRow", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Sub

'*************************************************************************
'関数名　　：ローリング期間の明細取得・ソート処理
'
'引　　数　：
'           TargetSheets    I       対象ワークシート
'           Sisan           O       配列
'
'戻り値　　：なし
'
'機能説明　：ローリング期間の明細を配列に格納する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_Sisan1_To_Dim(ByRef TargetSheets As Object _
                                  , ByRef Sisan() As Variant) As Boolean
On Error GoTo ErrorSection

    Dim lngStart        As Long
    Dim lngEnd          As Long
    Dim lngRow          As Long
    Dim lngCnt          As Long

    func_Sisan1_To_Dim = False
    
    'ローリング期間の開始行取得
    Call sub_SearchStartEndRow(TargetSheets, 1, lngStart, lngEnd)
    With TargetSheets

        ReDim Sisan(lngEnd - lngStart + 1, Sisan1_MaxCols)
        
        For lngRow = lngStart To lngEnd
            lngCnt = lngRow - lngStart + 1
            Sisan(lngCnt, 0) = lngCnt               '明細行番号
            Sisan(lngCnt, 1) = CStr(Cells(lngRow, 5))     'ローリング期間From
            Sisan(lngCnt, 2) = CStr(Cells(lngRow, 7))     'ローリング期間To
            Sisan(lngCnt, 3) = Mid(Cells(lngRow, 9), 1, gfunc_Separate("年率換算方式リスト"))    '年率換算方式
            Sisan(lngCnt, 4) = Mid(Cells(lngRow, 14), 1, gfunc_Separate("年間日数リスト"))  '年間日数
            Sisan(lngCnt, 5) = Cells(lngRow, 18)   'ファンド収益率
            Sisan(lngCnt, 6) = Cells(lngRow, 23)   'ベンチマーク収益率

        
        Next lngRow
    End With
    
    'ローリング期間From、ローリング期間Toでのソート
    Call sub_DoSort(Sisan, 1)
    
    func_Sisan1_To_Dim = True
    Exit Function

ErrorSection:

    func_Sisan1_To_Dim = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_Sisan1_To_Dim", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function



'*************************************************************************
'関数名　　：ローリング期間の明細ソート処理
'
'引　　数　：
'           Sisan           O       配列
'           strType         I       取得タイプ(1:ローリング期間
'
'戻り値　　：なし
'
'機能説明　：明細をソートし配列に格納する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub sub_DoSort(ByRef Sisan() As Variant, ByVal strType As String)

On Error GoTo ErrorSection


    Dim lngLoopCnt1     As Long
    Dim lngLoopCnt2     As Long
    Dim Work            As Variant
    Dim lngCols         As Long
    Dim lngColCnt       As Long
    Dim strSortKeyPre   As String
    Dim strSortKeyNow   As String
    
    strSortKeyPre = ""
    strSortKeyNow = ""
    
    
    'Work領域設定
    Select Case strType
    Case "1"
    'ローリング期間
        lngCols = Sisan1_MaxCols
    Case Else
    'その他
        Exit Sub
    End Select
    
    For lngLoopCnt1 = 1 To UBound(Sisan) - 1
    
        strSortKeyPre = ""
        
        For lngLoopCnt2 = 1 To UBound(Sisan)
        
            'ソート項目設定
            Select Case strType
            Case "1"
            'ローリング期間
                'ローリング期間From、ローリング期間Toの昇順
                strSortKeyNow = Sisan(lngLoopCnt2, 1) & Sisan(lngLoopCnt2, 2)
            End Select
            
            If Trim(strSortKeyPre) <> "" Then
            
                If strSortKeyPre > strSortKeyNow Then
                    
                    For lngColCnt = 0 To lngCols
                        Work = ""
                        Work = Sisan(lngLoopCnt2, lngColCnt)
                        Sisan(lngLoopCnt2, lngColCnt) = Sisan(lngLoopCnt2 - 1, lngColCnt)
                        Sisan(lngLoopCnt2 - 1, lngColCnt) = Work
                    Next lngColCnt
                    
                End If
            
            End If
            
            'ソートキー(前)の設定
            strSortKeyPre = strSortKeyNow
        
        Next lngLoopCnt2
    Next lngLoopCnt1
    
    
    Set Work = Nothing
    
    Exit Sub
    
ErrorSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "sub_DoSort(" & strType & ")", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Set Work = Nothing

End Sub

'*************************************************************************
'関数名　　：トランザクションチェック処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           lngStatus       O       エラーステータス(0:正常,1:ロック,2:削除済,-1:その他)
'           lngKikanIdFirst I       成功報酬期間ID
'
'戻り値　　：なし
'
'機能説明　：成功報酬期間テーブルの排他・ロックチェック
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_UpdateCheck(ByRef TargetSheets As Object _
                                , ByRef lngStatus As Long _
                                , ByVal lngKikanIdFirst As Long) As Boolean

On Error GoTo ErrorSection


    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean
    Dim strKouzaNo              As String
    

    func_UpdateCheck = False
    
    strErrCode = ""
    lngStatus = -1
    
    '口座番号
    strKouzaNo = TargetSheets.Range("口座番号").Value2

    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     SAKUJO_FLG"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_SH_SEIKOH_HOHSHU_KIKAN"
    strSql = strSql & " WHERE"
    strSql = strSql & "     KOHZA_NO='" & strKouzaNo & "' AND"
    strSql = strSql & "     SEIKOH_HOSYU_KIKAN_ID=" & lngKikanIdFirst
    strSql = strSql & " FOR UPDATE NOWAIT"

    'データ取得
    blnRet = gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If blnRet Then
        If Trim(strErrCode) <> "" Then
        
            If strErrCode = "ORA-00054" Then
            '他セッションからのロック状態
                lngStatus = 1
            Else
            'その他エラー
                lngStatus = -1
            End If
            
            Exit Function
        End If
    Else
        Exit Function
    End If

    '削除フラグの確認
    If lngRow > 0 Then
        If CStr(vData(0, 0)) <> "0" Then
            '削除フラグが"0"以外の場合、他ユーザーから更新済み
            lngStatus = 2
            Exit Function
        End If
    End If
    
    lngStatus = 0
    func_UpdateCheck = True

    Exit Function

ErrorSection:

    func_UpdateCheck = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_UpdateCheck", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------


End Function


'*************************************************************************
'関数名　　：成功報酬期間ID発番処理
'
'引　　数　：
'           lngId       I       採番した成功報酬期間ID
'
'戻り値　　：なし
'
'機能説明　：シーケンスから成功報酬期間IDの採番
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_GetSeikohId(ByRef lngId As Long) As Boolean

On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean

    func_GetSeikohId = False

    strSql = "SELECT KYK_ID_SQ.NEXTVAL FROM DUAL"

    'データ取得
    blnRet = gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If blnRet Then
        If Trim(strErrCode) <> "" Then
            Exit Function
        Else
            If IsNumeric(vData(0, 0)) Then
                lngId = CLng(vData(0, 0))
            Else
            '数値以外
                Exit Function
            End If
        End If
    Else
        Exit Function
    End If

    func_GetSeikohId = True

    Exit Function

ErrorSection:

    func_GetSeikohId = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetSeikohId", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function


'*************************************************************************
'関数名　　：成功報酬期間テーブル登録処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           lngId           I       成功報酬期間ID
'           lngKikanIdFirst I       検索時成功報酬期間ID
'           strError        O       エラーコード
'
'戻り値　　：なし
'
'機能説明　：成功報酬期間テーブルへ登録を行う
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_InsSEIKOH_KIKAN(ByVal TargetSheets As Object _
                                    , ByVal lngId As Long _
                                    , ByVal lngKikanIdFirst As Long _
                                    , ByRef Sisan() As Variant _
                                    , ByRef strError As String) As Boolean

On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean
    Dim lngSeikohKikanFrom      As Long      '成功報酬期間FROM
    Dim lngSeikohKikanTo        As Long      '成功報酬期間TO

    func_InsSEIKOH_KIKAN = False
    
    '成功報酬期間FROMの取得
    lngSeikohKikanFrom = CLng(Sisan(1, 1))
    '成功報酬期間TOの取得
    lngSeikohKikanTo = CLng(Sisan(TargetSheets.Range("ローリング年数"), 2))
     
    With TargetSheets
        strSql = ""
        strSql = strSql & " INSERT INTO KYK_SH_SEIKOH_HOHSHU_KIKAN ("
        strSql = strSql & " KOHZA_NO                        ," '口座番号
        strSql = strSql & " SEIKOH_HOSYU_KIKAN_ID           ," '成功報酬期間ID
        strSql = strSql & " MOKUHYO_TYOHKASYUEKI_RITSU_KBN  ," '目標超過収益率区分
        strSql = strSql & " MOKUHYO_TYOHKASYUEKI_RITSU      ," '目標超過収益率
        strSql = strSql & " SEIKOH_HOSYU_KIKAN_FROM         ," '成功報酬期間FROM
        strSql = strSql & " SEIKOH_HOSYU_KIKAN_TO           ," '成功報酬期間TO
        strSql = strSql & " TOHROKU_YMD                     ," '登録日
        strSql = strSql & " KOHSHIN_YMD                     ," '更新日
        strSql = strSql & " KOHSHIN_PGM_ID                  ," '更新プログラムID
        strSql = strSql & " KOHSHIN_TANTOHSHA               ," '更新担当者
        strSql = strSql & " SAKUJO_FLG                       " '削除フラグ
        strSql = strSql & " )"
        strSql = strSql & " VALUES"
        strSql = strSql & "   ("
        strSql = strSql & " '" & gfunc_KozaNoFormat(.Range("口座番号")) & "'"   '口座属性
        strSql = strSql & ",'" & lngId & "'"                                    '成功報酬期間ID
        '目標超過収益率のチェックを判定
        If .chkMokuhyo.Value = False Then                                       '目標超過収益率区分
            '目標超過収益率の設定なし
            strSql = strSql & ",'0'"
        Else
            '目標超過収益率の設定あり
            strSql = strSql & ",'1'"
        End If
        strSql = strSql & ",'" & Sisan(0, 0) & "'"                              '目標超過収益率
        strSql = strSql & ",'" & lngSeikohKikanFrom & "'"                       '成功報酬期間FROM
        strSql = strSql & ",'" & lngSeikohKikanTo & "'"                         '成功報酬期間TO
        strSql = strSql & ",SYSDATE"                                            '登録日
        strSql = strSql & ",SYSDATE"                                            '更新日
        strSql = strSql & ",'" & UPDATE_PGM_ID & "'"                            '更新プログラムID
        strSql = strSql & ",'" & UPDATE_USER & "'"                              '更新担当者
        strSql = strSql & ",     '0'"                                           '削除フラグ
        strSql = strSql & " )"
    End With
    
    '実行
    blnRet = gclsdb.DoExequteSql(gcINSERT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If blnRet = False Then
        If Trim(strErrCode) <> "" Then
            strError = strErrCode
        End If
        
        Exit Function
    End If
    
    Erase vData
    func_InsSEIKOH_KIKAN = True

    Exit Function

ErrorSection:

    func_InsSEIKOH_KIKAN = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_InsSEIKOH_KIKAN", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Erase vData


End Function



'*************************************************************************
'関数名　　：年ごと超過収益率テーブル登録処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           lngId           I       成功報酬期間ID
'           lngKikanIdFirst I       検索時年ごと超過収益率ID
'           Sisan()         I       ローリング期間の構造体配列
'           dblHeikinTyohka O       平均超過収益率
'           strError        O       エラーコード
'
'戻り値　　：なし
'
'機能説明　：年ごと超過収益率テーブルへ登録を行う
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_InsNENGOTO_SYUEKI(ByVal TargetSheets As Object _
                                    , ByVal lngId As Long _
                                    , ByVal lngKikanIdFirst As Long _
                                    , ByRef Sisan() As Variant _
                                    , ByRef dblHeikinTyohka As Double _
                                    , ByRef strError As String) As Boolean

On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean
    Dim dblSumNTR               As Double
    
    '計算値
    Dim lngKeisanKikanSuD       As Long
    Dim lngKeisanKikanSuM       As Long
    Dim dblNenkansango1         As Double
    Dim dblNenkansango2         As Double
    Dim dblNenkansango3         As Double
    Dim dblNenkansango4         As Double
    Dim lngRollomgMAX           As Long
    Dim dblTmp1                 As Double
    
    func_InsNENGOTO_SYUEKI = False
    
    With TargetSheets

    'ローリング年番号のMAX値を初期化する。
    lngRollomgMAX = 0
    '超過収益率の総和を初期化する。
    dblSumNTR = 0
    
        For lngCnt = 1 To UBound(Sisan)
            strSql = ""
            strSql = strSql & " INSERT INTO kyk_sh_nengoto_syueki_ritsu ("
            strSql = strSql & " KOHZA_NO                          ," '口座番号
            strSql = strSql & " SEIKOH_HOSYU_KIKAN_ID             ," '成功報酬期間ID
            strSql = strSql & " ROLLING_NEN_NO                    ," 'ローリング年番号
            strSql = strSql & " ROLLING_KIKAN_FROM                ," 'ローリング期間FROM
            strSql = strSql & " rolling_kikan_to                  ," 'ローリング期間TO
            strSql = strSql & " nenritsu_kansan_hohho             ," '年率換算方法
            strSql = strSql & " nenkan_nissu_kbn                  ," '年間日数区分
            strSql = strSql & " fund_syueki_ritsu                 ," 'ファンド収益率
            strSql = strSql & " benchmark_syueki_ritsu            ," 'ベンチマーク収益率
            strSql = strSql & " tyohkasyueki_ritsu                ," '超過収益率
            strSql = strSql & " NENKANSANGO_TYOHKASYUEKI_RITSU    ," '年率換算後超過収益率
            strSql = strSql & " KEISAN_KIKAN_SU                   ," '計算期間数
            strSql = strSql & " TOHROKU_YMD                       ," '登録日
            strSql = strSql & " KOHSHIN_YMD                       ," '更新日
            strSql = strSql & " KOHSHIN_PGM_ID                    ," '更新プログラムID
            strSql = strSql & " KOHSHIN_TANTOHSHA                 ," '更新担当者
            strSql = strSql & " SAKUJO_FLG                         " '削除フラグ
            strSql = strSql & " )"
            strSql = strSql & " VALUES"
            strSql = strSql & "   ("
            strSql = strSql & " '" & gfunc_KozaNoFormat(.Range("口座番号")) & "'"   '口座番号
            strSql = strSql & ",'" & lngId & "'"                                    '成功報酬期間ID
            strSql = strSql & ",'" & Sisan(lngCnt, 0) & "'"                         'ローリング年番号
            strSql = strSql & ",'" & Sisan(lngCnt, 1) & "'"                         'ローリング期間FROM
            strSql = strSql & ",'" & Sisan(lngCnt, 2) & "'"                         'ローリング期間TO
            strSql = strSql & ",'" & Sisan(lngCnt, 3) & "'"                         '年率換算方法
            strSql = strSql & ",'" & Sisan(lngCnt, 4) & "'"                         '年間日数区分
            strSql = strSql & ",'" & Sisan(lngCnt, 5) & "'"                         'ファンド収益率
            strSql = strSql & ",'" & Sisan(lngCnt, 6) & "'"                         'ベンチマーク収益率
            strSql = strSql & ",'" & Sisan(lngCnt, 7) & "'"                         '超過収益率
            strSql = strSql & ",'" & Sisan(lngCnt, 8) & "'"                         '年率換算後超過収益率
            strSql = strSql & ",'" & Sisan(lngCnt, 9) & "'"                         '計算期間数
            strSql = strSql & ",SYSDATE"                                            '登録日
            strSql = strSql & ",SYSDATE"                                            '更新日
            strSql = strSql & ",'" & UPDATE_PGM_ID & "'"                            '更新プログラムID
            strSql = strSql & ",'" & UPDATE_USER & "'"                              '更新担当者
            strSql = strSql & ",     '0'"                                           '削除フラグ
            strSql = strSql & " )"
            
            '実行
            blnRet = gclsdb.DoExequteSql(gcINSERT, strSql, vData, lngCol, lngRow, strErrCode)
            
        Next lngCnt
    
    End With
    

    If blnRet = False Then
        If Trim(strErrCode) <> "" Then
            strError = strErrCode
        End If
        
        Exit Function
    End If
    
    Erase vData
    func_InsNENGOTO_SYUEKI = True

    Exit Function

ErrorSection:

    func_InsNENGOTO_SYUEKI = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_InsNENGOTO_SYUEKI", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Erase vData


End Function



'*************************************************************************
'関数名　　：ローリング後超過収益率テーブル登録処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           lngId           I       成功報酬期間ID
'           lngKikanIdFirst I       検索時ローリング後超過収益率ID
'           dblHeikinTyohka I       平均超過収益率
'           strError        O       エラーコード
'
'戻り値　　：なし
'
'機能説明　：ローリング後超過収益率テーブルへ登録を行う
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_InsROLLING_KIKAN(ByVal TargetSheets As Object _
                                    , ByVal lngId As Long _
                                    , ByVal lngKikanIdFirst As Long _
                                    , ByRef Sisan() As Variant _
                                    , ByVal dblHeikinTyohka As Double _
                                    , ByRef strError As String) As Boolean

On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean

    func_InsROLLING_KIKAN = False
    
    With TargetSheets
        strSql = ""
        strSql = strSql & " INSERT INTO kyk_sh_rolling_go_syueki_ritsu ("
        strSql = strSql & " KOHZA_NO                          ," '口座番号
        strSql = strSql & " SEIKOH_HOSYU_KIKAN_ID             ," '成功報酬期間ID
        strSql = strSql & " HEIKIN_TYOHKASYUEKI_RITSU         ," '平均超過収益率
        strSql = strSql & " KAKEME_KISO_SUCHI                 ," '掛け目基礎数値
        strSql = strSql & " TOHROKU_YMD                       ," '登録日
        strSql = strSql & " KOHSHIN_YMD                       ," '更新日
        strSql = strSql & " KOHSHIN_PGM_ID                    ," '更新プログラムID
        strSql = strSql & " KOHSHIN_TANTOHSHA                 ," '更新担当者
        strSql = strSql & " SAKUJO_FLG                         " '削除フラグ
        strSql = strSql & " )"
        strSql = strSql & " VALUES"
        strSql = strSql & "   ("
        strSql = strSql & " '" & gfunc_KozaNoFormat(.Range("口座番号")) & "'"   '口座番号
        strSql = strSql & ",'" & lngId & "'"                                    '成功報酬期間ID
        strSql = strSql & ",'" & Sisan(0, 1) & "'"                              '平均超過収益率
        strSql = strSql & ",'" & Sisan(0, 2) & "'"                              '掛け目基礎数値
        strSql = strSql & ",SYSDATE"                                            '登録日
        strSql = strSql & ",SYSDATE"                                            '更新日
        strSql = strSql & ",'" & UPDATE_PGM_ID & "'"                            '更新プログラムID
        strSql = strSql & ",'" & UPDATE_USER & "'"                              '更新担当者
        strSql = strSql & ",     '0'"                                           '削除フラグ
        strSql = strSql & " )"
    End With
    
    '実行
    blnRet = gclsdb.DoExequteSql(gcINSERT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If blnRet = False Then
        If Trim(strErrCode) <> "" Then
            strError = strErrCode
        End If
        
        Exit Function
    End If
        
    Erase vData
    func_InsROLLING_KIKAN = True

    Exit Function

ErrorSection:

    func_InsROLLING_KIKAN = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_InsROLLING_KIKAN", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Erase vData


End Function

'*************************************************************************
'関数名　　：３テーブル削除処理
'
'引　　数　：
'           strKozaNo       I       検索対象の口座番号
'           Sisan()         I       ローリング期間の構造体配列
'           strError        O       エラーコード
'
'戻り値　　：なし
'
'機能説明　：成功報酬期間テーブル、年ごと超過収益率テーブル、
'            ローリング後超過収益率テーブルの論理削除
'            削除条件は、同一口座で同一の成功報酬期間が存在した場合、
'            論理削除を行う
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_Del3Tables(ByVal strKozaNo As String _
                               , ByRef Sisan() As Variant _
                               , ByRef strError As String) As Boolean

On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean
    Dim vTable                  As Variant
    Dim strTable                As String
    Dim lngSeikohKikanFrom      As Long      '成功報酬期間FROM
    Dim lngSeikohKikanTo        As Long      '成功報酬期間TO
    Dim lngSeikohKikanID        As Long      '削除対象の期間ID

    func_Del3Tables = False
    
    '成功報酬期間FROMの取得
    lngSeikohKikanFrom = CLng(Sisan(1, 1))
    '成功報酬期間TOの取得
    lngSeikohKikanTo = CLng(Sisan(ThisWorkbook.Worksheets(MAIN_SHEETNAME).Range("ローリング年数"), 2))
        
    '年ごと超過収益率,ローリング後超過収益率
    vTable = Array("KYK_SH_SEIKOH_HOHSHU_KIKAN", "KYK_SH_NENGOTO_SYUEKI_RITSU", "KYK_SH_ROLLING_GO_SYUEKI_RITSU")
    
    '成功報酬期間テーブルの論理削除対象レコード検索
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     SEIKOH_HOSYU_KIKAN_ID"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_SH_SEIKOH_HOHSHU_KIKAN"
    strSql = strSql & " WHERE"
    strSql = strSql & "     KOHZA_NO = '" & func_ChkQuoteshon(strKozaNo, "'") & "'  AND"
    strSql = strSql & "     SAKUJO_FLG = '0'      AND"
    strSql = strSql & "     (" & lngSeikohKikanFrom & " = SEIKOH_HOSYU_KIKAN_FROM"
    strSql = strSql & "      AND  " & lngSeikohKikanTo & " = SEIKOH_HOSYU_KIKAN_TO)"
    strSql = strSql & ""

    '実行
    blnRet = gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If blnRet = False Then
    
        If Trim(strErrCode) <> "" Then
            strError = strErrCode
        End If
        Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_Del3Tables" & _
                           " (" & strTable & ")(" & strError & ")" & strSql, _
                           Err.Number, Err.Description)
        Exit Function
    End If
    
    If lngRow > 0 Then
    
        lngSeikohKikanID = vData(0, 0)
        
        For lngCnt = 0 To UBound(vTable)
            '削除対象テーブル
            strTable = vTable(lngCnt)
        
            strSql = ""
            strSql = strSql & " UPDATE "
            strSql = strSql & strTable
            strSql = strSql & " SET"
            strSql = strSql & "     KOHSHIN_YMD=SYSDATE"
            strSql = strSql & "    ,KOHSHIN_PGM_ID='" & UPDATE_PGM_ID & "'"
            strSql = strSql & "    ,KOHSHIN_TANTOHSHA='" & UPDATE_USER & "'"
            strSql = strSql & "    ,SAKUJO_FLG='1'"
            strSql = strSql & " WHERE"
            strSql = strSql & "     SEIKOH_HOSYU_KIKAN_ID=" & lngSeikohKikanID
        
            '実行
            blnRet = gclsdb.DoExequteSql(gcUPDATE, strSql, vData, lngCol, lngRow, strErrCode)
            
            
            If blnRet = False Then
            
                If Trim(strErrCode) <> "" Then
                    strError = strErrCode
                End If
                Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_Del3Tables" & _
                                   " (" & strTable & ")(" & strError & ")" & strSql, _
                                   Err.Number, Err.Description)
                Exit Function
            End If
        Next lngCnt
    End If


    func_Del3Tables = True
    
    Erase vData
    Set vTable = Nothing
    
    Exit Function

ErrorSection:

    func_Del3Tables = False
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_Del3Tables (" & strTable & ")", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
    Erase vData
    Set vTable = Nothing

End Function


'*************************************************************************
'関数名　　：検索時成功報酬期間ID取得
'
'引　　数　：なし
'
'戻り値　　：検索時成功報酬期間ID
'
'機能説明　：検索時成功報酬期間IDを取得する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_GetKikanIdFirst() As Long

    Dim lngKikanIdFirst     As Long

    If IsNull(ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時成功報酬期間ID")) = False Then
        If IsNumeric(ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時成功報酬期間ID")) Then
            lngKikanIdFirst = CLng(ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時成功報酬期間ID"))
        Else
            lngKikanIdFirst = -1
        End If
    Else
        lngKikanIdFirst = -1
    End If

    func_GetKikanIdFirst = lngKikanIdFirst

End Function


'*************************************************************************
'関数名　　：テーブルカラム名取得
'
'引　　数　：
'           strTableName        I       'テーブル名
'           strInsColsList      O       '登録カラム一覧のカンマ区切り文字列
'           strTableCol()       O       '登録カラムの配列
'
'戻り値　　：なし
'
'機能説明　：指定されたテーブルのカンマ区切りカラムとカラムの配列を返す
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub sub_GetUserTabCols(ByVal strTableName As String, _
                               ByRef strInsColsList As String, _
                               ByRef strTableCol() As String)

On Error GoTo ErrorSection

    Dim vData()                 As Variant
    Dim strSql                  As String
    Dim lngCol                  As Long
    Dim lngRow                  As Long
    Dim lngCnt                  As Long
    Dim strErrCode              As String
    Dim blnRet                  As Boolean
    Dim strBuff                 As String
    
    strInsColsList = ""
    
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     COLUMN_NAME"
    strSql = strSql & " FROM"
    strSql = strSql & "     USER_TAB_COLS"
    strSql = strSql & " WHERE"
    strSql = strSql & "     TABLE_NAME='" & UCase(strTableName) & "'"
    strSql = strSql & " ORDER BY"
    strSql = strSql & "     COLUMN_ID"

    'データ取得
    blnRet = gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If blnRet Then
        If Trim(strErrCode) <> "" Then
            'Error
            Exit Sub
        End If
    Else
        Exit Sub
    End If

    '存在の確認
    If lngRow > 0 Then
        ReDim strTableCol(lngRow - 1)
        
        '口座番号を格納
        strBuff = CStr(vData(0, lngCnt))
        strTableCol(0) = CStr(vData(0, lngCnt))
        
        For lngCnt = 1 To lngRow - 1
            strBuff = strBuff & "," & CStr(vData(0, lngCnt))
            strTableCol(lngCnt) = CStr(vData(0, lngCnt))
        Next lngCnt
        
        strInsColsList = strBuff
    End If
    
    Erase vData
    
    Exit Sub

ErrorSection:

    strInsColsList = ""

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "sub_GetUserTabCols", Err.Number, Err.Description)
    On Error GoTo 0

    Erase vData
'---- Error Log End   ----------------------------------------------------

End Sub


'*************************************************************************
'関数名　　：整数部、小数部桁取得
'
'引　　数　：
'           strWkTo             I       数値文字列
'           lngIntegerLen       O       整数部桁数
'           lngDecimalLen       O       小数部桁数
'
'戻り値　　：なし
'
'機能説明　：整数部、小数部桁数を取得する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub sub_CheckIntDec(ByVal strWork As String, ByRef lngIntegerLen As Long, ByRef lngDecimalLen As Long)
    
    strWork = Replace(strWork, "-", "")
    
    If InStr(1, strWork, ".") = 0 Then
        lngDecimalLen = 0
        lngIntegerLen = Len(strWork)
    Else
        lngDecimalLen = (Len(strWork) - InStr(1, strWork, "."))
        lngIntegerLen = (Len(strWork) - (lngDecimalLen + 1))
    End If
    

End Sub
