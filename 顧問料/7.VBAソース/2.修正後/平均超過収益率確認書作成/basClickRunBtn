Attribute VB_Name = "basClickRunBtn"
Option Explicit

'*************************************************************************
'
'プロジェクト名：国内顧問料計算システム・平均超過収益率確認書作成
'
'オブジェクト名：basClickRunBtn
'
'機能概要　：確認書作成処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************

Const MODULE_NAME = "basClickRunBtn"

Private Const cSTART_RAW = 8
Private Const cBaseFile = "平均超過収益率確認書テンプレート.xls"
Private Const cPreFName = "平均超過収益率確認書"


'*************************************************************************
'関数名　　：平均超過収益率確認書(帳票作成)
'引　　数　：
'           objTarget        I           対象のシート
'戻り値　　：
'機能説明　：
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Public Function fncClickRunBtn(ByRef objTarget As Object) As Boolean
    
    Const PROCEDURE_NAME As String = "fncClickRunBtn"

On Error GoTo ErrSection

    Dim strKozaNo       As String
    Dim strKikan        As String
    Dim strSeikoFrom    As String
    Dim strSeikoTo      As String
    
    Dim strOutFName     As String
       
    Dim vDatKeiyaku()   As Variant      '顧問料請求期間情報データ
    Dim lngKeiyakuCnt   As Long         '顧問料請求期間情報データ件数
    Dim vDatKozaNo()    As Variant      '口座属性情報データ
    Dim lngKozaNoCnt    As Long         '口座属性情報データ件数
    Dim vDatSeiko()     As Variant      '成功報酬期間情報データ
    Dim lngSeikoCnt     As Long         '成功報酬期間情報データ件数
    Dim vDatRoring()    As Variant      '年ごと超過収益率情報データ
    Dim lngRoringCnt    As Long         '年ごと超過収益率情報データ件数
    Dim vDatRoringGo()  As Variant      'ローリング後超過収益率情報データ
    Dim lngRoringGoCnt  As Long         'ローリング後超過収益率情報データ件数

        
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    fncClickRunBtn = False
        
    With objTarget
    
        '保存フォルダの入力チェックを行う
        '入力必須チェック
        If Trim(.Range("保存フォルダ").Value) = "" Then
            Call gfunc_ErrorMsg(3, 27012, "保存フォルダ", "")
            Exit Function
        End If
        '入力した保存フォルダの存在チェック
        If Dir(CStr(.Range("保存フォルダ").Value), vbDirectory) = "" Then
            Call gfunc_ErrorMsg(3, 27013, "保存フォルダ", "")
            Exit Function
        End If
        
        strKozaNo = Trim(.Range("口座番号").Value)
        strKikan = Trim(.Range("基準日").Value)
        strSeikoFrom = Trim(.Range("成功報酬From").Value)
        strSeikoTo = Trim(.Range("成功報酬To").Value)
               
        ' データ取得
        If fncGetDBData(strKozaNo, strKikan, strSeikoFrom, strSeikoTo, _
                            vDatKeiyaku, lngKeiyakuCnt, _
                            vDatKozaNo, lngKozaNoCnt, _
                            vDatSeiko, lngSeikoCnt, _
                            vDatRoring, lngRoringCnt, _
                            vDatRoringGo, lngRoringGoCnt) = False Then
            GoTo RtnErrSection
        End If
     
        ' 帳票ファイル取得
        If fncGetFile(strKozaNo, strKikan, strOutFName) = False Then
            GoTo RtnErrSection
        End If
       
        ' フォルダをつける。
        strOutFName = .Range("保存フォルダ").Value & "\" & strOutFName
            
        ' ファイルコピー
        If fncCopyBook(cBaseFile, strOutFName) = False Then
            GoTo RtnErrSection
         End If
            
        ' ファイル作成
        If fncMakeBook(strOutFName, strKozaNo, strKikan, _
                           vDatKeiyaku, lngKeiyakuCnt, _
                           vDatKozaNo, lngKozaNoCnt, _
                           vDatSeiko, lngSeikoCnt, _
                           vDatRoring, lngRoringCnt, _
                           vDatRoringGo, lngRoringGoCnt) = False Then
            ' ファイル削除
            fncDeleteFile (strOutFName)
            GoTo RtnErrSection
        End If
    
        ' 印刷選択の場合
        If .chkPrint.Value = True Then
            ' ファイル印刷
            If fncPrintBook(cPreFName, .Range("保存フォルダ").Value) = False Then
                GoTo RtnErrSection
            End If
        End If
    
        If .chkPrint.Value = False Then
            ' 帳票を作成しました。
            Call gfunc_ErrorMsg(1, 27025, "平均超過収益率確認書", "")
        Else
            ' 帳票を作成・印刷しました。
            Call gfunc_ErrorMsg(1, 27026, "平均超過収益率確認書", "")
        End If
    
    End With
   
    fncClickRunBtn = True
        
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
'*******************************************************
'* リターンエラーセクション　　　　　　　　　　　　　　　  *
'*******************************************************
RtnErrSection:

    fncClickRunBtn = False
    
End Function


'*************************************************************************
'関数名　　：平均超過収益率確認書(帳票ファイル取得)
'引　　数　：
'           strKozaNo            I           口座番号
'           strKikan              I          基準日
'           strOutFName        O           出力ファイル名
'戻り値　　：処理結果
'機能説明　：平均超過収益率確認書を出力するファイルを取得する。
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function fncGetFile(ByVal strKozaNo As String, ByVal strKikan As String, _
                               ByRef strOutFName As String) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetFile"

On Error GoTo ErrSection

    Dim blnRet          As Boolean
    Dim strDate         As String
    Dim strTime         As String

    strOutFName = ""
    
    fncGetFile = False
    
    Call gfunc_GetSvDateTime(strDate, strTime)
       
    ' 出力帳票ファイル
    strOutFName = cPreFName
    strOutFName = strOutFName & "_" & strDate
    strOutFName = strOutFName & "_" & strTime
    strOutFName = strOutFName & "-(" & strKozaNo
    strOutFName = strOutFName & "-" & strKikan
    strOutFName = strOutFName & ")"
    strOutFName = strOutFName & ".xls"
    
    fncGetFile = True
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetFile = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    
End Function

'*************************************************************************
'関数名　　：平均超過収益率確認書(帳票データ取得)
'引　　数　：
'           strKozaNo          I           口座番号
'           strKikan           I           基準日
'           strSeikoFrom       I           成功報酬期間From
'           strSeikoTo         I           成功報酬期間To
'           vDatKeiyaku()      O           顧問料請求期間情報データ
'           lngKeiyakuCnt      O           顧問料請求期間情報データ件数
'           vDatKozaNo()       O           口座属性情報データ
'           lngKozaNoCnt       O           口座属性情報データ件数
'           vDatSeiko()        O           成功報酬期間情報データ
'           lngSeikoCnt        O           成功報酬期間情報データ件数
'           vDatRoring()       O           年ごと超過収益率情報データ
'           lngRoringCnt       O           年ごと超過収益率情報データ件数
'           vDatRoringGo()     O           ローリング後超過収益率情報データ
'           lngRoringGoCnt     O           ローリング後超過収益率情報データ件数
'戻り値　　：処理結果
'機能説明　：平均超過収益率確認書のデータを取得する。
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function fncGetDBData(ByVal strKozaNo As String, _
                                 ByVal strKikan As String, _
                                 ByVal strSeikoFrom As String, _
                                 ByVal strSeikoTo As String, _
                                 ByRef vDatKeiyaku() As Variant, _
                                 ByRef lngKeiyakuCnt As Long, _
                                 ByRef vDatKozaNo() As Variant, _
                                 ByRef lngKozaNoCnt As Long, _
                                 ByRef vDatSeiko() As Variant, _
                                 ByRef lngSeikoCnt As Long, _
                                 ByRef vDatRoring() As Variant, _
                                 ByRef lngRoringCnt As Long, _
                                 ByRef vDatRoringGo() As Variant, _
                                 ByRef lngRoringGoCnt As Long _
                                 ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncGetDBData"

On Error GoTo ErrSection

    Dim strSql      As String
    Dim vData()     As Variant
    Dim lngCol      As Long
    Dim lngRow      As Long
    Dim lngRowCnt   As Long
    Dim lngColCnt   As Long
    Dim blnRet      As Boolean
    Dim strSeikohId     As String       '成功報酬期間ID
    
    
    fncGetDBData = False
    
'*******************************************************
'* 各テーブルのデータ取得                              *
'*******************************************************

'２．【顧問料請求期間情報の取得】
    blnRet = func_GetKeiyakuInfo(ThisWorkbook.Worksheets(MAIN_SHEETNAME), strKozaNo, Replace(strKikan, "/", "") _
                                   , vDatKeiyaku, lngKeiyakuCnt)
                                   
    If blnRet = False Then
        Exit Function
    End If
    '請求期間IDの取得
    If lngKeiyakuCnt = 0 Then
        blnLockFlg = True
        Call gfunc_ErrorMsg(3, 27005, "基準日", "")
        Exit Function
    End If
    
' ３．【口座属性情報の取得】
    blnRet = func_KozaNoExist(ThisWorkbook.Worksheets(MAIN_SHEETNAME), strKozaNo, vDatKozaNo, lngKozaNoCnt)
    If blnRet = False Then
        Exit Function
    End If
    
    If lngKozaNoCnt = 0 Then
        blnLockFlg = True
        Call gfunc_ErrorMsg(3, 27006, "口座番号", "")
        Exit Function
    End If
    
'４．【成功報酬期間情報の取得】
    blnRet = func_GetSeikoInfo(ThisWorkbook.Worksheets(MAIN_SHEETNAME) _
                                , strKozaNo, Replace(strSeikoFrom, "/", "") _
                                , Replace(strSeikoTo, "/", "") _
                                , vDatSeiko, lngSeikoCnt)
    If blnRet = False Then
        Exit Function
    End If
    '成功報酬期間IDの取得
    If lngSeikoCnt > 0 Then
        strSeikohId = Trim(vDatSeiko(0, 0))
        ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時成功報酬期間ID") = Trim(vDatSeiko(0, 0))
    Else
        blnLockFlg = True
        Call gfunc_ErrorMsg(3, 27014, "成功報酬期間", "")
        Exit Function
    End If
    
'５．【年ごと超過収益率情報の取得】
    blnRet = func_GetRoringInfo(ThisWorkbook.Worksheets(MAIN_SHEETNAME), strKozaNo, strSeikohId _
                                   , vDatRoring, lngRoringCnt)
    If blnRet = False Then
        Exit Function
    End If
    
'６．【ローリング後超過収益率情報の取得】
    blnRet = func_GetRoringGoInfo(ThisWorkbook.Worksheets(MAIN_SHEETNAME), strKozaNo, strSeikohId _
                                   , vDatRoringGo, lngRoringGoCnt)
                                   
    If blnRet = False Then
        Exit Function
    End If

    fncGetDBData = True

    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    fncGetDBData = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------

    Erase vData
   
End Function


'*************************************************************************
'関数名　　：平均超過収益率確認書(帳票作成)
'引　　数　：
'           strOutFName        I           出力ファイル名
'           strKozaNo          I           口座番号
'           strKikan           I           基準日
'           vDatKeiyaku()      O           顧問料請求期間情報データ
'           lngKeiyakuCnt      O           顧問料請求期間情報データ件数
'           vDatKozaNo()       O           口座属性情報データ
'           lngKozaNoCnt       O           口座属性情報データ件数
'           vDatSeiko()        O           成功報酬期間情報データ
'           lngSeikoCnt        O           成功報酬期間情報データ件数
'           vDatRoring()       O           年ごと超過収益率情報データ
'           lngRoringCnt       O           年ごと超過収益率情報データ件数
'           vDatRoringGo()     O           ローリング後超過収益率情報データ
'           lngRoringGoCnt     O           ローリング後超過収益率情報データ件数
'戻り値　　：処理結果
'機能説明　：平均超過収益率確認書の作成を取得する。
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Public Function fncMakeBook(ByVal strOutFName As String, _
                                ByVal strKozaNo As String, _
                                ByVal strKikan As String, _
                                ByRef vDatKeiyaku() As Variant, _
                                ByRef lngKeiyakuCnt As Long, _
                                ByRef vDatKozaNo() As Variant, _
                                ByRef lngKozaNoCnt As Long, _
                                ByRef vDatSeiko() As Variant, _
                                ByRef lngSeikoCnt As Long, _
                                ByRef vDatRoring() As Variant, _
                                ByRef lngRoringCnt As Long, _
                                ByRef vDatRoringGo() As Variant, _
                                ByRef lngRoringGoCnt As Long _
                                ) As Boolean
    
    Const PROCEDURE_NAME As String = "fncMakeBook"

On Error GoTo ErrSection

    Dim blnRet         As Boolean

    Dim strTitleName   As String
    Dim lngRowCnt      As Long
    Dim lngColCnt      As Long
    Dim strSrcRange    As String
    Dim strDesRange    As String
    Dim lngRow         As Long
    Dim lngCnt         As Long
    Dim lngStartRow    As Long
    Dim strMAXNenNo    As String
    Dim strNenritsHohho   As String
    Dim srtNenkanNissu_kbn As String
    Dim strNenkanNissu    As String

    
    Call subBeginProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    ' ファイルOpen
    Workbooks.Open Filename:=strOutFName
    
    fncMakeBook = False
    
    strSrcRange = "A" & cSTART_RAW & ":Z" & cSTART_RAW
    strDesRange = "A" & (cSTART_RAW + 1) & ":Z" & (cSTART_RAW + lngRow - 1)
 
    strTitleName = gfunc_YmdFormat(strKozaNo) & " ～ " & gfunc_YmdFormat(strKikan)
    
 
    
    With Workbooks(Workbooks.Count).Worksheets(1)
        
        .Range("作成日") = ThisWorkbook.Worksheets(MAIN_SHEETNAME).Range("データ取得日時")
        
        ' 顧問料請求期間情報の帳票設定
        .Range("顧問料請求期間FROM") = CDate(gfunc_YmdFormat(CStr(vDatKeiyaku(0, 0))))
        .Range("顧問料請求期間TO") = CDate(gfunc_YmdFormat(CStr(vDatKeiyaku(1, 0))))

        
        '　口座属性情報の帳票設定
        .Range("口座番号1") = CStr(vDatKozaNo(0, 0))
        .Range("口座番号2") = CStr(vDatKozaNo(0, 0))
        .Range("口座名") = CStr(vDatKozaNo(1, 0))
        
        '　成功報酬期間情報の帳票設定
        .Range("目標超過収益率") = CStr(vDatSeiko(1, 0))
        
        '　年ごと超過収益率情報の帳票設定
        'カレント明細行番号取得
        lngStartRow = fncKsCmnSearchNewRow2(Workbooks(Workbooks.Count).Worksheets(1), 1) - 1
        
        For lngCnt = 1 To lngRoringCnt
            'ローリング期間のデータ表示
                '明細行の追加
               
                '行追加
                Call gKsC_fncKsCmnInsRow2(Workbooks(Workbooks.Count).Worksheets(1), lngStartRow, 10)
                Call gKsC_fncKsCmnInsRow2(Workbooks(Workbooks.Count).Worksheets(1), lngStartRow, 9)
                Call gKsC_fncKsCmnInsRow2(Workbooks(Workbooks.Count).Worksheets(1), lngStartRow, 8)
                Call gKsC_fncKsCmnInsRow2(Workbooks(Workbooks.Count).Worksheets(1), lngStartRow, 7)
                Call gKsC_fncKsCmnInsRow2(Workbooks(Workbooks.Count).Worksheets(1), lngStartRow, 6)
                Call gKsC_fncKsCmnInsRow2(Workbooks(Workbooks.Count).Worksheets(1), lngStartRow, 5)
 

                'データの設定
                .Cells(lngStartRow, 4).Value = vDatRoring(0, lngCnt - 1)     'ローリング期間タイトル①,ローリング年番号
                strMAXNenNo = vDatRoring(0, lngCnt - 1)                      'ローリング年番号最大値
                .Cells(lngStartRow, 6).Value = CDate(gfunc_YmdFormat(vDatRoring(1, lngCnt - 1)))     'ローリング期間タイトル②,ローリング期間FROM(年度)
                .Cells(lngStartRow, 14).Value = CDate(gfunc_YmdFormat(vDatRoring(1, lngCnt - 1)))    'ローリング期間タイトル③,ローリング期間FROM
                .Cells(lngStartRow, 22).Value = CDate(gfunc_YmdFormat(vDatRoring(2, lngCnt - 1)))    'ローリング期間タイトル④,ローリング期間TO
                .Range("ローリング期間TO") = CDate(gfunc_YmdFormat(vDatRoring(2, lngCnt - 1)))    ' ローリング期間TO
                .Cells(lngStartRow + 1, 34).Value = vDatRoring(5, lngCnt - 1) 'ファンド収益率
                .Cells(lngStartRow + 2, 34).Value = vDatRoring(6, lngCnt - 1) 'ベンチマーク収益率
                .Cells(lngStartRow + 3, 34).Value = vDatRoring(7, lngCnt - 1) '超過収益率
                '超過収益率計算方法
                strNenritsHohho = vDatRoring(3, lngCnt - 1)
                srtNenkanNissu_kbn = vDatRoring(4, lngCnt - 1)
                strNenkanNissu = vDatRoring(9, lngCnt - 1)
                Select Case strNenritsHohho
                Case "0" '年率換算方式が、なしの場合、空白
                    .Cells(lngStartRow + 4, 15).Value = ""
                Case "1" '年率換算方式が、月単利換算の場合
                    .Cells(lngStartRow + 4, 15).Value = "(③ × (12ヵ月 ÷ " + strNenkanNissu + "ヵ月))"
                Case "2" '年率換算方式が、月複利換算の場合
                    .Cells(lngStartRow + 4, 15).Value = "(((1 ＋ ③) ＾ (12ヵ月 ÷ " + strNenkanNissu + "ヵ月)) － 1)"
                Case "3" '年率換算方式が、日単利換算の場合
                    Select Case srtNenkanNissu_kbn
                    Case "1" '365日
                        .Cells(lngStartRow + 4, 15).Value = "(③ × (365日 ÷ " + strNenkanNissu + "日))"
                    Case "2" '366日
                        .Cells(lngStartRow + 4, 15).Value = "(③ × (366日 ÷ " + strNenkanNissu + "日))"
                    End Select
                Case "4" '年率換算方式が、日複利換算の場合
                    Select Case srtNenkanNissu_kbn
                    Case "1" '365日
                        .Cells(lngStartRow + 4, 15).Value = "(((1 ＋ ③) ＾ (365日 ÷ " + strNenkanNissu + "日)) － 1)"
                    Case "2" '366日
                        .Cells(lngStartRow + 4, 15).Value = "(((1 ＋ ③) ＾ (366日 ÷ " + strNenkanNissu + "日)) － 1)"
                    End Select
                End Select
                .Cells(lngStartRow + 4, 34).Value = vDatRoring(8, lngCnt - 1)  '超過収益率（年率換算後）
                
                lngStartRow = lngStartRow + 6
                
        Next lngCnt

        .Range("ローリング期間") = strMAXNenNo 'ローリング期間
        .Range("ローリング期間FROM") = CDate(gfunc_YmdFormat(vDatRoring(1, 0)))    ' ローリング期間FROM
        
        'サマリー行編集
        .Range("平均超過収益率タイトル") = strMAXNenNo + 1  'ローリング年番号最大値
        '　ローリング情報の帳票設定
        .Range("平均超過収益率") = CStr(vDatRoringGo(0, 0))
        .Range("掛け目基礎数値") = CStr(vDatRoringGo(1, 0))
        
    End With
        
    Workbooks(Workbooks.Count).Close SaveChanges:=True
        
    fncMakeBook = True
    
    Call subEndProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME)
    
    Exit Function
    
'*******************************************************
'* エラーセクション                                 　  *
'*******************************************************
ErrSection:

    Workbooks(Workbooks.Count).Close SaveChanges:=True
    fncMakeBook = False

'---- エラーログ出力 Start --------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, PROCEDURE_NAME, Err.Number, Err.Description)
    On Error GoTo 0
'   Err.Raise lng
'---- エラーログ出力 End  ---------------------------------
   
End Function



'*************************************************************************
'関数名　　：顧問料請求期間情報取得処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           strKozaNo       I       口座番号
'           strKikan        I       顧問料請求期間
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：顧問料請求期間情報を取得する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_GetKeiyakuInfo(ByRef TargetSheets As Object _
                                   , ByVal strKozaNo As String _
                                   , ByVal strKikan As String _
                                   , ByRef vData() As Variant _
                                   , ByRef lngRow As Long) As Boolean
On Error GoTo ErrHandler
    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String
    
    func_GetKeiyakuInfo = False
    
   strSql = ""
   strSql = strSql & " SELECT"
   strSql = strSql & "     SEIKYU_KIKAN_FROM,"
   strSql = strSql & "     SEIKYU_KIKAN_TO"
   strSql = strSql & " FROM"
   strSql = strSql & "     KYK_SEIKYU_KIKAN"
   strSql = strSql & " WHERE"
   strSql = strSql & "     KOHZA_NO = '" & func_ChkQuoteshon(strKozaNo, "'") & "' "
   strSql = strSql & " AND SEIKYU_KIKAN_FROM <= " & strKikan
   strSql = strSql & " AND SEIKYU_KIKAN_TO >= " & strKikan
   strSql = strSql & " AND SAKUJO_FLG = '0'"
    
    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        Exit Function
    End If
    
    func_GetKeiyakuInfo = True
    
    Exit Function

ErrHandler:
    func_GetKeiyakuInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetKeiyakuInfo", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function

'*************************************************************************
'関数名　　：口座番号の存在チェック処理
'
'引　　数　：
'           TargetSheets    I       表示シート
'           strKozaNo       I       口座番号
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：口座番号の存在チェック
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'          ：2013/04/12 GUT 梁賀松 顧問バック更改対応
'
'*************************************************************************
Private Function func_KozaNoExist(ByRef TargetSheets As Object _
                                  , ByVal strKozaNo As String _
                                  , ByRef vData() As Variant _
                                  , ByRef lngRow As Long) As Boolean
On Error GoTo ErrHandler
    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String

    func_KozaNoExist = False

   strSql = ""
   strSql = strSql & " SELECT"
   strSql = strSql & "     A.prtcd,"
   strSql = strSql & "     A.NAME_J,"      '正規名称を表示
   strSql = strSql & "     C.VALUE_NAME"
   strSql = strSql & "    ,NVL(A.CALLOFF_DATE,'0') "
   strSql = strSql & " FROM"
   strSql = strSql & "     VW_DT001D A,"
   strSql = strSql & "     KYK_KOHZA_ZOKUSEI B,"
   strSql = strSql & "     KYK_CODE_VALUE C"
   strSql = strSql & " WHERE"
   strSql = strSql & "     A.prtcd='" & func_ChkQuoteshon(strKozaNo, "'") & "'   AND"
   strSql = strSql & "     A.prtcd=B.KOHZA_NO   AND"
   strSql = strSql & "     C.CODE_NO(+) = '41'   AND"
   strSql = strSql & "     B.DATA_TYPE  = C.CODE_VALUE(+) AND"
'------------<顧問バック更改対応 2013/04/12 GUT梁賀松 START> ------------
'   strSql = strSql & "     A.settei_kbn='1'"
   strSql = strSql & "     A.settei_kbn='0'   AND"
   strSql = strSql & "     A.GENGO='0'"
'------------<顧問バック更改対応 2013/04/12 GUT梁賀松 END> ------------
   strSql = strSql & " "

    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        Exit Function
    End If

    func_KozaNoExist = True

    Exit Function

ErrHandler:
    func_KozaNoExist = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_KozaNoExist", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    Erase vData


End Function



'*************************************************************************
'関数名　　：成功報酬期間情報取得処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           strKozaNo       I       口座番号
'           strSeikoFrom    I       成功報酬期間From
'           strSeikoTo      I       成功報酬期間To
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：成功報酬期間情報を取得する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_GetSeikoInfo(ByRef TargetSheets As Object _
                                   , ByVal strKozaNo As String _
                                   , ByVal strSeikoFrom As String _
                                   , ByVal strSeikoTo As String _
                                   , ByRef vData() As Variant _
                                   , ByRef lngRow As Long) As Boolean
 On Error GoTo ErrHandler
    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String
    
    func_GetSeikoInfo = False
    
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     SEIKOH_HOSYU_KIKAN_ID,"
    strSql = strSql & "     MOKUHYO_TYOHKASYUEKI_RITSU"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_SH_SEIKOH_HOHSHU_KIKAN"
    strSql = strSql & " WHERE"
    strSql = strSql & "     KOHZA_NO = '" & func_ChkQuoteshon(strKozaNo, "'") & "' "
    strSql = strSql & " AND SEIKOH_HOSYU_KIKAN_FROM = " & strSeikoFrom
    If Trim(strSeikoTo) <> "" Then
        strSql = strSql & " AND SEIKOH_HOSYU_KIKAN_TO = " & strSeikoTo
    End If
    strSql = strSql & " AND SAKUJO_FLG = '0'"
    
    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        Exit Function
    End If
    
    func_GetSeikoInfo = True
    
    Exit Function

ErrHandler:
    func_GetSeikoInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetSeikoInfo", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function





'*************************************************************************
'関数名　　：年ごと超過収益率情報取得処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           strKozaNo       I       口座番号
'           strSeikohId     I       成功報酬期間ID
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：年ごと超過収益率情報を取得する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_GetRoringInfo(ByRef TargetSheets As Object _
                                   , ByVal strKozaNo As String _
                                   , ByVal strSeikohId As String _
                                   , ByRef vData() As Variant _
                                   , ByRef lngRow As Long) As Boolean
 On Error GoTo ErrHandler
    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String
    
    func_GetRoringInfo = False
    
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     ROLLING_NEN_NO,"
    strSql = strSql & "     ROLLING_KIKAN_FROM,"
    strSql = strSql & "     ROLLING_KIKAN_TO,"
    strSql = strSql & "     NENRITSU_KANSAN_HOHHO,"
    strSql = strSql & "     NENKAN_NISSU_KBN,"
    strSql = strSql & "     FUND_SYUEKI_RITSU,"
    strSql = strSql & "     BENCHMARK_SYUEKI_RITSU,"
    strSql = strSql & "     TYOHKASYUEKI_RITSU,"
    strSql = strSql & "     NENKANSANGO_TYOHKASYUEKI_RITSU,"
    strSql = strSql & "     KEISAN_KIKAN_SU"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_SH_NENGOTO_SYUEKI_RITSU"
    strSql = strSql & " WHERE"
    strSql = strSql & "     KOHZA_NO = '" & func_ChkQuoteshon(strKozaNo, "'") & "' "
    strSql = strSql & " AND SEIKOH_HOSYU_KIKAN_ID = " & strSeikohId
    strSql = strSql & " AND SAKUJO_FLG = '0'"
    strSql = strSql & " ORDER BY ROLLING_NEN_NO"
    
    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        Exit Function
    End If
    
    func_GetRoringInfo = True
    
    Exit Function

ErrHandler:
    func_GetRoringInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetRoringInfo", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function



'*************************************************************************
'関数名　　：ローリング後超過収益率情報取得処理
'
'引　　数　：
'           TargetSheets    I       処理対象ワークシート
'           strKozaNo       I       口座番号
'           strSeikohId     I       成功報酬期間ID
'           vdata()         O       取得結果
'           lngRow          O       取得件数
'
'戻り値　　：Boolean
'
'機能説明　：ローリング後超過収益率情報を取得する
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Function func_GetRoringGoInfo(ByRef TargetSheets As Object _
                                   , ByVal strKozaNo As String _
                                   , ByVal strSeikohId As String _
                                   , ByRef vData() As Variant _
                                   , ByRef lngRow As Long) As Boolean
 On Error GoTo ErrHandler
    Dim strSql      As String
    Dim lngCol      As Long
    Dim lngCnt      As Long
    Dim strErrCode  As String
    
    func_GetRoringGoInfo = False
    
    strSql = ""
    strSql = strSql & " SELECT"
    strSql = strSql & "     HEIKIN_TYOHKASYUEKI_RITSU,"
    strSql = strSql & "     KAKEME_KISO_SUCHI"
    strSql = strSql & " FROM"
    strSql = strSql & "     KYK_SH_ROLLING_GO_SYUEKI_RITSU"
    strSql = strSql & " WHERE"
    strSql = strSql & "     KOHZA_NO = '" & func_ChkQuoteshon(strKozaNo, "'") & "' "
    strSql = strSql & " AND SEIKOH_HOSYU_KIKAN_ID = " & strSeikohId
    strSql = strSql & " AND SAKUJO_FLG = '0'"
    
    
    'データ取得
    Call gclsdb.DoExequteSql(gcSELECT, strSql, vData, lngCol, lngRow, strErrCode)
    
    If Trim(strErrCode) <> "" Then
        Exit Function
    End If
    
    func_GetRoringGoInfo = True
    
    Exit Function

ErrHandler:
    func_GetRoringGoInfo = False

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "func_GetRoringGoInfo", Err.Number, Err.Description)
    On Error GoTo 0
    Err.Raise lng
'---- Error Log End   ----------------------------------------------------

End Function
