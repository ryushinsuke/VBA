VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit


'*************************************************************************
'
'プロジェクト名：国内顧問料計算システム・計算方法
'
'オブジェクト名：Sheet1
'
'機能概要　：メイン画面
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Const MODULE_NAME As String = "main"


'『ローリング期間情報』の新規
Private Sub cmdAdd1_Click()

    ThisWorkbook.Application.ScreenUpdating = False
    
    Call gsub_NewClick(1)

    ThisWorkbook.Application.ScreenUpdating = True
    
End Sub

'『ローリング期間情報』の削除
Private Sub cmdDel1_Click()

    ThisWorkbook.Application.ScreenUpdating = False
    
    Call gsub_DelClick(1)

    ThisWorkbook.Application.ScreenUpdating = True

End Sub


'*************************************************************************
'関数名　　：印刷ボタン・イベントプロシージャ
'
'引　　数　：
'
'戻り値　　：
'
'機能説明　：印刷ボタン押下時の処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub cmdPrint_Click()

On Error GoTo Error_Section

    ThisWorkbook.Application.Cursor = xlWait
    ThisWorkbook.Application.ScreenUpdating = False
        
    With ThisWorkbook.Worksheets(MAIN_SHEETNAME).PageSetup

        '印刷範囲を1ページに収まるよう設定
        .Zoom = 76
        .FitToPagesWide = 1

        '印刷の向きを横に設定
        .Orientation = xlLandscape

        '印刷のサイズをＡ４に設定
        .PaperSize = xlPaperA4
        
        'フッターに検索時の成功報酬期間ID表示
        .RightFooter = "(" & ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時成功報酬期間ID") & ")"
    End With
            
    ThisWorkbook.Worksheets(MAIN_SHEETNAME).PrintOut
    
    Call gfunc_VisibleCommandBtn(True)
        
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault

    Exit Sub

Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdPrint_Click", Err.Number, Err.Description)
    On Error GoTo 0
'    Err.Raise lng
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.Cursor = xlDefault
    ThisWorkbook.Application.ScreenUpdating = True
    
    '印刷ボタンの無効化
    cmdPrint.Enabled = True


End Sub

'*************************************************************************
'関数名　　：検索ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：検索ボタンを押下された場合の処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub cmdSearch_Click()

On Error GoTo Error_Section

    mainmenu.gOperationStatus = "cmdSearch_Click"

    If ThisWorkbook.Application.Cursor <> xlWait Then
    
        ThisWorkbook.Application.Cursor = xlWait
        
        '画面クリア処理
        ThisWorkbook.Application.ScreenUpdating = False
        Call g_subKeisanIniScreenH(ThisWorkbook.Worksheets(MAIN_SHEETNAME))   'ヘッダー部
        Call g_subKeisanIniScreenD(ThisWorkbook.Worksheets(MAIN_SHEETNAME))   '明細部
        ThisWorkbook.Application.ScreenUpdating = True
        
        '検索メイン
        ThisWorkbook.Application.ScreenUpdating = False
        Call gsub_KensakuMain(ThisWorkbook.Worksheets(MAIN_SHEETNAME))

        ThisWorkbook.Application.Cursor = xlDefault

    End If
    
    mainmenu.gOperationStatus = ""
    ThisWorkbook.Application.ScreenUpdating = True
    
    Exit Sub

Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdSearch_Click", Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault
    
    '印刷ボタンの無効化
    cmdPrint.Enabled = True
    
End Sub


'*************************************************************************
'関数名　　：更新ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：更新ボタンを押下された場合の処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub cmdUp_Click()

On Error GoTo Error_Section

    Dim blnRet      As Boolean
    Dim Sisan1()    As Variant      'ローリング期間

    Dim strMsg      As String
    Dim lngRet      As Long
    Dim lngKikanId  As Long         '採番　　請求期間ID
    
    Erase Sisan1
    
    blnRet = False
    

    ThisWorkbook.Application.ScreenUpdating = False
    ThisWorkbook.Application.Cursor = xlWait
    
    '入力チェック処理(更新時)
    blnRet = gfuncKeisan_UpdInputCheck(ThisWorkbook.Worksheets(MAIN_SHEETNAME), Sisan1)
    
    If blnRet = False Then
        ThisWorkbook.Application.ScreenUpdating = True
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Sisan1
        Exit Sub
    End If
    
    
    '最終処理確認
    strMsg = gfunc_ErrorMsg(2, 27015, "", "", False)
    lngRet = MsgBox(strMsg, vbQuestion + vbOKCancel, ThisWorkbook.Application.ActiveWorkbook.Name)
    If lngRet = vbCancel Then
        ThisWorkbook.Application.ScreenUpdating = True
        ThisWorkbook.Application.Cursor = xlDefault
        Erase Sisan1
        Exit Sub
    End If
    
    blnRet = False
    
    '更新処理
    blnRet = gfuncKeisan_DoUpdate(ThisWorkbook.Worksheets(MAIN_SHEETNAME), Sisan1, lngKikanId)
    
    If blnRet = False Then
    '更新エラー
        Call gfunc_ErrorMsg(1, 27016, "", "", True)
        Erase Sisan1
        
        ThisWorkbook.Application.ScreenUpdating = True
        ThisWorkbook.Application.Cursor = xlDefault
        
        Exit Sub
    Else
    '更新成功
    
        '検索時の成功報酬期間IDに登録した成功報酬期間IDで上書きする。
        ThisWorkbook.Worksheets(TEMPLATE_SHEETNAME).Range("検索時成功報酬期間ID").Value2 = lngKikanId
        
        Dim strDate As String
        Dim strTime As String
        'サーバ日付の取得
        Call gfunc_GetSvDateTime(strDate, strTime)
        
        'データ取得日時の設定
        Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), False)
        ThisWorkbook.Worksheets(MAIN_SHEETNAME).Range("データ取得日時").Value2 = gfunc_YmdFormat(strDate) & " " _
            & Mid(strTime, 1, 2) & ":" & Mid(strTime, 3, 2) & ":" & Mid(strTime, 5, 2)
        Call gCom01_SheetProtect(ThisWorkbook.Worksheets(MAIN_SHEETNAME), True)
        
        '印刷ボタンの有効化
        cmdPrint.Enabled = True
        '作成ボタンの有効化
        cmdSakusei.Enabled = True
        
        ThisWorkbook.Application.ScreenUpdating = True
    
        'メッセージ
        Call gfunc_ErrorMsg(1, 27017, "", "", True)
    End If
    
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault
    
    Erase Sisan1
    
    Exit Sub
Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdUp_Click", Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault
    Erase Sisan1
    


End Sub

'*************************************************************************
'関数名　　：作成ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：作成ボタンを押下された場合の処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub cmdSakusei_Click()

On Error GoTo Error_Section

    '実行メイン
    Application.ScreenUpdating = False

    Call fncClickRunBtn(ThisWorkbook.Worksheets(MAIN_SHEETNAME))
    
    Application.CutCopyMode = False
    Application.ScreenUpdating = True
    
    Exit Sub
Error_Section:
'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdSakusei_Click", Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    ThisWorkbook.Application.ScreenUpdating = True
    ThisWorkbook.Application.Cursor = xlDefault

End Sub

'*************************************************************************
'関数名　　：参照ボタン・イベントプロシージャ
'
'引　　数　：なし
'
'戻り値　　：なし
'
'機能説明　：参照ボタンを押下された場合の処理
'
'更新履歴　：2011/02/01 SRA 宮嵜     新規作成
'
'*************************************************************************
Private Sub cmdSansyo_Click()
    
On Error GoTo Error_Section
    
    Dim vRet            As Variant
    Dim strOrg          As String
    Dim strSaveFolder   As String
    Dim strMsg          As String
    
    strSaveFolder = ""
    strMsg = gfunc_ErrorMsg(3, 27012, "", "", False)
    
    '保存先フォルダーの指定
    Call GetFolder(strMsg, strSaveFolder)
    
    If Trim(strSaveFolder) <> "" Then
        ThisWorkbook.Worksheets(MAIN_SHEETNAME).Range("保存フォルダ").Value = strSaveFolder
    End If

    
    Exit Sub
    
Error_Section:

'---- Error Log Start ----------------------------------------------------
    Dim lng As Long
    lng = Err.Number
    Call subErrProcess(ThisWorkbook.Name & "\" & MODULE_NAME, "cmdRef_Click", Err.Number, Err.Description)
    On Error GoTo 0
'---- Error Log End   ----------------------------------------------------
    
End Sub
